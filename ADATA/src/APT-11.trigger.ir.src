rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem instead of read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

rem --- Read Barista Addon's apt_invoicedet instead of V6 apt-11

	setopts $080872202c2e$
	seterr trigger_error

    info!=BBjAPI().getCurrentSessionInfo()
    grpID$=str(info!.getGroupID())   
    sessionID$=str(info!.getSessionID())
    nskey$="APT11"+"_"+grpID$+"_"+sessionID$

    rem ' trace
    rem goto skip_trace;rem this out to do the trace
    tfl$="C:/temp_downloads/apt11.ir."+grpID$+"_"+sessionID$+"_"+str(fpt(tim))+".txt"
    erase tfl$,err=*next
    string tfl$
    tfl=unt
    open(tfl)tfl$
    settrace(tfl,MODE="UNTIMED")
skip_trace:
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

    keyed_read=0
	ky$=td!.getKey(err=non_keyed_read)
    keyed_read=1
    
non_keyed_read: rem --- if read doesn't have key=k$, will need to get ky$ from namespace rather than td!.getKey()
    if !keyed_read
        rem --- get partial/trip-read or last full key from namespace
        gns!=BBjAPI().getGroupNamespace()
        ky$ = gns!.getValue(nskey$,err=*next)
        rem --- if previous key read took us to EOF, namespace will contain special value to indicate that
        if ky$=nskey$+"**EOF"
            err_num=2
            goto trigger_error_forced
        endif
    endif
	
	wkdir$ = dir("")
	apdir$ = stbl("+APDATA")
print (tfl)"*****incoming ky$ is: "+ky$+":"
rem --- Template for the APT-11 record being caught

    intemp$="firm_id:c(2),ap_type:c(2),vendor_nbr:c(6),ap_inv_nbr:c(10),sequence_num:c(2*),"
    intemp$=intemp$+"trans_type:c(1),trans_date:c(3),trans_ref:c(7*),trans_amt:n(7*),trans_disc:n(7*),trans_ret:n(7*)"

	dim inRec$:intemp$

rem --- Open Barista AP Invoice Detail table

	apt_invoicedet=unt
	open (apt_invoicedet,err=trigger_error)apdir$+"apt-11"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("APT_INVOICEDET",16),dom=trigger_error) *,*,apt_invoicedet_tpl$
	dim apt_invoicedet$:apt_invoicedet_tpl$
	
rem --- Get the data

    dom_flag=1
    if len(ky$)>20 then ky$=ky$(1,20)+str(num(ky$(21)):"000");rem V6 key to apt-11 contains 2-char seq#, but incoming from Barista Addon is 3-char.

    if ky$="" and keyed_read=0
        read record (apt_invoicedet,end=trigger_error) apt_invoicedet$; dom_flag=0
    else
        read record (apt_invoicedet,key=ky$,dom=*next) apt_invoicedet$; dom_flag=0
    endif
    
    if dom_flag=0
        inRec.firm_id$ = apt_invoicedet.firm_id$
        inRec.ap_type$=apt_invoicedet.ap_type$
        inRec.vendor_nbr$=apt_invoicedet.vendor_id$
        inRec.ap_inv_nbr$=apt_invoicedet.ap_inv_no$
        inRec.sequence_num$=apt_invoicedet.sequence_no$
        inRec.trans_type$=apt_invoicedet.trans_type$
        inRec.trans_date$=fnd$(apt_invoicedet.trans_date$)
        inRec.trans_ref$=apt_invoicedet.trans_ref$
        inRec.trans_amt$=apt_invoicedet.trans_amt$
        inRec.trans_disc$=apt_invoicedet.trans_disc$
        inRec.trans_ret$=apt_invoicedet.trans_ret$
    endif

    next_ky$=nskey$+"**EOF";rem --- set default in case we take the end=
    next_ky$=key(apt_invoicedet,end=*next)
    if pos("**EOF"=next_ky$)=0 then
        if num(next_ky$(21,3))>99
            next_ky$=nskey$+"**EOF";rem --- since V6 can only accommodate up to 99 detail lines, force EOF if there are more on the Barista Addon side
        else
            seq$=str(num(next_ky$(21,3)):"00")
            next_ky$=next_ky$(1,20)+seq$;rem --- in V6, seq# is 2 char, but it's 3 in Barista Addon, so need to send back as 2 char.
        endif
    endif
print (tfl)"*****next_ky$ going out of ir is: "+next_ky$+":"
    rem --- store next key in group namespace
    gns!=BBjAPI().getGroupNamespace()
    gns!.setValue(nskey$,next_ky$)

    if dom_flag=1
        err_num=11
        goto trigger_error_forced
    else
        td!.setReadBuffer(inRec$)
        goto std_exit
    endif

trigger_error:

    rd_err_text$="", err_num=err
    
trigger_error_forced:

    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=11
        rem --- if we took the DOM above, store the next key in group namespace
        gns!=BBjAPI().getGroupNamespace()
        gns!.setValue(nskey$,next_ky$)
	endif
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
rem --- Functions

	rem --- fnyy$ returns a 2 character year (A0-E9) from a 4 character year
	def fnyy$(q1$)
		q3$=" ABCDE56789ABCDEFGHIJ"
		q1$(1,1)=q3$(pos(q1$(1,1)=" 0123456789ABCDEFGHIJ"))
		return q1$
	fnend

	rem --- returns a 3 character packed date from an 8 character year/month/day
	def fnd$(q$)
		q$=pad(q$,8)
		if cvs(q$,2)="" return "   "
		q1$=chr(num(q$(5,2))+32)+chr(num(q$(7,2))+32)
		if q$(1,2)="19"
			q2$=chr(num(q$(3,2))+32)
		else
			q2$=chr(num(q$(3,2))+132)
		endif
		return q2$+q1$
	fnend
	
std_exit: