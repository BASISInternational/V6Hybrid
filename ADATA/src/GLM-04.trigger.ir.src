rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem instead of read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

rem --- When reading V6 GLM-04 GL Account Xref, read Barista Addon glm_acct instead, using appropriate knum

	setopts $080872202c2e$
	seterr trigger_error

    info!=BBjAPI().getCurrentSessionInfo()
    grpID$=str(info!.getGroupID())   
    sessionID$=str(info!.getSessionID()) 
    nskey$="GLM04"+"_"+grpID$+"_"+sessionID$

    rem ' trace
    goto skip_trace;rem this line (and change pathname as applicable) to do the trace
    tfl$="C:/temp_downloads/glm-04.ir."+grpID$+"_"+sessionID$+"_"+str(fpt(tim))+".txt"
    erase tfl$,err=*next
    string tfl$
    tfl=unt
    open(tfl)tfl$
    settrace(tfl,MODE="UNTIMED")
skip_trace:
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

    keyed_read=0
	ky$=td!.getKey(err=non_keyed_read)
    keyed_read=1
    
non_keyed_read: rem --- if read doesn't have key=k$, will need to get ky$ from namespace rather than td!.getKey()
    if !keyed_read
        rem --- get key from namespace
        gns!=BBjAPI().getGroupNamespace()
        ky$ = gns!.getValue(nskey$,err=*next)
        rem --- if previous key read took us to EOF, namespace will contain special value to indicate that
        if ky$=nskey$+"**EOF"
            err_num=2
            goto trigger_error_forced
        endif
    endif

	wkdir$ = dir("")
	gldir$ = stbl("+GLDATA")

rem --- Template for the GLM-04 key being caught

    xref_key$="AO_DESC_ACCT"
    intemp$="firm_id:c(2),record_id:c(1),gl_acct_desc:c(35),gl_account:c(10)"

	dim inRec$:intemp$
	
rem --- Open Barista GL Account Master file

	glm_acct=unt
	open (glm_acct,err=trigger_error)gldir$+"glm-01"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("GLM_ACCT",16),dom=trigger_error) *,*,glm_acct_tpl$
	dim glm_acct$:glm_acct_tpl$
	
rem --- Get the data

    dom_flag=1

    if ky$="" and keyed_read=0
        read record (glm_acct,knum=xref_key$,end=trigger_error) glm_acct$; dom_flag=0
    else
    rem --- incoming key from V6 contains record ID A - strip it out
        if len(ky$)>3
            trip_key$=ky$(1,2)+ky$(4)
        else
            trip_key$=ky$(1,2)
        endif

        read record (glm_acct,key=trip_key$,knum=xref_key$,dom=*next) glm_acct$; dom_flag=0
    endif
    
    if dom_flag=0
        inRec.firm_id$ = glm_acct.firm_id$
        inRec.record_id$ = ky$(3,1)
        inRec.gl_acct_desc$ = glm_acct.gl_acct_desc$
        inRec.gl_account$ = glm_acct.gl_account$
    endif

    next_ky$=nskey$+"**EOF";rem --- set default in case we take the end=
    next_ky$=key(glm_acct,end=store_key)
    next_ky$=next_ky$(1,2)+ky$(3,1)+next_ky$(3)

store_key:    
    rem --- store next key in group namespace
    gns!=BBjAPI().getGroupNamespace()
    gns!.setValue(nskey$,next_ky$)

    if dom_flag=1
        err_num=11
        goto trigger_error_forced
    else
        td!.setReadBuffer(inRec$)
        goto std_exit
    endif

trigger_error:

    rd_err_text$="", err_num=err

trigger_error_forced:

    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=11
        rem --- if we took the DOM above, store the next key in group namespace
        gns!=BBjAPI().getGroupNamespace()
        gns!.setValue(nskey$,next_ky$)
	endif
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
    
rem --- Functions

	rem --- fnyy$ returns a 2 character year (A0-E9) from a 4 character year
	def fnyy$(q1$)
		q3$=" ABCDE56789ABCDEFGHIJ"
		q1$(1,1)=q3$(pos(q1$(1,1)=" 0123456789ABCDEFGHIJ"))
		return q1$
	fnend
	
std_exit: