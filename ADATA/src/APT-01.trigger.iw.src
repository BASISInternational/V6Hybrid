rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem instead of write
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

rem --- Write Barista Addon's apt_invoicehdr instead of V6 apt-01

	setopts $080872202c2e$
	seterr trigger_error

    info!=BBjAPI().getCurrentSessionInfo()
    grpID$=str(info!.getGroupID())   
    sessionID$=str(info!.getSessionID())
    nskey$="APT01"+"_"+grpID$+"_"+sessionID$

    rem ' trace
    goto skip_trace;rem this out to do the trace
    tfl$="C:/temp_downloads/apt01.iw."+grpID$+"_"+sessionID$+"_"+str(fpt(tim))+".txt"
    erase tfl$,err=*next
    string tfl$
    tfl=unt
    open(tfl)tfl$
    settrace(tfl,MODE="UNTIMED")
skip_trace:
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

    ky$=td!.getKey()
    
	wkdir$ = dir("")
	apdir$ = stbl("+APDATA")

rem --- Template for the APT-01 record being caught

    intemp$="firm_id:c(2),ap_type:c(2),vendor_nbr:c(6),ap_inv_nbr:c(10),sequence_00:c(2),dist_code:c(2),"
    intemp$=intemp$+"payment_grp:c(2),terms_code:c(2),invoice_date:c(3),accting_date:c(3),inv_due_date:c(3),disc_date:c(3),"
    intemp$=intemp$+"reference:c(10),ap_inv_memo:c(16),hold_flag:c(1),mc_inv_flag:c(1),mc_inv_adj:c(1),mc_inv_rev:c(1),"
    intemp$=intemp$+"reserved_str:c(1*=10),invoice_amt:n(7*=10),discount_amt:n(7*=10),retention:n(7*=10)"

	dim inRec$:intemp$
    inRec$=td!.getWriteBuffer()

rem --- Open Barista AP Invoice Header table

	apt_invoicehdr=unt
	open (apt_invoicehdr,err=trigger_error)apdir$+"apt-01"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("APT_INVOICEHDR",16),dom=trigger_error) *,*,apt_invoicehdr_tpl$
	dim apt_invoicehdr$:apt_invoicehdr_tpl$
	
rem --- Write the data

    apt_invoicehdr.firm_id$=inRec.firm_id$
    apt_invoicehdr.ap_type$=inRec.ap_type$
    apt_invoicehdr.vendor_id$=inRec.vendor_nbr$
    apt_invoicehdr.ap_inv_no$=inRec.ap_inv_nbr$
    apt_invoicehdr.sequence_00$=inRec.sequence_00$
    apt_invoicehdr.ap_dist_code$=inRec.dist_code$
    apt_invoicehdr.payment_grp$=inRec.payment_grp$
    apt_invoicehdr.ap_terms_code$=inRec.terms_code$
    value$=inRec.invoice_date$
    gosub fix_yymmdd_date
    apt_invoicehdr.invoice_date$=value$
    value$=inRec.accting_date$
    gosub fix_yymmdd_date
    apt_invoicehdr.accting_date$=value$
    value$=inRec.inv_due_date$
    gosub fix_yymmdd_date
    apt_invoicehdr.inv_due_date$=value$
    value$=inRec.disc_date$
    gosub fix_yymmdd_date
    apt_invoicehdr.disc_date$=value$
    apt_invoicehdr.reference$=inRec.reference$
    apt_invoicehdr.ap_inv_memo$=inRec.ap_inv_memo$
    apt_invoicehdr.hold_flag$=inRec.hold_flag$
    apt_invoicehdr.mc_inv_flag$=inRec.mc_inv_flag$
    apt_invoicehdr.mc_inv_adj$=inRec.mc_inv_adj$
    apt_invoicehdr.mc_inv_rev$=inRec.mc_inv_rev$
    apt_invoicehdr.invoice_amt=inRec.invoice_amt
    apt_invoicehdr.discount_amt=inRec.discount_amt
    apt_invoicehdr.retention=inRec.retention
    
    apt_invoicehdr$=field(apt_invoicehdr$)
    writerecord (apt_invoicehdr)apt_invoicehdr$

	goto std_exit

fix_yymmdd_date: rem --- Convert yy dates to yyyy, and yymmdd dates to yyyymmdd
    rem ---     Input/Out: value$
    if len(cvs(value$,3)) then
        value$=fnyy_yy21$(value$)
        if asc(value$)>=65 then
            value$=str(200+asc(value$)-65)+value$(2)
        else
            value$="19"+value$
        endif
        value$=str(num(value$(1,4),err=*next))+value$(5)
    else
        value$=pad("",8)
    endif
    return

trigger_error:

    rd_err_text$="", err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=11
        rem -- if we took the DOM above, store the partial key in group namespace for upcoming k$=key in calling code
        gns!=BBjAPI().getGroupNamespace()
        gns!.setValue(nskey$,ky$)
	endif
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
rem --- Functions

	rem --- fnyy$ returns a 2 character year (A0-E9) from a 4 character year
	def fnyy$(q1$)
		q3$=" ABCDE56789ABCDEFGHIJ"
		q1$(1,1)=q3$(pos(q1$(1,1)=" 0123456789ABCDEFGHIJ"))
		return q1$
	fnend

    rem --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"   
    def FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
   
    rem --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year
	def FNYY_YY21$(Q1$)
	Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
	return Q1$
	fnend

	rem --- returns a 3 character packed date from an 8 character year/month/day
	def fnd$(q$)
		q$=pad(q$,8)
		if cvs(q$,2)="" return "   "
		q1$=chr(num(q$(5,2))+32)+chr(num(q$(7,2))+32)
		if q$(1,2)="19"
			q2$=chr(num(q$(3,2))+32)
		else
			q2$=chr(num(q$(3,2))+132)
		endif
		return q2$+q1$
	fnend
	
std_exit: