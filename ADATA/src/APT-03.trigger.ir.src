rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem instead of read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

rem --- When reading V6 APT-03 Open Invoice Xref, read Barista Addon apt_invoicehdr instead, using appropriate knum
rem --- A for AO_VEND_INVDATE, B for AO_VEND_REF, C for AO_INV_TYPE_VEND

	setopts $080872202c2e$
	seterr trigger_error

    info!=BBjAPI().getCurrentSessionInfo()
    grpID$=str(info!.getGroupID())   
    sessionID$=str(info!.getSessionID())
    nskey$="APT03"+"_"+grpID$+"_"+sessionID$

    rem ' trace
    rem goto skip_trace;rem this out to do the trace
    tfl$="C:/temp_downloads/apt03.ir."+grpID$+"_"+sessionID$+"_"+str(fpt(tim))+".txt"
    erase tfl$,err=*next
    string tfl$
    tfl=unt
    open(tfl)tfl$
    settrace(tfl,MODE="UNTIMED")
skip_trace:
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

    keyed_read=0
	ky$=td!.getKey(err=non_keyed_read)
    keyed_read=1
    
non_keyed_read: rem --- if read doesn't have key=k$, will need to get ky$ from namespace rather than td!.getKey()
    if !keyed_read
        rem --- get partial/trip-read or last full key from namespace
        gns!=BBjAPI().getGroupNamespace()
        ky$ = gns!.getValue(nskey$,err=*next)
        rem --- if previous key read took us to EOF, namespace will contain special value to indicate that
        if ky$=nskey$+"**EOF"
            err_num=2
            goto trigger_error_forced
        endif
    endif
	
	wkdir$ = dir("")
	apdir$ = stbl("+APDATA")

rem --- Template for the APT-03 key being caught

    switch pos(ky$(3,1)="ABC")
        case 1;rem A=AP type/vendor/inv date/inv#
            xref_key$="AO_VEND_INVDATE"
            intemp$="firm_id:c(2),record_id:c(1),ap_type:c(2),vendor_nbr:c(6),invoice_date:c(3),ap_inv_nbr:c(10)"
        break
        case 2;rem B=AP type/vendor/ref/inv#
            xref_key$="AO_VEND_REF"
            intemp$="firm_id:c(2),record_id:c(1),ap_type:c(9),vendor_nbr:c(6),reference:c(10),ap_inv_nbr:c(10)"
        break
        case 3;rem C=Invoice/AP type/vendor
            xref_key$="AO_INV_TYPE_VEND"
            intemp$="firm_id:c(2),record_id:c(1),ap_inv_nbr:c(10),ap_type:c(9),vendor_nbr:c(6)"        
        break
    swend

	dim inRec$:intemp$
	
rem --- Open Barista AP Invoice Header file

	apt_invoicehdr=unt
	open (apt_invoicehdr,err=trigger_error)apdir$+"apt-01"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("APT_INVOICEHDR",16),dom=trigger_error) *,*,apt_invoicehdr_tpl$
	dim apt_invoicehdr$:apt_invoicehdr_tpl$
	
rem --- Get the data

    dom_flag=1

    if ky$="" and keyed_read=0
        read record (apt_invoicehdr,knum=xref_key$,end=trigger_error) apt_invoicehdr$; dom_flag=0
    else
    rem --- incoming key contains A, B, or C to match APT-03 format - strip it out
        if len(ky$)>3
            trip_key$=ky$(1,2)+ky$(4)
        else
            trip_key$=ky$(1,2)
        endif

        read record (apt_invoicehdr,key=trip_key$,knum=xref_key$,dom=*next) apt_invoicehdr$; dom_flag=0
    endif
    
    if dom_flag=0
        inRec.firm_id$ = apt_invoicehdr.firm_id$
        inRec.record_id$ = ky$(3,1)
        inRec.ap_type$ = apt_invoicehdr.ap_type$
        inRec.vendor_nbr$ = apt_invoicehdr.vendor_id$
        inRec.ap_inv_nbr$ = apt_invoicehdr.ap_inv_no$

        switch pos(ky$(3,1)="ABC")
            case 1;rem A=AP type/vendor/inv date/inv#
                inRec.invoice_date$ = fnd$(apt_invoicehdr.invoice_date$)
            break
            case 2;rem B=AP type/vendor/ref/inv#
                inRec.reference$ = apt_invoicehdr.reference$
            break
            case 3;rem C=Invoice/AP type/vendor - nothing else needed for this key
            break
        swend
    endif

    next_ky$=nskey$+"**EOF";rem --- set default in case we take the end=
    next_ky$=key(apt_invoicehdr,end=store_key)
    next_ky$=next_ky$(1,2)+ky$(3,1)+next_ky$(3)

store_key:    
    rem --- store next key in group namespace
    gns!=BBjAPI().getGroupNamespace()
    gns!.setValue(nskey$,next_ky$)

    if dom_flag=1
        err_num=11
        goto trigger_error_forced
    else
        td!.setReadBuffer(inRec$)
        goto std_exit
    endif

trigger_error:

    rd_err_text$="", err_num=err

trigger_error_forced:

    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=11
        rem --- if we took the DOM above, store the next key in group namespace
        gns!=BBjAPI().getGroupNamespace()
        gns!.setValue(nskey$,next_ky$)
	endif
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
rem --- Functions

	rem --- fnyy$ returns a 2 character year (A0-E9) from a 4 character year
	def fnyy$(q1$)
		q3$=" ABCDE56789ABCDEFGHIJ"
		q1$(1,1)=q3$(pos(q1$(1,1)=" 0123456789ABCDEFGHIJ"))
		return q1$
	fnend

	rem --- returns a 3 character packed date from an 8 character year/month/day
	def fnd$(q$)
		q$=pad(q$,8)
		if cvs(q$,2)="" return "   "
		q1$=chr(num(q$(5,2))+32)+chr(num(q$(7,2))+32)
		if q$(1,2)="19"
			q2$=chr(num(q$(3,2))+32)
		else
			q2$=chr(num(q$(3,2))+132)
		endif
		return q2$+q1$
	fnend
	
std_exit: