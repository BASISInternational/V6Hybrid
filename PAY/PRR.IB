0010 REM "PRR - Labor Distribution Report 2"
0020 REM "Program PRR.IB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS=1 THEN GOTO 9900
0200 REM " --- IOLists"
0210 PRM01A: IOLIST B0$,B1$
0220 PRM10A: IOLIST Y0$
0230 PRM10E: IOLIST H3$,*,*,*,*,*,*,*,*,B0,B1
0240 PRM10F: IOLIST H5$
0250 PRM10H: IOLIST X0$
0260 PRT05A: IOLIST A0$,A[ALL]
0500 REM " --- Init Data"
0510 LET L9=59,L=L9+1,HEADINGS=4,WIDTH=132,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$,HEADINGS$[3]=H4$
0540 DIM J$(120,"-"),T[7],U[7],R[7],S[7],X[7],Y[7],A[11]
0550 CALL "SYC.RM",M1$,M1,10,13
0560 LET M0=20,P9=POS(P9$="M1234NY")-1
0700 REM " --- Background"
0720 CALL "SYC.NB","Printing",5,COLUMN
0800 REM " --- Build Headings"
0810 LET U0$="",U1$=""
0820 READ (PRM10_DEV,KEY=N0$+"HA",DOM=0830)
0830 LET K$=KEY(PRM10_DEV,END=0890)
0835 IF POS(N0$+"HA"=K$)<>1 THEN GOTO 0890
0840 READ (PRM10_DEV)IOL=PRM10H
0845 LET U0$=U0$+X0$(5,2),X$=X0$(7,10)
0850 FOR X=1 TO 10
0855 IF X$(10,1)<>" " THEN EXITTO 0870
0860 LET X$=" "+X$(1,9)
0865 NEXT X
0870 LET U1$=U1$+X$
0880 GOTO 0830
0890 DIM X$(80)
0895 LET X$(1)=U1$,X$(61)=" Other Pay Total Pay",U1$=X$
1000 REM " --- Init Read"
1020 READ (PRT05_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read"
1120 LET K$=KEY(PRT05_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (PRT05_DEV)IOL=PRT05A
1200 REM " --- Breaks"
1220 IF K$(3,2)<>T0$ THEN GOSUB 6000
1240 IF K$(5,2)<>T1$ THEN GOSUB 6600
1260 IF K$(7,9)<>T2$ THEN GOSUB 6400
2100 REM " --- Accumulate"
2110 DIM Y0$(100)
2160 FIND (PRM10_DEV,KEY=N0$+"A"+A0$(16,2),DOM=2170)IOL=PRM10A
2180 LET X=POS(Y0$(22,2)=U0$,2)
2200 IF X>0 THEN LET X=INT(X/2)+1
2210 IF X>6 THEN LET X=0
2220 IF P9$="Y" THEN GOTO 2300
2230 LET X[X]=X[X]+A[P9],Y[X]=Y[X]+A[6+P9],T[X]=T[X]+A[P9],U[X]=U[X]+A[6+P9],R
2230:[X]=R[X]+A[P9],S[X]=S[X]+A[6+P9],V[X]=V[X]+A[P9],W[X]=W[X]+A[6+P9]
2240 LET X[7]=X[7]+A[P9],Y[7]=Y[7]+A[6+P9],T[7]=T[7]+A[P9],U[7]=U[7]+A[6+P9],R
2240:[7]=R[7]+A[P9],S[7]=S[7]+A[6+P9],V[7]=V[7]+A[P9],W[7]=W[7]+A[6+P9]
2290 GOTO 1100
2300 REM " --- YTD"
2320 LET X0=A[1]+A[2]+A[3]+A[4],X1=A[7]+A[8]+A[9]+A[10]
2330 LET X[X]=X[X]+X0,Y[X]=Y[X]+X1,T[X]=T[X]+X0,U[X]=U[X]+X1,R[X]=R[X]+X0,S[X]
2330:=S[X]+X1,V[X]=V[X]+X0,W[X]=W[X]+X1
2340 LET X[7]=X[7]+X0,Y[7]=Y[7]+X1,T[7]=T[7]+X0,U[7]=U[7]+X1,R[7]=R[7]+X0,S[7]
2340:=S[7]+X1,V[7]=V[7]+X0,W[7]=W[7]+X1
2390 GOTO 1100
4000 REM " --- Totals"
4010 LET T9$="E"
4020 GOSUB 6000
4030 IF L+5>L9 THEN GOSUB 5000
4040 PRINT (7)'LF',@(5),"Grand Totals:",@(M0),T[1]:M1$,T[2]:M1$,T[3]:M1$,T[4]:
4040:M1$,T[5]:M1$,T[6]:M1$,T[0]:M1$,T[7]:M1$
4050 PRINT (7)@(M0),U[1]:M1$,U[2]:M1$,U[3]:M1$,U[4]:M1$,U[5]:M1$,U[6]:M1$,U[0]
4050::M1$,U[7]:M1$
4060 LET X0=U0-T[7],X1=U1-U[7]
4070 PRINT (7)@(10),"Budget",@(M0+7*M1),U0:M1$,'LF',@(M0+7*M1),U1:M1$
4080 PRINT (7)@(10),"Variance",@(M0+7*M1),X0:M1$,'LF',@(M0+7*M1),X1:M1$
4100 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Employee"
5110 PRINT (7)@(0),"Number & Name",
5120 FOR X=1 TO LEN(U1$)/10
5130 PRINT (7)@(M0-11+M1*X),U1$(10*(X-1)+1,10),
5140 NEXT X
5150 PRINT (7)'LF',
5160 LET L=L+2
5180 GOSUB 5200
5190 RETURN
5200 REM " --- Subheading"
5220 PRINT (7)'LF',H5$
5240 LET L=L+2
5290 RETURN
6000 REM " --- Dept Break"
6020 IF T0$="" THEN GOTO 6100
6030 LET T9$="0"
6040 GOSUB 6600
6050 IF T9$="0" THEN LET T9$=""
6055 IF L+7>L9 THEN GOSUB 5000
6060 PRINT (7)'LF',@(5),"Dept Totals:",@(M0),R[1]:M1$,R[2]:M1$,R[3]:M1$,R[4]:M
6060:1$,R[5]:M1$,R[6]:M1$,R[0]:M1$,R[7]:M1$
6070 PRINT (7)@(M0),S[1]:M1$,S[2]:M1$,S[3]:M1$,S[4]:M1$,S[5]:M1$,S[6]:M1$,S[0]
6070::M1$,S[7]:M1$
6072 LET X1=P8*B0,X0=X1*B1,X2=X0-R[7],X3=X1-S[7]
6074 PRINT (7)@(10),"Budget",@(M0+7*M1),X0:M1$,'LF',@(M0+7*M1),X1:M1$
6076 PRINT (7)@(10),"Variance",@(M0+7*M1),X2:M1$,'LF',@(M0+7*M1),X3:M1$
6078 LET U0=U0+X0,U1=U1+X1
6080 LET L=L9+1
6100 IF T9$="E" THEN RETURN
6120 DIM R[7],S[7]
6140 LET T0$=A0$(3,2),T1$=""
6160 LET H3$=""
6170 FIND (PRM10_DEV,KEY=N0$+"E"+A0$(3,2),DOM=6200)IOL=PRM10E
6180 LET H3$=H3$(6,20),X=POS("  "=H3$)
6190 IF X>1 THEN LET H3$=H3$(1,X-1)
6200 LET HEADINGS$[2]=H3$
6290 RETURN
6400 REM " --- Employee Break"
6410 IF T2$="" THEN GOTO 6500
6415 IF L+3>L9 THEN GOSUB 5000
6420 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,2),
6422 LET X0=0
6424 FOR X=1 TO 7
6425 IF X[X]<>0 THEN EXITTO 6430
6426 NEXT X
6430 PRINT (7)'LF',@(0),FNF$(T2$(1,P[0]),M0$),
6432 FOR F=1 TO 6
6434 IF X[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),X[F]:M1$,
6436 NEXT F
6438 IF X[0]<>0 THEN PRINT (7)@(M0+6*M1),X[0]:M1$,
6440 PRINT (7)@(M0+7*M1),X[7]:M1$
6442 PRINT (7)@(2),X1$,
6444 FOR F=1 TO 6
6446 IF Y[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),Y[F]:M1$,
6448 NEXT F
6450 IF Y[0]<>0 THEN PRINT (7)@(M0+6*M1),Y[0]:M1$,
6452 PRINT (7)@(M0+7*M1),Y[7]:M1$
6460 LET L=L+3,L1=L1+1
6470 DIM X[7],Y[7]
6500 IF T9$<>"" THEN RETURN
6520 LET T2$=A0$(7,9)
6530 DIM X[7],Y[7]
6540 LET X1$="Not On File"
6550 FIND (PRM01_DEV,KEY=N0$+T2$,DOM=6570)IOL=PRM01A
6555 LET Q9=POS("  "=B1$(1,16))-1
6557 IF Q9<1 THEN LET Q9=16
6560 LET X1$=B1$(1,Q9)+", "+B1$(17,1)+"."
6580 IF L>L9 THEN GOSUB 5000
6590 RETURN
6600 REM " --- Title Break"
6610 IF T1$="" THEN GOTO 6700
6620 IF T9$="" THEN LET T9$="1"
6630 GOSUB 6400
6640 IF T9$="1" THEN LET T9$=""
6650 IF L1=1 THEN GOTO 6690
6655 IF L+3>L9 THEN GOSUB 5000
6660 PRINT (7)@(M0),J$(1,8*M1),'LF',@(M0),V[1]:M1$,V[2]:M1$,V[3]:M1$,V[4]:M1$,
6660:V[5]:M1$,V[6]:M1$,V[0]:M1$,V[7]:M1$
6670 PRINT (7)@(M0),W[1]:M1$,W[2]:M1$,W[3]:M1$,W[4]:M1$,W[5]:M1$,W[6]:M1$,W[0]
6670::M1$,W[7]:M1$
6680 LET L=L+3
6690 DIM V[7],W[7]
6700 IF T9$<>"" THEN RETURN
6720 LET T1$=K$(5,2),H5$="Not On File",L1=0,T2$=""
6730 DIM V[7],W[7]
6740 FIND (PRM10_DEV,KEY=N0$+"F"+T1$,DOM=6760)IOL=PRM10F
6750 LET H5$=H5$(6,20)
6760 IF L+2>L9 THEN GOSUB 5000 ELSE GOSUB 5200
6790 RETURN
8000 REM " --- Functions"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
