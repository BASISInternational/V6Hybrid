0010 REM "PRR - EARNINGS REPORT"
0020 REM "Program PRR.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-11",FILES$[3]="PRM-01"
0130 LET FILES$[4]="PRM-02",FILES$[5]="PRM-10",FILES$[6]="PRW-04"
0135 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRE01_DEV=CHANNELS[1],PRE11_DEV=CHANNELS[2],PRM01_DEV=CHANNELS[3]
0185 LET PRM02_DEV=CHANNELS[4],PRM10_DEV=CHANNELS[5],PRW04_DEV=CHANNELS[6]
0195 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 PRE01A: IOLIST A0$
0220 PRE11A: IOLIST W0$,W[0],W[1],W[2]
0230 PRM01A: IOLIST B0$,B1$
0240 PRM02A: IOLIST *,X9
0250 PRM10A: IOLIST Y0$
0260 PRM10E: IOLIST H3$
0270 PRM10H: IOLIST X0$
0280 PRW04A: IOLIST Z0$,Z[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0320 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0330 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0340 PRS01B: IOLIST *,X$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[4],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1))
0500 REM " --- Init Data
0510 LET H0=60,H1=120,H=0,H1$=N1$,H2$=N3$
0520 LET L9=59,L=L9+1,HEADINGS=3,CLOCK$="",WIDTH=132
0530 DIM J$(80,"-"),HEADINGS$[HEADINGS]
0540 DIM T[7],U[7],R[7],S[7],X[7],Y[7],W[2]
0545 CALL "SYC.RM",M1$,M1,8,13
0550 LET M0=LEN(M0$),G5=LEN(G5$)
0555 IF M0<20 THEN LET M0=20
0560 LET P7$="A",H8$="N"
0570 LET P[4]=NUM(P3$(7,1))
0580 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0582 LET H4$="Pay-Period Ending "+FNB$(X$(1,3))
0590 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H4$
0700 REM " --- Background
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0740 PRINT 'SB',@(30,3),"Printing:",'SF',
0750 LET V4$="Do You Want To Print The "+N3$
0760 CALL "SYC.YN",1,V4$,2,V$,V3
0770 IF V3=4 THEN GOTO 9900
0780 IF V$<>"Y" THEN GOTO 9900
0785 CALL "SYC.GA",7,1,"","",STATUS
0790 IF STATUS=1 THEN GOTO 9900
0795 GOSUB 6300; REM " Clear All Entries for this company"
0800 REM " --- Build Headings
0810 LET U0$="",U1$=""
0820 PRINT @(40,3),"Heading.....",
0850 READ (PRM10_DEV,KEY=N0$+"H"+P7$,DOM=0860)
0860 LET K$=KEY(PRM10_DEV,END=0980)
0870 IF K$(1,4)<>N0$+"H"+P7$ THEN GOTO 0980
0880 READ (PRM10_DEV)IOL=PRM10H
0890 LET U0$=U0$+X0$(5,2),X$=X0$(7,10)
0900 FOR X=1 TO 10
0910 IF X$(10,1)<>" " THEN EXITTO 0950
0920 LET X$=" "+X$(1,9)
0930 NEXT X
0950 LET U1$=U1$+X$
0960 GOTO 0860
0980 DIM X$(80)
0990 LET X$(1)=U1$,X$(61)=" Other Pay Total Pay",U1$=X$
1000 REM " --- Init Read
1010 PRECISION P[4]
1020 READ (PRE01_DEV,KEY=N0$,DOM=1030)
1060 PRINT @(40,3),'CL',
1100 REM " --- Main Read
1120 LET K$=KEY(PRE01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (PRE01_DEV)IOL=PRE01A
1180 IF H8$="Y" THEN PRINT @(40,3),FNF$(A0$(3,P[0]),M0$),
1400 REM " --- Get Name
1410 LET X1$="Not On File"
1420 DIM B1$(250)
1450 FIND (PRM01_DEV,KEY=A0$(1,11),DOM=1470)IOL=PRM01A
1455 LET X=POS("  "=B1$(1,16))-1
1457 IF X<1 THEN LET X=16
1460 LET X1$=B1$(1,X)+", "+B1$(17,1)+"."
1470 LET X9$=""
1480 FIND (PRM10_DEV,KEY=N0$+"E"+B1$(221,2),DOM=1490)
1482 GOTO 1500
1490 LET X9$="Missing Department"
1500 REM " --- Breaks
1520 IF K$(3,P[3])<>T0$ THEN GOSUB 6000
1600 REM " --- Print Heading
1620 IF L>L9 THEN GOSUB 5000
1640 PRINT (7)'LF',@(0),FNF$(A0$(3,P[0]),M0$),
1660 IF A0$(12,1)<>"C" THEN PRINT (7)@(M0+8*M1),A0$(12,1),A0$(15,7),
1800 REM " --- Pay Rate
1820 LET X9=0
1840 FIND (PRM02_DEV,KEY=A0$(1,11)+B1$(223,2),DOM=1850)IOL=PRM02A
1860 PRINT (7)@(LEN(M0$)+1),X9:M3$,
2000 REM " --- Detail
2040 DIM X[7],Y[7]
2090 READ (PRE11_DEV,KEY=A0$(1,21),DOM=2100)
2100 LET K$=KEY(PRE11_DEV,END=2500)
2120 IF K$(1,21)<>A0$(1,21) THEN GOTO 2500
2140 READ (PRE11_DEV)IOL=PRE11A
2160 FIND (PRM10_DEV,KEY=N0$+P7$+W0$(25,2),DOM=2100)IOL=PRM10A
2180 LET X=POS(Y0$(22,2)=U0$,2)
2200 IF X>0 THEN LET X=INT(X/2)+1
2210 IF X>6 THEN LET X=0
2215 IF POS(Y0$(30,1)="AC")=0 THEN LET W[1]=0
2217 REM "X[X]=Check TOTAL$,Y[X]=Check Total Units,R[X]=Dept TOTAL$,S[X]=Dept 
2217:Total Units,T[X]=Grand TOTAL$, U[X]=Grand Total Units
2220 LET X[X]=X[X]+W[2],Y[X]=Y[X]+W[1],X[7]=X[7]+W[2],Y[7]=Y[7]+W[1]
2230 LET T[X]=T[X]+W[2],U[X]=U[X]+W[1],R[X]=R[X]+W[2],S[X]=S[X]+W[1]
2235 LET T[7]=T[7]+W[2],U[7]=U[7]+W[1],R[7]=R[7]+W[2],S[7]=S[7]+W[1]
2240 GOSUB 6400
2250 FIND (PRM10_DEV,KEY=N0$+"E"+W0$(29,2),DOM=2270)
2260 GOTO 2100
2270 LET X9$="Missing Department"
2290 GOTO 2100
2500 REM " --- Print
2505 IF L+4>L9 THEN GOSUB 5000
2510 FOR F=1 TO 6
2515 IF X[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),X[F]:M1$,
2520 NEXT F
2525 IF X[0]<>0 THEN PRINT (7)@(M0+6*M1),X[0]:M1$,
2530 PRINT (7)@(M0+7*M1),X[7]:M1$
2535 PRINT (7)@(0),X1$,
2540 FOR F=1 TO 6
2545 IF Y[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),Y[F]:M1$,
2550 NEXT F
2555 IF Y[0]<>0 THEN PRINT (7)@(M0+6*M1),Y[0]:M1$,
2560 PRINT (7)@(M0+7*M1),Y[7]:M1$
2565 LET L=L+3
2570 IF X9$="" THEN GOTO 1100
2575 PRINT (7)@(0),"***",X9$,"***"
2580 LET L=L+1
2590 GOTO 1100
4000 REM " --- Totals
4020 LET T9$="E"
4040 GOSUB 6000
4060 PRINT (7)'LF',@(5),"Grand Totals:",@(M0),T[1]:M1$,T[2]:M1$,T[3]:M1$,T[4]:
4060:M1$,T[5]:M1$,T[6]:M1$,T[0]:M1$,T[7]:M1$
4080 PRINT (7)@(M0),U[1]:M1$,U[2]:M1$,U[3]:M1$,U[4]:M1$,U[5]:M1$,U[6]:M1$,U[0]
4080::M1$,U[7]:M1$
4100 GOTO 9900
5000 REM " --- Heading"
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Employee",
5110 FOR X=1 TO LEN(U1$)/10
5120 PRINT (7)@(M0-11+M1*X),U1$(10*(X-1)+1,10),
5130 NEXT X
5135 PRINT (7)'LF',@(0),"Number & Name",@(M0+1+8*M1),"Check",
5140 FOR X=1 TO LEN(U1$)/10
5150 IF U1$(10*(X-1)+1,10)="          " THEN GOTO 5170
5160 PRINT (7)@(M0-7+M1*X),"$/Hrs",
5170 NEXT X
5180 PRINT (7)'LF',
5190 LET L=L+3
5200 REM " --- Sub Heading
5220 IF P[3]=0 THEN RETURN
5240 PRINT (7)'LF',@(0),T0$," ",H3$
5280 LET L=L+2
5290 RETURN
6000 REM " --- Dept Break
6020 IF T0$="" THEN GOTO 6100
6030 IF P[3]=0 THEN GOTO 6100
6035 IF L+3>L9 THEN GOSUB 5000
6040 PRINT (7)'LF',@(5),"Dept Totals:",@(M0),R[1]:M1$,R[2]:M1$,R[3]:M1$,R[4]:M
6040:1$,R[5]:M1$,R[6]:M1$,R[0]:M1$,R[7]:M1$
6060 PRINT (7)@(M0),S[1]:M1$,S[2]:M1$,S[3]:M1$,S[4]:M1$,S[5]:M1$,S[6]:M1$,S[0]
6060::M1$,S[7]:M1$
6070 LET L=L+3
6100 IF T9$="E" THEN RETURN
6120 DIM R[7],S[7]
6140 LET T0$=K$(3,P[3])
6150 IF P[3]=0 THEN RETURN
6160 LET H3$=""
6170 FIND (PRM10_DEV,KEY=N0$+"E"+B1$(221,2),DOM=6200)IOL=PRM10E
6180 LET H3$=H3$(6,20)
6200 IF L>L9-4 THEN GOTO 6250
6220 GOSUB 5200
6230 RETURN
6250 GOSUB 5000
6290 RETURN
6300 REM " --- Remove all Entries for this Company
6320 READ (PRW04_DEV,KEY=N0$,DOM=6330)
6330 LET K$=KEY(PRW04_DEV,END=6390)
6350 IF K$(1,2)<>N0$ THEN GOTO 6390
6370 REMOVE (PRW04_DEV,KEY=K$,DOM=6380)
6380 GOTO 6330
6390 RETURN
6400 REM " --- Write Labor Distrib
6420 LET Z0$=W0$(1,2)+W0$(29,2)+W0$(27,2)+W0$(3,9)+W0$(25,2)
6430 DIM Z[1]
6440 FIND (PRW04_DEV,KEY=Z0$,DOM=6450)IOL=PRW04A
6450 LET Z[0]=Z[0]+W[1],Z[1]=Z[1]+W[2]
6460 WRITE (PRW04_DEV,KEY=Z0$)IOL=PRW04A
6490 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
