0010 REM "PRE - PERIOD DEFINITION ENTRY"
0020 REM "Program PRE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV1=CHANNELS[1],SYS01_DEV2=CHANNELS[2]
0200 REM " --- IOLists
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0320 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0330 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0340 PRS01B: IOLIST A0$,A1$(1)
0400 REM " --- Parameters
0410 FIND (SYS01_DEV2,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV2,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV2,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[6],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(5,1)),P[5]=NUM(P4$
0490:(1,2)),P[6]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data
0520 DIM A1$(25)
0560 CALL "SYC.PA",SYS01_DEV1,P[5],P[6],BEGIN$,G9$,STATUS
0600 REM " --- Additional File Opens
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[3]="GLM-01",FILES$[4]=GLW11$,OPTIONS$[4]="C"
0675 CALL "SYC.DA",1,3,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[3],GLW11_DEV=CHANNELS[4]
0700 REM " --- Background
0720 PRINT 'SB',@(19,3),"Payroll Ending Date:",@(27,4),"Pay Periods:",@(22,5),
0720:"Deduction Period:",@(17,6),"Automatic Processing?:",@(26,7),"Accrual Day
0720:s:",
0790 PRINT 'SF',
0900 REM " --- Init File
1000 REM " --- Key
1010 PRINT 'CF',
1020 LET A0$=N0$+N2$+"01"
1030 EXTRACT (SYS01_DEV1,KEY=A0$,DOM=2000)IOL=PRS01B
1040 GOSUB 5000
1050 GOTO 4000
2000 REM " --- Ending Date
2020 LET V0$="D",V1$="RE",V2$=A1$(1,3),V0=8,V1=40,V2=3,I0=0
2030 GOSUB 7000
2035 IF V3=4 THEN GOTO 9900
2037 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
2038 IF STATUS>99 THEN GOTO 2000
2040 LET A1$(1,3)=V$
2050 IF A1$(1,3)<=G9$ THEN GOTO 3900
2055 PRINT @(10,22),'RB',
2060 LET V4$="Period "+STR(P[5])+" Has Not Been Closed <Enter>: "
2070 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
2075 GOSUB 7000
2080 GOTO 2000
2100 REM " --- Deduct Period
2120 LET V0$="S",V1$="RC",V2$=A1$(4,1),V3$="12345",V4$=" ",V0=1,V1=40,V2=5
2140 GOSUB 7000
2160 LET A1$(4,1)=V$
2190 GOTO 3900
2200 REM " --- Automatic
2220 LET V0$="Y",V1$="R",V2$=A1$(5,1),V0=1,V1=40,V2=6
2240 GOSUB 7000
2260 LET A1$(5,1)=V$
2290 GOTO 3900
2300 REM " --- Pay Periods
2320 LET V0$="S",V1$="CR",V2$=A1$(6,10),V4$="D=Daily  W=Wkly  B=Bi-Wkly  S=Sem
2320:imntly  M=Mnthly  Q=Qrtly  A=Yearly",V0=10,V1=40,V2=4
2330 GOSUB 7000
2340 FOR X=1 TO LEN(V$)
2350 IF POS(V$(X,1)="DWBSMQA ")=0 THEN EXITTO 2300
2360 NEXT X
2370 LET A1$(6,10)=V$
2390 GOTO 3900
2400 REM " --- Accrual Days
2420 LET V0$="Z",V2$=A1$(19,2),V0=2,V1=40,V2=7
2440 GOSUB 7000
2450 IF V>13 THEN GOTO 2400
2460 LET A1$(19,2)=V$
2490 GOTO 3900
3900 REM " --- Next Field
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2300,2100,2200,2400,4000
4000 REM " --- Correct?
4010 LET V0$="Y",V1$="C",V4$="Is The Above Information Correct (Y/N)?"
4020 LET V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4035 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 2000
4050 IF V$<>"Y" THEN GOTO 4000
4100 WRITE (SYS01_DEV1,KEY=A0$)IOL=PRS01B
4120 IF A1$(5,1)="Y" THEN RUN "PRU.BA"
4140 GOTO 9900
5000 REM " --- Display
5020 PRINT @(40,3),FNB$(A1$(1,3)),@(40,4),A1$(6,10),@(40,5),A1$(4,1),@(40,6),A
5020:1$(5,1),@(40,7),A1$(19,2),
5090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
