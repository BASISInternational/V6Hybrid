0010 REM "PRX - Conversion Entry"
0020 REM "Program PRX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4000
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-10",FILES$[3]="PRT-01"
0130 LET FILES$[4]="PRT-11",FILES$[5]="PRT-21",FILES$[6]="PRT-31"
0135 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM10_DEV=CHANNELS[2],PRT01_DEV=CHANNELS[3]
0185 LET PRT11_DEV=CHANNELS[4],PRT21_DEV=CHANNELS[5],PRT31_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 PRM01A: IOLIST A0$(1),A1$(1)
0260 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0450 DIM P[3]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(5,1)),P[3]=NUM(P3$
0490:(7,1))
0500 REM " --- Init Data
0520 DIM A0$(11),A1$(230)
0530 LET K9=11,L0=6
0540 LET A0$(1,2)=N0$
0550 LET M1=LEN(M1$),M2=LEN(M2$)
0700 REM " --- Background
0720 CALL "SYC.WC",1,0,80,0,0,5,0
0740 PRINT 'SB',@(2,3),"Employee No:",@(4,4),"Last Name:",@(33),"First Name:",
0790 PRINT 'SF',
0900 REM " --- Init File
0920 READ (PRM01_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key
1010 GOSUB 6200
1020 LET V2$="",V2$=KEY(PRM01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="E",V3$=P1$,V0=9,V1=15,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$
1200 REM " --- Find Key
1220 FIND (PRM01_DEV,KEY=A0$(1,K9),DOM=1000)IOL=PRM01A
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
2000 REM " --- Overlay
2020 LET O0=1,O1=0,O9=0
2040 RUN "PRM.MB"
4000 REM " --- Options
4010 GOSUB 6000
4030 CALL "SYC.WC",1,0,80,0,0,5,0
4060 PRINT @(20,7),"1. Earnings Conversion",@(20,8),"2. Deduction Conversion",
4060:@(20,9),"3. Tax Conversion",@(20,10),"4. Contribution Conversion",
4100 SETERR 9000
4110 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Enter Your Option:",V0=1,V1=53,V2
4110:=22
4115 GOSUB 7000
4120 IF V$="" OR V3=4 THEN GOTO 1000
4140 LET I=NUM(V$,ERR=4100)
4150 IF I<1 OR I>4 THEN GOTO 4100
4160 LET O0=1,O1=1,O9=0
4200 ON I GOTO 4100,4210,4220,4230,4240
4210 RUN "PRX.AB"
4220 RUN "PRX.AC"
4230 RUN "PRX.AD"
4240 RUN "PRX.AE"
5000 REM " --- Display
5020 PRINT 'SB',@(15,3),FNF$(A0$(3,P[0]),M0$),@(15,4),A1$(1,16),@(45,4),A1$(17
5020:,14),'SF',
5090 RETURN
6000 REM " --- Clear Screen
6020 PRINT @(0,L0),'CE',
6090 RETURN
6200 REM " --- Clear Bkg
6220 PRINT 'CF',@(15,4),FILL(17),@(45,4),'CL',
6290 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
