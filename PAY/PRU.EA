0010 REM "PRU - Year End Update"
0020 REM "Program PRU.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=12
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-02",FILES$[3]="PRM-03"
0130 LET FILES$[4]="PRM-10",FILES$[5]="PRM-12",FILES$[6]="PRT-01"
0135 LET FILES$[7]="PRT-02",FILES$[8]="PRT-05",FILES$[9]="PRT-11"
0140 LET FILES$[10]="PRT-21",FILES$[11]="PRT-31",FILES$[12]="SYS-01"
0150 LET OPTIONS$[6]="L",OPTIONS$[9]="L",OPTIONS$[10]="L",OPTIONS$[11]="L"
0160 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM02_DEV=CHANNELS[2],PRM03_DEV=CHANNELS[3]
0185 LET PRM10_DEV=CHANNELS[4],PRM12_DEV=CHANNELS[5],PRT01_DEV=CHANNELS[6]
0190 LET PRT02_DEV=CHANNELS[7],PRT05_DEV=CHANNELS[8],PRT11_DEV=CHANNELS[9]
0195 LET PRT21_DEV=CHANNELS[10],PRT31_DEV=CHANNELS[11],SYS01_DEV=CHANNELS[12]
0200 REM " --- IOLists"
0210 PRM10A: IOLIST X0$
0220 PRT02A: IOLIST B0$
0230 PRT05A: IOLIST C0$,C[ALL]
0250 PRM01A: IOLIST X0$,X1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0280 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0310 PRT01A: IOLIST Y0$(1),Y[ALL],Z[ALL]
0320 PRT11A: IOLIST Y0$(1),Y[ALL],Z[ALL]
0330 PRT21A: IOLIST Y0$(1),Y[ALL],Z[ALL]
0340 PRT31A: IOLIST Y0$(1),Y[ALL],Z[ALL]
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM G[0],P[4]
0460 LET G[0]=NUM(G2$(1,2))
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=FNYY_YEAR(P4$(3,2)),P[3]=N
0470:UM(P3$(7,1)),P[4]=NUM(P4$(1,2))
0500 REM " --- Init Data"
0510 PRECISION P[3]
0520 LET M0=LEN(M0$)
0600 REM " --- Determine Calendar Year"
0605 CALL "SYC.PA",SYS01_DEV,P[4],P[2],V0$,V9$,STATUS
0610 LET P8$=V9$; REM "Current period end date
0615 LET P[4]=P[4]-1
0620 IF P[4]=0 THEN LET P[4]=G[0],P[2]=P[2]-1
0625 CALL "SYC.PA",SYS01_DEV,P[4],P[2],V0$,V9$,STATUS
0630 LET P9$=V9$; REM "Previous period end date
0635 IF P9$(2)<=P8$(2) THEN GOTO 0650
0640 LET P9$=P9$(1,1)
0645 GOTO 0700
0650 REM " --- December Not Closed"
0660 DIM MESSAGE$[1]
0670 LET MESSAGE$[0]="December Has Not Been Closed (<Enter>=Exit)"
0680 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
0690 GOTO 9900
0700 REM " --- Background"
0720 FOR X=4 TO 6 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(18,3),"Information Will Be Cleared For The Year: "+STR(1900+
0760:FNYY_YEAR(FNA$(P9$,"00")):"0000"),'SF',
0800 REM " --- Correct?"
0810 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0820 ON V3 GOTO 0830,0800,0800,0800,9900
0830 ON POS(V$="YN") GOTO 0800,0840,0800
0850 REM " --- Disallow 'M'enu option in Error Routine
0860 LET EXIT_CTRL=1
0900 REM " --- Get Accrual Codes"
0905 PRINT @(0,10),'CE',
0910 CALL "SYC.NB","Accrual Codes",0,COLUMN
0920 READ (PRM10_DEV,KEY=N0$+"A",DOM=0930)
0930 LET K$=KEY(PRM10_DEV,END=1000)
0940 IF K$(1,3)<>N0$+"A" THEN GOTO 1000
0950 READ (PRM10_DEV)IOL=PRM10A
0960 IF X0$(26,1)="N" THEN GOTO 0930
0970 IF X0$(27,1)<>"C" THEN GOTO 0930
0980 LET ACC_CODE$=ACC_CODE$+X0$(4,2)
0990 GOTO 0930
1000 REM " --- Earnings"
1005 GOSUB 6000; REM "Clear terminated employees
1010 PRINT @(0,10),'CE',
1020 CALL "SYC.NB","Employee Earnings",M0+3,COLUMN
1030 DIM Y0$(14),Y[11],Z[4]
1040 READ (PRT01_DEV,KEY=N0$,DOM=1050)
1050 LET K0$=KEY(PRT01_DEV,END=1200)
1060 IF K0$(1,2)<>N0$ THEN GOTO 1200
1080 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1100 READ (PRT01_DEV)IOL=PRT01A
1120 IF POS(Y0$(13,2)=ACC_CODE$,2)=0 THEN GOTO 1150
1130 LET Z[0]=Z[0]+Z[1]-Z[4],Z[1]=Z[2],Z[2]=0,Z[4]=0
1150 FOR X=0 TO 1
1160 LET Y[X*6+1]=0,Y[X*6+2]=0,Y[X*6+3]=0,Y[X*6+4]=0
1170 NEXT X
1180 WRITE (PRT01_DEV,KEY=K0$)IOL=PRT01A
1190 GOTO 1050
1200 REM " --- Deductions"
1210 PRINT @(0,10),'CE',
1220 CALL "SYC.NB","Employee Deductions",M0+3,COLUMN
1230 DIM Y0$(21),Y[5],Z[1]
1240 READ (PRT11_DEV,KEY=N0$,DOM=1250)
1250 LET K0$=KEY(PRT11_DEV,END=1400)
1260 IF K0$(1,2)<>N0$ THEN GOTO 1400
1280 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1300 READ (PRT11_DEV)IOL=PRT11A
1320 FOR X=0 TO 0
1330 LET Y[X*6+1]=0,Y[X*6+2]=0,Y[X*6+3]=0,Y[X*6+4]=0
1340 NEXT X
1360 WRITE (PRT11_DEV,KEY=K0$)IOL=PRT11A
1390 GOTO 1250
1400 REM " --- Taxes"
1410 PRINT @(0,10),'CE',
1420 CALL "SYC.NB","Employee Taxes",M0+3,COLUMN
1430 DIM Y0$(48),Y[17],Z[0]
1440 READ (PRT21_DEV,KEY=N0$,DOM=1450)
1450 LET K0$=KEY(PRT21_DEV,END=1600)
1460 IF K0$(1,2)<>N0$ THEN GOTO 1600
1480 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1500 READ (PRT21_DEV)IOL=PRT21A
1520 FOR X=0 TO 2
1530 LET Y[X*6+1]=0,Y[X*6+2]=0,Y[X*6+3]=0,Y[X*6+4]=0
1540 NEXT X
1560 WRITE (PRT21_DEV,KEY=K0$)IOL=PRT21A
1590 GOTO 1450
1600 REM " --- Contributions"
1610 PRINT @(0,10),'CE',
1620 CALL "SYC.NB","Employee Contributions",M0+3,COLUMN
1630 DIM Y0$(19),Y[17],Z[1]
1640 READ (PRT31_DEV,KEY=N0$,DOM=1650)
1650 LET K0$=KEY(PRT31_DEV,END=2000)
1660 IF K0$(1,2)<>N0$ THEN GOTO 2000
1680 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1700 READ (PRT31_DEV)IOL=PRT31A
1720 FOR X=0 TO 2
1730 LET Y[X*6+1]=0,Y[X*6+2]=0,Y[X*6+3]=0,Y[X*6+4]=0
1740 NEXT X
1760 WRITE (PRT31_DEV,KEY=K0$)IOL=PRT31A
1790 GOTO 1650
2000 REM " --- Clear Checks"
2010 PRINT @(0,10),'CE',
2020 CALL "SYC.NB","Examining Check",7,COLUMN
2030 READ (PRT02_DEV,KEY=N0$,DOM=2040)
2100 REM " --- Main Check Read"
2120 LET K0$=KEY(PRT02_DEV,END=3000)
2140 IF K0$(1,2)<>N0$ THEN GOTO 3000
2160 READ (PRT02_DEV)IOL=PRT02A
2180 PRINT @(COLUMN,11),B0$(12,7),
2200 REM " --- Keep Or Remove"
2220 IF B0$(19,1)>P9$ THEN GOTO 2100
2800 REM " --- Remove Check"
2820 REMOVE (PRT02_DEV,KEY=K0$)
2900 GOTO 2100
3000 REM " --- Labor Dist"
3010 PRINT @(0,10),'CE',
3020 CALL "SYC.NB","Updating Department",7,COLUMN
3030 READ (PRT05_DEV,KEY=N0$,DOM=3040)
3100 REM " --- Main Labor Dist Read"
3105 DIM C[11]
3110 LET Z9$=KEY(PRT05_DEV,END=3400)
3120 READ (PRT05_DEV,END=3400)IOL=PRT05A
3130 LET C0$=Z9$
3140 IF POS(N0$=C0$)<>1 THEN GOTO 3400
3150 PRINT @(COLUMN,11),C0$(3,4),
3160 FIND (PRM01_DEV,KEY=N0$+C0$(7,9),DOM=3300)
3170 FOR X0=0 TO 1
3180 LET C[6*X0+1]=0,C[6*X0+2]=0,C[6*X0+3]=0,C[6*X0+4]=0
3190 NEXT X0
3200 FOR X0=0 TO 11
3220 IF C[X0]<>0 THEN EXITTO 3280
3230 NEXT X0
3240 GOTO 3300
3280 WRITE (PRT05_DEV,KEY=C0$)IOL=PRT05A
3290 GOTO 3100
3300 REMOVE (PRT05_DEV,KEY=C0$,DOM=3310)
3390 GOTO 3100
4000 REM " --- Done"
4020 GOTO 9900
6000 REM " --- Clear Terminated"
6005 PRINT @(0,10),'CE',
6010 CALL "SYC.NB","Terminating",M0,COLUMN
6020 READ (PRM01_DEV,KEY=N0$,DOM=6030)
6030 LET K$=KEY(PRM01_DEV,END=6490)
6040 IF K$(1,2)<>N0$ THEN GOTO 6490
6050 READ (PRM01_DEV)IOL=PRM01A
6060 IF X1$(170,3)="   " THEN GOTO 6030
6070 PRINT @(COLUMN,11),FNF$(X0$(3,P[0]),M0$),
6080 REMOVE (PRM03_DEV,KEY=X0$(1,2)+"A"+X1$(1,16)+X0$(3,9),DOM=6090)
6090 REMOVE (PRM03_DEV,KEY=X0$(1,2)+"B"+X1$(145,10)+X0$(3,9),DOM=6100)
6100 REM " --- Remove Pay Rate Detail"
6110 READ (PRM02_DEV,KEY=K$,DOM=6120)
6120 LET K1$=KEY(PRM02_DEV,END=6150)
6125 IF POS(K$=K1$)<>1 THEN GOTO 6150
6130 REMOVE (PRM02_DEV,KEY=K1$)
6140 GOTO 6120
6150 REM " --- Remove Pay Change Detail"
6160 READ (PRM12_DEV,KEY=K$,DOM=6170)
6170 LET K1$=KEY(PRM12_DEV,END=6200)
6175 IF POS(K$=K1$)<>1 THEN GOTO 6200
6180 REMOVE (PRM12_DEV,KEY=K1$)
6190 GOTO 6170
6200 REM " --- Remove Earnings History"
6210 READ (PRT01_DEV,KEY=K$,DOM=6220)
6220 LET K1$=KEY(PRT01_DEV,END=6250)
6225 IF POS(K$=K1$)<>1 THEN GOTO 6250
6230 REMOVE (PRT01_DEV,KEY=K1$)
6240 GOTO 6220
6250 REM " --- Remove Deduction History"
6260 READ (PRT11_DEV,KEY=K$,DOM=6270)
6270 LET K1$=KEY(PRT11_DEV,END=6300)
6275 IF POS(K$=K1$)<>1 THEN GOTO 6300
6280 REMOVE (PRT11_DEV,KEY=K1$)
6290 GOTO 6270
6300 REM " --- Remove Tax History"
6310 READ (PRT21_DEV,KEY=K$,DOM=6320)
6320 LET K1$=KEY(PRT21_DEV,END=6350)
6325 IF POS(K$=K1$)<>1 THEN GOTO 6350
6330 REMOVE (PRT21_DEV,KEY=K1$)
6340 GOTO 6320
6350 REM " --- Remove Contribuion History"
6360 READ (PRT31_DEV,KEY=K$,DOM=6370)
6370 LET K1$=KEY(PRT31_DEV,END=6400)
6375 IF POS(K$=K1$)<>1 THEN GOTO 6400
6380 REMOVE (PRT31_DEV,KEY=K1$)
6390 GOTO 6370
6400 REM " --- Remove Employee Master"
6420 REMOVE (PRM01_DEV,KEY=K$)
6440 GOTO 6030
6490 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
