0010 REM "PRR - Employee Maintenance Listing 3"
0020 REM "Program PRR.ZC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 PRM02A: IOLIST B0$(1),B[ALL]
0220 PRM10A: IOLIST X$
0225 PRM10B: IOLIST X$
0230 PRM10C: IOLIST X$
0235 PRM10D: IOLIST X$
0240 PRM10F: IOLIST X$
0250 PRM12A: IOLIST C0$(1),C[ALL]
0260 PRT01A: IOLIST E0$(1),E[ALL]
0270 PRT11A: IOLIST E0$(1),E[ALL]
0280 PRT21A: IOLIST E0$(1),E[ALL]
0290 PRT31A: IOLIST E0$(1),E[ALL]
0500 REM " --- Init Data"
0510 LET L9=59,L=L9+1
0520 DIM B0$(13),B[1],C0$(17),C[1]
0530 LET M2=LEN(M2$),M3=LEN(M3$)
0540 LET Q8$=M2$,Q9$="######.00-",Q9=LEN(Q9$)
0550 CALL "SYC.RM",Q8$,Q8,10,10
1000 REM " --- Print Name & Address"
1010 GOSUB 5000
1020 LET V$="Employee Information"; GOSUB 5400
1040 PRINT (7)@(5),"Address1: ",A1$(31,24),@(41),"Alt Sequence: ",A1$(145,10),
1040:'LF',@(5),"Address2: ",A1$(55,24),@(50),"Sex: ",A1$(155,1),'LF',@(3),"Cit
1040:y/State: ",A1$(79,24),@(49),"Race: ",A1$(156,1),'LF',@(3),"Zip/Postal: ",
1040:FNZ$(A1$(103)),@(50),"M/S: ",A1$(157,1),'LF',@(3),"Home Phone: ",FNF$(A1$
1040:(112,10),M8$),@(43),"Birth Date: ",FNB$(A1$(158,3)),'LF',@(3),"Work Phone
1040:: ",FNF$(A1$(122,10),M8$),@(30),"Ext: ",A1$(132,4),@(44),"Hire Date: ",FN
1040:B$(A1$(161,3)),'LF',@(3),"Soc-Sec-No: ",FNF$(A1$(136,9),M9$),@(43),"Anniv
1040: Date: ",FNB$(A1$(164,3)),'LF',@(42),"Review Date: ",FNB$(A1$(167,3)),'LF
1040:',@(0),"Emerg Contact: ",A1$(176,24),@(44),"Term Date: ",FNB$(A1$(170,3))
1040:,'LF',@(5),"Relation: ",A1$(200,10),@(44),"Term Code: ",A1$(173,3),'LF',@
1040:(2),"Emerg Phone: ",FNF$(A1$(210,10),M8$),@(36),"Normal Pay Period: ",A1$
1040:(220,1),'LF',@(9),"Memo: ",A1$(227,10),@(38),"Home Department: ",A1$(221,
1040:2)
1050 IF P3$(9,1)="Y" THEN PRINT (7)@(3),"Union Code: ",A1$(238,3),
1060 PRINT (7)@(41),"Normal Title: ",A1$(223,2),'LF',@(38),"Hourly/Salaried: "
1060:,A1$(225,1),'LF',@(41),"Pension Plan: ",A1$(226,1),'LF',@(39),"Full/Part 
1060:Time: ",A1$(237,1),'LF'
1080 LET L=L+17
1200 REM " --- Pay Rate"
1210 LET V$="Pay Rate Maintenance"; GOSUB 5400
1220 PRINT (7)@(5),"Title ID:",@(25+M3),"Rate",@(24+M3+M2),"Hours"
1230 READ (PRM02_DEV,KEY=A0$,DOM=1240)
1240 LET K$=KEY(PRM02_DEV,END=1300)
1245 IF POS(A0$=K$)<>1 THEN GOTO 1300
1250 READ (PRM02_DEV)IOL=PRM02A
1255 LET X$="**Not On File**"
1260 FIND (PRM10_DEV,KEY=N0$+"F"+B0$(12,2),DOM=1280)IOL=PRM10F
1270 LET X$=X$(6,20)
1280 PRINT (7)@(5),B0$(12,2)," ",X$,@(30),B[0]:M3$,B[1]:M2$
1285 LET L=L+1
1290 GOTO 1240
1300 PRINT (7)
1310 LET L=L+1
1400 REM " --- Pay Change"
1410 LET V$="Pay Change Maintenance"; GOSUB 5400
1420 PRINT (7)@(12),"Date",@(25+M3),"Rate",@(24+M3+M2),"Hours"
1430 READ (PRM12_DEV,KEY=A0$,DOM=1440)
1440 LET K$=KEY(PRM12_DEV,END=1500)
1445 IF POS(A0$=K$)<>1 THEN GOTO 1500
1450 READ (PRM12_DEV)IOL=PRM12A
1480 PRINT (7)@(10),FNB6$(C0$(12,6)),@(30),C[0]:M3$,C[1]:M2$
1485 LET L=L+1
1490 GOTO 1440
1500 PRINT (7)
1510 LET L=L+1
1600 REM " --- Pay Codes"
1605 LET V$="Pay Code Maintenance"; GOSUB 5400
1610 READ (PRT01_DEV,KEY=A0$+"A",DOM=1620)
1620 LET K$=KEY(PRT01_DEV,END=1800)
1630 IF POS(A0$+"A"=K$)<>1 THEN GOTO 1800
1640 DIM E0$(14),E[16]
1660 READ (PRT01_DEV)IOL=PRT01A
1670 LET X$="**Not On File**"
1680 FIND (PRM10_DEV,KEY=N0$+"A"+E0$(13,2),DOM=1700)IOL=PRM10A
1690 LET X$=X$(6,16)
1700 IF L+7<=L9 THEN GOTO 1730
1710 GOSUB 5000
1720 LET V$="Pay Code Maintenance"; GOSUB 5400
1730 PRINT (7)@(0),"Pay Code: ",E0$(13,2)," ",X$,'LF',@(10+Q9-6),"M-T-D",@(10+
1730:2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(10+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Q
1730:tr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),"Next Mo"
1740 PRINT (7)@(1),"Dollars:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q
1740:9$,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$
1750 PRINT (7)@(3),"Units:",@(10),E[6]:Q8$,E[7]:Q8$,E[8]:Q8$,E[9]:Q8$,E[10]:Q8
1750:$,E[7]+E[8]+E[9]+E[10]:Q8$,E[11]:Q8$
1760 PRINT (7)@(10+Q9-6),"Prior",@(10+2*Q9-8),"Current",@(10+3*Q9-5),"Next",@(
1760:10+3*Q9+M3-5),"Rate",@(10+4*Q9+M3-5),"Used"
1770 PRINT (7)@(0),"Accruals:",@(10),E[12]:Q9$,E[13]:Q9$,E[14]:Q9$,E[15]:M3$,E
1770:[16]:Q9$,'LF'
1780 LET L=L+7
1790 GOTO 1620
1800 REM " --- Deduction Codes"
1805 LET V$="Deduction Code Maintenance"; GOSUB 5400
1810 READ (PRT11_DEV,KEY=A0$+"B",DOM=1820)
1820 LET K$=KEY(PRT11_DEV,END=2000)
1830 IF POS(A0$+"B"=K$)<>1 THEN GOTO 2000
1840 DIM E0$(20),E[7]
1860 READ (PRT11_DEV)IOL=PRT11A
1870 LET X$="**Not On File**"
1880 FIND (PRM10_DEV,KEY=N0$+"B"+E0$(13,2),DOM=1900)IOL=PRM10B
1890 LET X$=X$(6,16)
1900 IF L+4<=L9 THEN GOTO 1930
1910 GOSUB 5000
1920 LET V$="Deduction Code Maintenance"; GOSUB 5400
1930 PRINT (7)@(0),"Ded Code: ",E0$(13,2)," ",X$," Per: ",E0$(15,5)," Auto: ",
1930:E0$(20,1)," Rate:",E[6]:M3$,"Lmt:",E[7]:Q9$
1935 PRINT (7)@(10+Q9-6),"M-T-D",@(10+2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(1
1935:0+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Qtr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),
1935:"Next Mo"
1940 PRINT (7)@(1),"Dollars:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q
1940:9$,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$,'LF'
1980 LET L=L+4
1990 GOTO 1820
2000 REM " --- Tax Codes"
2005 LET V$="Tax Code Maintenance"; GOSUB 5400
2010 READ (PRT21_DEV,KEY=A0$+"C",DOM=2020)
2020 LET K$=KEY(PRT21_DEV,END=2200)
2030 IF POS(A0$+"C"=K$)<>1 THEN GOTO 2200
2040 DIM E0$(48),E[18]
2060 READ (PRT21_DEV)IOL=PRT21A
2070 LET X$="**Not On File**"
2080 FIND (PRM10_DEV,KEY=N0$+"C"+E0$(13,2),DOM=2100)IOL=PRM10C
2090 LET X$=X$(6,16)
2100 IF L+7<=L9 THEN GOTO 2130
2110 GOSUB 5000
2120 LET V$="Tax Code Maintenance"; GOSUB 5400
2130 PRINT (7)@(0),"Tax Code: ",E0$(13,2)," ",X$," Table: ",E0$(15,3),"  Std D
2130:ed: ",E0$(18,2),"  Item Ded: ",E0$(20,2),'LF',@(28),"Add/Rpl: ",E0$(22,1)
2130:,"    Pct/Amt: ",E0$(23,1),"       Rate:",E[18]:M3$
2135 PRINT (7)@(10+Q9-6),"M-T-D",@(10+2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(1
2135:0+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Qtr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),
2135:"Next Mo"
2140 PRINT (7)@(3),"Gross:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q9$
2140:,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$
2150 PRINT (7)@(1),"Taxable:",@(10),E[6]:Q9$,E[7]:Q9$,E[8]:Q9$,E[9]:Q9$,E[10]:
2150:Q9$,E[7]+E[8]+E[9]+E[10]:Q9$,E[11]:Q9$
2160 PRINT (7)@(5),"Tax:",@(10),E[12]:Q9$,E[13]:Q9$,E[14]:Q9$,E[15]:Q9$,E[16]:
2160:Q9$,E[13]+E[14]+E[15]+E[16]:Q9$,E[17]:Q9$,'LF'
2180 LET L=L+7
2190 GOTO 2020
2200 REM " --- Contribution Codes"
2205 LET V$="Contribution Code Maintenance"; GOSUB 5400
2210 READ (PRT31_DEV,KEY=A0$+"D",DOM=2220)
2220 LET K$=KEY(PRT31_DEV,END=2400)
2230 IF POS(A0$+"D"=K$)<>1 THEN GOTO 2400
2240 DIM E0$(19),E[19]
2260 READ (PRT31_DEV)IOL=PRT31A
2270 LET X$="**Not On File**"
2280 FIND (PRM10_DEV,KEY=N0$+"D"+E0$(13,2),DOM=2300)IOL=PRM10D
2290 LET X$=X$(6,16)
2300 IF L+6<=L9 THEN GOTO 2330
2310 GOSUB 5000
2320 LET V$="Contribution Code Maintenance"; GOSUB 5400
2330 PRINT (7)@(1),"Cont Cd: ",E0$(13,2)," ",X$,"  Periods: ",E0$(15,5),"  Rat
2330:e: ",E[18]:M3$
2335 PRINT (7)@(10+Q9-6),"M-T-D",@(10+2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(1
2335:0+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Qtr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),
2335:"Next Mo"
2340 PRINT (7)@(3),"Gross:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q9$
2340:,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$
2350 PRINT (7)@(3),"Basis:",@(10),E[6]:Q9$,E[7]:Q9$,E[8]:Q9$,E[9]:Q9$,E[10]:Q9
2350:$,E[7]+E[8]+E[9]+E[10]:Q9$,E[11]:Q9$
2360 PRINT (7)@(1),"Contrib:",@(10),E[12]:Q9$,E[13]:Q9$,E[14]:Q9$,E[15]:Q9$,E[
2360:16]:Q9$,E[13]+E[14]+E[15]+E[16]:Q9$,E[17]:Q9$,'LF'
2380 LET L=L+6
2390 GOTO 2220
4000 REM " --- Next"
4020 LET O9=1
4100 RUN "PRR.ZB"
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5080 GOSUB 5200
5190 RETURN
5200 REM " --- Employee Subheading"
5220 PRINT (7)@(2),"Employee No: ",FNF$(A0$(3,P[0]),M0$),'LF',@(4),"Last Name:
5220: ",A1$(1,16),@(43),"First Name: ",A1$(17,14),'LF'
5280 LET L=L+4
5290 RETURN
5400 REM " --- Subtitle"
5420 PRINT (7)@(0),FILL(80,"-"),@(15),V$
5480 LET L=L+1
5490 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(AB
8085:S(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
