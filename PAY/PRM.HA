0010 REM "PRM - Check Code Maintenance"
0020 REM "Program PRM.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-10",FILES$[2]="PRM-10",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM10_DEV1=CHANNELS[1],PRM10_DEV2=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 PRM10A: IOLIST X0$
0220 PRM10HA: IOLIST X0$
0230 PRM10HF: IOLIST X0$
0240 PRM10HH: IOLIST X0$
0250 PRM10H: IOLIST A0$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Init Data
0520 DIM A0$(32)
0530 LET K0=1,K9=6
0540 LET A0$(1,3)=N0$+"H"
0550 LET M1=LEN(M1$),M2=LEN(M2$)
0700 REM " --- Background
0720 CALL "SYC.WC",1,0,80,0,0,8,0
0730 PRINT 'SB',@(11,3),"Report Type:",@(16,4),"Column:",@(11,5),"Description:
0730:",@(7,6),"Register Column:",@(2,7),"Check History Column:",
0790 PRINT 'SF',
0900 REM " --- Init File
0920 READ (PRM10_DEV1,KEY=A0$(1,3),DOM=0930)
1000 REM " --- Key
1010 PRINT @(14,4),'SB',"  Column:",'CF',
1020 LET V2$="",V2$=KEY(PRM10_DEV1,END=1045)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET A0$(5,2)=V2$(5,2),V2$=V2$(4,1)
1045 PRINT 'SF',@(3,11),"Valid Report Types:  A=Earnings",@(24,12),"B=Deductio
1045:ns",@(24,13),"C=Taxes",@(24,14),"D=Contributions",@(24,15),"E=Accruals",@
1045:(24,16),"F=Register",@(24,17),"G=Exception",@(24,18),"H=Check History",
1050 LET V0$="S",V1$="CEK",V3$="",V0=1,V1=24,V2=3,O9=0
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1080 IF V$="" THEN GOTO 0900
1085 IF POS(V$="ABCDEFGH")=0 THEN GOTO 1000
1090 GOSUB 5200
1095 LET A0$(4,1)=V$
1100 REM " --- Report Column
1110 LET V0$="Z",V1$="KC",V4$="01 Thru 07",V2$=A0$(5,2),V0=2,V1=24,V2=4
1115 IF A0$(4,1)<>"G" THEN GOTO 1130
1120 LET V0$="S",V2$="",V4$="Enter A Valid Pay Code"
1125 PRINT 'SB',@(14,4),"Pay Code:",'SF',
1130 GOSUB 7000
1132 IF V$="" THEN GOTO 0900
1135 IF V3=4 THEN GOTO 9900
1140 IF V3=2 THEN GOTO 1000
1142 IF A0$(4,1)="G" THEN GOTO 1150
1145 IF V$<"01" OR V$>"07" THEN GOTO 1100
1150 LET A0$(5,2)=V$
1160 IF A0$(4,1)<>"G" THEN GOTO 1220
1170 FIND (PRM10_DEV2,KEY=N0$+"A"+A0$(5,2),DOM=1200)IOL=PRM10A
1180 PRINT @(28,4),X0$(6,16),
1190 GOTO 1300
1200 DIM MESSAGE$[0]
1203 LET MESSAGE$[0]="*** "+A0$(5,2)+" Is An Invalid Pay Code ***  <Enter>: "
1205 IF A0$(4,1)="E" THEN LET MESSAGE$[0]="*** "+A0$(5,2)+" Is Not A Defined E
1205:arnings Report Column ***  <Enter>: "
1210 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
1215 GOTO 1100
1220 IF A0$(4,1)<>"E" THEN GOTO 1300
1230 FIND (PRM10_DEV2,KEY=N0$+"HA"+A0$(5,2),DOM=1200)IOL=PRM10HA
1240 PRINT @(27,4),X0$(7,10),
1300 REM " --- Find Key
1320 FIND (PRM10_DEV1,KEY=A0$(1,K9),DOM=1400)IOL=PRM10H
1340 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New Record
1420 CALL "SYC.YN",1,"Is This A New Record?",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup
1510 LET A0$(7)=""
2000 REM " --- Description
2020 LET V0$="S",V1$="C",V2$=A0$(7,10),V3$="",V4$="Enter Up To 10 Characters",
2020:V0=10,V1=24,V2=5,I0=0,O9=1
2040 GOSUB 7000
2060 LET A0$(7,10)=V$
2090 GOTO 3900
2100 REM " --- Reg Total
2110 IF POS(A0$(4,1)="ABC")=0 THEN GOTO 3900
2120 LET V0$="Z",V1$="",V2$=A0$(17,2),V4$="01 Thru 07",V0=2,V1=24,V2=6
2130 GOSUB 7000
2135 IF V3=2 THEN GOTO 3900
2140 LET A0$(17,2)=V$
2150 FIND (PRM10_DEV2,KEY=N0$+"HF"+A0$(17,2),DOM=2180)IOL=PRM10HF
2160 PRINT @(V0+V1+1,V2),X0$(7,10),
2170 GOTO 3900
2180 DIM MESSAGE$[0]
2185 LET MESSAGE$[0]="*** "+A0$(17,2)+" Is Not A Defined Register Column ***  
2185:<Enter>:"
2190 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
2195 GOTO 2100
2200 REM " --- Check History
2210 IF POS(A0$(4,1)="ABC")=0 THEN GOTO 3900
2220 LET V0$="Z",V1$="",V2$=A0$(19,2),V4$="01 Thru 07",V0=2,V1=24,V2=7
2230 GOSUB 7000
2235 IF V3=2 THEN GOTO 3900
2240 LET A0$(19,2)=V$
2250 FIND (PRM10_DEV2,KEY=N0$+"HH"+A0$(19,2),DOM=2280)IOL=PRM10HH
2260 PRINT @(V0+V1+1,V2),X0$(7,10),
2270 GOTO 3900
2280 DIM MESSAGE$[0]
2285 LET MESSAGE$[0]="*** "+A0$(19,2)+" Is Not A Defined Check History Column 
2285:***  <Enter>:"
2290 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
2292 GOSUB 7000
2295 GOTO 2200
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,3990
3990 LET I0=K0
4000 REM " --- Options
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write
4120 WRITE (PRM10_DEV1,KEY=A0$(1,K9))IOL=PRM10H
4190 GOTO 1000
4200 REM " --- Delete
4220 REMOVE (PRM10_DEV1,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print
4520 CALL "SYC.YN",1,"Do You Wish To Print A List Of All Records",1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4560 LET O0=1
4580 RUN "PRM.HB"
5000 REM " --- Display
5020 PRINT @(24,5),A0$(7,10),@(24,6),A0$(17,2),@(24,7),A0$(19,2),
5090 RETURN
5200 REM " --- Display Report Type
5210 PRINT @(27,3),
5220 ON POS(V$="ABCDEFGH") GOTO 5320,5230,5240,5250,5260,5270,5280,5290,5300
5230 PRINT "Earnings"; GOTO 5310
5240 PRINT "Deductions"; GOTO 5310
5250 PRINT "Taxes"; GOTO 5310
5260 PRINT "Contributions"; GOTO 5310
5270 PRINT "Accruals"; GOTO 5310
5280 PRINT "Register"; GOTO 5310
5290 PRINT "Exception"; GOTO 5310
5300 PRINT "Check History"; GOTO 5310
5310 PRINT @(0,11),'CE'
5320 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
