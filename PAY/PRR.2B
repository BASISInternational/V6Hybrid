0010 REM "PRR - Check History Report"
0020 REM "Program  PRR.2B"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0220 PRM01A: IOLIST B0$,B1$
0250 PRM10H: IOLIST X0$
0280 PRT02A: IOLIST A0$,A[ALL]
0600 REM " --- Additional Opens
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS=1 THEN GOTO 9900
0640 CALL "SYC.NB","Printing: ",LEN(M0$),COLUMN
0700 REM " --- Build Headings
0705 LET WIDTH=132,PAGE=0,HEADINGS=4,CLOCK$="",WHEN$=F0$(7,3)
0710 DIM HEADINGS$[HEADINGS]
0715 LET L9=59,L=L9+1,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0720 LET X0$="First Date",X1$="Last Date"
0730 IF FROMDATE$<>"" THEN LET X0$=FNB$(FROMDATE$)
0740 IF THRUDATE$<>"" THEN LET X1$=FNB$(THRUDATE$)
0750 LET H4$="From: "+X0$+" Thru: "+X1$
0760 LET X0$="First Employee",X1$="Last Employee"
0770 IF FROMEMPL$<>"" THEN LET X0$="Employee: "+FNF$(FROMEMPL$(1,P[0]),M0$)
0780 IF THRUEMPL$<>"" THEN LET X1$="Employee: "+FNF$(THRUEMPL$(1,P[0]),M0$)
0790 LET H5$="From: "+X0$+" Thru: "+X1$
0795 LET HEADINGS$[2]=H4$,HEADINGS$[3]=H5$
0800 REM " --- Build Column Headings from PRM-10H
0810 LET RPTHEADS$=""
0820 PRINT @(COLUMN,11),"Heading"
0830 READ (PRM10_DEV,KEY=N0$+"H",DOM=0831)
0840 LET K$=KEY(PRM10_DEV,END=0980)
0850 IF K$(1,3)<>N0$+"H" THEN GOTO 0980
0860 READ (PRM10_DEV)IOL=PRM10H
0890 IF K$(4,1)<>"H" THEN GOTO 0840
0900 LET X$=X0$(7,10)
0910 FOR X=1 TO 10
0920 IF X$(10,1)<>" " THEN EXITTO 0950
0930 LET X$=" "+X$(1,9)
0940 NEXT X
0950 LET RPTHEADS$=RPTHEADS$+X$
0960 GOTO 0840
0980 DIM X$(80)
0990 LET X$(1)=RPTHEADS$,X$(71)="     Total",RPTHEADS$=X$
1000 REM " --- Init Data
1020 READ (PRT02_DEV,KEY=N0$+FROMEMPL$,DOM=1030)
1060 PRINT @(COLUMN,11),FILL(LEN(M0$))
1100 REM " --- Main Read
1110 LET K$=KEY(PRT02_DEV,END=4000)
1120 IF K$(1,2)<>N0$ THEN GOTO 4000
1130 READ (PRT02_DEV)IOL=PRT02A
1140 IF THRUEMPL$<>"" THEN IF THRUEMPL$<A0$(3,LEN(THRUEMPL$)) THEN GOTO 4000
1150 REM " --- Breaks
1160 IF LEN(A0$)<22 THEN GOTO 1100
1170 IF FROMDATE$<>"" THEN IF A0$(19,3)<FROMDATE$ THEN GOTO 1100
1180 IF THRUDATE$<>"" THEN IF A0$(19,3)>THRUDATE$ THEN GOTO 1100
1190 IF K$(3,P[0])<>T0$ THEN GOSUB 6000
1200 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1600 REM " --- Print Heading
1620 IF L>L9 THEN GOSUB 5200
2000 REM " --- Add for Totals
2010 FOR I=0 TO 7
2020 LET EMPTOT[I]=EMPTOT[I]+A[I]
2030 LET GRANDTOT[I]=GRANDTOT[I]+A[I]
2040 NEXT I
2500 REM " --- Print
2510 IF L+1>L9 THEN GOSUB 5200
2520 PRINT (7)@(5),A0$(12,7),"  ",FNB$(A0$(19,3)),@(M0),A[1]:M1$,A[2]:M1$,A[3]
2520::M1$,A[4]:M1$,A[5]:M1$,A[6]:M1$,A[7]:M1$,A[0]:M1$
2540 LET L=L+1
2590 GOTO 1100
4000 REM " --- Totals
4020 LET T9$="E"
4040 GOSUB 6000
4060 PRINT (7)'LF',@(9),"Grand Totals:",@(M0),GRANDTOT[1]:M1$,GRANDTOT[2]:M1$,
4060:GRANDTOT[3]:M1$,GRANDTOT[4]:M1$,GRANDTOT[5]:M1$,GRANDTOT[6]:M1$,GRANDTOT[
4060:7]:M1$,GRANDTOT[0]:M1$
4100 GOTO 9900
5000 REM " --- Heading
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS<>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Employee Number & Name"
5110 PRINT (7)@(6),"Check     Date",
5150 FOR X=1 TO LEN(RPTHEADS$)/10
5160 PRINT (7)@(M0-11+M1*X),RPTHEADS$(10*(X-1)+1,10),
5170 NEXT X
5180 PRINT (7)'LF',
5185 LET L=L+3
5190 RETURN
5200 REM " --- Sub Heading
5220 IF L+3>L9 THEN GOSUB 5000
5240 PRINT (7)'LF',@(0),FNF$(A0$(3,P[0]),M0$)," ",X1$
5280 LET L=L+2
5290 RETURN
6000 REM " --- Emp Break
6020 IF T0$="" THEN GOTO 6100
6030 IF P[0]=0 THEN GOTO 6100
6035 IF L+2>L9 THEN GOSUB 5200
6040 PRINT (7)'LF',@(11),"Emp Totals:",@(M0),EMPTOT[1]:M1$,EMPTOT[2]:M1$,EMPTO
6040:T[3]:M1$,EMPTOT[4]:M1$,EMPTOT[5]:M1$,EMPTOT[6]:M1$,EMPTOT[7]:M1$,EMPTOT[0
6040:]:M1$
6070 LET L=L+2
6100 IF T9$="E" THEN RETURN
6120 DIM EMPTOT[7]
6140 LET T0$=K$(3,P[0])
6150 IF P[0]=0 THEN RETURN
6160 REM " --- Get Name
6165 LET X1$="**** Not On File ****"
6167 DIM B1$(250)
6170 FIND (PRM01_DEV,KEY=A0$(1,11),DOM=1470)IOL=PRM01A
6172 LET Q=POS("  "=B1$(1,16))-1
6174 IF Q<1 THEN LET Q=16
6176 LET X1$=B1$(1,Q)+", "+B1$(17,1)+"."
6180 GOSUB 5200
6230 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
