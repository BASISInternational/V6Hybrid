0010 REM "PRR - Earnings History Report 2"
0020 REM "Program PRR.WB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS=1 THEN GOTO 9900
0200 REM " --- IOLists"
0210 PRM01A: IOLIST A0$,A1$
0220 PRM10A: IOLIST X0$
0230 PRM10E: IOLIST H3$
0240 PRT01A: IOLIST B0$,B[ALL]
0500 REM " --- Init Data"
0510 LET L9=59,L=L9+1,HEADINGS=3,WIDTH=132,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$
0540 IF P9$="M" THEN LET HEADINGS$[2]="Month-To-Date" ELSE IF P9$="Q" THEN LET
0540: HEADINGS$[2]="Quarter "+P8$ ELSE IF P9$="Y" THEN LET HEADINGS$[2]="Year-
0540:To-Date Thru Quarter "+P8$
0550 DIM B[16],R[7],S[7],T[7],U[7],W[2],X[7],Y[7]
0560 CALL "SYC.RM",M1$,M1,8,13
0570 LET M0=20,M9$="000-00-0000"
0800 REM " --- Build Headings"
0810 CALL "SYC.NB","Creating Headings",0,COLUMN
0820 LET U0$="",U1$=""
0850 READ (PRM10_DEV,KEY=N0$+"H"+"A",DOM=0860)
0860 LET K$=KEY(PRM10_DEV,END=0980)
0870 IF K$(1,4)<>N0$+"H"+"A" THEN GOTO 0980
0880 READ (PRM10_DEV)IOL=PRM10A
0890 LET U0$=U0$+X0$(5,2),X$=X0$(7,10)
0900 FOR X=1 TO 10
0910 IF X$(10,1)<>" " THEN EXITTO 0950
0920 LET X$=" "+X$(1,9)
0930 NEXT X
0950 LET U1$=U1$+X$
0960 GOTO 0860
0980 DIM X$(80)
0990 LET X$(1)=U1$,X$(61)=" Other Pay Total Pay",U1$=X$
1000 REM " --- Init Read"
1010 PRINT @(0,10),'CE',
1020 CALL "SYC.NB","Printing",LEN(M0$),COLUMN
1040 READ (PRM01_DEV,KEY=N0$,DOM=1050)
1100 REM " --- Main Read"
1120 READ (PRM01_DEV,END=4000)IOL=PRM01A
1130 IF A0$(1,2)<>N0$ THEN GOTO 4000
1140 LET Q=POS("  "=A1$(1,16))-1
1150 IF Q<1 THEN LET Q=16
1160 LET X1$=A1$(1,Q)+", "+A1$(17,1)+"."
1180 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1500 REM " --- Breaks"
1520 IF A0$(3,P[3])<>T0$ THEN GOSUB 6000
1600 REM " --- Print Heading"
1620 IF L+4>L9 THEN GOSUB 5000
1640 PRINT (7)'LF',@(0),FNF$(A0$(3,P[0]),M0$),
2000 REM " --- Detail"
2040 DIM X[7],Y[7]
2060 READ (PRT01_DEV,KEY=A0$(1,11)+"A",DOM=2070)
2100 LET K$=KEY(PRT01_DEV,END=2500)
2120 IF K$(1,12)<>A0$(1,11)+"A" THEN GOTO 2500
2140 READ (PRT01_DEV)IOL=PRT01A
2160 FIND (PRM10_DEV,KEY=N0$+"A"+B0$(13,2),DOM=2100)IOL=PRM10A
2180 LET X=POS(X0$(22,2)=U0$,2)
2200 IF X>0 THEN LET X=INT(X/2)+1
2210 IF X>6 THEN LET X=0
2300 REM " --- Dollars/Units"
2310 IF P9$="M" THEN LET W[1]=B[6],W[2]=B[0]
2320 IF P9$="Q" THEN LET W[1]=B[P8+6],W[2]=B[P8]
2340 IF P9$<>"Y" THEN GOTO 2400
2350 LET W[1]=0,W[2]=0
2360 FOR W0=1 TO P8
2370 LET W[1]=W[1]+B[6+W0],W[2]=W[2]+B[W0]
2380 NEXT W0
2420 LET X[X]=X[X]+W[2],Y[X]=Y[X]+W[1],T[X]=T[X]+W[2],U[X]=U[X]+W[1],R[X]=R[X]
2420:+W[2],S[X]=S[X]+W[1]
2440 LET X[7]=X[7]+W[2],Y[7]=Y[7]+W[1],T[7]=T[7]+W[2],U[7]=U[7]+W[1],R[7]=R[7]
2440:+W[2],S[7]=S[7]+W[1]
2490 GOTO 2100
2500 REM " --- Print"
2502 FOR F=1 TO 6
2504 IF X[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),X[F]:M1$,
2506 NEXT F
2508 IF X[0]<>0 THEN PRINT (7)@(M0+6*M1),X[0]:M1$,
2510 PRINT (7)@(M0+7*M1),X[7]:M1$
2512 PRINT (7)@(2),X1$,
2514 FOR F=1 TO 6
2516 IF Y[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),Y[F]:M1$,
2518 NEXT F
2520 IF Y[0]<>0 THEN PRINT (7)@(M0+6*M1),Y[0]:M1$,
2522 PRINT (7)@(M0+7*M1),Y[7]:M1$
2560 PRINT (7)@(2),"(",FNF$(A1$(136,9),M9$),")"
2570 LET L=L+4
2590 GOTO 1100
4000 REM " --- Totals"
4020 LET T9$="E"
4040 GOSUB 6000
4050 IF L+2>L9 THEN GOSUB 5000
4060 PRINT (7)'LF',@(5),"Grand Totals:",@(M0),T[1]:M1$,T[2]:M1$,T[3]:M1$,T[4]:
4060:M1$,T[5]:M1$,T[6]:M1$,T[0]:M1$,T[7]:M1$
4080 PRINT (7)@(M0),U[1]:M1$,U[2]:M1$,U[3]:M1$,U[4]:M1$,U[5]:M1$,U[6]:M1$,U[0]
4080::M1$,U[7]:M1$
4100 REM " --- Done"
4120 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Employee",
5150 FOR X=1 TO LEN(U1$)/10
5160 PRINT (7)@(M0-11+M1*X),U1$(10*(X-1)+1,10),
5170 NEXT X
5172 PRINT (7)'LF',@(0),"Number & Name",
5174 FOR X=1 TO LEN(U1$)/10
5176 IF U1$(10*(X-1)+1,10)="          " THEN GOTO 5180
5178 PRINT (7)@(M0-7+M1*X),"$/Hrs",
5180 NEXT X
5185 PRINT (7)'LF',
5190 LET L=L+2
5200 REM " --- Sub Heading"
5210 IF P[3]=0 THEN RETURN
5230 IF L+5>L9 THEN GOTO 5000
5240 PRINT (7)'LF',@(0),T0$," ",H3$
5280 LET L=L+2
5290 RETURN
6000 REM " --- Dept Break"
6010 IF T0$="" THEN GOTO 6100
6020 IF P[3]=0 THEN GOTO 6100
6030 IF L+3>L9 THEN GOSUB 5000
6040 PRINT (7)'LF',@(5),"Dept Totals:",@(M0),R[1]:M1$,R[2]:M1$,R[3]:M1$,R[4]:M
6040:1$,R[5]:M1$,R[6]:M1$,R[0]:M1$,R[7]:M1$
6060 PRINT (7)@(M0),S[1]:M1$,S[2]:M1$,S[3]:M1$,S[4]:M1$,S[5]:M1$,S[6]:M1$,S[0]
6060::M1$,S[7]:M1$
6070 LET L=L+3
6100 IF T9$="E" THEN RETURN
6120 DIM R[7],S[7]
6140 LET T0$=A0$(3,P[3])
6150 IF P[3]=0 THEN RETURN
6160 LET H3$=""
6170 FIND (PRM10_DEV,KEY=N0$+"E"+A1$(221,2),DOM=6200)IOL=PRM10E
6180 LET H3$=H3$(6,20)
6200 GOSUB 5200
6290 RETURN
8000 REM " --- Functions"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
