0010 REM "PRE - Daily Entry 2"
0020 REM "Program PRE.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 PRE02A: IOLIST A0$,A1$(1),A[ALL]
0220 PRM02A: IOLIST C0$,C[ALL]
0230 PRM10A: IOLIST X0$,X[ALL]
0240 PRM10E: IOLIST R0$,R1$,R2$(1),R3$,R4$(1),R5$,R6$(1)
0250 PRM10F: IOLIST X$,A[0]
0500 REM " --- Init Data"
0510 PRECISION P[3]
0520 DIM A0$(32),A1$(12),A[2],C[1],X[3],U[2]
0530 LET A0$(1)=B0$+"000"+F0$(7,3)
0540 LET T0=0,T4=0
0550 LET L1=10,L9=20,L=L1
0560 GOSUB 6300
0800 REM " --- Options"
0900 LET U0=1
0920 GOSUB 5100
0940 LET U0=0
1000 REM " --- Overflow"
1010 PRINT @(0,21),'CL',
1020 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L1),'LD',
1040 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 DIM U[2]
1120 LET K$=KEY(PRE02_DEV,END=1300)
1130 IF LEN(K$)<11 THEN GOTO 1300
1140 IF K$(1,11)<>A0$(1,11) THEN GOTO 1300
1150 READ (PRE02_DEV)IOL=PRE02A
1160 GOTO 1490
1300 LET W0=NUM(A0$(12,3))+1
1320 IF W0>999 THEN LET W0=1
1340 LET A0$(12,3)=STR(W0:"000")
1350 PRINT @(0,L),A0$(12,3),
1390 GOTO 1480
1400 REM " --- Seq"
1420 LET V0$="Z",V1$="KER",V2$=A0$(12,3),V3$="",V0=3,V1=0,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(0,L),"   "; GOTO 4000
1450 IF V$="00" THEN GOTO 1400
1460 LET A0$(12,3)=V$
1480 FIND (PRE02_DEV,KEY=A0$(1,14),DOM=1600)IOL=PRE02A
1490 IF A0$(1,14)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1510 LET U[1]=-A[1],U[2]=-A[2],U[0]=-A[0]
1520 LET I0=7
1530 GOTO 3300
1600 REM " --- Setup"
1620 DIM A[2],U[2]
1630 LET A0$(18)="",A0$(20)=B1$(223,2)+B1$(221,2)
1800 REM " --- Date"
1820 LET V0$="D",V1$="R",V2$=FNC$(A0$(15,3)),V0=8,V1=4,V2=L
1840 GOSUB 7000
1850 IF V3=4 THEN GOTO 4000
1855 IF V3=2 THEN GOTO 1400
1860 LET A0$(15,3)=V$
2000 REM " --- Pay Code
2020 LET V0$="S",V1$="KCRE",V2$=A0$(18,2),V4$="Enter A Valid Earning Code, <F3
2020:>=Code Lookup",V0=2,V1=13,V2=L
2040 GOSUB 7000
2041 IF V3=2 THEN GOTO 1800
2042 IF V3<>3 THEN GOTO 2050
2046 CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,16,V$,"A",N0$
2048 IF V$="" THEN GOTO 2000
2050 LET A0$(18,2)=V$
2070 FIND (PRT01_DEV,KEY=A0$(1,11)+"A"+A0$(18,2),DOM=2000)
2080 FIND (PRM10_DEV,KEY=N0$+"A"+A0$(18,2),DOM=2000)IOL=PRM10A
2090 PRINT 'SB',@(0,21),"Earnings: ",'SF',X0$(6,16),'CL',@(16,L),A0$(20,2),
2110 IF P3$(6,1)="Y" THEN GOTO 2200
2120 LET I0=4
2190 GOTO 2250
2200 REM " --- Title"
2210 PRECISION P[3]
2220 LET V0$="S",V1$="KCR",V2$=A0$(20,2),V4$="Enter A Valid Title Code, <F3>=C
2220:ode Lookup",V0=2,V1=16,V2=L,I0=3
2230 GOSUB 7000
2231 IF V3=2 THEN GOTO 2000
2232 IF V3<>3 THEN GOTO 2240
2236 CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,20,V$,"F",N0$
2238 IF V$="" THEN GOTO 2200
2240 LET A0$(20,2)=V$
2250 FIND (PRM02_DEV,KEY=A0$(1,11)+A0$(20,2),DOM=2200)IOL=PRM02A
2255 FIND (PRM10_DEV,KEY=N0$+"F"+A0$(20,2),DOM=2200)IOL=PRM10F
2260 IF C[0]=0 THEN LET C[0]=A[0]
2265 IF P3$(6,1)<>"Y" THEN PRINT 'SB',@(0,21),"Title: ",'SF',X$(6,20),'CL',
2270 IF X[0]=0 THEN LET A[0]=C[0]*X[1]
2272 PRECISION 2
2274 IF B1$(225,1)="S" AND C[1]<>0 THEN LET A[0]=A[0]/C[1]
2276 IF A[1]=0 THEN IF X0$(24,1)="Y" THEN IF A0$(18,2)=P3$(1,2) THEN LET A[1]=
2276:C[1],A[2]=A[0]*A[1]
2278 PRECISION P[3]
2280 IF B1$(225,1)="S" THEN LET A[2]=C[0]
2282 PRINT @(19,L),A[0]:M3$,@(20+M3),A0$(22,2),@(23+M3),A[1]:M2$,@(23+M3+M2),A
2282:[2]:M1$,
2284 GOSUB 6000
2286 IF RESULT$="NEWPAYCODE" THEN GOTO 3700
2290 GOTO 3200
2300 REM " --- Rate"
2320 LET V0$="N",V2$=STR(A[0]),V3$=M3$,V1=19,V2=L
2340 GOSUB 7000
2360 LET A[0]=V
2390 GOTO 3200
2400 REM " --- Dept"
2420 LET V0$="S",V1$="KCR",V2$=A0$(22,2),V4$="Enter A Valid Department Code, <
2420:F3>=Code Lookup",V0=2,V1=20+M3,V2=L
2430 GOSUB 7000
2440 LET A0$(22,2)=V$
2445 IF V3<>3 THEN GOTO 2460
2454 CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,20,V$,"E",N0$
2455 IF V$="" THEN GOTO 2400
2470 GOSUB 6000
2480 IF RESULT$="NEWPAYCODE" THEN GOTO 3700
2490 GOTO 3200
2500 REM " --- Units"
2510 IF X0$(24,1)="N" THEN GOTO 3200
2520 LET V0$="N",V2$=STR(A[1]),V3$=M2$,V1=23+M3,V2=L
2540 GOSUB 7000
2560 LET A[1]=V
2562 IF V3=3 THEN GOTO 2567
2563 IF A0$(18,2)=P3$(1,2) THEN IF A[2]<>0 THEN IF B1$(225,1)="S" THEN IF A[1]
2563:<>C[1] THEN GOTO 2564 ELSE GOTO 2567
2565 CALL "SYC.YN",1,"Recalculate Dollar Amount",2,V$,V3
2566 IF V$="N" THEN GOTO 2580
2567 PRECISION 2
2570 LET A[2]=A[0]*A[1]*1
2575 PRECISION P[3]
2580 PRINT @(23+M3+M2),A[2]:M1$,
2590 GOTO 3200
2600 REM " --- Amount"
2620 LET V0$="N",V2$=STR(A[2]),V3$=M1$,V1=23+M3+M2,V2=L
2640 GOSUB 7000
2660 LET A[2]=V
2690 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2200,2300,2400,2500,2600,3300
3300 REM " --- Correct?"
3320 LET V0$="S",V1$="C",V2$="",V4$="<Enter>=OKay   <F2>=Modify   <D>=Delete",
3320:V0=1,V1=77,V2=L,K9$=A0$(1,14)
3330 GOSUB 7000
3340 IF V3=2 THEN GOTO 3200
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3370 GOTO 3300
3400 REM " --- Write"
3402 PRECISION 2
3404 LET A[2]=A[2]*1
3406 PRECISION P[3]
3430 WRITE (PRE02_DEV,KEY=A0$(1,14))IOL=PRE02A
3440 GOSUB 5800
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Delete"
3720 REMOVE (PRE02_DEV,KEY=A0$(1,14),DOM=3730)
3730 DIM A[2]
3740 GOSUB 5800
3750 PRINT @(0,L),'CL',
3790 GOTO 1000
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Earnings Information Corre
4010:ct (Y/N/Review/List/Delete)?",V0=7,V1=FNV(V4$),V2=22
4015 GOSUB 7000
4020 IF V3=4 THEN RUN "PRE.CA"
4030 IF V$="DELETE" THEN GOTO 4200
4035 IF V$="R" OR V$="Review" THEN LET V$="REVIEW" ELSE IF V$="L" OR V$="List"
4035: THEN LET V$="LIST"
4040 IF V$="REVIEW" THEN GOTO 4600
4050 IF V$="LIST" THEN GOTO 4400
4060 IF V$="" THEN GOTO 4100
4070 ON POS(V$="YN") GOTO 4000,4100,1000
4100 RUN "PRE.CA"
4200 REM " --- Remove"
4220 READ (PRE02_DEV,KEY=A0$(1,11),DOM=4230)
4230 LET K$=KEY(PRE02_DEV,END=4290)
4240 IF LEN(K$)<11 THEN GOTO 4290
4250 IF K$(1,11)<>A0$(1,11) THEN GOTO 4290
4260 REMOVE (PRE02_DEV,KEY=K$)
4270 GOTO 4230
4290 RUN "PRE.CA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (PRE02_DEV,KEY=A0$(1,11),DOM=4630)
4640 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5010 LET X$=""
5020 FIND (PRM10_DEV,KEY=N0$+"A"+A0$(18,2),DOM=5050)IOL=PRM10A
5030 LET X$=X0$(6,16)
5050 PRINT @(0,L),A0$(12,3),@(4),FNB$(A0$(15,3)),@(13),A0$(18,2),@(16),A0$(20,
5050:2),@(19),A[0]:M3$,@(20+M3),A0$(22,2),@(23+M3),A[1]:M2$,A[2]:M1$,
5060 IF U0=1 THEN GOSUB 5800
5090 RETURN
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (PRE02_DEV,KEY=A0$(1,11),DOM=5130)
5130 LET K$=KEY(PRE02_DEV,END=5300)
5140 IF LEN(K$)<11 THEN GOTO 5300
5150 IF K$(1,11)<>A0$(1,11) THEN GOTO 5300
5160 READ (PRE02_DEV)IOL=PRE02A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<Enter>=Continue   <F4>=End>  ",V
5210:0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=A0$(1,14)
5290 GOTO 5130
5300 RETURN
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN
5500 REM " --- Error Messages For Incomplete Department Code"
5505 DIM MESSAGE$[1]
5510 ON POS(CODETYPE$="PTC") GOTO 5590,5520,5540,5550
5520 LET MESSAGE$[0]="                   Pay Code '"+A0$(18,2)+"' Undefined In
5520: Dept '"+A0$(22,2)+"'"; GOTO 5560
5540 LET MESSAGE$[0]="                   Tax Code '"+CODELIST$(CNT+1,2)+"' Und
5540:efined In Dept '"+A0$(22,2)+"'"; GOTO 5560
5550 LET MESSAGE$[0]="          Contribution Code '"+CODELIST$(CNT+1,2)+"' Und
5550:efined In Dept '"+A0$(22,2)+"'"
5560 LET MESSAGE$[1]="<Enter> To Reselect  <F1> To Allow Undefined Code  <F4> 
5560:To Exit "
5570 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
5575 IF V3=0 THEN LET RESULT$="NEWDEPTCODE"
5580 IF V3=1 THEN LET RESULT$="OK"
5585 IF V3=4 THEN LET RESULT$="NEWPAYCODE"
5590 RETURN
5800 REM " --- Update Totals"
5960 IF POS(X0$(30,1)="AC")<>0 THEN LET T4=T4+A[1]+U[1]
5970 LET T0=T0+A[2]+U[2]
5980 PRINT @(0,7),T4:M2$,@(M1,7),T0:M1$,
5990 RETURN
6000 REM " --- Test Department Codes for GL #'s
6010 LET RESULT$=""
6020 DIM R2$(28),R4$(20),R6$(28),R[1]
6030 READ (PRM10_DEV,KEY=N0$+"E"+A0$(22,2),DOM=6250)IOL=PRM10E
6040 PRINT 'SB',@(0,21),"Dept: ",'SF',R0$(6,20),'CL',
6050 LET PAYCD$=R2$,TAXCD$=R4$,CONTRCD$=R6$
6060 LET CODETYPE$="P"
6070 IF POS(A0$(18,2)=PAYCD$)=0 THEN GOSUB 5500; GOTO 6240
6080 FOR CNT=1 TO LEN(CODELIST$) STEP 3
6085 IF CODELIST$(CNT,1)="A" THEN GOTO 6230
6090 IF CODELIST$(CNT,1)<>"C" THEN GOTO 6150
6100 IF POS(CODELIST$(CNT+1,2)=TAXCD$)<>0 THEN GOTO 6230
6110 LET CODETYPE$="T"
6120 GOSUB 5500
6130 GOTO 6220
6150 IF POS(CODELIST$(CNT+1,2)=CONTRCD$)<>0 THEN GOTO 6230
6160 LET CODETYPE$="C"
6170 GOSUB 5500
6220 IF RESULT$<>"OK" THEN EXITTO 6250
6230 NEXT CNT
6240 IF RESULT$="NEWPAYCODE" THEN GOTO 6290
6250 IF RESULT$<>"OK" THEN LET I0=3; REM "User has to change depts
6290 RETURN
6300 REM " --- Accum list of Pay,Tax & Contrib Codes for this employee
6310 LET CODELIST$=""
6320 READ (PRT01_DEV,KEY=A0$(1,11)+"A",END=6390,DOM=6321)
6330 LET K4$=KEY(PRT01_DEV,END=6390)
6340 IF K4$(1,11)<>A0$(1,11) THEN GOTO 6390
6350 IF K4$(12,1)="B" THEN READ (PRT01_DEV,KEY=A0$(1,11)+"C",END=6390,DOM=6330
6350:)
6360 LET CODELIST$=CODELIST$+K4$(12,3)
6370 READ (PRT01_DEV)
6380 GOTO 6330
6390 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
