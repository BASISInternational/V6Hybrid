0010 REM "PRR - Employee Label Printing"
0020 REM "Program PRR.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-03",FILES$[3]="PRM-10"
0130 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM03_DEV=CHANNELS[2],PRM10_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0450 DIM P[2]
0460 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(5,1))
0500 REM " --- Init Data"
0510 LET W0=NUM(P3$(10,2)),W1=NUM(P3$(14,2)),W2=NUM(P3$(12,2)),W3=1
0520 LET P6$=F0$(7,3),P7$="N",W3=1
0700 REM " --- Background"
0720 FOR X=4 TO 10 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(28,3),"Label Type:",@(21,5),"Labels Per Record:",@(23,7),"Pr
0760:int Addresses:",@(34,9),"Date:",'SF',
1000 REM " --- Type"
1010 PRINT 'CF',
1020 LET V0$="S",V1$="KREC",V2$=P7$,V3$="NA",V4$="N=Numeric A=Alternate Sequen
1020:ce",V0=1,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P7$=V$
1090 GOTO 3900
1200 REM " --- Labels/Record"
1220 LET V0$="Z",V1$="R",V2$=STR(W3),V3$="",V4$="",V0=2,V1=40,V2=5
1240 GOSUB 7000
1260 IF V<1 OR V>20 THEN GOTO 1200
1280 LET W3=V
1290 GOTO 3900
1400 REM " --- Addresses"
1420 LET V0$="Y",V1$="RC",V2$=P8$,V3$="YN",V4$="Y=Yes N=No",V0=1,V1=40,V2=7
1440 GOSUB 7000
1460 LET P8$=V$
1490 GOTO 3900
1600 REM " --- Date"
1610 IF P8$="Y" THEN GOTO 3900
1620 LET V0$="D",V1$="R",V2$=P6$,V0=8,V1=40,V2=9
1640 GOSUB 7000
1660 LET P6$=V$
1690 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 3990
3920 IF V3<>2 THEN GOTO 3950
3925 IF I0=0 THEN GOTO 4200
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
4000 REM " --- Correct?"
4020 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 RUN "PRR.PB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
