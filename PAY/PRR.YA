0010 REM "PRR - Exception Report
0020 REM "Program PRR.YA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-11",FILES$[3]="PRM-01"
0130 LET FILES$[4]="PRM-10",FILES$[5]="SYS-01"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRE01_DEV=CHANNELS[1],PRE11_DEV=CHANNELS[2],PRM01_DEV=CHANNELS[3]
0185 LET PRM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLists
0210 PRE01A: IOLIST A0$
0220 PRE11A: IOLIST W0$,W[ALL]
0230 PRM01A: IOLIST B0$,B1$
0240 PRM02A: IOLIST *,*,X9
0250 PRM10A: IOLIST Y0$
0260 PRM10E: IOLIST H3$
0280 PRM10H: IOLIST X0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0320 PRS01B: IOLIST *,X$
0400 REM " --- Parameters
0405 DIM P[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0450 LET H4$="Pay Period Ending "+FNB$(X$(1,3))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1))
0500 REM " --- Init Data
0510 DIM T[7],U[7],R[7],S[7],X[7],Y[7],W[2],HEADINGS$[3]
0520 LET H0=131,H=0,HEADINGS=3,L9=59,L=L9+1,WHEN$=F0$(7,3),CLOCK$=""
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,HEADINGS$[2]=H4$
0545 CALL "SYC.RM",M1$,M1,8,13
0570 LET M0=20,RECID$="A",RPT_TYPE$="G",P[4]=NUM(P3$(7,1))
0700 REM " --- Run Report?"
0710 LET V4$="Do You Want To Print The "+N3$
0720 CALL "SYC.YN",1,V4$,2,V$,V3
0730 IF V3=4 THEN GOTO 9900
0740 IF V$<>"Y" THEN GOTO 9900
0750 CALL "SYC.GA",7,1,"","",STATUS
0760 IF STATUS>0 THEN GOTO 9900
0770 CALL "SYC.NB","Printing",LEN(M0$),COLUMN
0800 REM " --- Build Headings
0810 LET U0$="",U1$=""
0850 READ (PRM10_DEV,KEY=N0$+"H"+RPT_TYPE$,DOM=0860)
0860 LET K$=KEY(PRM10_DEV,END=0980)
0870 IF K$(1,4)<>N0$+"H"+RPT_TYPE$ THEN GOTO 0980
0880 READ (PRM10_DEV)IOL=PRM10H
0890 LET U0$=U0$+X0$(5,2),X$=X0$(7,10)
0900 FOR X=1 TO 10
0910 IF X$(10,1)<>" " THEN EXITTO 0950
0920 LET X$=" "+X$(1,9)
0930 NEXT X
0950 LET U1$=U1$+X$
0960 GOTO 0860
0980 DIM X$(80)
0990 LET X$(1)=U1$,X$(61)=" Other Pay Total Pay",U1$=X$
1000 REM " --- Init Read
1010 PRECISION P[4]
1020 READ (PRE01_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read
1120 LET K$=KEY(PRE01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (PRE01_DEV)IOL=PRE01A
1180 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1400 REM " --- Get Name
1410 LET X1$="Not On File"
1420 DIM B1$(250)
1450 FIND (PRM01_DEV,KEY=A0$(1,11),DOM=1470)IOL=PRM01A
1455 LET Q=POS("  "=B1$(1,16))-1
1457 IF Q<1 THEN LET Q=16
1460 LET X1$=B1$(1,Q)+", "+B1$(17,1)+"."
1500 REM " --- Breaks
1520 IF K$(3,P[3])<>T0$ THEN GOSUB 6000
1800 REM " --- Init Detail
1840 READ (PRE11_DEV,KEY=A0$(1,21),DOM=2000)
2000 REM " --- Detail
2040 DIM X[7],Y[7]
2100 LET K$=KEY(PRE11_DEV,END=2500)
2120 IF K$(1,21)<>A0$(1,21) THEN GOTO 2500
2140 READ (PRE11_DEV)IOL=PRE11A
2160 FIND (PRM10_DEV,KEY=N0$+RECID$+W0$(25,2),DOM=2100)IOL=PRM10A
2180 LET X=POS(W0$(25,2)=U0$,2)
2190 IF X=0 THEN GOTO 2100
2200 IF X>0 THEN LET X=INT(X/2)+1
2210 IF X>6 THEN LET X=0
2220 LET X[X]=X[X]+W[2],Y[X]=Y[X]+W[1],T[X]=T[X]+W[2],U[X]=U[X]+W[1],R[X]=R[X]
2220:+W[2],S[X]=S[X]+W[1]
2240 LET X[7]=X[7]+W[2],Y[7]=Y[7]+W[1],T[7]=T[7]+W[2],U[7]=U[7]+W[1],R[7]=R[7]
2240:+W[2],S[7]=S[7]+W[1]
2290 GOTO 2100
2500 REM " --- Print
2510 IF X[7]=0 THEN IF Y[7]=0 THEN GOTO 1100
2520 IF L+3>L9 THEN GOSUB 5000
2530 PRINT (7)'LF',@(0),FNF$(A0$(3,P[0]),M0$),
2540 IF A0$(12,1)<>"C" THEN PRINT (7)@(M0+8*M1),A0$(12,1),A0$(15,7),
2545 FOR F=1 TO 6
2550 IF X[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),X[F]:M1$,
2555 NEXT F
2560 IF X[0]<>0 THEN PRINT (7)@(M0+6*M1),X[0]:M1$,
2565 PRINT (7)@(M0+7*M1),X[7]:M1$
2570 PRINT (7)@(0),X1$,
2575 FOR F=1 TO 6
2580 IF Y[F]<>0 THEN PRINT (7)@(M0+(F-1)*M1),Y[F]:M1$,
2582 NEXT F
2584 IF Y[0]<>0 THEN PRINT (7)@(M0+6*M1),Y[0]:M1$,
2586 PRINT (7)@(M0+7*M1),Y[7]:M1$
2588 LET L=L+3
2590 GOTO 1100
4000 REM " --- Totals
4020 LET T9$="E"
4040 GOSUB 6000
4060 PRINT (7)'LF',@(5),"Grand Totals:",@(M0),T[1]:M1$,T[2]:M1$,T[3]:M1$,T[4]:
4060:M1$,T[5]:M1$,T[6]:M1$,T[0]:M1$,T[7]:M1$
4080 PRINT (7)@(M0),U[1]:M1$,U[2]:M1$,U[3]:M1$,U[4]:M1$,U[5]:M1$,U[6]:M1$,U[0]
4080::M1$,U[7]:M1$
4100 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)"Employee",
5150 FOR X=1 TO LEN(U1$)/10
5155 PRINT (7)@(M0-11+M1*X),U1$(10*(X-1)+1,10),
5160 NEXT X
5162 PRINT (7)'LF',@(0),"Number & Name",@(M0+8*M1),"Check",
5165 FOR X=1 TO LEN(U1$)/10
5170 IF U1$(10*(X-1)+9,1)<>" " THEN PRINT (7)@(M0-7+M1*X),"$/Hrs",
5175 NEXT X
5180 PRINT (7)'LF',
5190 LET L=L+2
5200 REM " --- Sub Heading
5220 IF P[3]=0 THEN RETURN
5240 PRINT (7)'LF',@(0),T0$," ",H3$
5280 LET L=L+2
5290 RETURN
6000 REM " --- Dept Break
6020 IF T0$="" THEN GOTO 6100
6030 IF P[3]=0 THEN GOTO 6100
6040 PRINT (7)'LF',@(5),"Dept Totals:",@(M0),R[1]:M1$,R[2]:M1$,R[3]:M1$,R[4]:M
6040:1$,R[5]:M1$,R[6]:M1$,R[0]:M1$,R[7]:M1$
6060 PRINT (7)@(M0),S[1]:M1$,S[2]:M1$,S[3]:M1$,S[4]:M1$,S[5]:M1$,S[6]:M1$,S[0]
6060::M1$,S[7]:M1$
6070 LET L=L+3
6100 IF T9$="E" THEN RETURN
6120 DIM R[7],S[7]
6140 LET T0$=K$(3,P[3])
6150 IF P[3]=0 THEN RETURN
6160 LET H3$=""
6170 FIND (PRM10_DEV,KEY=N0$+"E"+B1$(221,2),DOM=6200)IOL=PRM10E
6180 LET H3$=H3$(6,20)
6200 IF L>L9-4 THEN GOTO 6250
6220 GOSUB 5200
6230 RETURN
6250 GOSUB 5000
6290 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
