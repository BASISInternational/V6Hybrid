0010 REM "PRU - Purge Union History File
0020 REM "Program PRU.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRT-04"
0130 LET FILES$[2]="PRM-10",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 6800
0180 CALL "SYC.DA",1,2,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0190 IF STATUS>0 THEN GOTO 9900
0195 LET PRT04_DEV=CHANNELS[1],PRM10_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 PRT04A: IOLIST A0$(1),A1$,A[ALL]
0240 PRM10J: IOLIST X$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 DIM P[4]
0450 FOR X=0 TO 1
0460 LET P[X]=NUM(P2$(X*2+1,2))
0470 NEXT X
0500 REM " --- Init Data
0540 DIM A0$(47),A[4]
0560 LET P9$="",P8$="",P7$=""
0700 REM " --- Background
0710 PRINT 'CF'
0720 FOR X=4 TO 12 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(29,3),"From Date: ",@(29,5),"Thru Date: ",@(28,7),"Union Cod
0760:e:",'SF',@(0,22),'CL',
0800 REM " --- Options
0805 PRINT @(40,3),"MMYY",'CL',@(40,3),P9$,
0810 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Enter Month And Year As MMYY",V0=4
0810:,V1=40,V2=3
0811 GOSUB 7000
0812 IF V3=4 THEN GOTO 9900
0814 IF V$="" THEN LET V$=P9$
0816 LET P9$=V$
0820 IF LEN(P9$)<>4 THEN GOTO 0800
0822 PRINT @(40,3),P9$(1,2),"/",P9$(3,2),
0824 IF P9$(1,2)<"01" OR P9$(1,2)>"12" THEN GOTO 0800
0828 PRINT @(40,5),"MMYY",'CL',@(40,5),P8$,
0830 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Enter Month And Year As MMYY",V0=4
0830:,V1=40,V2=5
0831 GOSUB 7000
0832 IF V3=2 THEN GOTO 0800
0834 IF V$="" THEN LET V$=P8$
0836 LET P8$=V$
0840 IF LEN(P8$)<>4 THEN GOTO 0828
0842 PRINT @(40,5),P8$(1,2),"/",P8$(3,2),
0845 IF P8$(1,2)<"01" OR P8$(1,2)>"12" THEN GOTO 0828
0850 IF P8$(3,2)+P8$(1,2)<P9$(3,2)+P9$(1,2) THEN GOTO 0828
0855 PRINT @(40,7),"---",@(40,7),P7$,
0860 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Enter A Valid Union Code (<F1>=All
0860:)",V0=3,V1=40,V2=7
0861 GOSUB 7000
0862 IF V3=2 THEN GOTO 0828
0864 IF V$="" THEN LET V$=P7$
0866 LET P7$=V$
0868 IF V3<>1 THEN GOTO 0880
0870 LET P7$=""
0872 PRINT @(40,7),"All",'CL',
0875 GOTO 0900
0880 DIM X$(40)
0881 IF LEN(P7$)<3 THEN LET P7$=P7$+FILL(3-LEN(P7$))
0882 FIND (PRM10_DEV,KEY=N0$+"J"+P7$,DOM=0855)IOL=PRM10J
0885 PRINT @(44,7),X$(7,30),
0900 REM " --- Correct?
0910 LET C0$="S",V1$="KCE",V2$="",V3$="",V4$="Is The Above Information Correct
0910: (Yes/No)?",V0=3,V1=FNV(V4$),V2=22
0915 GOSUB 7000
0920 IF V3=4 THEN GOTO 9900
0930 IF V$="NO" THEN GOTO 0800
0940 IF V$<>"YES" THEN GOTO 0900
0950 LET P8$=P8$(3,2)+P8$(1,2)
0960 LET P9$=P9$(3,2)+P9$(1,2)
0980 REM " --- Disallow 'M'enu option in Error Routine
0990 LET EXIT_CTRL=1
1000 REM " --- Init Key
1020 READ (PRT04_DEV,KEY=N0$+P7$,DOM=1030)
1080 PRINT 'SB',@(30,9),"Checking: ",@(31,11),"Purging: ",'SF',
1100 REM " --- Main Read
1120 LET A0$(1)=""
1140 READ (PRT04_DEV,END=4000)IOL=PRT04A
1160 IF A0$(1,2)<>N0$ THEN GOTO 4000
1170 IF P7$<>"" THEN IF A0$(3,3)<>P7$ THEN GOTO 4000
1180 PRINT @(40,9),A0$(6,30)," ",FNB$(A0$(45,3)),
1200 REM " --- Within Date Range?
1210 LET X$=FNC$(A0$(45,3))
1220 LET X$=X$(5,2)+X$(1,2)
1240 IF X$<P9$ THEN GOTO 1400
1260 IF X$>P8$ THEN GOTO 1400
1300 REM " --- Purge
1320 REMOVE (PRT04_DEV,KEY=A0$(1,47),DOM=1330)
1380 PRINT @(40,11),A0$(6,30)," ",FNB$(A0$(45,3)),
1400 REM " --- Next
1490 GOTO 1100
4000 REM " --- End
4100 GOTO 9900
6800 REM " --- Not Available
6810 DIM MESSAGE$[1]
6820 LET MESSAGE$[0]="The Is No Union History To Be Purged"
6830 LET MESSAGE$[1]="  Press <Enter> To Continue"
6840 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6890 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
