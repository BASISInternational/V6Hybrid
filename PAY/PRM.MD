0010 REM "PRM - Employee Maintenance - Pay Rate/Change
0020 REM "Program PRM.MD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0085 SETERR 9000
0090 PRECISION P[3]
0095 ON O9 GOTO 0200,4000
0200 REM " --- IOLists
0220 PRM02A: IOLIST B0$(1),B[ALL]
0230 PRM12A: IOLIST D0$(1),D[ALL]
0270 PRM10F: IOLIST X0$,X0
0280 PRM10F1: IOLIST X0$
0500 REM " --- Init Data
0510 DIM D0$(17),D[1],B0$(13),B[1]
0550 LET DISP_LINES=5-1; REM "# of display lines -1 for zero-based arrays"
0560 DIM DISPB0$[DISP_LINES](13),DISPD0$[DISP_LINES](17)
0570 DIM DISPB[1,DISP_LINES],DISPD[1,DISP_LINES]
0700 REM " --- Background
0710 PRINT @(0,L0),'CE',
0725 CALL "SYC.WC",1,0,80,0,0,13,0
0730 PRINT 'SB',@(15,5),"Pay Rate Maintenance",@(15,13),"Pay Change Maintenanc
0730:e",
0740 PRINT @(5,6),"Title ID:",@(25+M3),"Rate",@(24+M3+M2),"Hours",@(12,14),"Da
0740:te",@(25+M3),"Rate",@(24+M3+M2),"Hours",
0750 FOR X=0 TO P7
0760 PRINT @(2,X+6),X:"#.",
0770 NEXT X
0790 PRINT 'SF',
0900 REM " --- Init File
0910 GOSUB FILL_DISP_VARS
1000 IF NEWREC$="Y" THEN GOTO 1400
1040 GOSUB 5000
1060 GOTO 4000
1400 REM " --- New Record
1420 DIM DISPB[1,DISP_LINES],DISPD[1,DISP_LINES]
2000 REM " --- Title ID
2010 FOR X=0 TO DISP_LINES
2020 LET V0$="S",V1$="KC",V2$=DISPB0$[X](12,2),V0=2,V1=5,V2=X+7,O9=1,V4$="Ente
2020:r A Valid Title Code, <F1>=Delete, <F3>=Code Lookup"
2030 GOSUB 7000
2040 IF V3=2 THEN GOTO 2220
2050 IF V3<>3 THEN GOTO 2080
2060 LET V4=1,V6=4,V7=2,V8=6,V9=20,V5$="F"
2070 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
2080 IF V$<>FILL(2) THEN LET DISPB0$[X](1)=A0$(1,11)+V$
2090 IF V3<>1 THEN GOTO 2180
2100 PRINT @(V1,X+7),'CL',
2110 LET DISPB0$[X]=FILL(13),DISPB[0,X]=0,DISPB[1,X]=0
2120 LET Z=X
2130 GOSUB 6200
2150 GOSUB 5000
2160 LET X=Z
2170 GOTO 2020
2180 IF V3=4 THEN EXITTO 3000
2190 FIND (PRM10_DEV,KEY=N0$+"F"+DISPB0$[X](12,2),DOM=2020)IOL=PRM10F
2200 PRINT @(V1+3,V2),X0$(6,20),
2210 IF DISPB[0,X]=0 THEN LET DISPB[0,X]=X0
2220 IF V3<>2 THEN GOTO 2500
2230 IF X=0 THEN GOTO 2020
2240 LET X=X-1
2250 GOTO 2020
2500 REM " --- Rate
2510 LET V0$="N",V1$="C",V2$=STR(DISPB[0,X]),V3$=M3$,V4$="Enter Pay Rate",V1=3
2510:0,V2=X+7
2520 GOSUB 7000
2530 LET DISPB[0,X]=V
2540 IF V3=2 THEN GOTO 2020
2700 REM " --- Hours
2710 LET V0$="N",V1$="C",V2$=STR(DISPB[1,X]),V3$=M2$,V4$="Enter Hours",V1=30+M
2710:3,V2=X+7
2720 GOSUB 7000
2730 LET DISPB[1,X]=V
2740 IF V3=2 THEN GOTO 2500
2750 NEXT X
3000 REM " --- New Change Date
3010 FOR X=0 TO DISP_LINES
3020 LET V0$="D",V1$="6N",V2$=DISPD0$[X](12,6),V0=8,V1=10,V2=X+15,V4$="Enter C
3020:hange Date, <F1>=Delete"
3025 GOSUB 7000
3030 IF V$<>"" THEN LET DISPD0$[X](1)=A0$(1,11)+V$
3035 IF V3<>1 THEN GOTO 3055
3040 PRINT @(0,V2),'CL',
3045 LET DISPD0$[X]=FILL(17),DISPD[0,X]=0,DISPD[1,X]=0
3050 GOTO 3380
3055 IF V3=4 THEN EXITTO 3900
3070 IF V3<>2 THEN GOTO 3200
3080 IF X=0 THEN GOTO 3020
3090 LET X=X-1
3100 GOTO 3020
3200 REM " --- New Change Rate
3220 LET V0$="N",V1$="C",V2$=STR(DISPD[0,X]),V3$=M3$,V4$="Enter New Pay Rate",
3220:V1=30,V2=X+15
3240 GOSUB 7000
3260 LET DISPD[0,X]=V
3280 IF V3=2 THEN GOTO 3020
3300 REM " --- New Change Hours
3320 LET V0$="N",V1$="C",V2$=STR(DISPD[1,X]),V3$=M2$,V4$="Enter New Hours",V1=
3320:30+M3,V2=X+15
3330 GOSUB 7000
3340 LET DISPD[1,X]=V
3350 IF V3=2 THEN GOTO 3200
3380 NEXT X
3900 REM " --- Re-sort
3920 GOSUB 6100
3940 GOSUB 5130
4000 REM " --- OPTIONS
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4045 IF O9=0 THEN IF V$="" OR V$="Y" THEN GOTO 4180
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- WRITE
4110 GOSUB DELETE_EM
4150 FOR LINECNT=0 TO DISP_LINES
4155 LET B0$=DISPB0$[LINECNT],B[0]=DISPB[0,LINECNT],B[1]=DISPB[1,LINECNT]
4160 LET D0$=DISPD0$[LINECNT],D[0]=DISPD[0,LINECNT],D[1]=DISPD[1,LINECNT]
4165 IF B0$<>FILL(13) THEN WRITE (PRM02_DEV,KEY=B0$)IOL=PRM02A
4170 IF D0$<>FILL(17) THEN WRITE (PRM12_DEV,KEY=D0$)IOL=PRM12A
4175 NEXT LINECNT
4180 LET O9=0
4190 IF O8=0 THEN RUN "PRM.MN"
4195 RUN "PRM.MA"
4200 REM " --- Delete
4210 GOSUB DELETE_EM
4285 IF O8=0 THEN GOTO 4180
4290 RUN "PRM.MA"
5000 REM " --- Display
5020 FOR X=0 TO DISP_LINES
5040 LET X0$=""
5050 IF DISPB0$[X](12,2)="  " THEN PRINT @(5,X+7),'CL',; GOTO 5120
5060 FIND (PRM10_DEV,KEY=N0$+"F"+DISPB0$[X](12,2),DOM=5100)IOL=PRM10F1
5070 LET X0$=X0$(6,20)
5100 PRINT @(5,X+7),DISPB0$[X](12,2)," ",X0$,@(30),DISPB[0,X]:M3$,DISPB[1,X]:M
5100:2$,
5120 NEXT X
5130 FOR X=0 TO DISP_LINES
5132 PRINT @(10,X+15),'CL',
5135 IF DISPD0$[X](12,6)=FILL(6) THEN GOTO 5150
5140 PRINT @(10,X+15),FNB6$(DISPD0$[X](12,6)),@(30),DISPD[0,X]:M3$,DISPD[1,X]:
5140:M2$,
5150 NEXT X
5190 RETURN
6000 REM " --- Subroutines
6100 REM " --- Re-sort
6110 LET S=0
6120 FOR X=0 TO DISP_LINES-1
6130 IF DISPD0$[X](12,6)<=DISPD0$[X+1](12,6) THEN GOTO 6170
6150 LET S=1,X2=X+1
6160 LET X$=DISPD0$[X],DISPD0$[X]=DISPD0$[X+1],DISPD0$[X+1]=X$
6165 LET X3=DISPD[0,X],DISPD[0,X]=DISPD[0,X2],DISPD[0,X2]=X3,X4=DISPD[1,X],DIS
6165:PD[1,X]=DISPD[1,X2],DISPD[1,X2]=X4
6170 NEXT X
6180 IF S=1 THEN GOTO 6100
6195 RETURN
6200 REM " --- Sort Out Spaces
6220 FOR X=0 TO DISP_LINES-1
6230 IF DISPB0$[X]>FILL(13) THEN GOTO 6265
6235 LET DISPB0$[X]=DISPB0$[X+1],DISPB0$[X+1]=FILL(13)
6245 LET DISPB[0,X]=DISPB[0,X+1],DISPB[1,X]=DISPB[1,X+1]
6250 LET DISPB[0,X+1]=0,DISPB[1,X+1]=0
6265 NEXT X
6280 RETURN
6400 REM " --- Fill Screen Display Temp Vars
6410 FILL_DISP_VARS:
6420 LET RECCNT=1,NEWREC$="N"
6430 READ (PRM02_DEV,KEY=A0$(1,11),DOM=6440)
6440 LET K02$=KEY(PRM02_DEV,END=6500)
6450 IF K02$(1,11)<>A0$(1,11) OR RECCNT>DISP_LINES+1 THEN GOTO 6500
6460 READ (PRM02_DEV,KEY=K02$)IOL=PRM02A
6470 LET DISPB0$[RECCNT-1]=B0$
6475 LET DISPB[0,RECCNT-1]=B[0],DISPB[1,RECCNT-1]=B[1]
6480 LET RECCNT=RECCNT+1
6490 GOTO 6440
6500 REM 
6510 IF RECCNT=1 THEN LET NEWREC$="Y"; GOTO 6600
6520 LET LINENO=DISP_LINES
6530 READ (PRM12_DEV,KEY=A0$(1,11)+$FF$,DOM=6540)
6540 LET K12$=KEYP(PRM12_DEV,END=6600)
6550 IF K12$(1,11)<>A0$(1,11) OR LINENO<0 THEN GOTO 6600
6560 READ (PRM12_DEV,KEY=K12$,DIR=-1)IOL=PRM12A
6570 LET DISPD0$[LINENO]=D0$
6575 LET DISPD[0,LINENO]=D[0],DISPD[1,LINENO]=D[1]
6580 LET LINENO=LINENO-1
6585 LET K12$=KEY(PRM12_DEV,END=6600)
6590 GOTO 6550
6600 RETURN
6700 REM " --- Delete Routine
6705 DELETE_EM:
6710 READ (PRM02_DEV,KEY=B0$(1,11),DOM=6715)
6715 LET K02$=KEY(PRM02_DEV,END=6750)
6720 IF K02$(1,11)<>B0$(1,11) THEN GOTO 6750
6730 REMOVE (PRM02_DEV,KEY=K02$)
6740 GOTO 6715
6750 READ (PRM12_DEV,KEY=B0$(1,11),DOM=6755)
6755 LET K12$=KEY(PRM12_DEV,END=6785)
6760 IF K12$(1,11)<>B0$(1,11) THEN GOTO 6785
6770 REMOVE (PRM12_DEV,KEY=K12$)
6780 GOTO 6755
6790 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 RUN "PRM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
