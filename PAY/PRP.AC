0010 REM "PRP - Payroll Parameter Maintenance"
0020 REM "Program PRP.AC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOList
0210 PRS01C: IOLIST A0$,A1$(1),A2$,A3$,A4$,A5$,A6$(1),A7$(1),A8$(1),A9$
0220 PRM10C: IOLIST X0$
0500 REM " --- Init Data
0510 DIM A1$(113),A2$(15),A3$(15),A4$(2),A5$(2),A6$(10),A7$(4),A8$(20)
0520 LET K0=0,I0=0,K9=6,L0=3
0530 LET A0$=N0$+N2$+"02",M1$="00000-0000"
0700 REM " --- Background
0720 PRINT @(0,3),'CE','SB',@(1,4),"Payroll Name:",@(4,5),"Address 1:",@(4,6),
0720:"Address 2:",@(9,7),"City:",@(52,6),"State:",@(47,7),"Zip/Postal:",@(1,8)
0720:,"Federal ID #:",@(3,9),"State ID #:",@(5,10),"941 Code:",@(3,11),"State 
0720:Code:",@(2,12),"Non-Taxable:",@(2,13),"Federal Tax:",@(4,14),"State Tax:"
0720:,'SF',
0800 REM " --- Do any valid tax codes exist?"
0810 READ (PRM10_DEV,KEY=N0$+"C",DOM=0820)
0820 LET K$=KEY(PRM10_DEV,END=0840)
0830 IF POS(N0$+"C"=K$)=1 THEN GOTO 1000
0840 DIM MESSAGE$[2]
0850 LET MESSAGE$[0]="At Least One Valid Tax Code Must Be Defined Using The"
0860 LET MESSAGE$[1]="Tax Code Maintenance Task Before The Report Form"
0870 LET MESSAGE$[2]="Parameters May Be Entered (<Enter>=Continue)"
0880 CALL "SYC.XA",2,MESSAGE$[ALL],2,-1,-1,V$,V3
0890 GOTO 9900
1200 REM " --- Find Key
1220 FIND (SYS01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=PRS01C
1300 REM " --- Display
1320 GOSUB 5000
1390 GOTO 4000
2000 REM " --- Name
2020 LET V0$="S",V2$=A1$(1,30),V0=30,V1=15,V2=4,I0=0
2040 GOSUB 7000
2060 LET A1$(1,30)=V$
2090 GOTO 3900
2100 REM " --- Address
2105 LET X0=I0-1
2110 LET V0$="S",V1$="CR",V2$=A1$(31+X0*24,24),V0=24,V1=15,V2=5+X0
2120 IF X0=0 THEN LET V4$="Enter Street Address"
2125 IF X0=1 THEN LET V4$="Additional Street Address"
2130 IF X0=2 THEN LET V4$="Enter City Name",V0=22,V2$=A1$(31+X0*24,22)
2140 GOSUB 7000
2160 IF X0<>2 THEN LET A1$(31+X0*24,24)=V$ ELSE LET A1$(31+X0*24,22)=V$
2190 GOTO 3900
2200 REM " --- Fed ID
2210 LET X0=I0-7,V2$=A2$
2220 IF X0=1 THEN LET V2$=A3$
2230 LET V0$="S",V1$="C",V0=15,V1=15,V2=8+X0
2235 IF X0=0 THEN LET V4$="Enter Federal ID # (Only Digits 0-9 And '-')"
2240 GOSUB 7000
2250 IF V$="" THEN GOTO 2230
2260 IF X0=0 THEN LET A2$=V$; GOSUB 3700
2270 IF X0=1 THEN LET A3$=V$
2290 GOTO 3900
2300 REM " --- 941/State Codes
2310 LET X0=I0-9,V2$=A4$
2320 IF X0=1 THEN LET V2$=A5$
2330 LET V0$="S",V0=2,V1=15,V2=10+X0
2340 GOSUB 7000
2350 FIND (PRM10_DEV,KEY=N0$+"C"+V$,DOM=2300)IOL=PRM10C
2360 PRINT @(V1+3,V2),X0$(6,16),
2370 IF X0=0 THEN LET A4$=V$
2380 IF X0=1 THEN LET A5$=V$
2390 GOTO 3900
2500 REM " --- Non-tax
2520 LET V0$="S",V2$=A6$,V0=LEN(A6$),V1=15,V2=12
2530 GOSUB 7000
2540 LET A6$(1)=V$
2590 GOTO 3900
2700 REM " --- Tax Codes
2710 LET X0=I0-12
2720 LET V0$="S",V2$=A7$(X0*2+1,2),V0=2,V1=15,V2=13+X0
2730 GOSUB 7000
2740 LET A7$(X0*2+1,2)=V$
2750 FIND (PRM10_DEV,KEY=N0$+"C"+A7$(X0*2+1,2),DOM=2700)IOL=PRM10C
2760 PRINT @(V1+3,V2),X0$(6,16),
2790 GOTO 3900
3300 REM " --- State Code For Address
3310 LET V0$="S",V1$="CR",V2$=A1$(101,2),V0=2,V1=59,V2=6,V4$="Enter Postal 2-C
3310:har State Designation"
3320 GOSUB 7000
3330 LET A1$(101,2)=V$
3340 GOTO 3900
3400 REM " --- Zip Code
3410 LET V0$="S",V1$="CR",V2$=A1$(103,9),V3$=M1$,V4$="Enter Zip/Postal Code",V
3410:0=9,V1=59,V2=7
3415 IF POS(" "=V2$)<>0 THEN LET V2$=V2$(1,POS(" "=V2$)-1)
3420 GOSUB 7000
3450 LET A1$(103,9)=V$
3460 GOTO 3900
3500 REM " --- Consolidated/Parent Company (Obsolete)
3590 GOTO 3900
3700 REM " --- Check up on Fed ID Number
3705 LET Z1$="",T1$="00070809101718192026272829304049506069707879808990"
3710 FOR I=1 TO LEN(A2$)
3715 IF A2$(I,1)<>"-" THEN IF A2$(I,1)<"0" OR A2$(I,1)>"9" THEN GOTO 3775
3720 IF A2$(I,1)>="0" AND A2$(I,1)<="9" THEN LET Z1$=Z1$+A2$(I,1)
3725 NEXT I
3730 IF LEN(Z1$)<9 THEN LET Z1$=Z1$+FILL(9-LEN(Z1$),"0")
3735 IF LEN(Z1$)<10 THEN GOTO 3745
3740 LET V4$="Federal ID May Not Contain More Than 9 Digits"
3743 GOTO 3780
3745 LET Z1$=FNF$(STR(NUM(Z1$,ERR=3775)),"00-0000000")
3750 IF POS(Z1$(1,2)=T1$,2)=0 THEN GOTO 3760
3755 LET V4$=Z1$(1,2)+" Is Not Valid As The First Segment Of The Fed ID #"
3757 GOTO 3780
3760 LET A2$=Z1$
3765 PRINT @(V1,V2),A2$,
3770 GOTO 3790
3775 LET V4$="Federal ID May Only Contain Digits (0-9)"
3780 GOSUB 6900
3785 EXITTO 2200
3790 RETURN
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2100,2100,3300,3400,3500,2200,2200,2300,2300,2500,27
3960:00,2700,4000
4000 REM " --- Correct?
4010 LET V0$="S",V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record
4110 EXTRACT (SYS01_DEV,KEY=A0$,DOM=4120)
4120 WRITE (SYS01_DEV,KEY=A0$)IOL=PRS01C
4190 GOTO 4900
4200 REM " --- Remove records
4210 REMOVE (SYS01_DEV,KEY=A0$,DOM=4220)
4290 GOTO 9900
4900 REM " --- Run menu overlay
4990 RUN "PRP.AA"
5000 REM " --- Display
5020 PRINT @(15,4),A1$(1,30),@(15,5),A1$(31,24),@(15,6),A1$(55,24),@(15,7),A1$
5020:(79,22),@(59,6),A1$(101,2),@(59,7),FNZ$(A1$(103)),@(15,8),A2$,@(59,8),A1$
5020:(112,2),@(15,9),A3$,@(15,10),A4$,@(15,11),A5$,@(15,12),A6$,@(15,13),A7$(1
5020:,2),@(15,14),A7$(3,2),
5090 RETURN
6900 REM " --- Standard Warning/Error
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
6930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6990 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(AB
8085:S(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
