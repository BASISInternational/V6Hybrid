0010 REM "PRR - Union History Report 2"
0020 REM "Program PRR.VB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOlists
0220 PRM01A: IOLIST B0$,B1$(1)
0240 PRM10D: IOLIST Z0$(1),Z[ALL]
0260 PRM10J: IOLIST H3$(1),H4$(1),H5$(1),H9$(1)
0270 PRT04A: IOLIST A0$(1),A1$(1),A[ALL]
0500 REM " --- Init Data
0510 LET HEADINGS=4,CLOCK$="",WIDTH=80
0520 DIM H3$(40),H4$(100),H5$(20),H9$(40),HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H6$
0550 LET FIRSTTIME$="Y",P7$=P7$(1,2)+FNYY_YY21$(P7$(3,2))
0600 REM " --- Printer
0620 CALL "SYC.GA",7,1,"","",STATUS
0625 IF STATUS=1 THEN GOTO 9900
0700 REM " --- Background
0720 CALL "SYC.NB","Printing: ",LEN(M0$)+13,COLUMN
1000 REM " --- Init Read
1020 READ (PRT04_DEV,KEY=N0$+P9$,DOM=1100)
1100 REM " --- Main Read
1110 DIM A0$(47),A1$(9),A[4]
1120 READ (PRT04_DEV,END=4000)IOL=PRT04A
1130 IF A0$(1,2)<>N0$ THEN GOTO 4000
1140 IF P8$<>"" THEN IF A0$(3,3)>P8$ THEN GOTO 4000
1180 IF H8$="Y" THEN PRINT @(COLUMN,11),A0$(3,3)," ",FNF$(A0$(36,P[0]),M0$)," 
1180:",FNB$(A0$(45,3))
1200 REM " --- Breaks
1220 IF A0$(3,3)<>T0$ THEN GOSUB 6000
1225 IF T1$="" THEN GOSUB 6400
1230 IF A0$(1,44)<>T1$(1,44) THEN GOSUB 6400
1400 REM " --- Check Date
1420 LET V9$=FNYY_YY21$(FNM$(FNC$(A0$(45,3)))),V$=V9$(3,2)+V9$(1,2)
1440 IF V9$>P5$ THEN IF V$=P7$ THEN GOTO 1600
1460 GOTO 1900
1600 REM " --- Which Week?
1620 LET V9$=V9$(3,2)
1640 LET X=POS(V9$<=P6$,2)
1660 IF X=0 THEN LET X=1
1670 LET X=(X+1)/2-1
1680 LET W[X]=W[X]+A[0]
1685 LET Z9=Z9+A[1]
1690 LET T8=1
1900 GOTO 1100
4000 REM " --- Totals
4020 LET T9$="E"
4040 GOSUB 6000
4050 IF FIRSTTIME$<>"Y" THEN GOTO 9900
4060 GOSUB 5000
4080 PRINT (7)@(20),"***Nothing To Report***"
4100 GOTO 9900
5000 REM " --- Heading"
5020 LET L=HEADINGS+1,FIRSTTIME$="N"
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5050 PRINT (7)@(0),R1$(1,30),@(45),H3$(7,30)
5060 PRINT (7)@(0),R1$(31,24),@(45),H4$(1,24)
5070 PRINT (7)@(0),R1$(55,24),@(45),H4$(25,24)
5080 PRINT (7)@(0),R1$(79,24),@(45),H4$(49,24)
5090 PRINT (7)@(0),"License #",H5$(1,20),@(45),FNF$(H4$(73,M7),M7$),'LF'
5100 PRINT (7)@(0),DASHLINE$
5110 PRINT (7)@(35),"Social",@(49),"--Weekly Hours Worked--"
5120 PRINT (7)@(0),"Seq",@(6),"Employee Name",@(33),"Security No",@(51),"1",@(
5120:56),"2",@(61),"3",@(66),"4",@(71),"5",@(75),"Total"
5130 PRINT (7)@(0),DASHLINE$,'LF'
5180 LET L=L+11
5190 RETURN
5700 REM " --- Union Contrib Code Heading
5710 IF L+2>L9 THEN GOSUB 5000
5720 PRINT (7)@(20),"Cont. Code",@(36),"Cont. Rate",@(36+M1),"Cont. Amount",'L
5720:F'
5730 LET L=L+2
5740 RETURN
5800 REM " --- Display Union Contrib Codes
5805 LET Z0=0
5807 GOSUB 5700
5810 FOR X=1 TO 20
5820 LET X$=H9$(2*(X-1)+1,2)
5830 IF X$="  " THEN GOTO 5980
5840 DIM Z0$(30),Z[0]
5860 LET Z0$(6)="**Not On File**"
5870 FIND (PRM10_DEV,KEY=N0$+"D"+X$,DOM=5900)IOL=PRM10D
5900 IF L+1>L9 THEN GOSUB 5000
5902 LET AMNT=0
5905 IF Z0$(24,1)="F" THEN LET AMNT=Z[0]
5910 IF Z0$(24,1)="U" THEN LET AMNT=Z[0]*(S[0]+S[1]+S[2]+S[3]+S[4])
5915 IF Z0$(24,1)="D" THEN LET AMNT=Z[0]*Z9
5920 PRINT (7)@(20),X$," ",Z0$(6,16)," ",Z[0]:M3$,@(50),AMNT:M1$
5930 LET L=L+1
5940 LET Z0=Z0+AMNT
5980 NEXT X
5982 IF L+5>L9 THEN GOSUB 5000
5985 IF Z0<>0 THEN PRINT (7)@(50),FILL(LEN(M1$)),'LF',@(25),"Total Amount Due:
5985:",@(50),Z0:M1$; LET L=L+2
5988 PRINT (7)@(1),'LF','LF',"Signed By",UNDERS$(1,20)," ","Title",UNDERS$(1,1
5988:5)," ","License No.",UNDERS$(1,15)
5989 LET L=L+3
5990 RETURN
6000 REM " --- Union Break
6020 IF T0$="" THEN GOTO 6100
6030 GOSUB 6400
6032 IF L+4>L9 THEN GOSUB 5000
6035 PRINT (7)@(49),DASHLINE$(1,31)
6040 PRINT (7)@(20),"Union Totals: ",@(49),S[0]:M2$,S[1]:M2$,S[2]:M2$,S[3]:M2$
6040:,S[4]:M2$,@(76),S[0]+S[1]+S[2]+S[3]+S[4]:M2$,'LF','LF'
6070 LET L=L+4
6080 GOSUB 5800
6100 IF T9$="E" THEN RETURN
6120 DIM S[10]
6140 LET T0$=A0$(3,3),T9=1
6150 LET Z9=0
6160 LET H3$(1)="",H4$(1)="",H5$(1)="",H9$(1)=""
6170 FIND (PRM10_DEV,KEY=N0$+"J"+A0$(3,3),DOM=6250)IOL=PRM10J
6250 GOSUB 5000
6290 RETURN
6400 REM " --- Employee Break
6410 DIM B1$(240)
6415 FIND (PRM01_DEV,KEY=N0$+A0$(36,9),DOM=6420)IOL=PRM01A
6420 IF T1$="" THEN GOTO 6500
6430 IF T8=0 THEN GOTO 6500
6440 IF L+1>L9 THEN GOSUB 5000
6450 PRINT (7)@(0),T9:"##0",@(6),T3$,@(33),T2$,@(49),W[0]:M2$,W[1]:M2$,W[2]:M2
6450:$,W[3]:M2$,W[4]:M2$,@(75),W[0]+W[1]+W[2]+W[3]+W[4]:M2$
6460 LET L=L+1,T9=T9+1
6470 FOR X=0 TO 4
6480 LET S[X]=S[X]+W[X],T[X]=T[X]+W[X]
6490 NEXT X
6500 LET T1$=A0$,T2$=FNF$(B1$(136,9),M9$)
6510 LET T8=0
6520 DIM W[10]
6600 FOR X=16 TO 1 STEP -1
6610 IF A0$(X+5,1)<>" " THEN EXITTO 6650
6620 NEXT X
6630 LET X=16
6650 LET T3$=A0$(6,X)+", "+A0$(22,14)
6690 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
