0010 REM "PRR - Employee Cross Reference Report"
0020 REM "Program PRR.OA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-01",FILES$[3]="PRM-03"
0130 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM01_DEV2=CHANNELS[2],PRM03_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists"
0210 PRM01A: IOLIST A0$,A1$
0220 PRM01A2: IOLIST C0$,C1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 DIM P[2]
0450 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Init Data"
0510 LET L9=59,L=L9+1,HEADINGS=2,WIDTH=80,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$
0540 DIM O[4],W0$(2),Z0$(WIDTH)
0550 LET M0=LEN(M0$),O[0]=1,O[1]=MAX(O[0]+M0+1,10),O[4]=WIDTH-9,O[3]=O[4]-M0-2
0550:,O[2]=INT((O[4]+1)/2)
0700 REM " --- Background
0720 FOR X=4 TO 4 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(32,3),"Option:",'SF',
0800 REM " --- Options"
0810 LET V0$="S",V1$="RE",V2$=P9$,V3$="ATB",V4$="A=Active T=Terminated B=Both"
0810:,V0=1,V1=40,V2=3
0820 GOSUB 7000
0830 ON V3 GOTO 0840,0800,0800,0800,9900
0840 LET P9$=V$
0850 REM " --- Correct?
0860 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0870 ON V3 GOTO 0880,0850,0850,0850,9900
0880 ON POS(V$="YN") GOTO 0850,0890,0800
0950 REM " --- Open Printer"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS>0 THEN GOTO 9900
0970 CALL "SYC.NB","Printing",M0,COLUMN
1000 REM " --- Initial Read"
1020 READ (PRM01_DEV,KEY=N0$,DOM=1030)
1030 READ (PRM03_DEV,KEY=N0$+"A",DOM=1040)
1100 REM " --- Employee Read"
1110 IF W0$(1,1)="E" THEN GOTO 2000
1120 READ (PRM01_DEV,END=1900)IOL=PRM01A
1125 IF A0$(1,2)<>N0$ THEN GOTO 1900
1130 IF P9$="A" THEN IF A1$(170,3)<>"   " THEN GOTO 1100
1140 IF P9$="T" THEN IF A1$(170,3)="   " THEN GOTO 1100
1500 REM " --- Print"
1510 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1520 DIM X$(O[2]-O[1]-1)
1530 LET X$(1)=FNP$(A1$(1,16))+", "+FNP$(A1$(17,14))
1540 LET Z0$(O[0])=FNF$(A0$(3,P[0]),M0$),Z0$(O[1])=X$
1550 LET T0=T0+1
1590 GOTO 2000
1900 REM " --- End Of Employees"
1920 LET W0$(1,1)="E"
2000 REM " --- Alpha Read"
2010 IF W0$(2,1)="E" THEN GOTO 3000
2020 LET K$=KEY(PRM03_DEV,END=2900)
2040 READ (PRM03_DEV)
2060 IF K$(1,3)<>N0$+"A" THEN GOTO 2900
2400 REM " --- Find Employee"
2420 FIND (PRM01_DEV2,KEY=N0$+K$(20),DOM=2000)IOL=PRM01A2
2430 IF P9$="A" THEN IF C1$(170,3)<>"   " THEN GOTO 2000
2440 IF P9$="T" THEN IF C1$(170,3)="   " THEN GOTO 2000
2500 REM " --- Print"
2520 DIM X$(O[3]-O[2]-1)
2530 LET X$(1)=FNP$(C1$(1,16))+", "+FNP$(C1$(17,14))
2540 LET Z0$(O[2])=X$,Z0$(O[3])=FNF$(C0$(3,P[0]),M0$)
2550 IF P9$<>"A" THEN IF C1$(170,3)<>"   " THEN LET Z0$(O[4])=FNB$(C1$(170,3))
2590 GOTO 3000
2900 REM " --- Alpha End"
2920 LET W0$(2,1)="E"
3900 REM " --- Main Print"
3910 IF W0$="EE" THEN GOTO 4000
3940 IF L+1>L9 THEN GOSUB 5000
3950 PRINT (7)Z0$
3960 LET L=L+1,T=T+1
3980 DIM Z0$(WIDTH)
3990 GOTO 1100
4000 REM " --- Totals"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)'LF',@(0),"Total Employees:",T0
4030 LET L=L+2
4100 REM " --- Done"
4120 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(O[0]-1),"Employee Number Sequence",@(O[2]-1),"Alphabetic Seque
5100:nce",@(O[4]-1),"Terminated",'LF'
5120 LET L=L+2
5190 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
