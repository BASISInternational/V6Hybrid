0010 REM "PRR - Union History Report
0020 REM "Program PRR.VA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="PRT-04"
0140 LET FILES$[2]="PRM-01",FILES$[3]="PRM-10",FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 6800
0180 CALL "SYC.DA",1,2,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0185 IF STATUS>0 THEN GOTO 9900
0190 LET PRT04_DEV=CHANNELS[1],PRM01_DEV=CHANNELS[2],PRM10_DEV=CHANNELS[3]
0195 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0230 PRM10J: IOLIST X$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0320 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0330 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0340 PRS01C: IOLIST R0$,R1$(1)
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[4],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1))
0492 DIM R1$(110)
0495 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=0500)IOL=PRS01C
0500 REM " --- Init Data
0510 LET H1$=N1$,H2$=N3$
0520 LET L9=59,L=L9+1
0530 LET DASHLINE$=FILL(80,"-"),UNDERS$=FILL(35,"_")
0540 DIM S[10],T[10],W[10]
0545 LET M2$="###- "
0547 LET M9$="000-00-0000"
0550 LET M0=20,M1=LEN(M1$),G5=LEN(G5$)
0560 LET P9$="",P8$="",P7$=""
0570 LET X=POS("-"=P1$)
0580 IF X>0 THEN LET P[4]=X-1
0590 LET M7$="00000",M7=NUM(P3$(5,1))
0595 IF M7=9 THEN LET M7$=M7$+"-0000"
0630 LET H8$="N",X=POS(F0$(13,1)="LP")
0640 IF X>0 THEN LET H8$="Y"
0700 REM " --- Background
0720 FOR X=4 TO 8 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0750 LET X=14
0760 CALL "SYC.WC",1,0,80,0,0,X,0
0780 PRINT 'SB',@(28,3),"From Union:",@(28,5),"Thru Union:",@(33,7),"Month:",@
0780:(20,9),"Ending Date Week 1:",@(37,10),"2:",@(37,11),"3:",@(37,12),"4:",@(
0780:37,13),"5:",
0790 PRINT 'SF','CF',
1000 REM " --- From Union
1020 LET V0$="S",V1$="C",V2$=P9$,V4$="Enter Valid Union Code (<F1>=First)",V0=
1020:3,V1=40,V2=3
1025 IF V2$="" THEN PRINT @(V1,V2),'CL',
1030 GOSUB 7000
1035 IF V3=4 THEN GOTO 9900
1040 IF V3<>1 THEN GOTO 1070
1050 LET P9$=""
1055 PRINT @(V1,V2),"First",'CL',
1060 GOTO 1100
1070 LET P9$=V$
1072 IF LEN(P9$)<>3 THEN LET P9$=P9$+FILL(3-LEN(P9$))
1075 DIM X$(40)
1080 FIND (PRM10_DEV,KEY=N0$+"J"+P9$,DOM=1000)IOL=PRM10J
1090 PRINT @(V1+V0+1,V2),X$(7,30),
1100 REM " --- Thru Union
1120 LET V0$="S",V1$="C",V2$=P8$,V4$="Enter Valid Union Code (<F1>=Last)",V0=3
1120:,V1=40,V2=5
1125 IF V2$="" THEN PRINT @(V1,V2),'CL',
1130 GOSUB 7000
1140 IF V3<>1 THEN GOTO 1170
1150 LET P8$=""
1155 PRINT @(V1,V2),"Last",'CL',
1160 GOTO 1200
1170 LET P8$=V$
1172 IF LEN(P8$)<>3 THEN LET P8$=P8$+FILL(3-LEN(P8$))
1175 DIM X$(40)
1180 FIND (PRM10_DEV,KEY=N0$+"J"+P8$,DOM=1100)IOL=PRM10J
1190 PRINT @(V1+V0+1,V2),X$(7,30),
1195 IF P8$<P9$ THEN GOTO 1100
1200 REM " --- Month
1220 LET V0$="S",V1$="C",V2$=P7$,V4$="Enter Month/Year As MMYY",V0=4,V1=40,V2=
1220:7
1230 GOSUB 7000
1240 IF LEN(V$)<>4 THEN GOTO 1200
1250 LET X=NUM(V$,ERR=1200),JULDATE=JUL(1900+FNYY_YEAR(V$(3,2)),NUM(V$(1,2)),1
1250:,ERR=1200)
1260 LET P7$=V$,H6$=DATE(JULDATE:"%Ml %Yl")
1280 PRINT @(V1,V2),P7$(1,2),"/",P7$(3,2),"   ",H6$,'CL',
1300 REM " --- Get Dates For Sundays In The Month"
1310 LET XDAY=JULDATE,XMONTH$=P7$(1,2),P6$=""
1320 LET XDAY$=DATE(XDAY:"%Ds%Dz"),XMONTH$=DATE(XDAY:"%Mz"),XDAY=XDAY+1
1330 IF XMONTH$<>P7$(1,2) THEN GOTO 1370
1340 LET LASTDAY$=XDAY$(4,2)
1350 IF XDAY$(1,3)="Sun" THEN LET P6$=P6$+LASTDAY$
1360 GOTO 1320
1370 FOR X=1 TO LEN(P6$) STEP 2
1380 PRINT @(40,(X+1)/2+8),P6$(X,2),
1390 NEXT X
1400 REM " --- Get Date For Monday Of First Week"
1410 LET XDAY=JULDATE,XMONTH$=P7$(1,2),P5$=""
1420 LET XDAY$=DATE(XDAY:"%Ds%Yz%Mz%Dz"),XDAY=XDAY-1
1430 IF XDAY$(1,3)<>"Mon" THEN GOTO 1420
1440 LET P5$=FNYY_YY21$(XDAY$(4,2))+XDAY$(6)
1500 REM " --- Week Ending Dates
1510 FOR X=1 TO LEN(P6$)/2
1520 LET V0$="Z",V1$="",V2$=P6$(2*(X-1)+1,2),V0=2,V1=40,V2=8+X
1530 GOSUB 7000
1540 IF V$<"01" OR V$>LASTDAY$ THEN GOTO 1530
1560 LET P6$(2*(X-1)+1,2)=V$
1570 IF X>1 THEN IF P6$(2*(X-1)+1,2)<P6$(2*(X-1)-1,2) THEN GOTO 1530
1580 NEXT X
1590 GOTO 4000
4000 REM " --- Correct?
4020 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/End)? ",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Overlay
4200 RUN "PRR.VB"
6800 REM " --- Not Available
6820 PRINT 'RB',@(10,8),"Sorry But This Report Is Not Available, <Enter>: ",
6830 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=60,V2=8
6840 GOSUB 7000
6890 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
