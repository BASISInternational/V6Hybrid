0010 REM "PRM - Union Code Maintenance"
0020 REM "Program PRM.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O9 GOTO 0070,4900
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-10",FILES$[2]="PRT-04",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM10_DEV=CHANNELS[1],PRT04_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 PRM10J: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(36),A1$(112),A2$(20),A3$(40),A4$(20),A5$(20)
0530 LET K0=1,K9=6,L0=11
0540 LET A0$(1,3)=N0$+"J"
0550 LET M1=LEN(M1$),M2=LEN(M2$)
0560 LET M7$="00000",M8$="(###)-###-####",M7=5,M8=10
0570 IF P3$(5,1)="9" THEN LET M7$=M7$+"-0000",M7=9
0700 REM " --- Background"
0720 CALL "SYC.WC",1,0,80,0,0,10,0
0740 PRINT 'SB',@(3,3),"Union Code:",@(3,4),"Union Name:",@(6,6),"Address:",@(
0740:44),"Telephone:",@(13,7),":",@(46),"Contact:",@(13,8),":",@(5,9),"Zip Cod
0740:e:",@(34),"Employer License No:",
0790 PRINT 'SF',
0900 REM " --- Init File"
0920 READ (PRM10_DEV,KEY=A0$(1,3),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="CEK",V3$="",V4$="Enter Up To 3 Characters, <F3>=Lookup, 
1050:<F4>=End",V0=3,V1=15,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,3,7,30,V$,"J",N0$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Find Key"
1220 FIND (PRM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=PRM10J
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4900
1400 REM " --- New Record"
1420 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up"
1510 LET A0$(7)="",A1$(1)="",A2$(1)="",A3$(1)="",A4$(1)="",A5$(1)=""
2000 REM " --- Name"
2020 LET V0$="S",V1$="C",V2$=A0$(7,30),V0=30,V1=15,V2=4,I0=0
2030 GOSUB 7000
2040 LET A0$(7,30)=V$
2090 GOTO 3900
2100 REM " --- Address"
2110 LET X=I0
2120 LET V0$="S",V1$="",V2$=A1$(24*(X-1)+1,24),V0=24,V1=15,V2=X+5
2130 GOSUB 7000
2140 LET A1$(24*(X-1)+1,24)=V$
2190 GOTO 3900
2200 REM " --- Zip Code"
2220 LET V0$="M",V1$="",V2$=A1$(73,M7),V3$=M7$,V0=M7,V1=15,V2=9
2230 GOSUB 7000
2240 LET A1$(73,9)=V$
2290 GOTO 3900
2300 REM " --- Telephone"
2320 LET V0$="H",V1$="",V2$=A1$(82,10),V3$=M8$,V0=10,V1=55,V2=6
2330 GOSUB 7000
2340 LET A1$(82,10)=V$
2390 GOTO 3900
2400 REM " --- Contact"
2420 LET V0$="S",V1$="",V2$=A1$(92,20),V0=20,V1=55,V2=7
2430 GOSUB 7000
2440 LET A1$(92,20)=V$
2490 GOTO 3900
2500 REM " --- Employer License Number"
2520 LET V0$="S",V1$="",V2$=A2$(1,20),V0=20,V1=55,V2=9
2530 GOSUB 7000
2540 LET A2$(1,20)=V$
2590 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2100,2100,2200,2300,2400,2500,2600,2600,2600,2600,26
3960:00,2600,2600,2600,2600,2600,2600,2600,2600,2600,2600,2600,2600,2600,2600,
3960:2600,4000
4000 REM " --- Correct?"
4010 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct 
4010:(Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (PRM10_DEV,KEY=A0$(1,K9))IOL=PRM10J
4190 GOTO 4900
4200 REM " --- Delete"
4210 READ (PRT04_DEV,KEY=N0$+A0$(4,3),DOM=4220)
4220 LET K$=KEY(PRT04_DEV,END=4260)
4240 IF (N0$+A0$(4,3)=K$)<>1 THEN GOTO 4260
4250 GOTO 4800
4260 REMOVE (PRM10_DEV,KEY=A0$(1,K9),DOM=4270)
4290 GOTO 1000
4500 REM " --- Print"
4520 CALL "SYC.YN",1,"Do You Wish To Print A List Of All Records",1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4560 LET O0=1
4580 RUN "PRM.JE"
4800 REM " --- Ignore Delete"
4810 DIM MESSAGE$[1]
4820 LET MESSAGE$[0]="Union History Records Exist - Deletion Request Ignored"
4830 LET MESSAGE$[1]="               Press <Enter> To Continue"
4840 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4890 GOTO 4000
4900 REM " --- Submenu"
4910 DIM MENUS$[1]
4920 LET MENUS=1,MENUS$[MENUS]="PRM.JA",WIN_X=40,WIN_Y=L0+1
4965 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4970 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4975 PRINT @(0,L0),'CE',
4980 IF SELECTION$="" THEN GOTO 1000
4985 IF SELECTION$=PGM(-2) THEN GOTO 2000
4990 RUN SELECTION$,ERR=4900
5000 REM " --- Display"
5020 PRINT @(15,4),A0$(7,30),@(15,6),A1$(1,24),@(55),FNF$(A1$(82,10),M8$),@(15
5020:,7),A1$(25,24),@(55),A1$(92,20),@(15,8),A1$(49,24),@(15,9),FNF$(A1$(73,M7
5020:),M7$),@(55),A2$,
5190 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
