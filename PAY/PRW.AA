0010 REM "PRX - W-2 Configuration Maintenance"
0020 REM "Program PRW.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-10",FILES$[2]="PRM-10",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM10_DEV=CHANNELS[1],PRM10_DEV2=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 PRM10W: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A[ALL],A1[ALL]
0250 PRM10ABCD: IOLIST X0$; REM "Used for PRM10A, PRM10B, PRM10C & PRM10D"
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET M2$="##00.00#-"
0500 REM " --- Init Data
0505 REM "I = For Next variable in line entry
0510 REM "J = For Next to roll line entries to delete
0515 REM "J1= Pos of first blank entry in string
0520 REM "K = For Next varialbe in display (5000,5199)
0525 REM "W = Save 'I' before redisplaying line entries while still in For/Nex
0525:t after delete
0530 LET S=10; REM "Number of codes that can be entered for the Box/Line
0540 DIM A0$(92),A1$(S),A2$(S*2),A3$(S),A[S-1],A1[S-1]
0700 REM " --- Background
0705 CALL "SYC.WC",1,0,80,0,0,5,0
0710 PRINT 'SB',@(2,3),"W2 Box Number:",@(22,3),"Box Description:",@(1,4),"W2 
0710:Box-Line ID:",@(21,4),"Line Description:"
0730 PRINT @(1,6),"Column Print Position:"
0735 PRINT @(12,7),"State Code:",@(12,8),"Local Code:",
0740 PRINT @(5,9),"Type",@(11),"Code",@(17),"Description",@(50),"Column",@(58)
0740:,"Operator",@(68),"Multiplier"
0743 CALL "SYC.WC",1,0,4,0,5,10,0
0746 CALL "SYC.WC",1,0,4,0,11,10,0
0750 CALL "SYC.WC",1,0,11,0,17,10,0
0753 CALL "SYC.WC",1,0,6,0,50,10,0
0756 CALL "SYC.WC",1,0,8,0,58,10,0
0758 CALL "SYC.WC",1,0,10,0,68,10,0
0759 PRINT 'SB'
0760 FOR I=1 TO 10
0770 PRINT @(1,I+10),I:"##"+")",
0780 NEXT I
0790 PRINT 'SF',
0900 REM " --- Init File
0910 READ (PRM10_DEV,KEY=N0$+"W",DOM=0915)
1000 REM " --- Key
1003 PRM10KEY:
1005 DIM A0$(92),A1$(S),A2$(S*2),A3$(S),A[S-1],A1[S-1]
1010 LET A0$(1,3)=N0$+"W",I0=0
1020 LET V2$="",V2$=KEY(PRM10_DEV,END=1040)
1025 IF V2$(1,3)<>N0$+"W" THEN LET V2$=""
1030 IF V2$<>"" THEN LET X9$=V2$(4),V2$=V2$(4,2)
1035 REM " --- Box ID
1040 PRINT 'CF',
1050 LET V0$="S",V1$="CK",V3$="",V4$="Enter The W-2 Box # To Be Modified (a-f 
1050:or 1-21)",V0=2,V1=17,V2=3,I0=0
1055 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1065 IF LEN(V$)>2 THEN GOTO PRM10KEY
1070 IF LEN(V$)=1 AND POS(V$="abcdef")>0 THEN GOTO 1085
1075 IF NUM(V$,ERR=PRM10KEY)<1 OR NUM(V$)>21 THEN GOTO PRM10KEY
1080 LET V$=STR(NUM(V$):"00")
1085 LET A0$(4,2)=V$
1090 PRINT @(V1,V2),A0$(4,2),
1095 GOTO NEXTFIELD
1100 REM " --- Box-Line ID
1103 BOXLINE_ID:
1105 LET V0$="S",V1$="C",V3$="",V4$="Enter The W-2 Box-Line ID To Be Modified 
1105:(01 Thru 'z')",V0=1,V1=17,V2=4
1110 IF A0$(4,2)="18" THEN LET V4$="Enter The W-2 Box-Line ID To Be Modified (
1110:'a' Thru 'c')"
1112 IF POS(A0$(4,2)=X9$)=1 AND LEN(X9$)=3 THEN LET V2$=X9$(3,1)
1115 GOSUB 7000
1117 IF V3=4 THEN GOTO 9900
1120 IF V3=2 THEN GOTO 1010
1125 IF LEN(V$)>1 THEN GOTO BOXLINE_ID
1130 IF V$<>"" THEN LET A0$(6,1)=V$; GOTO 1145
1135 READ (PRM10_DEV,KEY=A0$(1,5),DOM=1140)
1140 LET K0$=KEY(PRM10_DEV,END=1200)
1142 IF K0$(1,5)<>A0$(1,5) THEN GOTO 1200 ELSE LET A0$(1,6)=K0$(1,6)
1145 READ (PRM10_DEV,KEY=A0$(1,6),DOM=1200)IOL=PRM10W
1150 PRINT @(17,3),A0$(4,2),@(17,4),A0$(6,1),; GOSUB 5000
1155 LET V0$="S",V1$="CE",V2$="Y",V3$="",V4$="Is The Information Correct (Y/N)
1155:?",V0=1,V1=FNV(V4$),V2=22
1160 GOSUB 7000
1165 IF V3=4 THEN READ (PRM10_DEV,KEY=A0$(1,5)+"z",DOM=PRM10KEY); GOTO PRM10KE
1165:Y
1170 IF V3=2 THEN GOTO PRM10KEY
1173 IF V$="" THEN LET V$="Y"
1174 IF V$="Y" THEN WRITE (PRM10_DEV,KEY=A0$(1,6))IOL=PRM10W
1175 ON POS(V$="YN") GOTO 1155,1035,1195
1180 LET A0$(6,1)=V$
1185 READ (PRM10_DEV,KEY=A0$(1,6),DOM=1200)IOL=PRM10W
1190 GOSUB 5000
1195 LET I0=1; GOTO NEXTFIELD
1200 REM " --- New Box?
1220 CALL "SYC.YN",1,"Is This A New Definition?",2,V$,V3
1240 ON POS(V$="YN") GOTO NEXTFIELD,NEXTFIELD,1045
2000 GET_ENTRIES:
2010 FOR I=1 TO S
2100 REM " --- Get Entries
2105 LET V8$=A1$(I,1); IF A1$(I,1)="C" THEN LET V8$="D" ELSE IF A1$(I,1)="D" T
2105:HEN LET V8$="C"
2110 REM "******* Code Type ******"
2115 LET V0$="S",V1$="CE",V2$=V8$,V3$="ABCDE",V4$="Enter Code A=Pay B=Deduct C
2115:=Contribution D=Tax E=Delete <F1>=Skip",V0=1,V1=6,V2=I+10
2120 GOSUB 7000
2122 IF V3=4 THEN EXITTO NEXTFIELD
2125 IF I=1 AND V3=2 THEN EXITTO NEXTFIELD
2130 IF V3<>0 AND V3<>3 THEN GOTO 2500
2135 IF V$<>"E" THEN GOTO 2160
2140 LET L=I; GOSUB 5300
2145 FOR W=1 TO 10; PRINT @(5,W+10),'CL',; NEXT W
2150 GOSUB 5000
2155 GOTO 2100
2160 IF V$="C" THEN LET V$="D" ELSE IF V$="D" THEN LET V$="C"
2165 LET A1$(I,1)=V$
2170 IF V$="A" THEN LET V9$="Pay " ELSE IF V$="B" THEN LET V9$="Deduction "
2180 IF V$="C" THEN LET V9$="Tax " ELSE IF V$="D" THEN LET V9$="Contribution "
2190 IF V$="" THEN GOTO 2110
2200 REM "******** Code Identifier ********"
2210 LET V0$="S",V1$="EC",V2$=A2$(I*2-1,2),V3$="",V4$="Enter A Valid "+V9$+"Co
2210:de (<F3>=Lookup)",V0=2,V1=12,V2=I+10
2220 GOSUB 7000
2225 IF V3<>3 THEN GOTO 2250
2230 LET V4=1,V6=4,V7=2,V8=6,V9=16,V5$=A1$(I,1)
2240 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV2,V6,V7,V8,V9,V$,V5$,N0$
2250 IF V3=2 THEN GOTO 2110
2260 IF V3=4 THEN GOTO 2500
2270 FIND (PRM10_DEV2,KEY=N0$+A1$(I,1)+V$,DOM=2200)IOL=PRM10ABCD
2280 PRINT @(16,V2),X0$(6,16),
2290 LET A2$(I*2-1,2)=V$
2300 REM "************ Amount Column ********"
2310 LET V0$="N",V1$="EC",V2$=STR(A[I-I]),V3$="#",V4$="Enter Column 1=Gross 2=
2310:Taxable/Basis 3=Accumulated Amount",V0=1,V1=52,V2=I+10
2320 IF POS(A1$(I,1)="AB")<>0 THEN LET V=1; PRINT @(V1,V2),STR(V),; GOTO 2330
2325 GOSUB 7000
2330 IF V3=2 THEN GOTO 2200
2340 IF V3=4 THEN GOTO 2500
2360 IF V<1 OR V>3 THEN GOTO 2300
2370 LET A[I-1]=V
2400 REM "****** Operator **********"
2405 IF A3$(I,1)=" " THEN LET A3$(I,1)="+"
2410 LET V0$="S",V1$="EC",V2$=A3$(I,1),V3$="+-",V4$="Enter Operator <+ Or ->",
2410:V0=1,V1=61,V2=I+10
2415 GOSUB 7000
2420 IF V3=2 THEN GOTO 2300
2430 IF V3=4 THEN GOTO 2500
2440 LET A3$(I,1)=V$
2450 REM "********* Multiplier ********"
2460 LET V0$="N",V1$="CE",V2$=STR(A1[I-1]),V3$=M2$,V4$="Enter A Mulitplier For
2460: A Percentage/Factor Of The Amount",V0=7,V1=68,V2=I+10
2470 GOSUB 7000
2480 IF V3=4 THEN GOTO 2500
2490 IF V<=0 THEN LET V=1; PRINT @(V1,V2),V:M2$,
2495 LET A1[I-1]=V
2500 REM "***** Validate the Entry *******"
2510 IF A1$(I,1)=" " THEN LET A2$(I*2-1,2)="  ",A3$(I,1)=" ",A[I-1]=0,A1[I-1]=
2510:0; GOTO 2570
2520 IF A2$(I*2-1,2)="  " THEN GOTO 2200
2530 IF A[I-1]=0 THEN GOTO 2300
2540 IF A3$(I,1)=" " THEN GOTO 2400
2550 IF A1[I-1]=0 THEN GOTO 2450
2570 IF I>1 AND V3=2 THEN LET I=I-2; GOTO 2590
2575 IF I=1 AND V3=2 THEN EXITTO NEXTFIELD
2580 IF V3=4 THEN EXITTO 3990
2590 NEXT I
2595 GOTO 3990
2600 REM "********* BOX DESCRIPTION ***********"
2603 BOXDESC:
2620 LET V0$="S",V1$="CE",V2$=A0$(7,40),V3$="",V4$="Enter A Description For W2
2620:-Box '"+A0$(4,2)+"-"+A0$(6,1)+"'",V0=40,V1=39,V2=3
2625 IF A0$(4,2)="26" THEN LET V4$=V4$+" Up To 16 Characters",V0=16,V2$=A0$(7,
2625:16)
2630 GOSUB 7000
2640 IF V3=4 THEN GOTO 0010
2650 IF LEN(V$)<40 THEN LET V$=V$+FILL(40-LEN(V$))
2660 LET A0$(7,40)=V$
2690 GOTO NEXTFIELD
2700 REM "********** LINE DESCRIPTION ********"
2703 LINEDESC:
2710 IF A0$(6,1)=" " THEN LET A0$(47,40)=FILL(40); GOTO NEXTFIELD
2720 LET V0$="S",V1$="CE",V2$=A0$(47,40),V3$="",V4$="Enter Description For W2-
2720:Box Line '"+A0$(6,1)+"'",V0=40,V1=39,V2=4
2730 GOSUB 7000
2740 IF V3=4 THEN GOTO 0010
2750 LET A0$(47,40)=V$
2790 GOTO NEXTFIELD
2800 REM "********* COLUMN PRINT POSITION ********"
2803 COLUMN_POS:
2820 LET V0$="Z",V1$="CE",V2$=A0$(87,2),V3$="00",V4$="Enter The Horizontal Pri
2820:nt Position On The W2 Where This Line Is To Print",V0=2,V1=24,V2=6
2840 GOSUB 7000
2850 IF V3=4 THEN GOTO 3990
2855 LET V$=STR(NUM(V$):"00"); PRINT @(V1,V2),V$,
2860 IF V$<"00" OR V$>"79" THEN GOTO COLUMN_POS
2870 LET A0$(87,2)=V$
2890 GOTO NEXTFIELD
2900 REM "********* STATE CODE **********"
2903 STATECODE:
2920 LET V0$="S",V1$="CE",V2$=A0$(89,2),V3$="",V4$="If This Box Pertains To On
2920:e Particular State, Enter The State ID Code",V0=2,V1=24,V2=7
2930 GOSUB 7000
2940 IF V3=4 THEN GOTO 3990
2950 IF V$="" OR V$=" " THEN LET V$="  "
2960 LET A0$(89,2)=V$
2970 GOTO NEXTFIELD
3000 REM "********** LOCAL CODE *********"
3003 LOCALCODE:
3020 LET V0$="S",V1$="CE",V2$=A0$(91,2),V3$="",V4$="If This Box Pertains To A 
3020:Particular Local Tax, Enter The Local Code",V0=2,V1=24,V2=8
3040 GOSUB 7000
3050 IF V3=4 THEN GOTO 3990
3060 LET A0$(91,2)=V$
3070 GOTO NEXTFIELD
3900 REM "******** NEXT FIELD *******"
3903 NEXTFIELD:
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO PRM10KEY,BOXLINE_ID,BOXDESC,LINEDESC,COLUMN_POS,STATECODE,LOCA
3960:LCODE,GET_ENTRIES,3990
3990 LET I0=K0
4000 REM " --- Correct?
4010 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct 
4010:(Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4025 IF V$="DELETE" THEN GOTO 4200
4030 ON POS(V$="YN") GOTO 4000,4100,4300
4100 REM " --- Write
4110 WRITE (PRM10_DEV,KEY=A0$(1,6))IOL=PRM10W
4120 GOTO PRM10KEY
4200 REM "********** DELETE ENTRY ********"
4210 IF V$<>"DELETE" THEN GOTO 4000
4220 REMOVE (PRM10_DEV,KEY=A0$(1,6),DOM=4230)
4230 GOTO 0900
4300 REM "********** INFORMATION NOT CORRECT **********"
4320 LET I0=3
4330 GOTO NEXTFIELD
5000 REM "************ DISPLAY THE HEADING ******"
5010 PRINT @(39,3),A0$(7,40),@(39,4),A0$(47,40),@(24,6),A0$(87,2),@(24,7),A0$(
5010:89,2),@(24,8),A0$(91,2),
5100 REM "******** DISPLAY THE CODE DEFINITION ********"
5110 FOR K=1 TO S
5120 IF A1$(K,1)=" " THEN GOTO 5180
5125 DIM X0$(21); LET X0$(6,16)="**Not on File** "
5130 FIND (PRM10_DEV2,KEY=N0$+A1$(K,1)+A2$(K*2-1,2),DOM=5200)IOL=PRM10ABCD
5135 LET V8$=A1$(K,1); IF A1$(K,1)="C" THEN LET V8$="D" ELSE IF A1$(K,1)="D" T
5135:HEN LET V8$="C"
5150 PRINT @(6,K+10),V8$,
5155 PRINT @(12),A2$(K*2-1,2),
5160 PRINT @(16,K+10),X0$(6,16),@(52),A[K-1]:"#",
5165 PRINT @(61),A3$(K,1),
5170 PRINT @(68),A1[K-1]:M2$,
5180 NEXT K
5190 RETURN
5200 REM "*******Code in existing definition no longer valid ********"
5210 IF A1$(K,1)="A" THEN LET V9$="Pay" ELSE IF A1$(K,1)="B" THEN LET V9$="Ded
5210:uction"
5220 IF A1$(K,1)="C" THEN LET V9$="Tax" ELSE IF A1$(K,1)="D" THEN LET V9$="Con
5220:tribution"
5230 LET V0$="S",V1$="EC",V2$="",V3$="",V4$=V9$+" Code '"+A2$(K*2-1,2)+"' Is N
5230:ot Valid, R=Remove, <Enter>=Skip:",V0=1,V1=FNV(V4$),V2=22
5240 GOSUB 7000
5245 IF V$="" THEN PRINT @(16,K+10),X0$(6,16),; GOTO 5180
5250 IF V$="R" THEN LET L=K; GOSUB 5300 ELSE GOTO 5200
5260 GOTO 5120
5300 REM " --- Remove & Reorder the Entries
5310 LET J1=POS(" "=A1$(1,10))
5320 IF J1=L+1 OR L=10 THEN LET J1=L; GOTO 5380
5330 IF J1=0 THEN LET J1=11 ELSE IF J1=1 THEN GOTO 5380
5340 LET J1=J1-1
5350 FOR J=L TO J1-1
5360 LET A1$(J,1)=A1$(J+1,1),A2$(J*2-1,2)=A2$((J+1)*2-1,2),A3$(J,1)=A3$(J+1,1)
5360:,A[J-1]=A[J],A1[J-1]=A1[J]
5370 NEXT J
5380 LET A1$(J1,1)=" ",A2$(J1*2-1,2)="  ",A3$(J1,1)=" ",A[J1-1]=0,A1[J1-1]=0
5390 RETURN
6000 REM " --- Subroutines
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
