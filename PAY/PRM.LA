0010 REM "PRM - Labor Distribution Maintenance"
0020 REM "Program PRM.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-10",FILES$[3]="PRT-05"
0130 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM10_DEV=CHANNELS[2],PRT05_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists"
0210 PRM01A: IOLIST X$,X0$
0220 PRM10A: IOLIST X0$
0230 PRM10E: IOLIST X0$
0240 PRM10F: IOLIST X0$
0250 PRT05A: IOLIST A0$(1),A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0450 DIM P[1]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(17),A[11]
0530 LET K0=1,K9=17
0540 LET A0$(1,2)=N0$
0550 LET M1=LEN(M1$),M2=LEN(M2$),M0=LEN(M0$)
0700 REM " --- Background"
0720 CALL "SYC.WC",1,0,80,0,0,7,0
0730 PRINT 'SB',@(0,3),"Department ID:",@(5,4),"Title ID:",@(2,5),"Employee No
0730::",@(5,6),"Pay Code:",
0740 PRINT @(6+M1,8),"Dollars",@(8+M1+M2),"Units",@(0,9),"Month-To-Date",@(2,1
0740:0),"1st Quarter:",@(2,11),"2nd Quarter:",@(2,12),"3rd Quarter:",@(2,13),"
0740:4th Quarter:",@(1,14),"Year-To-Date:",@(3,15),"Next Month:",
0790 PRINT 'SF',
0900 REM " --- Init File"
0920 READ (PRT05_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRT05_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET A0$(5,13)=V2$(5,13),V2$=V2$(3,2)
1050 LET V0$="S",V1$="CEK",V3$="",V0=2,V1=15,V2=3,O9=0,V4$="Enter Department C
1050:ode, <F3>=Code Lookup"
1060 GOSUB 7000
1062 IF V3<>3 THEN GOTO 1070
1066 CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,20,V$,"E",N0$
1068 IF V$="" THEN GOTO 0900
1070 IF V3=4 THEN GOTO 9900
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3,2)=V$
1092 FIND (PRM10_DEV,KEY=N0$+"E"+A0$(3,2),DOM=1000)IOL=PRM10E
1094 PRINT @(V1+V0+1,V2),X0$(6,20),
1100 REM " --- Title"
1120 LET V0$="S",V1$="KCE",V2$=A0$(5,2),V0=2,V1=15,V2=4,V4$="Enter Title Code,
1120: <F3>=Code Lookup"
1140 GOSUB 7000
1141 IF V3=2 THEN GOTO 1000
1142 IF V3<>3 THEN GOTO 1150
1146 CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,20,V$,"F",N0$
1148 IF V$="" THEN GOTO 1100
1150 LET A0$(5,2)=V$
1160 FIND (PRM10_DEV,KEY=N0$+"F"+A0$(5,2),DOM=1100)IOL=PRM10F
1170 PRINT @(V1+V0+1,V2),X0$(6,20),
1300 REM " --- Employee"
1320 LET V0$="M",V1$="KCE",V2$=A0$(7,P[0]),V3$=P1$,V0=9,V1=15,V2=5
1325 LET V4$="Enter Employee Number, <F3>=Lookup"
1330 GOSUB 7000
1332 IF V3=2 THEN GOTO 1100
1334 IF V3=3 THEN CALL "SYC.LP",V$
1336 IF V3=4 THEN GOTO 9900
1340 LET A0$(7,9)=V$+J0$
1350 FIND (PRM01_DEV,KEY=N0$+A0$(7,9),DOM=1300)IOL=PRM01A
1360 PRINT @(V1,V2),FNF$(A0$(7,P[0]),M0$)," ",X0$(1,30),
1400 REM " --- Pay Code"
1420 LET V0$="S",V2$=A0$(16,2),V0=2,V1=15,V2=6,V1$="KC",V4$="Enter Pay Code, <
1420:F3>=Code Lookup"
1430 GOSUB 7000
1431 IF V3=2 THEN GOTO 1300
1432 IF V3<>3 THEN GOTO 1440
1436 CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,16,V$,"A",N0$
1438 IF V$="" THEN GOTO 1400
1440 LET A0$(16,2)=V$
1450 FIND (PRM10_DEV,KEY=N0$+"A"+A0$(16,2),DOM=1400)IOL=PRM10A
1460 PRINT @(V1+V0+1,V2),X0$(6,16),
1500 REM " --- Find Record"
1520 FIND (PRT05_DEV,KEY=A0$,DOM=1600)IOL=PRT05A
1540 GOSUB 5000
1590 GOTO 4000
1600 REM " --- New Record"
1620 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1640 ON POS(V$="YN") GOTO 1600,1700,1000
1700 REM " --- Set Up"
1720 DIM A[11]
2000 REM " --- Overlay"
2010 LET O9=1
2020 FOR X0=0 TO 5
2100 REM " --- Dollars"
2120 LET V0$="N",V2$=STR(A[X0]),V3$=M1$,V1=15,V2=9+X0
2130 IF X0=5 THEN LET V2=V2+1
2140 GOSUB 7000
2150 LET A[X0]=V
2160 IF V3=4 THEN GOTO 3000
2170 IF V3<>2 THEN GOTO 2200
2180 IF X0>0 THEN LET X0=X0-1
2190 GOTO 2100
2200 REM " --- Dollars"
2220 LET V0$="N",V2$=STR(A[X0+6]),V3$=M2$,V1=15+M1,V2=9+X0
2230 IF X0=5 THEN LET V2=V2+1
2240 GOSUB 7000
2260 LET A[X0+6]=V
2270 IF V3=2 THEN GOTO 2100
2290 NEXT X0
3000 GOSUB 5000
3010 GOTO 3200
4000 REM " --- Options"
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4026 PRINT @(0,22),'CL',
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (PRT05_DEV,KEY=A0$(1,K9))IOL=PRT05A
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (PRT05_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
5000 REM " --- Display"
5020 PRINT @(15,9),A[0]:M1$,A[6]:M2$,@(15,10),A[1]:M1$,A[7]:M2$,@(15,11),A[2]:
5020:M1$,A[8]:M2$,@(15,12),A[3]:M1$,A[9]:M2$,@(15,13),A[4]:M1$,A[10]:M2$,@(15,
5020:14),A[1]+A[2]+A[3]+A[4]:M1$,A[7]+A[8]+A[9]+A[10]:M2$,@(15,15),A[5]:M1$,A[
5020:11]:M2$,
5090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
