0010 REM "PRU - Period-End Update"
0020 REM "Program PRU.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRT-01",FILES$[2]="PRT-03",FILES$[3]="PRT-05"
0130 LET FILES$[4]="PRT-11",FILES$[5]="PRT-21",FILES$[6]="PRT-31"
0135 LET FILES$[7]="SYS-01"
0150 LET OPTIONS$[1]="L",OPTIONS$[2]="L"
0155 LET OPTIONS$[4]="L",OPTIONS$[5]="L",OPTIONS$[6]="L"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRT01_DEV=CHANNELS[1],PRT03_DEV=CHANNELS[2],PRT05_DEV=CHANNELS[3]
0185 LET PRT11_DEV=CHANNELS[4],PRT21_DEV=CHANNELS[5],PRT31_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists"
0210 PRT01A: IOLIST Y0$,Y[ALL],Z[ALL]
0220 PRT03A: IOLIST B0$
0230 PRT05A: IOLIST C0$,C[ALL]
0240 PRT11A: IOLIST Y0$,Y[ALL],Z[ALL]
0250 PRT21A: IOLIST Y0$,Y[ALL],Z[ALL]
0260 PRT31A: IOLIST Y0$,Y[ALL],Z[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[3],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2)),P[3]=N
0490:UM(P3$(7,1))
0500 REM " --- Init Data"
0510 PRECISION P[3]
0520 DIM C[11]
0530 LET M0=LEN(M0$)
0540 LET P9=P[1]
0700 REM " --- Background"
0720 FOR X=4 TO 4 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(32,3),"Period:",'SF','CF',
0800 REM " --- Options"
0810 LET V0$="N",V1$="E",V2$=STR(P9:"##"),V3$="##",V4$="1 Thru "+STR(P[1]:"##"
0810:),V0=2,V1=40,V2=3
0820 GOSUB 7000
0830 ON V3 GOTO 0840,0800,0800,0800,9900
0840 IF V<1 OR V>P[1] THEN GOTO 0800
0850 LET P9=V
0860 CALL "SYC.PA",SYS01_DEV,P9,P[2],P8$,P9$,STATUS
0880 PRINT @(40,3),STR(P9:"##")+"  From "+FNB$(P8$)+" To "+FNB$(P9$)
0900 REM " --- Correct?
0910 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0920 ON V3 GOTO 0930,0900,0900,0900,9900
0930 ON POS(V$="YN") GOTO 0900,1000,0800
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Earnings"
1010 PRINT @(0,10),'CE',
1020 CALL "SYC.NB","Employee Earnings",M0+3,COLUMN
1030 DIM Y[11],Z[4]
1040 READ (PRT01_DEV,KEY=N0$,DOM=1050)
1050 LET K0$=KEY(PRT01_DEV,END=1200)
1060 IF K0$(1,2)<>N0$ THEN GOTO 1200
1080 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1100 READ (PRT01_DEV)IOL=PRT01A
1120 LET Y[0]=Y[5],Y[5]=0,Y[6]=Y[11],Y[11]=0
1140 WRITE (PRT01_DEV,KEY=K0$)IOL=PRT01A
1190 GOTO 1050
1200 REM " --- Deductions"
1210 PRINT @(0,10),'CE',
1220 CALL "SYC.NB","Employee Deductions",M0+3,COLUMN
1230 DIM Y[5],Z[1]
1240 READ (PRT11_DEV,KEY=N0$,DOM=1250)
1250 LET K0$=KEY(PRT11_DEV,END=1400)
1260 IF K0$(1,2)<>N0$ THEN GOTO 1400
1280 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1300 READ (PRT11_DEV)IOL=PRT11A
1320 LET Y[0]=Y[5],Y[5]=0
1340 WRITE (PRT11_DEV,KEY=K0$)IOL=PRT11A
1390 GOTO 1250
1400 REM " --- Taxes"
1410 PRINT @(0,10),'CE',
1420 CALL "SYC.NB","Employee Taxes",M0+3,COLUMN
1430 DIM Y[17],Z[0]
1440 READ (PRT21_DEV,KEY=N0$,DOM=1450)
1450 LET K0$=KEY(PRT21_DEV,END=1600)
1460 IF K0$(1,2)<>N0$ THEN GOTO 1600
1480 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1500 READ (PRT21_DEV)IOL=PRT21A
1520 LET Y[0]=Y[5],Y[5]=0,Y[6]=Y[11],Y[11]=0,Y[12]=Y[17],Y[17]=0
1540 WRITE (PRT21_DEV,KEY=K0$)IOL=PRT21A
1590 GOTO 1450
1600 REM " --- Contributions"
1610 PRINT @(0,10),'CE',
1620 CALL "SYC.NB","Employee Contributions",M0+3,COLUMN
1630 DIM Y[17],Z[1]
1640 READ (PRT31_DEV,KEY=N0$,DOM=1650)
1650 LET K0$=KEY(PRT31_DEV,END=2000)
1660 IF K0$(1,2)<>N0$ THEN GOTO 2000
1680 PRINT @(COLUMN,11),FNF$(K0$(3,P[0]),M0$)," ",K0$(13,2),
1700 READ (PRT31_DEV)IOL=PRT31A
1720 LET Y[0]=Y[5],Y[5]=0,Y[6]=Y[11],Y[11]=0,Y[12]=Y[17],Y[17]=0
1740 WRITE (PRT31_DEV,KEY=K0$)IOL=PRT31A
1790 GOTO 1650
2000 REM " --- Init Check Read"
2010 PRINT @(0,10),'CE',
2020 CALL "SYC.NB","Examining Check",7,COLUMN
2030 READ (PRT03_DEV,KEY=N0$,DOM=2040)
2100 REM " --- Main Check Read"
2120 LET K0$=KEY(PRT03_DEV,END=3000)
2140 IF K0$(1,2)<>N0$ THEN GOTO 3000
2160 READ (PRT03_DEV)IOL=PRT03A
2180 PRINT @(COLUMN,11),B0$(3,7),
2200 REM " --- Keep Or Remove"
2210 IF LEN(B0$)<25 THEN GOTO 2800
2220 IF B0$(23,3)>P9$ THEN GOTO 2100
2800 REM " --- Remove Check"
2820 REMOVE (PRT03_DEV,KEY=K0$)
2900 GOTO 2100
3000 REM " --- Labor Dist"
3010 PRINT @(0,10),'CE',
3020 CALL "SYC.NB","Updating Department",7,COLUMN
3030 READ (PRT05_DEV,KEY=N0$,DOM=3040)
3100 REM " --- Main Labor Dist Read"
3110 LET K$=KEY(PRT05_DEV,END=3500)
3120 READ (PRT05_DEV,END=3500)IOL=PRT05A
3130 IF K$="" THEN GOTO 3500
3140 IF POS(N0$=K$)<>1 THEN GOTO 3500
3160 PRINT @(COLUMN,11),C0$(3,4),
3200 LET C[0]=C[5],C[6]=C[11],C[5]=0,C[11]=0
3300 WRITE (PRT05_DEV,KEY=K$)IOL=PRT05A
3390 GOTO 3100
4000 REM " --- Increment Period"
4010 IF P9<P[1] THEN GOTO 4100
4020 LET P[1]=P[1]+1
4030 IF P[1]>G[0] THEN LET P[1]=1,P[2]=P[2]+1
4050 EXTRACT (SYS01_DEV,KEY=N0$+N2$+"00",DOM=4060)IOL=PRS01A
4060 LET P4$(1,2)=STR(P[1]:"00"),P4$(3,2)=FNYEAR_YY21$(P[2])
4070 WRITE (SYS01_DEV,KEY=N0$+N2$+"00")IOL=PRS01A
4100 REM " --- Done"
4120 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
