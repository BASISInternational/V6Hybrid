0010 REM "PRE - Payroll Entry - Employee Information"
0020 REM "Program PRE.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,1000
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=21
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-11",FILES$[3]="PRE-21"
0130 LET FILES$[4]="PRE-31",FILES$[5]="PRE-31",FILES$[6]="PRE-41"
0135 LET FILES$[7]="PRE-51",FILES$[8]="PRM-01",FILES$[9]="PRM-02"
0140 LET FILES$[10]="PRM-10",FILES$[11]="PRM-12",FILES$[12]="PRT-01"
0150 LET FILES$[13]="PRT-02",FILES$[14]="PRT-03",FILES$[15]="PRT-11"
0155 LET FILES$[16]="PRT-21",FILES$[17]="PRT-31",FILES$[18]="PRW-01"
0157 LET FILES$[19]="SYS-01"
0160 CALL "SYC.DA",1,1,19,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET PRE01_DEV=CHANNELS[1],PRE11_DEV=CHANNELS[2],PRE21_DEV=CHANNELS[3]
0180 LET PRE31_DEV1=CHANNELS[4],PRE31_DEV2=CHANNELS[5],PRE41_DEV=CHANNELS[6]
0185 LET PRE51_DEV=CHANNELS[7],PRM01_DEV=CHANNELS[8],PRM02_DEV=CHANNELS[9]
0190 LET PRM10_DEV=CHANNELS[10],PRM12_DEV=CHANNELS[11],PRT01_DEV=CHANNELS[12]
0193 LET PRT02_DEV=CHANNELS[13],PRT03_DEV=CHANNELS[14],PRT11_DEV=CHANNELS[15]
0195 LET PRT21_DEV=CHANNELS[16],PRT31_DEV=CHANNELS[17],PRW01_DEV=CHANNELS[18]
0197 LET SYS01_DEV=CHANNELS[19]
0200 REM " --- IOLists
0210 PRE01A: IOLIST A0$(1)
0220 PRM01A: IOLIST B0$,B1$
0250 PRT02A: IOLIST E0$
0270 PRT03A: IOLIST X$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0320 PRS01C: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0330 PRS01B: IOLIST X0$,X1$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0445 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=PRS01C
0450 DIM P[3]
0460 LET P9$=X1$(6,10),PERIOD$=X1$(4,1)
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(5,1)),P[3]=NUM(P3$
0490:(7,1))
0500 REM " --- Init Data
0520 DIM B0$(11),A0$(32)
0530 LET K0=1,K9=11,L0=10,L1=11,L9=20
0540 LET G5=LEN(G5$)
0550 CALL "SYC.RM",M1$,M1,7,13
0560 CALL "SYC.RM",M2$,M2,7,10
0565 CALL "SYC.RM",M3$,M3,7,10
0570 LET B0$(1)=N0$
0600 REM " --- Test if Check Printing is already in process
0605 READ (PRW01_DEV,KEY=N0$,DOM=0610)
0610 LET K$=KEY(PRW01_DEV,END=0645)
0615 IF K$(1,2)<>N0$ THEN GOTO 0645
0620 DIM MESSAGE$[1]
0625 LET MESSAGE$[0]="I'm Sorry But Check Printing Has Already Been Started",M
0625:ESSAGE$[1]="Press <Enter> To Continue "
0630 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
0635 IF V$="SPECIAL" THEN GOTO 0645
0640 GOTO 9900
0645 CLOSE (PRW01_DEV,ERR=0650)
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[20]="GLM-01",FILES$[21]=GLW11$,OPTIONS$[21]="C"
0675 CALL "SYC.DA",1,20,21,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATU
0675:S
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[20],GLW11_DEV=CHANNELS[21]
0695 PRECISION P[3]
0700 REM " --- Background"
0720 FOR X=6 TO 9 STEP 3
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0750 PRINT 'SB',@(2,3),"Employee No:",@(4,4),"Last Name:",@(33),"First Name:",
0750:@(63),"Pay Period:",@(3,5),"Check Type:",@(20),"Seq:",@(35),"Check No:",@
0750:(64),"Date:",@(M2-7,7),"Hours",@(2*M1-10),"Earnings",@(3*M1-12),"Deductio
0750:ns",@(4*M1-7),"Taxes",@(5*M1-5),"Net",@(6*M1-9),"Contrib",
0790 PRINT 'SF',
0800 REM " --- Options
0900 REM " --- Init File
0920 READ (PRM01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key
1005 LET T1=0,T2=0
1010 PRINT 'CF','SB',@(0,10),'CL','SF',
1020 LET V2$="",V2$=KEY(PRM01_DEV,END=1050)
1025 IF V2$(1,2)<>B0$(1,2) THEN LET V2$=""
1030 IF V2$="" THEN GOTO 1050
1032 READ (PRM01_DEV,KEY=V2$)IOL=PRM01A
1034 IF B1$(170,3)<>"   " THEN GOTO 1020
1036 IF B1$(225,1)="S" THEN GOTO 1020
1038 IF POS(B1$(220,1)=P9$)=0 THEN GOTO 1020
1040 LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KEC",V3$=P1$,V4$="Enter A Valid Employee Number (<F3>=Lo
1050:okup)",V0=9,V1=15,V2=3
1060 GOSUB 7000
1065 IF V3=3 THEN GOTO 3100
1070 IF V3=4 THEN GOTO 9900
1080 IF V$="" THEN GOTO 0900
1090 LET B0$(3)=V$
1200 REM " --- Find Key
1220 FIND (PRM01_DEV,KEY=B0$(1,11),DOM=1000)IOL=PRM01A
1230 LET DEDUCT$="",TAX$="",CONTRIB$=""
1300 REM " --- Display
1310 GOSUB 6800
1320 GOSUB 5000
1325 GOSUB 5300
1330 IF B1$(170,3)="   " THEN GOTO 1370
1335 DIM MESSAGE$[1]
1340 LET MESSAGE$[0]="This Employee Has Been Terminated",MESSAGE$[1]="Press <E
1340:nter> To Continue"
1345 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
1350 GOTO 1000
1370 LET A0$(1)=B0$,A0$(12,1)="C",A0$(13,2)="01",A0$(25,1)=B1$(220,1)
1375 GOSUB STD_RATEHRS
1380 IF RATEHOURS$="N" THEN GOTO 1000
1385 IF A0$(25,1)=" " THEN GOTO 1400
1390 GOTO 1500
1400 REM " --- Period
1405 LET V4$="D=Daily/W=Weekly/B=Biweekly/S=Semimonthly/M=Monthly/Q=Quarterly/
1405:A=Annually"
1410 LET V0$="S",V1$="R",V2$=A0$(25,1),V3$="DWBSMQA",V0=1,V1=75,V2=4
1415 IF A0$(25,1)=" " THEN LET V4$="Enter Pay Period, Employee Masterfile Miss
1415:ing Default ("+V3$+")"
1420 GOSUB 7000
1425 LET A0$(25,1)=V$
1430 IF POS(A0$(25,1)=P9$)>0 THEN GOTO 1455
1435 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Pay Period Defined For '"+P9$(1,P
1435:OS(" "=P9$)-1)+"' Period, <Enter>=Reselect,<F4>=Exit",V0=1,V1=FNV(V4$),V2
1435:=22; GOSUB 7000
1440 IF V3=4 THEN LET V2$=B0$(3,P[0]); PRINT 'CF',; GOTO 1050
1450 LET A0$(25,1)=B1$(220,1); GOTO 1400
1455 IF A0$(25,1)=" " THEN GOTO 1400
1460 IF B1$(220,1)<>" " THEN GOTO 1495
1465 CALL "SYC.YN",1,"Update Employee Masterfile With Pay Period Selection",1,
1465:V$,V3
1475 ON POS(V$="YN") GOTO 1465,1480,1495
1480 LET B1$(220,1)=A0$(25,1)
1485 WRITE (PRM01_DEV,KEY=B0$)IOL=PRM01A
1495 GOTO 3900
1500 REM " --- Check Type
1520 LET V0$="S",V1$="KCER",V2$=A0$(12,1),V3$="CMRV",V4$="C=Computer  M=Manual
1520:  R=Reversal  V=Void",V0=1,V1=15,V2=5,I0=1
1530 GOSUB 7000
1540 IF V3=4 THEN GOTO 1000
1550 LET A0$(12,1)=V$
1560 IF A0$(12,1)<>"C" THEN LET A0$(13,2)="00"
1570 IF A0$(12,1)="C" THEN LET A0$(15,7)=""
1590 GOTO 3900
1600 REM " --- Check Sequence
1610 IF A0$(12,1)<>"C" THEN PRINT @(25,5),"   ",; GOTO 3900
1615 PRINT @(45,5),FILL(10),@(70,5),FILL(9),
1620 LET V0$="Z",V1$="KR",V2$=A0$(13,2),V4$=" ",V0=2,V1=25,V2=5
1630 GOSUB 7000
1640 IF V=0 THEN GOTO 1600
1650 LET A0$(13,2)=V$
1690 GOTO 3900
1700 REM " --- Check #
1710 IF A0$(12,1)="C" THEN GOTO 1800
1720 LET V0$="Z",V1$="KR",V2$=A0$(15,7),V4$=" ",V0=7,V1=45,V2=5
1725 IF V3=2 THEN GOTO 3900
1730 GOSUB 7000
1735 IF NUM(V$,ERR=1700)=0 THEN GOTO 1700
1740 LET A0$(15,7)=V$
1800 REM " --- Find Check
1810 IF V3<>2 THEN GOSUB 6000
1820 FIND (PRE01_DEV,KEY=A0$(1,21),DOM=1900)IOL=PRE01A
1830 IF A0$(25,1)=" " THEN LET A0$(25,1)=B1$(220,1)
1840 GOSUB 5200
1860 GOTO 3900
1900 REM " --- Open Checks
1920 IF POS(A0$(12,1)="CVM")<>0 THEN GOTO 3900
1940 FIND (PRT02_DEV,KEY=B0$+A0$(15,7),DOM=1700)IOL=PRT02A
1950 LET A0$(22,3)=E0$(19,3)
1960 PRINT @(70,5),FNB$(A0$(22,3)),
1990 GOTO 3900
2000 REM " --- Check Date
2010 IF POS(A0$(12,1)="MRV")=0 THEN GOTO 3900
2020 LET V0$="D",V1$="R",V2$=A0$(22,3),V0=8,V1=70,V2=5,I0=4
2040 GOSUB 7000
2050 IF V3=2 THEN GOTO 2090
2060 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
2070 IF STATUS>99 THEN GOTO 2000
2080 LET A0$(22,3)=V$
2090 GOTO 3900
3100 REM " --- Alpha Search
3110 PRINT @(V1,V2),'SB',"(Lookup)",'CL','SF',
3120 LET V0$=M0$,V5=2,V6=9,V$=""
3140 CALL "SYC.LP",V$
3150 PRINT @(V1,V2),'CL',
3160 IF V$="" THEN GOTO 1020
3170 LET X$=FNF$(V$(1,P[0]),M0$)
3180 PRINT @(V1,V2),X$
3190 GOTO 1090
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1400,1500,1600,1700,2000,4000
4000 REM " --- Correct?
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Heading Information Corre
4020:ct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="" THEN IF O9=0 THEN GOTO 4100
4040 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,1500
4100 WRITE (PRE01_DEV,KEY=A0$(1,21))IOL=PRE01A
4120 IF A0$(12,1)="V" THEN GOTO 1000
4130 LET O0=1
4190 RUN "PRE.BB"
4200 REM " --- Delete
4210 REM " --- Delete Earnings Rec 
4220 READ (PRE11_DEV,KEY=A0$(1,21),DOM=4230)
4230 LET K11$=KEY(PRE11_DEV,END=4290)
4240 IF K11$(1,21)<>A0$(1,21) THEN GOTO 4290
4250 REMOVE (PRE11_DEV,KEY=K11$)
4260 GOTO 4230
4300 REM " --- Delete Deduction Rec 
4320 READ (PRE21_DEV,KEY=A0$(1,21),DOM=4330)
4330 LET K21$=KEY(PRE21_DEV,END=4390)
4340 IF K21$(1,21)<>A0$(1,21) THEN GOTO 4390
4350 REMOVE (PRE21_DEV,KEY=K21$)
4360 GOTO 4330
4400 REM " --- Delete Tax Rec 
4420 READ (PRE31_DEV1,KEY=A0$(1,21),DOM=4430)
4430 LET K31$=KEY(PRE31_DEV1,END=4490)
4440 IF K31$(1,21)<>A0$(1,21) THEN GOTO 4490
4450 REMOVE (PRE31_DEV1,KEY=K31$)
4460 GOTO 4430
4500 REM " --- Delete Contribution Rec
4520 READ (PRE41_DEV,KEY=A0$(1,21),DOM=4530)
4530 LET K41$=KEY(PRE41_DEV,END=4590)
4540 IF K41$(1,21)<>A0$(1,21) THEN GOTO 4590
4550 REMOVE (PRE41_DEV,KEY=K41$)
4560 GOTO 4530
4600 REM " --- Delete Accrual Rec
4620 READ (PRE51_DEV,KEY=A0$(1,21),DOM=4630)
4630 LET K51$=KEY(PRE51_DEV,END=4690)
4640 IF K51$(1,21)<>A0$(1,21) THEN GOTO 4690
4650 REMOVE (PRE51_DEV,KEY=K51$)
4660 GOTO 4630
4700 REM " --- Delete Header Rec
4720 REMOVE (PRE01_DEV,KEY=A0$(1,21),DOM=4730)
4730 LET K$=KEY(PRE01_DEV,END=4790)
4735 IF LEN(K$)<21 THEN GOTO 4790
4740 IF K$(1,21)<>A0$(1,21) THEN GOTO 4790
4750 REMOVE (PRE01_DEV,KEY=K$)
4760 GOTO 4730
4790 GOTO 1000
5000 REM " --- Display
5020 PRINT @(15,4),B1$(1,16),@(45,4),B1$(17,14),@(75,4),B1$(220,1),
5090 RETURN
5200 REM " --- Check Info
5220 IF A0$(12,1)<>"C" THEN PRINT @(70,5),FNB$(A0$(22,3)),
5240 PRINT @(75,4),A0$(25,1),
5290 RETURN
5300 REM " --- Accumulate List of Pay,Tax & Contrib Codes for this Employee
5305 REM " --- Pay Codes
5310 LET X9$=""
5320 READ (PRT01_DEV,KEY=B0$+"A",END=5390,DOM=5330)
5330 LET X8$=KEY(PRT01_DEV,END=5390)
5340 IF X8$(1,11)<>B0$ THEN GOTO 5390
5360 LET X9$=X9$+X8$(12,3)
5370 READ (PRT01_DEV)
5380 GOTO 5330
5405 REM " --- Tax Codes
5420 READ (PRT21_DEV,KEY=B0$+"C",END=5490,DOM=5430)
5430 LET X8$=KEY(PRT21_DEV,END=5490)
5440 IF X8$(1,11)<>B0$ THEN GOTO 5490
5460 LET X9$=X9$+X8$(12,3)
5470 READ (PRT21_DEV)
5480 GOTO 5430
5505 REM " --- Contrib Codes
5520 READ (PRT31_DEV,KEY=B0$+"D",END=5590,DOM=5530)
5530 LET X8$=KEY(PRT31_DEV,END=5590)
5540 IF X8$(1,11)<>B0$ THEN GOTO 5590
5560 LET X9$=X9$+X8$(12,3)
5570 READ (PRT31_DEV)
5580 GOTO 5530
5590 RETURN
6000 REM " --- Find Employee/Check
6020 FIND (PRT02_DEV,KEY=A0$(1,11)+A0$(15,7),DOM=6100)
6030 IF A0$(12,1)="R" THEN GOTO 6100
6035 DIM MESSAGE$[2]
6040 LET MESSAGE$[0]="This Check Has Already Been Updated To This Employee's"
6045 LET MESSAGE$[1]="History File - You Must Use "+$22$+"R"+$22$+" In Order T
6045:o Reverse It"
6050 LET MESSAGE$[2]="             Press <Enter> To Continue"
6060 CALL "SYC.XA",2,MESSAGE$[ALL],2,22,-1,V$,V3
6090 EXITTO 1700
6100 REM " --- Find Monthly Check Record"
6120 FIND (PRT03_DEV,KEY=A0$(1,2)+A0$(15,7),DOM=6190)IOL=PRT03A
6125 IF A0$(1,11)=X$(1,2)+X$(14,9) THEN GOTO 6190
6130 IF X$(14,1)="X" THEN LET X0$="*VOID*" ELSE LET X0$=FNF$(X$(14,P[0]),M0$)
6135 DIM MESSAGE$[3]
6140 LET MESSAGE$[0]="    This Check Was Previously Issued On "+FNB$(X$(11,3))
6140:+" To"
6145 LET MESSAGE$[1]="    Employee Number "+X0$+" - You Must Use Type "+$22$+"
6145:R"+$22$
6150 LET MESSAGE$[2]="With The Correct Employee Number In Order To Reverse It"
6155 LET MESSAGE$[3]="Press <Enter> To Continue or <F2> To Change Type"
6160 CALL "SYC.XA",2,MESSAGE$[ALL],3,22,-1,V$,V3
6180 EXITTO 1700
6190 RETURN
6200 REM " --- Check that Standard Rate & Hours exist for employee
6205 STD_RATEHRS:
6210 LET RATEHOURS$="Y"
6220 READ (PRM02_DEV,KEY=B0$(1,11),DOM=6230)
6230 LET K02$=KEY(PRM02_DEV,END=6250)
6240 IF K02$(1,11)<>B0$(1,11) THEN LET RATEHOURS$="N"
6290 RETURN
6800 REM " --- Clear Lower Screen
6840 PRINT @(0,L0),'CL',
6890 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
