0010 REM "PRM - Employee Maintenance"
0020 REM "Program PRM.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4000
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=12
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-02",FILES$[3]="PRM-03"
0130 LET FILES$[4]="PRM-10",FILES$[5]="PRM-12",FILES$[6]="PRT-01"
0135 LET FILES$[7]="PRT-02",FILES$[8]="PRT-04",FILES$[9]="PRT-11"
0140 LET FILES$[10]="PRT-21",FILES$[11]="PRT-31",FILES$[12]="SYS-01"
0160 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM02_DEV=CHANNELS[2],PRM03_DEV=CHANNELS[3]
0185 LET PRM10_DEV=CHANNELS[4],PRM12_DEV=CHANNELS[5],PRT01_DEV=CHANNELS[6]
0190 LET PRT02_DEV=CHANNELS[7],PRT04_DEV=CHANNELS[8],PRT11_DEV=CHANNELS[9]
0195 LET PRT21_DEV=CHANNELS[10],PRT31_DEV=CHANNELS[11],SYS01_DEV=CHANNELS[12]
0200 REM " --- IOLists
0210 PRM01A: IOLIST A0$(1),A1$(1)
0250 PRM10H: IOLIST X$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[4],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(5,1)),P[3]=NUM(P3$
0490:(7,1))
0500 REM " --- Init Data
0520 DIM A0$(11),A1$(240),B0$(11),B1$(5*2),B[5],C[5]
0530 LET K0=1,K9=11,L0=6,L9=20,L1=6
0540 LET A0$(1,2)=N0$
0550 LET M8$="(###)-###-####",M9$="000-00-0000",M1=LEN(M1$),M2=LEN(M2$),M3=LEN
0550:(M3$)
0650 GOSUB 6600
0700 REM " --- Background
0720 CALL "SYC.WC",1,0,80,0,0,5,0
0740 PRINT 'SB',@(2,3),"Employee No:",@(4,4),"Last Name:",@(33),"First Name:",
0740:'SF',
0900 REM " --- Init File
0920 READ (PRM01_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key
1010 PRINT 'CF',@(15,4),FILL(17),@(45,4),'CL',
1020 LET V2$="",V2$=KEY(PRM01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KEC",V3$=P1$,V4$="Enter Employee Number (<F3>=Lookup)",V
1050:0=9,V1=15,V2=3,O8=1
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1075 IF V3=3 THEN CALL "SYC.LP",V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$+FILL(20,"0")
1200 REM " --- Find Key
1220 FIND (PRM01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=PRM01A
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record
1420 CALL "SYC.YN",1,"Is This A New Record?",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up
1510 LET A1$(1)=""
2000 REM " --- Overlay
2020 LET O0=1,O1=0,O9=0,O8=0
2040 RUN "PRM.MB"
4000 REM " --- Options"
4010 DIM MENUS$[3]
4015 FIND (PRM01_DEV,KEY=A0$(1,K9),DOM=4016)IOL=PRM01A
4020 LET MENUS=1,MENUS$[MENUS]="PRM.MA",WIN_X=40,WIN_Y=L0+1,O0=1,O9=0
4030 IF P3$(9,1)="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="PRM.MP"
4035 LET MENUS=MENUS+1,MENUS$[MENUS]="PRM.MJ"
4065 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4070 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4075 PRINT @(0,L0),'CE',
4080 IF SELECTION$="" THEN GOTO 1000
4085 IF SELECTION$="SYC.KA" THEN GOTO 4100
4090 RUN SELECTION$,ERR=4000
4100 REM " --- Comments"
4110 DIM INFO$[5],INFO[5]
4120 LET X$="Employee "+FNF$(A0$(3,P[0]),M0$)+" "+FNP$(A1$(1,16))+", "+FNP$(A1
4120:$(17,14))
4130 LET INFO$[0]="PRM-09",INFO$[1]=A0$,INFO$[2]=X$,ACTION=0,INFO[1]=L0
4150 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
4190 GOTO 4000
5000 REM " --- Display
5020 PRINT 'SB',@(15,3),FNF$(A0$(3,P[0]),M0$),@(15,4),A1$(1,16),@(45,4),A1$(17
5020:,14),'SF',
5090 RETURN
6000 REM " --- Clear Screen
6020 PRINT @(0,L0),'CE',
6090 RETURN
6600 REM " --- Create Heading
6610 LET H5$=""
6620 READ (PRM10_DEV,KEY=N0$+"HH",DOM=6630)
6630 READ (PRM10_DEV,END=6790)IOL=PRM10H
6640 IF X$(1,4)<>N0$+"HH" THEN GOTO 6790
6650 LET X$=X$(7,9)
6660 IF X$(9,1)<>" " THEN GOTO 6700
6670 LET X$=" "+X$,X$=X$(1,9)
6680 GOTO 6660
6700 LET H5$=H5$+X$
6720 GOTO 6630
6790 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 0900
9220 GOTO 1050
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
