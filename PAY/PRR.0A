0010 REM "PRR - Entry Earnings Report 
0020 REM "Program PRR.0A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-11",FILES$[3]="PRM-01"
0130 LET FILES$[4]="PRM-10",FILES$[5]="SYS-01"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRE01_DEV=CHANNELS[1],PRE11_DEV=CHANNELS[2],PRM01_DEV=CHANNELS[3]
0185 LET PRM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLists
0210 PRE01A: IOLIST A0$
0220 PRE11A: IOLIST W0$,W[0],W[1],W[2],W1$,W[3]
0250 PRM01A: IOLIST B0$,B1$
0270 PRM10E: IOLIST X0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0330 PRS01B: IOLIST *,X$
0400 REM " --- Parameters
0405 DIM P[4],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0445 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0450 LET H4$="Pay-Period Ending "+FNB$(X$(1,3))
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1))
0500 REM " --- Init Data
0510 LET H0=40,H1=70,H=0,H1$=N1$,H2$=N3$
0520 LET L9=59,L=L9+1,HEADINGS=3,WIDTH=80,CLOCK$=""
0530 DIM DASHES$(80,"-"),DOUBLEDASHES$(80,"="),A0$(32),HEADINGS$[HEADINGS]
0540 DIM R[2],S[2],T[2],U[2],W0$(34),W[3]
0542 CALL "SYC.RM",M1$,M1,8,13
0550 LET M0=LEN(M0$),G5=LEN(G5$),M3=LEN(M3$),M2=LEN(M2$)
0560 LET P7$="A"
0570 LET P[4]=NUM(P3$(7,1))
0580 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H4$
0700 REM " --- Run Report?"
0710 LET V4$="Do You Want To Print The "+N3$
0720 CALL "SYC.YN",1,V4$,2,V$,V3
0730 IF V3=4 THEN GOTO 9900
0740 IF V$<>"Y" THEN GOTO 9900
0750 CALL "SYC.GA",7,1,"","",STATUS
0760 IF STATUS=1 THEN GOTO 9900
0770 CALL "SYC.NB","Printing",9,COLUMN
1000 REM " --- Init Read
1010 PRECISION P[4]
1020 READ (PRE01_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read
1120 LET K$=KEY(PRE01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1133 IF K$(1,11)<>A0$(1,11) THEN GOTO 1140
1135 IF K$(12,10)<>A0$(12,10) THEN LET A0$(12,10)=K$(12,10); GOSUB 6520
1140 READ (PRE01_DEV,END=4000)IOL=PRE01A
1145 IF K$(15,7)<>A0$(15,7) THEN LET A0$(15,7)=K$(15,7); GOSUB 6520
1150 IF K$(1,2)<>N0$ THEN GOTO 4000
1180 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1200 REM " --- Employee
1210 DIM B1$(150)
1220 FIND (PRM01_DEV,KEY=A0$(1,11),DOM=1300)IOL=PRM01A
1400 REM " --- Breaks
1420 IF K$(3,P[3])<>SAVEK$ THEN GOSUB 6000
1440 IF K$(3,9)<>SAVEA0$ THEN GOSUB 6400
1490 READ (PRE11_DEV,KEY=A0$(1,21),DOM=1500)
1500 REM " --- Detail Read
1520 LET K11$=KEY(PRE11_DEV,END=4000)
1530 IF K11$(3,9)<>SAVEA0$ THEN GOTO NEXTHEADER
1540 IF K11$(1,21)<>K$ THEN GOTO NEXTHEADER
1560 READ (PRE11_DEV,KEY=K11$)IOL=PRE11A
2000 REM " --- Print
2020 IF L+1>L9 THEN GOSUB 5400
2040 PRINT (7)@(5),W0$(23,2),@(9),@(18),W0$(25,2),@(21),W0$(27,2),@(24),W0$(29
2040:,2),@(27),W[0]:M3$,W[1]:M2$,W[2]:M1$
2060 LET L=L+1
2100 FOR X=1 TO 2
2119 REM T(X)=Employee Totals, R(X)=Check Sequence Totals, S(X)=Dept. Totals, 
2119:U(X)=Grand Totals
2120 LET T[X]=T[X]+W[X],R[X]=R[X]+W[X],U[X]=U[X]+W[X],S[X]=S[X]+W[X]
2140 NEXT X
2190 GOTO 1500
2200 REM " --- Loop up for next header record
2205 NEXTHEADER:
2210 GOTO 1100
4000 REM " --- Totals
4010 REM "T9$=E Means all entries have been read, now must print all appropria
4010:te total lines before the end
4020 LET T9$="E"
4030 GOSUB 5500
4040 GOSUB 6000
4060 PRINT (7)'LF',@(10),"Grand Totals:",@(27+M3),U[1]:M2$,U[2]:M1$
4100 GOTO 9900
5000 REM " --- Heading"
5020 LET L=HEADINGS+1
5040 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5060 PRINT (7)'LF',
5100 PRINT (7)@(0),"Employee"
5110 PRINT (7)@(0),"No.",@(M0+1),"Name",@(M0+29),"Check Seq #"
5140 PRINT (7)@(5),"Seq",@(18),"EC",@(21),"TC",@(24),"DC",@(22+M3),"Rate",@(21
5140:+M3+M2),"Units",@(20+M3+M2+M1),"Amount"
5180 LET L=L+4
5190 RETURN
5200 REM " --- Sub Heading
5210 IF L+2>L9 THEN GOSUB 5000
5220 IF P[3]=0 THEN RETURN
5240 PRINT (7)'LF',@(0),SAVEK$," ",CODEDESC$
5280 LET L=L+2
5290 RETURN
5400 REM " --- Employee Heading
5410 IF L+3>L9 THEN GOSUB 5200
5420 PRINT (7)'LF',@(0),FNF$(SAVEA0$(1,P[0]),M0$)," ",EMPLNAME$,"  ",@(M0+35),
5420:A0$(13,2),
5435 PRINT (7)'LF'
5440 LET L=L+3
5490 RETURN
5500 REM " --- Check Sequence Break
5510 IF LEN(W0$)=0 THEN DIM W0$(22)
5520 PRINT (7)@(27+M3),DASHES$(1,LEN(M2$)+LEN(M1$))
5525 IF L+3>L9 THEN GOSUB 5000
5530 IF POS(W0$(12,1)="RM")=0 THEN PRINT (7)@(3),"Check Sequence '",W0$(13,2),
5530:"'",
5540 IF W0$(12,1)="M" THEN PRINT (7)@(3),"Manual Check ",W0$(15,7),
5550 IF W0$(12,1)="R" THEN PRINT (7)@(3),"Reversed Check ",W0$(15,7),
5560 PRINT (7)" Totals:",@(27+M3),R[1]:M2$,R[2]:M1$
5570 DIM R[2]
5580 LET L=L+3
5590 RETURN
6000 REM " --- Dept Break
6005 REM "T9$=D means Department Break taking place
6010 IF P[3]<>0 THEN IF SAVEK$="" THEN GOTO 6100
6020 IF T9$="" THEN LET T9$="D"
6022 GOSUB 6400
6024 IF T9$="D" THEN LET T9$=""
6026 LET SAVEA0$=""
6030 IF P[3]=0 THEN GOTO 6100
6035 IF L+2>L9 THEN GOSUB 5000
6040 PRINT (7)'LF',@(10),"Dept Totals:",@(27+M3),S[1]:M2$,S[2]:M1$
6070 LET L=L+2
6100 IF T9$="E" THEN RETURN
6120 DIM S[2]
6140 LET SAVEK$=K$(3,P[3])
6150 IF P[3]=0 THEN RETURN
6160 LET CODEDESC$=""
6170 FIND (PRM10_DEV,KEY=N0$+"E"+B1$(221,2),DOM=6200)IOL=PRM10E
6180 LET CODEDESC$=X0$(6,20)
6200 GOSUB 5200
6290 RETURN
6400 REM " --- Employee Break
6420 IF SAVEA0$="" THEN GOTO 6500
6430 IF K$(1,11)<>W0$(1,11) THEN IF W0$(13,2)>"01" THEN GOSUB 5500
6435 IF K$(12,1)<>W0$(12,1) THEN GOSUB 5500
6440 IF L+3>L9 THEN GOSUB 5000
6450 PRINT (7)@(10),"Employee Totals:",@(27+M3),T[1]:M2$,T[2]:M1$,'LF',@(10),D
6450:OUBLEDASHES$(1,51),'LF'
6460 LET L=L+3
6470 DIM R[2],W0$(34)
6500 IF T9$<>"" THEN RETURN
6510 DIM T[2]
6520 LET SAVEA0$=A0$(3,9),EMPLNAME$="***Not On File***",L2=0
6550 LET EMPLNAME$=B1$(1,30),X=POS("  "=EMPLNAME$(1,16))
6560 IF X>1 THEN LET EMPLNAME$=B1$(1,X-1)+", "+B1$(17,14)
6570 IF X<=1 THEN LET EMPLNAME$=B1$(1,16)+", "+B1$(17,14)
6575 IF POS(" "<>W0$)>0 THEN IF K$(1,11)=A0$(1,11) THEN IF K$(12,10)<>W0$(12,1
6575:0) THEN GOSUB 5500
6580 GOSUB 5400
6590 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
