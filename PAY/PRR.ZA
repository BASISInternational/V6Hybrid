0010 REM "PRR - Employee Maintenance Listing"
0020 REM "Program PRR.ZA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-02",FILES$[3]="PRM-03"
0130 LET FILES$[4]="PRM-10",FILES$[5]="PRM-12",FILES$[6]="PRT-01"
0135 LET FILES$[7]="PRT-11",FILES$[8]="PRT-21",FILES$[9]="PRT-31"
0140 LET FILES$[10]="SYS-01"
0160 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM02_DEV=CHANNELS[2],PRM03_DEV=CHANNELS[3]
0185 LET PRM10_DEV=CHANNELS[4],PRM12_DEV=CHANNELS[5],PRT01_DEV=CHANNELS[6]
0190 LET PRT11_DEV=CHANNELS[7],PRT21_DEV=CHANNELS[8],PRT31_DEV=CHANNELS[9]
0195 LET SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLists"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 DIM P[3]
0450 LET P[0]=NUM(P2$(1,2)),P[2]=NUM(P3$(5,1)),P[1]=NUM(P3$(5,1)),P[3]=NUM(P3$
0450:(7,1))
0460 LET M0=LEN(M0$)
0500 REM " --- Init Data"
0510 LET M9$="000-00-0000",M8$="(###)-###-####"
0700 REM " --- Background"
0720 FOR X=4 TO 10 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(32,3),"Option:",@(30,5),"Sequence:",@(25,7),"From Employee:"
0760:,@(25,9),"Thru Employee:",'SF',
1000 REM " --- Options"
1020 LET V0$="S",V1$="REC",V2$=P9$,V3$="ATB",V4$="A=Active T=Terminated B=Both
1020:",V0=1,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P9$=V$
1090 GOTO 3900
1200 REM " --- Sequence"
1220 LET V0$="S",V1$="C",V2$=P8$,V3$="AN",V4$="A=Alphabetic  N=Numeric",V0=1,V
1220:1=40,V2=5
1240 GOSUB 7000
1250 IF V3=2 THEN GOTO 1290
1260 LET P8$=V$
1290 GOTO 3900
1400 REM " --- From"
1410 LET P7$=""
1420 LET V0$="S",V1$="C",V2$=P7$,V4$="Enter Beginning Employee Surname",V0=16,
1420:V1=40,V2=7
1430 IF P8$="N" THEN LET V0$="M",V3$=P1$,V4$="Enter Beginning Employee Number"
1430:,V0=9
1435 LET V4$=V4$+" Or <F1>=First"
1440 GOSUB 7000
1450 IF V3<>1 THEN GOTO 1460
1452 LET P7$=""
1454 PRINT @(V1,V2),"First",'CL',
1456 GOTO 1490
1460 LET P7$=V$
1490 GOTO 3900
1600 REM " --- Thru"
1610 LET P6$=""
1620 LET V0$="S",V1$="C",V2$=P6$,V4$="Enter Ending Employee Surname",V0=16,V1=
1620:40,V2=9
1630 IF P8$="N" THEN LET V0$="M",V3$=P1$,V4$="Enter Ending Employee Number",V0
1630:=9
1635 LET V4$=V4$+" Or <F1>=Last"
1640 GOSUB 7000
1645 IF V3=2 THEN GOTO 1690
1650 IF V3<>1 THEN GOTO 1660
1652 LET P6$=""
1654 PRINT @(V1,V2),"Last",'CL',
1656 GOTO 1690
1660 LET P6$=V$
1670 IF P7$<>"" AND P6$<P7$ THEN GOTO 1600
1690 GOTO 3900
3900 REM " --- Next Field"
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
4000 REM " --- Correct"
4020 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N)?",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 ON POS(V$="YN") GOTO 4000,4100,1000
4100 RUN "PRR.ZB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
