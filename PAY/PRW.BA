0010 REM "PRX - Copy/Remove W-2 Configuration"
0020 REM "Program PRW.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-10",FILES$[2]="PRM-10",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM10_DEV=CHANNELS[1],PRM10_DEV2=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET M2$="##00.00#-"
0500 REM " --- Init Data
0505 REM "R1$ = C/R Copy or Remove
0510 REM "R2$ = Firm ID to be copied or removed
0515 REM "R2$ = Destination firm ID for copy
0560 DIM A0$(6)
0700 REM " --- Background
0710 PRINT 'CF',@(0,3),'CE'
0720 LET R1$="",R2$="",R3$=""
0730 FOR X=4 TO 8 STEP 2
0735 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(0,3),@(10,3),"Copy/Remove W-2 Configuration:",
0770 PRINT @(30,5),"From Firm:",@(23,7),"Destination Firm:",
0790 PRINT 'SF',
0900 REM " --- Init File
1000 REM "******** Copy or Remove the Configuration ****** "
1005 PRINT 'CF',
1010 LET V0$="S",V1$="CE",V3$="",V4$="C=Copy  R=Remove",V0=1,V1=41,V2=3
1015 LET A0$(1,3)=N0$+"W",I0=0
1020 GOSUB 7000
1030 IF V3=4 THEN GOTO 9900
1040 IF POS(V$="CR")=0 THEN GOTO 1000
1050 LET R1$=V$
1060 IF R1$="R" THEN PRINT @(28,5),'CL','SB',"Remove Firm:",'SF',@(0,7),'CL',
1100 REM "******** copy from or remove firm ID ****** "
1110 LET V0$="S",V1$="RCE",V2$=R2$,V3$="",V0=2,V1=42,V2=5
1120 IF R1$="C" THEN LET V4$="Enter A Valid Firm ID To Be Copied From" ELSE LE
1120:T V4$="Enter A Valid Firm ID To Be Removed"
1130 GOSUB 7000
1140 IF V3=4 THEN GOTO 9900
1150 IF V3=2 THEN GOTO 0700
1155 GOSUB 8300
1160 IF X$<>"OK" THEN GOTO 1100
1165 LET R2$=V$
1170 READ (PRM10_DEV,KEY=R2$+"W",DOM=1180)
1180 LET K$=KEY(PRM10_DEV,END=8400)
1190 IF K$(1,3)<>R2$+"W" THEN GOTO 8400
1200 REM "*********** Destination firm id *******"
1210 IF R1$="R" THEN GOTO 4000
1220 LET V0$="S",V1$="CER",V2$=R3$,V4$="Enter A Valid Destination Firm ID",V0=
1220:2,V1=42,V2=7
1230 GOSUB 7000
1240 IF V3=4 THEN GOTO 9900
1250 IF V3=2 THEN GOTO 1100
1255 GOSUB 8300
1260 IF X$<>"OK" THEN GOTO 1200
1265 LET R3$=V$
1270 READ (PRM10_DEV2,KEY=R3$+"W",DOM=1280)
1280 LET K1$=KEY(PRM10_DEV2,END=2000)
1290 IF K1$(1,3)=R3$+"W" THEN GOTO 8500
1300 GOTO 4000
2000 REM "********* Re-init PRM-10 (1) *******"
2010 READ (PRM10_DEV,KEY=R2$+"W",DOM=2015)
2015 IF R1$<>"R" THEN GOTO 2100
2020 DIM MESSAGE$[1]
2025 LET MESSAGE$[0]="All Configuration Records For Firm "+R2$+" Will Be",MESS
2025:AGE$[1]="  Removed From PRM-10.   C=Continue"
2030 CALL "SYC.XA",0,MESSAGE$[ALL],1,22,-1,V$,V3
2035 PRINT @(0,21),'CL',
2040 IF V3=4 THEN GOTO 9900
2050 IF V$<>"C" THEN GOTO 2000
2100 REM "******* Key read from PRM-10 (1) ******"
2120 LET K$=KEY(PRM10_DEV,END=4500)
2130 IF K$(1,3)<>R2$+"W" THEN GOTO 4500
2140 READ (PRM10_DEV)
2200 REM "********* Copy to (2)/Remove from (1) config records ******"
2220 IF R1$="R" THEN REMOVE (PRM10_DEV,KEY=K$,DOM=2100); GOTO 2100
2230 READ RECORD(PRM10_DEV,KEY=K$)A0$
2240 LET A0$(1,2)=R3$
2250 WRITE RECORD(PRM10_DEV2,KEY=A0$(1,6))A0$
2260 GOTO 2100
4000 REM " --- Correct?
4010 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct 
4010:(Y/N)? ",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 ON POS(V$="YN") GOTO 4000,2000,0700
4500 REM "****** Copy/Remove Complete *****"
4505 DIM MESSAGE$[0]
4510 IF R1$="C" THEN LET V5$="Copy" ELSE LET V5$="Removal"
4520 LET MESSAGE$[0]="W-2 Configuration "+V5$+" Complete, <Enter>:"
4530 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
4540 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8300 REM " --- Verify Valid Firm for Payroll"
8310 LET X$=""
8320 FIND (SYS01_DEV,KEY=V$+"PR00",DOM=8390)
8330 LET X$="OK"
8390 RETURN
8400 REM "******** Copy From/Remove Configuration Records Missing
8410 DIM MESSAGE$[0]
8420 LET MESSAGE$[0]="Firm "+R2$+" Has No W-2 Configuration Records, <Enter>:"
8430 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
8450 GOTO 1100
8500 REM "*********** Destination Configuration Records Present"
8510 DIM MESSAGE$[1]
8520 LET MESSAGE$[0]="Firm "+R3$+" Has W-2 Configuration Records.",MESSAGE$[1]
8520:="Remove Before Copying, <Enter>: "
8530 CALL "SYC.XA",0,MESSAGE$[ALL],1,22,-1,V$,V3
8560 GOTO 0700
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
