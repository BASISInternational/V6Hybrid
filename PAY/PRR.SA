0010 REM "PRR - DATE LIST"
0020 REM "Program PRR.SA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-10",FILES$[3]="PRW-02"
0130 LET FILES$[4]="SYS-01"
0140 LET OPTIONS$[3]="C"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM10_DEV=CHANNELS[2],PRW02_DEV=CHANNELS[3]
0190 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data
0510 LET H0=40,H1=70,H=0,H1$=N1$,H2$=N3$
0520 LET L9=50,L=L9+1
0550 LET X=POS("CR"=M1$)
0560 IF X>1 THEN LET M1$="-"+M1$(1,X-1)
0580 LET M0=LEN(M0$)
0590 LET P9$="C"
0700 REM " --- Background
0720 FOR X=4 TO 10 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(29,3),"Date Type:",@(29,5),"From Date:",@(29,7),"Thru Date:"
0760:,@(32,9),"Option:",
0790 PRINT 'SF',
1000 REM " --- Date Type
1010 PRINT 'CF',
1015 LET P7$="",P8$="",P9$=""
1020 LET V0$="S",V1$="ERC",V2$=P9$,V3$="ABHRT",V4$="A=Anniversary  B=Birth  H=
1020:Hire  R=Review  T=Termination",V0=1,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P9$=V$
1100 REM " --- Create Headin
1120 IF P9$="A" THEN LET H3$="Anniversary",P9=164
1130 IF P9$="B" THEN LET H3$="Birth",P9=158
1140 IF P9$="H" THEN LET H3$="Hire",P9=161
1150 IF P9$="R" THEN LET H3$="Review",P9=167
1160 IF P9$="T" THEN LET H3$="Termination",P9=170
1170 LET H3$=H3$+" Date"
1190 GOTO 3900
1200 REM " --- From Date
1220 LET V0$="D",V1$="",V2$=P8$,V0=8,V1=40,V2=5
1240 GOSUB 7000
1260 LET P8$=V$
1270 IF POS(" "<>P8$)=0 THEN LET P8$=""
1280 IF P8$="" THEN PRINT @(40,5),"First Date",
1290 GOTO 3900
1400 REM " --- Thru Date
1420 LET V0$="D",V1$="",V2$=P7$,V0=8,V1=40,V2=7
1440 GOSUB 7000
1460 LET P7$=V$
1470 IF POS(" "<>P7$)=0 THEN LET P7$=""
1480 IF P7$="" THEN PRINT @(40,7),"Last Date",
1490 GOTO 3900
1600 REM " --- Active/Terminated
1620 LET V0$="S",V1$="RC",V2$=P6$,V3$="ATB",V4$="A=Active, T=Terminated, B=Bot
1620:h",V0=1,V1=40,V2=9
1640 GOSUB 7000
1660 LET P6$=V$
1690 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
4000 REM " --- Correct?
4020 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/End)?",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 RUN "PRR.SB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
