0010 REM "PRR - DATE LIST 2"
0020 REM "Program PRR.SB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 PRM01A: IOLIST A0$,A1$
0250 PRW02A: IOLIST X0$
0500 REM " --- Init Data
0510 LET HEADINGS=7,CLOCK$="",WIDTH=80
0515 DIM HEADINGS$[HEADINGS]
0520 LET T0=0
0530 LET X0$="First Date",X1$="Last Date"
0540 IF P8$<>"" THEN LET X0$=FNB$(P8$)
0550 IF P7$<>"" THEN LET X1$=FNB$(P7$)
0560 LET H4$="From "+X0$+" Thru "+X1$
0590 GOSUB 6000
0600 CALL "SYC.GA",7,1,"","",STATUS
0605 IF STATUS=1 THEN GOTO 9900
0620 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H3$,HEADINGS$[3]=H4$
0700 REM " --- Background
0720 CALL "SYC.NB","Printing:",8,COLUMN
1000 REM " --- Init Read
1020 READ (PRW02_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read
1120 LET K0$=KEY(PRW02_DEV,END=4000)
1130 IF K0$(1,2)<>N0$ THEN GOTO 4000
1140 READ (PRW02_DEV)
1160 PRINT @(COLUMN,11),FNB$(K0$(3,3)),
1400 REM " --- Employee
1410 FIND (PRM01_DEV,KEY=K0$(1,2)+K0$(6,9),DOM=1100)IOL=PRM01A
1420 LET Q=POS("  "=A1$(1,16))-1
1430 IF Q<1 THEN LET Q=16
1440 LET X1$=A1$(1,Q)+", "+A1$(17,13)
1450 IF P6$="A" THEN IF A1$(170,3)<>"   " THEN GOTO 1100
1460 IF P6$="T" THEN IF A1$(170,3)="   " THEN GOTO 1100
2000 REM " --- Print
2020 IF L+1>L9 THEN GOSUB 5000
2100 PRINT (7)@(0),FNB$(A1$(P9,3)),@(10),FNF$(A0$(3,P[0]),M0$)," ",X1$
2120 LET S0$=K0$(3,3),L=L+1,T0=T0+1
2190 GOTO 1100
4000 REM " --- Totals
4100 GOTO 9900
5000 REM " --- Heading"
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(10),"Employee"
5110 PRINT (7)@(2),"Date",@(10),"No.",@(11+M0),"Name",'LF'
5170 LET L=L+3
5190 RETURN
6000 REM " --- Sort
6020 CALL "SYC.NB","Sorting:",M0,COLUMN
6040 READ (PRM01_DEV,KEY=N0$,DOM=6050)
6100 READ (PRM01_DEV,END=6290)IOL=PRM01A
6120 IF A0$(1,2)<>N0$ THEN GOTO 6290
6140 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
6150 LET Y0$=A1$(P9,3)
6160 IF Y0$="   " THEN GOTO 6100
6170 IF P8$<>"" THEN IF Y0$<P8$ THEN GOTO 6100
6180 IF P7$<>"" THEN IF Y0$>P7$ THEN GOTO 6100
6190 IF P9$="B" THEN LET Y0$(1,1)=""
6200 LET X0$=N0$+Y0$+A0$(3,9)
6240 WRITE (PRW02_DEV,KEY=X0$)IOL=PRW02A
6250 GOTO 6100
6290 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
