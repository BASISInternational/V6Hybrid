0010 REM "PRR - Check History Report
0020 REM "Program  PRR.2A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-10",FILES$[3]="PRT-02"
0140 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM10_DEV=CHANNELS[2],PRT02_DEV=CHANNELS[3]
0195 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0240 PRM01A: IOLIST *,X$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[4],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1))
0500 REM " --- Init Data
0540 DIM GRANDTOT[7],EMPTOT[7],A[7]
0542 LET X=POS("CR"=M1$)
0544 IF X<>0 THEN LET M1$=M1$(1,X-1)+"-"
0546 IF LEN(M1$)>13 THEN LET M1$="#####,###.00-"
0550 LET M0=25,M1=LEN(M1$)
0570 LET X=POS("-"=P1$)
0580 IF X>0 THEN LET P[4]=X-1
0700 REM " --- Background
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(29,3),"From Date:",@(29,5),"Thru Date:",@(25,7),"From Employ
0740:ee:",@(25,9),"Thru Employee:",'SF',
1000 REM " --- From Date
1005 PRINT 'CF',
1010 LET V0$="D",V1$="EC",V2$=FROMDATE$,V4$="Enter Starting Date, <F1> For Fir
1010:st",V0=8,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 IF V3=4 THEN GOTO 9900
1040 IF V$="" OR V3=1 THEN PRINT @(40,3),"First Date",'CL',; LET FROMDATE$="";
1040: GOTO 1090
1050 LET FROMDATE$=V$
1060 IF POS(" "<>FROMDATE$)=0 THEN LET V$=""; GOTO 1040
1090 GOTO 3900
1100 REM " --- Thru Date
1110 LET V0$="D",V1$="EC",V2$=THRUDATE$,V4$="Enter Ending Date, <F1> For Last"
1110:,V0=8,V1=40,V2=5
1120 GOSUB 7000
1130 IF V3=4 THEN GOTO 9900
1140 IF V$="" OR V3=1 THEN PRINT @(40,5),"Last Date",'CL',; LET THRUDATE$=""; 
1140:GOTO 1190
1150 LET THRUDATE$=V$
1160 IF THRUDATE$<FROMDATE$ THEN GOTO 1100
1170 IF POS(" "<>THRUDATE$)=0 THEN LET V$=""; GOTO 1140
1190 GOTO 3900
1200 REM " --- From Employee
1210 LET V0$="M",V1$="EC",V2$=FROMEMPL$,V3$=M0$,V4$="Enter Employee Number, <F
1210:1>=First, <F3>=Lookup",V0=P[0],V1=40,V2=7
1220 GOSUB 7000
1230 IF V3=4 THEN GOTO 9900
1235 IF V3=3 THEN CALL "SYC.LP",V$
1240 IF V$="" OR V3=1 THEN PRINT @(V1,V2),"First",'CL',; LET FROMEMPL$=""; GOT
1240:O 1290
1250 LET X0$=V$+FILL(9-LEN(V$),"0")
1260 FIND (PRM01_DEV,KEY=N0$+X0$,DOM=1200)IOL=PRM01A
1270 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",X$(1,30),
1280 LET FROMEMPL$=V$
1290 GOTO 3900
1300 REM " --- Thru Employee
1310 LET V0$="M",V1$="EC",V2$=THRUEMPL$,V3$=M0$,V4$="Enter Employee Number, <F
1310:1>=Last, <F3>=Lookup",V0=P[0],V1=40,V2=9
1320 GOSUB 7000
1330 IF V3=4 THEN GOTO 9900
1335 IF V3=3 THEN CALL "SYC.LP",V$
1340 IF V$="" OR V3=1 THEN PRINT @(V1,V2),"Last",'CL',; LET THRUEMPL$=""; GOTO
1340: 1390
1350 LET X1$=V$+FILL(9-LEN(V$),"0")
1355 IF X1$<X0$ THEN GOTO 1300
1360 FIND (PRM01_DEV,KEY=N0$+X1$,DOM=1300)IOL=PRM01A
1370 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",X$(1,30),
1380 LET THRUEMPL$=V$
1390 GOTO 3900
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,3990
4000 REM " --- Options
4010 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct 
4010:(Y/N)?",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="N" THEN GOTO 1000
4040 IF V3=4 THEN GOTO 9900
4050 IF V$<>"Y" THEN GOTO 4000
4100 RUN "PRR.2B"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
