0010 REM "PRM - Deduction Code Maintenance Listing"
0020 REM "Program PRM.BC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS=1 THEN GOTO 9900
0200 REM " --- IOLISTs"
0210 PRM10B: IOLIST A0$(1),A1$(1),A[ALL],A2$(1),A3$(1)
0500 REM " --- Init Data"
0505 PRECISION P[3]
0510 LET L9=59,L=L9+1,HEADINGS=2,WIDTH=80,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$+" Listing"
0540 LET T0=0
0550 DIM A0$(33),A1$(10),A2$(10),A3$(5),A[1]
0700 REM " --- Background"
0720 CALL "SYC.NB","Printing",19,COLUMN
1000 REM " --- Initial Read"
1020 READ (PRM10_DEV,KEY=N0$+"B",DOM=1030)
1100 REM " --- Main Read"
1120 LET K$=KEY(PRM10_DEV,END=4000)
1130 IF POS(N0$+"B"=K$)<>1 THEN GOTO 4000
1140 READ (PRM10_DEV)IOL=PRM10B
1200 REM " --- Breaks"
1220 IF A0$(1,3)<>N0$+"B" THEN GOTO 4000
1240 PRINT @(COLUMN,11),A0$(4,2)," ",A0$(6,16),
2000 REM " --- Print"
2010 IF L+2>L9 THEN GOSUB 5000
2020 PRINT (7)@(0),A0$(4,2),@(5),A0$(6,16),@(22),A0$(22,2),@(25),A0$(24,1),@(2
2020:7),A0$(25,5),@(33),A0$(30,2),
2030 IF GL$="Y" THEN PRINT (7)@(36),FNF$(A1$(1,G[3]),G5$),
2040 PRINT (7)@(36+G5),A[0]:M1$,A[1]:M1$,
2050 FOR X=0 TO 4
2060 PRINT (7)@(37+G5+2*M1+X*3),A2$(X*2+1,2),
2070 NEXT X
2080 PRINT (7)'LF',
2090 LET L=L+1
2100 FOR X=0 TO 4
2110 PRINT (7)@(38+G5+2*M1+X*3),A3$(X+1,1),
2120 NEXT X
2130 PRINT (7)'LF',
2200 LET L=L+1,T0=T0+1
2290 GOTO 1100
4000 REM " --- End Of File"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)'LF',@(10),"Total Records:",T0
4030 LET L=L+2
4100 GOTO 9900
5000 REM " --- Heading"
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS<>0 THEN EXITTO 9900
5110 PRINT (7)@(0),"Deduction",@(27),"Period",
5120 IF GL$="Y" THEN PRINT (7)@(36),"GL",
5130 PRINT (7)@(28+G5+M1),"Deduct",@(28+G5+2*M1),"Deduct",@(37+G5+2*M1),"Total
5130: To/"
5140 PRINT (7)@(0),"Code",@(5),"Description",@(22),"RC",@(25),"T",@(27),"Taken
5140:",@(33),"TC",
5150 IF GL$="Y" THEN PRINT (7)@(36),"Account",
5160 PRINT (7)@(30+G5+M1),"Rate",@(29+G5+2*M1),"Limit",@(37+G5+2*M1),"Operator
5160:",'LF'
5180 LET L=L+3
5190 RETURN
8000 REM " --- Functions"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
