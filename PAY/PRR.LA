0010 REM "PRR - Contribution Status Report"
0020 REM "Program PRR.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-10",FILES$[3]="PRT-31"
0130 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM10_DEV=CHANNELS[2],PRT31_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists"
0210 PRM01A: IOLIST B0$,B1$
0220 PRM10D: IOLIST A0$
0230 PRT31A: IOLIST Y0$,Y[ALL],Z[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0480 DIM P[2]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0510 LET L9=59,L=L9+1,HEADINGS=3,WIDTH=132,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$
0540 DIM T[2],U[2],V[2],Y[17],Z[1]
0550 LET M0=LEN(M0$)
0560 CALL "SYC.RM",M1$,M1,8,12
0570 LET M9$="000-00-0000"
0700 REM " --- Background"
0720 FOR X=4 TO 6 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(31,3),"Quarter:",@(21,5),"Contribution Code:",'SF','CF',
0800 REM " --- Quarter"
0805 LET V0$="N",V1$="E",V2$=STR(P9:"#"),V3$="#",V4$="1=First  2=Second  3=Thi
0805:rd  4=Fourth",V0=1,V1=40,V2=3
0810 GOSUB 7000
0820 ON V3 GOTO 0830,0800,0800,0800,9900
0830 IF V<1 OR V>4 THEN GOTO 0800
0840 LET P9=V,HEADINGS$[2]="Quarter "+STR(P9)
0850 REM " --- Contribution Code"
0855 LET V0$="S",V1$="KC",V2$=P8$,V3$="",V4$="<F1>=All",V0=2,V1=40,V2=5
0860 GOSUB 7000
0865 ON V3 GOTO 0875,0870,0800,0850,0850
0870 LET P8$=""; PRINT @(V0+V1+1,V2),'CL',"All",; GOTO 0900
0875 IF POS(" "<>V$)=0 THEN GOTO 0870
0880 LET V$=V$+FILL(V0),V$=V$(1,V0)
0885 FIND (PRM10_DEV,KEY=N0$+"D"+V$,DOM=0850)IOL=PRM10D
0890 LET P8$=V$
0895 PRINT @(V0+V1+1,V2),A0$(6,16),
0900 REM " --- Correct?"
0910 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0920 ON V3 GOTO 0930,0900,0900,0900,9900
0930 ON POS(V$="YN") GOTO 0900,0950,0800
0950 REM " --- Open Printer"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS>0 THEN GOTO 9900
0970 CALL "SYC.NB","Printing",16,COLUMN
1000 REM " --- Init Read"
1020 READ (PRM10_DEV,KEY=N0$+"D",DOM=1030)
1100 REM " --- Main Read"
1120 LET K0$=KEY(PRM10_DEV,END=4000)
1130 IF K0$(1,3)<>N0$+"D" THEN GOTO 4000
1140 READ (PRM10_DEV)IOL=PRM10D
1160 PRINT @(COLUMN,11),A0$(6,16),
1170 IF P8$<>"" THEN IF P8$<>A0$(4,2) THEN GOTO 1100
1180 LET L=L9+1,L1=0
1190 READ (PRM01_DEV,KEY=N0$,DOM=1200)
1400 REM " --- Employee"
1410 READ (PRM01_DEV,END=3000)IOL=PRM01A
1420 IF B0$(1,2)<>N0$ THEN GOTO 3000
1440 LET X1$=B1$(1,16),X=POS("  "=X1$)
1450 IF X>1 THEN LET X1$=X1$(1,X-1)
1460 IF LEN(X1$)=16 THEN IF X1$(16,1)=" " THEN LET X1$=X1$(1,15)
1470 LET X1$=X1$+", "+B1$(17,14)
1600 REM " --- Get Detail"
1620 READ (PRT31_DEV,KEY=B0$+A0$(3,3),DOM=1400)IOL=PRT31A
1630 LET Y0=0,Y1=0,Y2=0
1640 FOR X=1 TO P9
1660 LET Y0=Y0+Y[X],Y1=Y1+Y[6+X],Y2=Y2+Y[12+X]
1670 LET V[0]=V[0]+Y[X],V[1]=V[1]+Y[6+X],V[2]=V[2]+Y[12+X]
1680 NEXT X
2000 REM " --- Print"
2010 FOR X=0 TO 2
2012 IF Y[X*6]<>0 THEN IF X*6+P9<>0 THEN EXITTO 2020
2014 NEXT X
2016 IF Y0=0 THEN IF Y1=0 THEN IF Y2=0 THEN GOTO 1400
2020 IF L+3>L9 THEN GOSUB 5000
2040 PRINT (7)@(0),FNF$(B0$(3,P[0]),M0$),@(1+M0),Y[0]:M1$,Y[6]:M1$,Y[12]:M1$,Y
2040:[P9]:M1$,Y[6+P9]:M1$,Y[12+P9]:M1$,Y0:M1$,Y1:M1$,Y2:M1$
2050 PRINT (7)@(2),X1$,@(32),"(",FNF$(B1$(136,9),M9$),")",'LF'
2060 LET L=L+3,L1=L1+1
2100 FOR X=0 TO 2
2120 LET T[X]=T[X]+Y[X*6],U[X]=U[X]+Y[X*6+P9]
2130 NEXT X
2190 GOTO 1400
3000 REM " --- Subtatal"
3020 GOSUB 6000
3090 GOTO 1100
4000 REM " --- Totals"
4100 REM " --- Done"
4120 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5070 PRINT (7)@(0),"Contribution Code: ",A0$(4,2)+" "+A0$(6,16),'LF'
5100 PRINT (7)@(0),"Employee",@((-9)+M0+2*M1),"Current Month",@((-9)+M0+5*M1),
5100:"Current Quarter",@((-8)+M0+8*M1),"Year-To-Date"
5110 PRINT (7)@(0),"Number",@((-4)+M0+M1),"Gross",@((-6)+M0+2*M1),"Taxable",@(
5110:(-3)+M0+3*M1),"Tax",@((-4)+M0+4*M1),"Gross",@((-6)+M0+5*M1),"Taxable",@((
5110:-3)+M0+6*M1),"Tax",@((-4)+M0+7*M1),"Gross",@((-6)+M0+8*M1),"Taxable",@((-
5110:3)+M0+9*M1),"Tax",'LF',@(2),"Name",@(32),"SS Number",'LF'
5180 LET L=L+6
5190 RETURN
6000 REM " --- Subtotal"
6020 IF L1=0 THEN RETURN
6030 IF L+1>L9 THEN GOSUB 5000
6040 PRINT (7)'LF',@(0),"Totals:",@(1+M0),T[0]:M1$,T[1]:M1$,T[2]:M1$,U[0]:M1$,
6040:U[1]:M1$,U[2]:M1$,V[0]:M1$,V[1]:M1$,V[2]:M1$
6050 LET L=L+1
6060 DIM T[2],U[2],V[2]
6090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
