0010 REM "PRM - Department Maintenance - Contributions"
0020 REM "Program PRM.ED"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 PRM10D: IOLIST X0$
0220 PRM10E: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A7$(1),A8
0220:$(1),A[ALL]
0230 GLM01A: IOLIST X$,X0$
0700 REM " --- Background"
0710 PRINT @(0,L0),'CE',
0720 PRINT 'SB',@(15,L0-1),"Contribution Code",
0730 IF GL$="Y" THEN PRINT @(35),"GL Expense",@(55),"GL Accrual",
0740 FOR X=1 TO P7
0750 PRINT @(11,L0-1+X),X:"##.",
0760 NEXT X
0790 PRINT 'SF',
1000 REM " --- Display"
1020 GOSUB 5000
1040 IF POS(" "<>A6$)<>0 THEN GOTO 4000
2000 REM " --- Contribution Code"
2010 FOR X=0 TO P7-1
2020 LET V0$="S",V1$="KEC",V2$=A6$(X*2+1,2),V0=2,V1=15,V2=L0+X
2025 LET V4$="Enter A Valid Contribution Code,  <F3>=Lookup"
2040 GOSUB 7000
2050 IF V3=4 THEN EXITTO 4000
2060 IF V3<>2 THEN GOTO 2100
2070 IF X=0 THEN GOTO 2020
2080 LET X=X-1
2090 GOTO 2020
2100 IF V3<>3 THEN GOTO 2110
2104 CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,16,V$,"D",N0$
2106 IF V$="" THEN GOTO 2020
2110 LET A6$(X*2+1,2)=V$
2120 FIND (PRM10_DEV,KEY=N0$+"D"+A6$(X*2+1,2),DOM=2020)IOL=PRM10D
2130 PRINT @(V1+3,V2),X0$(6,16),
2200 REM " --- GL Expense"
2210 IF GL$="N" THEN GOTO 2390
2220 LET V0$="M",V1$="C",V2$=A7$(X*G[4]+1,G[3]),V3$=G1$,V4$="Enter A Valid G/L
2220: Account Number,  <F3>=Lookup",V0=G[4],V1=35,V2=L0+X
2240 GOSUB 7000
2242 IF V3<>3 THEN GOTO 2250
2244 CALL "SYC.LG",V$
2246 IF V$="" THEN GOTO 2200
2248 PRINT @(V1,V2),FNF$(V$(1,G[3]),G1$)
2250 LET A7$(X*G[4]+1,G[4])=V$
2260 IF V3=2 THEN GOTO 2020
2270 FIND (GLM01_DEV,KEY=N0$+A7$(X*G[4]+1,G[4]),DOM=2200)IOL=GLM01A
2280 PRINT @(V1,V2),FNF$(A7$(X*G[4]+1,G[3]),G5$),@(V1+G5+1,V2),X0$(1,MIN(35,80
2280:-V1+G5+1))
2300 REM " --- GL Accrual"
2320 LET V0$="M",V1$="C",V2$=A8$(X*G[4]+1,G[3]),V3$=G1$,V4$="Enter A Valid G/L
2320: Account Number,  <F3>=Lookup",V0=G[4],V1=55,V2=L0+X
2340 GOSUB 7000
2342 IF V3<>3 THEN GOTO 2350
2344 CALL "SYC.LG",V$
2346 IF V$="" THEN GOTO 2300
2348 PRINT @(V1,V2),FNF$(V$(1,G[3]),G1$)
2350 LET A8$(X*G[4]+1,G[4])=V$
2360 IF V3=2 THEN GOTO 2200
2370 FIND (GLM01_DEV,KEY=N0$+A8$(X*G[4]+1,G[4]),DOM=2300)IOL=GLM01A
2380 PRINT @(V1,V2),FNF$(A8$(X*G[4]+1,G[3]),G5$),
2390 NEXT X
4000 REM " --- Options"
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4026 PRINT @(0,22),'CL',
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (PRM10_DEV,KEY=A0$(1,K9))IOL=PRM10E
4190 RUN "PRM.EA"
4200 REM " --- Delete"
4220 LET A6$(1)="",A7$(1)="",A8$(1)=""
4290 RUN "PRM.EA"
5000 REM " --- Display"
5020 FOR X=0 TO P7-1
5030 IF A6$(X*2+1,2)="  " THEN GOTO 5050
5040 PRINT @(15,L0+X),A6$(X*2+1,2),
5050 IF GL$="Y" THEN PRINT @(35,L0+X),FNF$(A7$(X*G[4]+1,G[3]),G5$),@(55,L0+X),
5050:FNF$(A8$(X*G[4]+1,G[3]),G5$),
5060 NEXT X
5090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
