0010 REM "PRM - Department Maintenance"
0020 REM "Program PRM.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4900
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-10",FILES$[2]="SYS-01",FILES$[3]="GLM-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists"
0210 PRM10E: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A7$(1),A8
0210:$(1),A[ALL]
0230 GLM01A: IOLIST X$,X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0280 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(32),A1$(2*G[4])
0530 LET K0=1,K9=5,L0=8,O0=1
0540 LET A0$(1,3)=N0$+"E"
0550 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),G5=LEN(G5$)
0560 LET P9=14,P8=10,P7=14
0570 DIM A2$(P9*2),A3$(P9*G[4]),A4$(P8*2),A5$(P8*G[4]),A6$(P7*2),A7$(P7*G[4]),
0570:A8$(P7*G[4]),A[1]
0600 REM " --- Additional Opens"
0610 DIM INFO$[20]
0620 CALL "SYC.VA","PR",INFO$[ALL]
0630 LET GL$=INFO$[9]
0640 IF GL$<>"Y" THEN GOTO 0700
0650 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0660 IF STATUS>0 THEN GOTO 9900
0670 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0720 CALL "SYC.WC",1,0,80,0,0,7,0
0730 PRINT 'SB',@(0,3),"Department ID:",@(2,4),"Description:",
0740 IF GL$="Y" THEN PRINT @(1,5),"Cash Account:",@(1,6),"Acc. Account:",
0750 PRINT @(79-(M2+23),3),"Budgeted Daily Hours:",@(79-(M3+30),4),"Budgeted A
0750:verage Hourly Rate:",
0790 PRINT 'SF',
0900 REM " --- Init File"
0920 READ (PRM10_DEV,KEY=A0$(1,3),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="CEK",V3$="",V4$="Enter Up To 2 Characters, <F3>=Lookup, 
1050:<F4>=End",V0=2,V1=15,V2=3,O9=0
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,16,V$,"E",N0$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Find Key"
1220 FIND (PRM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=PRM10E
1230 IF GL$="N" THEN LET A1$(1)="",A3$(1)="",A5$(1)="",A7$(1)="",A8$(1)=""
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4900
1400 REM " --- New Record"
1420 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup"
1510 LET A0$(6)="",A1$(1)="",A2$(1)="",A3$(1)="",A4$(1)="",A5$(1)="",A6$(1)=""
1510:,A7$(1)="",A8$(1)=""
1520 DIM A[1]
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=15,V2=4,I0=0
2040 GOSUB 7000
2060 LET A0$(6,20)=V$
2090 GOTO 3900
2100 REM " --- Accounts"
2110 LET X0=I0-1
2115 IF GL$="N" THEN GOTO 2190
2120 LET V0$="M",V1$="RC",V2$=A1$(G[4]*X0+1,G[3]),V3$=G1$,V0=G[4],V1=15,V2=X0+
2120:5
2125 LET V4$="Enter A Valid G/L Account, <F3>=Lookup"
2130 GOSUB 7000
2140 IF V3=2 OR V3=4 THEN GOTO 3900
2146 IF V3=3 THEN CALL "SYC.LG",V$
2148 IF V$="" THEN GOTO 2120
2150 LET A1$(G[4]*X0+1,G[4])=V$
2160 FIND (GLM01_DEV,KEY=N0$+A1$(G[4]*X0+1,G[4]),DOM=2100)IOL=GLM01A
2170 PRINT @(V1,V2),FNF$(A1$(G[4]*X0+1,G[3]),G5$)," ",X0$(1,35),
2190 GOTO 3900
2200 REM " --- Hours"
2220 LET V0$="N",V1$="",V2$=STR(A[0]),V3$=M2$,V0=M2,V1=79-M2,V2=3
2240 GOSUB 7000
2260 LET A[0]=V
2290 GOTO 3900
2300 REM " --- Cost"
2320 LET V0$="N",V1$="",V2$=STR(A[1]),V3$=M3$,V0=M3,V1=79-M3,V2=4
2340 GOSUB 7000
2360 LET A[1]=V
2390 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2100,2200,2300,3990
4000 REM " --- Options"
4002 IF GL$="N" THEN GOTO 4020
4005 GOSUB 6500
4010 IF F9$<>"11" THEN GOTO 4400
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (PRM10_DEV,KEY=A0$(1,K9))IOL=PRM10E
4190 GOTO 4900
4200 REM " --- Delete"
4220 REMOVE (PRM10_DEV,KEY=A0$(1,5),DOM=4230)
4230 LET K$=KEY(PRM10_DEV,END=4290)
4240 IF K$(1,5)<>A0$(1,5) THEN GOTO 4290
4250 REMOVE (PRM10_DEV,KEY=K$)
4260 GOTO 4230
4290 GOTO 1000
4400 REM " --- GL Account Required"
4410 DIM MESSAGE$[1]
4420 LET MESSAGE$[0]=" G/L Account Number Is Required!"
4425 LET MESSAGE$[1]="<Enter>=Re-Try, <F4>=Exit"
4430 CALL "SYC.XA",3,MESSAGE$[ALL],1,22,-1,V$,V3
4440 IF V3=4 THEN GOTO 4460
4450 GOTO 3960
4460 DIM MESSAGE$[1]
4470 LET MESSAGE$[0]="This Department Code Will Be Deleted!"
4475 LET MESSAGE$[1]="    <Enter> To Continue"
4480 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4490 GOTO 4200
4500 REM " --- Print"
4520 CALL "SYC.YN",1,"Do You Wish To Print A List Of All Records",1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4580 RUN "PRM.EE"
4900 REM " --- Submenu"
4910 DIM MENUS$[1]
4920 LET MENUS=1,MENUS$[MENUS]="PRM.EA",WIN_X=40,WIN_Y=L0+1
4965 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4970 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4975 PRINT @(0,L0),'CE',
4980 IF SELECTION$="" THEN GOTO 1000
4985 IF SELECTION$=PGM(-2) THEN GOTO 2000
4990 RUN SELECTION$,ERR=4900
5000 REM " --- Display"
5010 PRINT @(15,4),A0$(6,20),
5020 IF GL$="Y" THEN PRINT @(15,5),FNF$(A1$(1,G[3]),G5$),@(15,6),FNF$(A1$(G[4]
5020:+1,G[3]),G5$),
5030 PRINT @(79-M2,3),A[0]:M2$,@(79-M3,4),A[1]:M3$,
5090 RETURN
6500 REM " --- Verify
6510 DIM F9$(2)
6520 FOR I=0 TO 1
6530 FIND (GLM01_DEV,KEY=N0$+A1$(G[4]*I+1,G[4]),DOM=6560)
6550 LET F9$(I+1,1)="1"
6560 NEXT I
6590 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functoins"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
