0010 REM "PRR - Monthly Check Report"
0020 REM "Program PRR.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRT-03",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRT03_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists"
0210 PRM01A: IOLIST B0$,B1$
0220 PRT03A: IOLIST A0$,A0
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0480 DIM P[2]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0510 LET L9=59,L=L9+1,HEADINGS=3,WIDTH=80,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$
0540 LET M0=MAX(6,LEN(M0$)),M1=LEN(M1$)
0550 LET H0=40,H1=72,H2=H0*2-M1-1
0590 LET P9=P[1]
0700 REM " --- Background"
0720 FOR X=4 TO 4 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(32,3),"Period:",'SF','CF',
0800 REM " --- Options"
0810 LET V0$="N",V1$="E",V2$=STR(P9:"##"),V3$="##",V4$="1 Thru "+STR(P[1]:"##"
0810:),V0=2,V1=40,V2=3
0820 GOSUB 7000
0830 ON V3 GOTO 0840,0800,0800,0800,9900
0840 IF V<1 OR V>P[1] THEN GOTO 0800
0850 LET P9=V
0860 CALL "SYC.PA",SYS01_DEV,P9,P[2],P8$,P9$,STATUS
0870 LET HEADINGS$[2]="Period "+STR(P9:"##")+"  From "+FNB$(P8$)+" To "+FNB$(P
0870:9$)
0880 PRINT @(40,3),HEADINGS$[2](8),
0900 REM " --- Correct?
0910 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0920 ON V3 GOTO 0930,0900,0900,0900,9900
0930 ON POS(V$="YN") GOTO 0900,0950,0800
0950 REM " --- Open Printer"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS>0 THEN GOTO 9900
0970 CALL "SYC.NB","Printing",7,COLUMN
1000 REM " --- Init Read"
1020 READ (PRT03_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read"
1120 LET K0$=KEY(PRT03_DEV,END=4000)
1130 IF K0$(1,2)<>N0$ THEN GOTO 4000
1140 READ (PRT03_DEV)IOL=PRT03A
1160 PRINT @(COLUMN,11),A0$(3,7),
1190 IF A0$(11,3)>P9$ THEN GOTO 1100
1600 REM " --- Check Header"
1620 IF A0$(10,1)<>"V" THEN GOTO 2000
1800 REM " --- Void"
1820 IF L+1>L9 THEN GOSUB 5000
1840 PRINT (7)@(0),A0$(3,7),@(8),FNB$(A0$(11,3))," Void"
1850 LET L=L+1
1890 GOTO 1100
2000 REM " --- Type"
2020 LET X2$=" Computer"
2030 IF A0$(10,1)="M" THEN LET X2$=" Manual"
2040 IF A0$(10,1)="R" THEN LET X2$=" Reversal"
2100 REM " --- Checks"
2120 IF L+1>L9 THEN GOSUB 5000
2140 GOSUB 6400
2160 PRINT (7)@(0),A0$(3,7),@(8),FNB$(A0$(11,3)),X2$,@(28),X0$," ",X1$,@(H2),A
2160:0:M1$
2180 LET L=L+1,T0=T0+A0
2190 GOTO 1100
4000 REM " --- Total"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)'LF',@(10),"Grand Total:",@(H2),T0:M1$
4100 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Check",@(10),"Check",@(28),"Employee",@(H2+M1-7),"Check"
5110 PRINT (7)@(0),"Number",@(10),"Date",@(17),"Type",@(28),"Number",@(29+M0),
5110:"Name",@(H2+M1-7),"Amount",'LF'
5180 LET L=L+3
5190 RETURN
6400 REM " --- Employee Name"
6420 LET X0$=FNF$(A0$(14,P[0]),M0$),X1$="Not On File"
6440 FIND (PRM01_DEV,KEY=N0$+A0$(14,9),DOM=6490)IOL=PRM01A
6445 LET Q=POS("  "=B1$(1,16))-1
6450 IF Q<1 THEN LET Q=16
6460 LET X1$=B1$(1,Q)+", "+B1$(17,13)
6490 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
