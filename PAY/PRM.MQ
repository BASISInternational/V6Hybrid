0010 REM "PRM - Employee Maintenance/Hard Copy
0020 REM "Program PRM.MQ"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS=1 THEN GOTO 9900
0200 REM " --- IOLists
0210 PRM02A: IOLIST B0$(1),B[ALL]
0220 PRM10A: IOLIST X$
0230 PRM10B: IOLIST X$
0240 PRM10C: IOLIST X$
0250 PRM10D: IOLIST X$
0260 PRM10F: IOLIST X0$
0265 PRM10H: IOLIST X0$
0270 PRM12A: IOLIST D0$(1),D[ALL]
0280 PRT01A: IOLIST E0$(1),E[ALL]
0290 PRT02A: IOLIST B0$(1),B[ALL]
0300 PRT11A: IOLIST E0$(1),E[ALL]
0310 PRT21A: IOLIST E0$(1),E[ALL]
0320 PRT31A: IOLIST E0$(1),E[ALL]
0500 REM " --- Init Data
0510 LET H0=40,H1=70,H=0,H1$=N4$+" Listing"
0520 LET L9=59,L=L9+1,T0=0,HEADINGS=2,WIDTH=80,WHEN$=F0$(7,3)
0530 DIM B0$(13),D0$(17),B[1],D[1]
0540 DIM X[8],W[2],HEADINGS$[HEADINGS]
0550 LET Q8$=M2$
0560 LET HEADINGS$[0]=N1$,HEADINGS$[1]=H1$
0580 CALL "SYC.RM",Q8$,Q8,10,10
0590 LET Q9$="######.00-",Q9=LEN(Q9$)
0700 REM " --- Background
0710 GOSUB 6000
0720 CALL "SYC.WC",1,0,32,0,23,12,0
0730 PRINT @(30,12),"Printing Hard Copy",
1000 REM " --- Print Name & Addresss
1010 GOSUB 5000
1020 LET V$="Employee Information"; GOSUB 5400
1030 PRINT (7)@(6),"Address: ",A1$(31,24),@(41),"Alt Sequence: ",A1$(145,10)
1040 PRINT (7)@(13),": ",A1$(55,24),@(50),"Sex: ",A1$(155,1)
1050 PRINT (7)@(13),": ",A1$(79,24),@(49),"Race: ",A1$(156,1)
1060 PRINT (7)@(3),"Zip/Postal: ",FNZ$(A1$(103)),@(50),"M/S: ",A1$(157,1)
1070 PRINT (7)@(3),"Home Phone: ",FNF$(A1$(112,10),M8$),@(43),"Birth Date: ",F
1070:NB$(A1$(158,3))
1080 PRINT (7)@(3),"Work Phone: ",FNF$(A1$(122,10),M8$),@(30),"Ext: ",A1$(132,
1080:4),@(44),"Hire Date: ",FNB$(A1$(161,3))
1090 PRINT (7)@(3),"Soc-Sec-No: ",FNF$(A1$(136,9),M9$),@(43),"Anniv Date: ",FN
1090:B$(A1$(164,3))
1100 PRINT (7)@(42),"Review Date: ",FNB$(A1$(167,3))
1110 PRINT (7)@(0),"Emerg Contact: ",A1$(176,24),@(44),"Term Date: ",FNB$(A1$(
1110:170,3))
1115 PRINT (7)@(5),"Relation: ",A1$(200,10),@(44),"Term Code: ",A1$(173,3)
1120 PRINT (7)@(2),"Emerg Phone: ",FNF$(A1$(210,10),M8$),@(36),"Normal Pay Per
1120:iod: ",A1$(220,1)
1130 PRINT (7)@(9),"Memo: ",A1$(227,10),@(38),"Home Department: ",A1$(221,2)
1140 IF P3$(9,1)="Y" THEN PRINT (7)@(3),"Union Code: ",A1$(238,3),
1150 PRINT (7)@(41),"Normal Title: ",A1$(223,2)
1160 PRINT (7)@(38),"Hourly/Salaried: ",A1$(225,1)
1170 PRINT (7)@(41),"Pension Plan: ",A1$(226,1)
1180 PRINT (7)@(39),"Full/Part Time: ",A1$(237,1),'LF'
1190 LET L=L+17
1200 REM " --- Pay Rate
1210 LET V$="Pay Rate Maintenance"; GOSUB 5400
1220 PRINT (7)@(5),"Title ID:",@(25+M3),"Rate",@(24+M3+M2),"Hours"
1230 READ (PRM02_DEV,KEY=A0$,DOM=1240)
1240 LET KM02$=KEY(PRM02_DEV,END=1340)
1250 IF KM02$(1,11)<>A0$ THEN GOTO 1340
1260 READ (PRM02_DEV,KEY=KM02$)IOL=PRM02A
1270 IF POS(" "<>B0$(12,2))=0 THEN GOTO 1330
1280 LET X$="**Not On File**"
1290 FIND (PRM10_DEV,KEY=N0$+"F"+B0$(12,2),DOM=1310)IOL=PRM10F
1300 LET X$=X0$(6,20)
1310 PRINT (7)@(5),B0$(12,2)," ",X$,@(30),B[0]:M3$,B[1]:M2$
1320 LET L=L+1
1330 GOTO 1240
1340 PRINT (7)
1350 LET L=L+1
1400 REM " --- Pay Change
1410 LET V$="Pay Change Maintenance"; GOSUB 5400
1420 PRINT (7)@(12),"Date",@(25+M3),"Rate",@(24+M3+M2),"Hours"
1430 READ (PRM12_DEV,KEY=A0$,DOM=1440)
1440 LET KM12$=KEY(PRM12_DEV,END=1510)
1450 IF KM12$(1,11)<>A0$ THEN GOTO 1510
1460 READ (PRM12_DEV,KEY=KM12$)IOL=PRM12A
1470 IF POS(" "<>D0$(12,6))=0 THEN GOTO 1500
1480 PRINT (7)@(10),FNB6$(D0$(12,6)),@(30),D[0]:M3$,D[1]:M2$
1490 LET L=L+1
1500 GOTO 1440
1510 PRINT (7)
1520 LET L=L+1
1600 REM " --- Pay Codes
1605 LET V$="Pay Code Maintenance"; GOSUB 5400
1610 READ (PRT01_DEV,KEY=A0$+"A",DOM=1620)
1620 LET K$=KEY(PRT01_DEV,END=1800)
1630 IF K$(1,12)<>A0$+"A" THEN GOTO 1800
1640 DIM E0$(14),E[16]
1660 READ (PRT01_DEV)IOL=PRT01A
1670 LET X$="**Not On File**"
1680 FIND (PRM10_DEV,KEY=N0$+"A"+E0$(13,2),DOM=1700)IOL=PRM10A
1690 LET X$=X$(6,16)
1700 IF L+7<=L9 THEN GOTO 1730
1710 GOSUB 5000
1720 LET V$="Pay Code Maintenance"; GOSUB 5400
1730 PRINT (7)@(0),"Pay Code: ",E0$(13,2)," ",X$
1735 PRINT (7)@(10+Q9-6),"M-T-D",@(10+2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(1
1735:0+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Qtr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),
1735:"Next Mo"
1740 PRINT (7)@(1),"Dollars:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q
1740:9$,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$
1750 PRINT (7)@(3),"Units:",@(10),E[6]:Q8$,E[7]:Q8$,E[8]:Q8$,E[9]:Q8$,E[10]:Q8
1750:$,E[7]+E[8]+E[9]+E[10]:Q8$,E[11]:Q8$
1760 PRINT (7)@(10+Q9-6),"Prior",@(10+2*Q9-8),"Current",@(10+3*Q9-5),"Next",@(
1760:10+3*Q9+M3-5),"Rate",@(10+4*Q9+M3-5),"Used"
1770 PRINT (7)@(0),"Accruals:",@(10),E[12]:Q9$,E[13]:Q9$,E[14]:Q9$,E[15]:M3$,E
1770:[16]:Q9$,'LF'
1780 LET L=L+7
1790 GOTO 1620
1800 REM " --- Deduction Codes
1805 LET V$="Deduction Code Maintenance"; GOSUB 5400
1810 READ (PRT11_DEV,KEY=A0$+"B",DOM=1820)
1820 LET K$=KEY(PRT11_DEV,END=2000)
1830 IF K$(1,12)<>A0$+"B" THEN GOTO 2000
1840 DIM E0$(20),E[7]
1860 READ (PRT11_DEV)IOL=PRT11A
1870 LET X$="**Not On File**"
1880 FIND (PRM10_DEV,KEY=N0$+"B"+E0$(13,2),DOM=1900)IOL=PRM10B
1890 LET X$=X$(6,16)
1900 IF L+4<=L9 THEN GOTO 1930
1910 GOSUB 5000
1920 LET V$="Deduction Code Maintenance"; GOSUB 5400
1930 PRINT (7)@(0),"Ded Code: ",E0$(13,2)," ",X$," Per: ",E0$(15,5)," Auto: ",
1930:E0$(20,1)," Rate:",E[6]:M3$," Lmt:",E[7]:Q9$
1935 PRINT (7)@(10+Q9-6),"M-T-D",@(10+2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(1
1935:0+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Qtr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),
1935:"Next Mo"
1940 PRINT (7)@(1),"Dollars:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q
1940:9$,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$,'LF'
1980 LET L=L+4
1990 GOTO 1820
2000 REM " --- Tax Codes
2005 LET V$="Tax Code Maintenance"; GOSUB 5400
2010 READ (PRT21_DEV,KEY=A0$+"C",DOM=2020)
2020 LET K$=KEY(PRT21_DEV,END=2200)
2030 IF K$(1,12)<>A0$+"C" THEN GOTO 2200
2040 DIM E0$(48),E[18]
2060 READ (PRT21_DEV)IOL=PRT21A
2070 LET X$="**Not On File**"
2080 FIND (PRM10_DEV,KEY=N0$+"C"+E0$(13,2),DOM=2100)IOL=PRM10C
2090 LET X$=X$(6,16)
2100 IF L+7<=L9 THEN GOTO 2130
2110 GOSUB 5000
2120 LET V$="Tax Code Maintenance"; GOSUB 5400
2130 PRINT (7)@(0),"Tax Code: ",E0$(13,2)," ",X$," Table: ",E0$(15,3),"  St Al
2130:low: ",E0$(18,2),"  Fed Allow: ",E0$(20,2)
2135 PRINT (7)@(28),"Add/Rpl: ",E0$(22,1),"    Pct/Amt: ",E0$(23,1),"       Ra
2135:te:",E[18]:M3$
2140 PRINT (7)@(10+Q9-6),"M-T-D",@(10+2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(1
2140:0+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Qtr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),
2140:"Next Mo"
2145 PRINT (7)@(3),"Gross:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q9$
2145:,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$
2150 PRINT (7)@(1),"Taxable:",@(10),E[6]:Q9$,E[7]:Q9$,E[8]:Q9$,E[9]:Q9$,E[10]:
2150:Q9$,E[7]+E[8]+E[9]+E[10]:Q9$,E[11]:Q9$
2160 PRINT (7)@(5),"Tax:",@(10),E[12]:Q9$,E[13]:Q9$,E[14]:Q9$,E[15]:Q9$,E[16]:
2160:Q9$,E[13]+E[14]+E[15]+E[16]:Q9$,E[17]:Q9$,'LF'
2180 LET L=L+7
2190 GOTO 2020
2200 REM " --- Contribution Codes
2205 LET V$="Contribution Code Maintenance"; GOSUB 5400
2210 READ (PRT31_DEV,KEY=A0$+"D",DOM=2220)
2220 LET K$=KEY(PRT31_DEV,END=2400)
2230 IF K$(1,12)<>A0$+"D" THEN GOTO 2400
2240 DIM E0$(19),E[19]
2260 READ (PRT31_DEV)IOL=PRT31A
2270 LET X$="**Not On File**"
2280 FIND (PRM10_DEV,KEY=N0$+"D"+E0$(13,2),DOM=2300)IOL=PRM10D
2290 LET X$=X$(6,16)
2300 IF L+6<=L9 THEN GOTO 2330
2310 GOSUB 5000
2320 LET V$="Contribution Code Maintenance"; GOSUB 5400
2330 PRINT (7)@(1),"Cont Cd: ",E0$(13,2)," ",X$,"  Periods: ",E0$(15,5),"  Rat
2330:e: ",E[18]:M3$
2335 PRINT (7)@(10+Q9-6),"M-T-D",@(10+2*Q9-6),"Qtr-1",@(10+3*Q9-6),"Qtr-2",@(1
2335:0+4*Q9-6),"Qtr-3",@(10+5*Q9-6),"Qtr-4",@(10+6*Q9-6),"Y-T-D",@(10+7*Q9-8),
2335:"Next Mo"
2340 PRINT (7)@(3),"Gross:",@(10),E[0]:Q9$,E[1]:Q9$,E[2]:Q9$,E[3]:Q9$,E[4]:Q9$
2340:,E[1]+E[2]+E[3]+E[4]:Q9$,E[5]:Q9$
2350 PRINT (7)@(3),"Basis:",@(10),E[6]:Q9$,E[7]:Q9$,E[8]:Q9$,E[9]:Q9$,E[10]:Q9
2350:$,E[7]+E[8]+E[9]+E[10]:Q9$,E[11]:Q9$
2360 PRINT (7)@(1),"Contrib:",@(10),E[12]:Q9$,E[13]:Q9$,E[14]:Q9$,E[15]:Q9$,E[
2360:16]:Q9$,E[13]+E[14]+E[15]+E[16]:Q9$,E[17]:Q9$,'LF'
2380 LET L=L+6
2390 GOTO 2220
2400 REM " --- Check History
2402 LET U0$="",U1$="",U2$="",U3$=""
2405 READ (PRM10_DEV,KEY=N0$+"H",DOM=2410)
2410 LET K$=KEY(PRM10_DEV,END=2475)
2415 IF K$(1,3)<>N0$+"H" THEN GOTO 2475
2420 READ (PRM10_DEV,KEY=K$)IOL=PRM10H
2425 IF K$(4,1)>"C" THEN GOTO 2435
2430 LET U0$=U0$+X0$(4,3),U2$=U2$+X0$(17,2)
2435 IF K$(4,1)<>"H" THEN GOTO 2410
2440 LET U3$=U3$+X0$(5,2),X$=X0$(7,10)
2445 FOR X=1 TO 10
2450 IF X$(10,1)<>" " THEN EXITTO 2465
2455 LET X$=" "+X$(1,9)
2460 NEXT X
2465 LET U1$=U1$+X$
2470 GOTO 2410
2475 DIM X$(80)
2480 LET X$(1)=U1$,X$(71)="     Total",U1$=X$
2485 DIM R[7]
2505 LET V$="Check History"; GOSUB 5500
2510 READ (PRT02_DEV,KEY=A0$,DOM=2520)
2520 LET K$=KEY(PRT02_DEV,END=2650)
2530 IF K$(1,11)<>A0$ THEN GOTO 2650
2540 DIM B0$(22),B[7]
2550 READ (PRT02_DEV,KEY=K$)IOL=PRT02A
2560 IF L+1>L9 THEN GOSUB 5000; GOSUB 5500
2570 PRINT (7)@(0),B0$(12,7)," ",FNB$(B0$(19,3))," ",B[1]:M1$,B[2]:M1$,B[3]:M1
2570:$,B[4]:M1$,B[5]:M1$,B[6]:M1$,B[7]:M1$,B[0]:M1$
2580 LET L=L+1
2590 FOR X=0 TO 7
2600 LET R[X]=R[X]+B[X]
2610 NEXT X
2620 GOTO 2520
2650 REM " --- Check Totals
2660 IF L+2>L9 THEN GOSUB 5000; GOSUB 5500
2670 PRINT (7)'LF',@(0),"Check Totals",@(17),R[1]:M1$,R[2]:M1$,R[3]:M1$,R[4]:M
2670:1$,R[5]:M1$,R[6]:M1$,R[7]:M1$,R[0]:M1$
2680 LET L=L+2
4000 REM " --- End
4020 PRINT @(0,L0),'CE',
4040 CLOSE (7,ERR=4100)
4100 RUN "PRM.MA"
5000 REM " --- Heading"
5020 LET L=HEADINGS+1
5040 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5220 PRINT (7)@(2),"Employee No: ",FNF$(A0$(3,P[0]),M0$)
5230 PRINT (7)@(4),"Last Name: ",A1$(1,16),@(43),"First Name: ",A1$(17,14),'LF
5230:'
5280 LET L=L+3
5290 RETURN
5400 REM " --- Subtitle
5420 PRINT (7)@(0),FILL(80,"-"),@(15),V$
5440 IF F0$(13,1)<>"T" AND T0$<>V$ THEN PRINT @(24,14),FILL(30),@(INT(40-LEN(V
5440:$)/2)),V$,
5480 LET L=L+1,T0$=V$
5490 RETURN
5500 REM " --- Subheading for Contrib Codes
5510 PRINT (7)@(0),FILL(132,"-"),@(15),V$
5520 IF F0$(13,1)<>"T" AND T0$<>V$ THEN PRINT @(24,14),FILL(30),@(INT(40-LEN(V
5520:$)/2)),V$,
5530 LET L=L+1,T0$=V$
5540 PRINT (7)@(0),"Check     Date",
5550 FOR X=1 TO LEN(U1$)/10
5560 PRINT (7)@(19-M1+M1*X),U1$(10*(X-1)+1,10),
5570 NEXT X
5580 PRINT (7)""
5585 LET L=L+1
5590 RETURN
6000 REM " --- Clear Screen
6020 PRINT @(0,L0),'CE'
6090 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(AB
8085:S(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
