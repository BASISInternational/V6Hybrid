0010 REM "PRR - Employee Master List"
0020 REM "Program PRR.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-02",FILES$[3]="PRM-03"
0130 LET FILES$[4]="PRM-10",FILES$[5]="PRT-21",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM02_DEV=CHANNELS[2],PRM03_DEV=CHANNELS[3]
0185 LET PRM10_DEV=CHANNELS[4],PRT21_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists"
0210 PRM01A: IOLIST A0$,A1$(1)
0220 PRM02A: IOLIST B0$,B[ALL]
0230 PRM10F: IOLIST X$,X0
0240 PRM10J: IOLIST Q9$(1)
0250 PRT21A: IOLIST C0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 PRS01C: IOLIST *,*,*,*,*,*,*,R7$(1)
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0435 DIM R7$(4)
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=PRS01C
0445 DIM P[3]
0450 LET P[0]=NUM(P2$(1,2)),P[2]=NUM(P3$(5,1)),P[1]=NUM(P3$(5,1)),P[3]=NUM(P3$
0450:(7,1))
0460 LET M0=LEN(M0$)
0500 REM " --- Init Data"
0510 LET L9=59,L=L9+1,HEADINGS=3,WIDTH=132,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$,HEADINGS$[2]="All Employees"
0540 LET M8$="000-00-0000",M9$="(###)-###-####"
0550 LET S0$="DWBSMQA",S1$="365052026024012004001"
0560 DIM A1$(240)
0700 REM " --- Background"
0720 FOR X=4 TO 6 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(32,3),"Option:",@(30,5),"Sequence:",'SF',
0800 REM " --- Options"
0810 LET V0$="S",V1$="RE",V2$=P9$,V3$="ATB",V4$="A=Active T=Terminated B=Both"
0810:,V0=1,V1=40,V2=3
0820 GOSUB 7000
0830 ON V3 GOTO 0840,0800,0800,0800,9900
0840 LET P9$=V$
0845 IF P9$="A" THEN LET HEADINGS$[2]="Active Employees" ELSE IF P9$="T" THEN 
0845:LET HEADINGS$[2]="Terminated Employees"
0850 REM " --- Sequence"
0860 LET V0$="S",V1$="RC",V2$=P8$,V3$="AN",V4$="A=Alphabetic N=Numeric",V0=1,V
0860:1=40,V2=5
0870 GOSUB 7000
0880 ON V3 GOTO 0890,0850,0800,0850,0850
0890 LET P8$=V$
0900 REM " --- Correct?"
0910 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0920 ON V3 GOTO 0930,0900,0900,0900,9900
0930 ON POS(V$="YN") GOTO 0900,0950,0800
0950 REM " --- Open Printer"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS>0 THEN GOTO 9900
0970 CALL "SYC.NB","Printing",M0+33,COLUMN
1000 REM " --- Initial Read"
1020 READ (PRM01_DEV,KEY=N0$,DOM=1030)
1040 READ (PRM03_DEV,KEY=N0$+"A",DOM=1050)
1100 REM " --- Main Read"
1110 ON POS(P8$="AN") GOTO 9900,1200,1120
1120 READ (PRM01_DEV,END=4000)IOL=PRM01A
1130 IF A0$(1,2)<>N0$ THEN GOTO 4000
1190 GOTO 1250
1200 REM " --- Alpha"
1210 LET K$=KEY(PRM03_DEV,END=4000)
1220 IF K$(1,3)<>N0$+"A" THEN GOTO 4000
1230 READ (PRM03_DEV)
1240 FIND (PRM01_DEV,KEY=N0$+K$(20),DOM=1200)IOL=PRM01A
1250 REM " --- Active/Inactive"
1270 IF P9$="A" THEN IF A1$(170,3)<>"   " THEN GOTO 1100
1280 IF P9$="T" THEN IF A1$(170,3)="   " THEN GOTO 1100
1290 PRINT @(COLUMN,11),FILL(M0+33),@(COLUMN,11),FNF$(A0$(3,P[0]),M0$)," ",FNP
1290:$(A1$(1,16)),", ",FNP$(A1$(17,14)),
1300 REM " --- Tax Status"
1320 DIM C0$(21),R0$(5),R1$(5)
1340 FIND (PRT21_DEV,KEY=A0$(1,11)+"C"+R7$(1,2),DOM=1355)IOL=PRT21A
1350 LET R0$=C0$(15,3)+C0$(20,2)
1360 FIND (PRT21_DEV,KEY=A0$(1,11)+"C"+R7$(3,2),DOM=1380)IOL=PRT21A
1370 LET R1$=C0$(15,3)+C0$(18,2)
1400 REM " --- Pay Code"
1410 DIM B[1]
1420 LET X0$=A1$(223,2),X1$="",X1=0,X2=POS(A1$(220,1)=S0$)
1430 FIND (PRM10_DEV,KEY=A0$(1,2)+"F"+X0$,DOM=1500)IOL=PRM10F
1440 LET X1$=X$(6,20)
1450 FIND (PRM02_DEV,KEY=A0$+X0$,DOM=1470)IOL=PRM02A
1460 IF B[0]<>0 THEN LET X0=B[0]
1470 IF A1$(225,1)="S" THEN LET B[1]=1; REM "Don't extend hours if salary
1480 IF X2>0 THEN LET X1=X0*B[1]*NUM(S1$(X2*3-2,3))
1800 REM " --- Union Code Description"
1820 DIM Q9$(40)
1840 FIND (PRM10_DEV,KEY=N0$+"J"+A1$(238,3),DOM=1850)IOL=PRM10J
1860 LET Q9$=Q9$(7,30)
2000 REM " --- Print"
2020 IF L+10>L9 THEN GOSUB 5000
2040 PRINT (7)@(7),"Number: ",FNF$(A0$(3,P[0]),M0$),@(34),"Dept: ",A1$(221,2)
2050 PRINT (7)@(9),"Name: ",A1$(1,30),'LF',@(5),"Address1: ",A1$(31,24),@(55),
2050:"Sex: ",A1$(155,1),@(81),"Federal: ",R0$(1,3)," ",R0$(4,2),'LF',@(5),"Add
2050:ress2: ",A1$(55,24),@(49),"Job Class: ",X0$," ",X1$,@(83),"State:  ",R1$(
2050:1,3)," ",R1$(4,2),'LF',@(3),"City/State: ",A1$(79,24),@(50),"Pay Type: ",
2050:A1$(225,1),@(78),"Birth Date: ",FNB$(A1$(158,3)),'LF',@(3),"Zip/Postal: "
2050:,FNZ$(A1$(103)),@(79),"Hire Date: ",FNB$(A1$(161,3)),@(50),"Pay Rate: ",X
2050:0:M3$
2060 PRINT (7)@(4),"Telephone: ",FNF$(A1$(112,10),M9$),@(52),"Annual: ",X1:M1$
2060:,@(79),"Term Date: ",FNB$(A1$(170,3)),'LF',@(3),"Soc-Sec-No: ",FNF$(A1$(1
2060:36,9),M8$),@(54),"Memo: ",A1$(227,10),@(78),"Union Code: ",A1$(238,3)," "
2060:,Q9$,'LF','LF'
2070 LET L=L+10,T0=T0+1
2090 GOTO 1100
4000 REM " ---Totals"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)'LF',@(0),"Total Employees:",T0
4030 LET L=L+2
4100 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5190 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(AB
8085:S(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
