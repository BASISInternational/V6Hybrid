0010 REM "PRE - Daily Entry"
0020 REM "Program PRE.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,1000
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-02",FILES$[2]="PRM-01",FILES$[3]="PRM-02"
0130 LET FILES$[4]="PRM-10",FILES$[5]="PRT-01",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRE02_DEV=CHANNELS[1],PRM01_DEV=CHANNELS[2],PRM02_DEV=CHANNELS[3]
0185 LET PRM10_DEV=CHANNELS[4],PRT01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists"
0220 PRM01A: IOLIST B0$,B1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 PRS01B: IOLIST X0$,X1$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0450 DIM P[3]
0460 LET P9$=X1$(6,10)
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(5,1)),P[3]=NUM(P3$
0490:(7,1))
0500 REM " --- Init Data"
0510 PRECISION P[3]
0520 DIM B0$(11)
0530 LET B0$(1)=N0$,O0=1
0540 CALL "SYC.RM",M1$,M1,5,13
0550 CALL "SYC.RM",M2$,M2,7,12
0560 CALL "SYC.RM",M3$,M3,7,12
0700 REM " --- Background"
0720 FOR X=5 TO 8 STEP 3
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0750 PRINT 'SB',@(2,3),"Employee No:",@(4,4),"Last Name:",@(33),"First Name:",
0750:@(M2-6,6),"Hours",@(2*M1-9),"Earnings",@(0,8),
0760 PRINT @(0,9),"Seq",@(6),"Date",@(13),"EC",@(16),"TC",@(14+M3),"Rate",@(20
0760:+M3),"DC",@(17+M3+M2),"Units",@(16+M3+M2+M1),"Amount",@(77),"?",
0790 PRINT 'SF',
0900 REM " --- Init File"
0920 READ (PRM01_DEV,KEY=N0$,DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRM01_DEV,END=1050)
1030 IF V2$(1,2)<>B0$(1,2) THEN LET V2$=""
1032 IF V2$<>"" THEN READ (PRM01_DEV,KEY=V2$)IOL=PRM01A
1034 IF V2$<>"" THEN IF B1$(170,3)<>"   " THEN GOTO 1020
1036 IF V2$<>"" THEN IF POS(B1$(220,1)=P9$)=0 THEN GOTO 1020
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KEC",V3$=P1$,V4$="Valid Employee Number   <F3>=Lookup",V
1050:0=9,V1=15,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1075 IF V3=3 THEN CALL "SYC.LP",V$
1080 IF V$="" THEN GOTO 0900
1090 LET B0$(3)=V$
1200 REM " --- Find Key"
1220 FIND (PRM01_DEV,KEY=B0$(1,11),DOM=1000)IOL=PRM01A
1300 REM " --- Display"
1320 GOSUB 5000
1400 REM " --- Employee Terminated"
1410 IF B1$(170,3)="   " THEN GOTO 1500
1420 DIM MESSAGE$[1]
1430 LET MESSAGE$[0]="This Employee Has Been Terminated (<Enter>=Continue)"
1440 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
1450 GOTO 1000
1500 REM " --- Pay Rates"
1520 READ (PRM02_DEV,KEY=B0$(1,11)+B1$(223,2),DOM=1550)
1540 GOTO 4000
1550 REM " --- No Pay Rates"
1560 DIM MESSAGE$[1]
1570 LET MESSAGE$[0]="This Employee Has No Standard Pay Rate (<Enter>=Continue
1570:)"
1580 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
1590 GOTO 1000
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1400,1500,1600,1700,2000,3990
4000 REM " --- Correct?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Heading Information Corre
4020:ct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4040 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 RUN "PRE.CB"
4200 REM " --- Delete"
4220 READ (PRE02_DEV,KEY=B0$,DOM=4230)
4230 LET K$=KEY(PRE02_DEV,END=4290)
4240 IF POS(B0$=K$)<>1 THEN GOTO 4290
4250 REMOVE (PRE02_DEV,KEY=K$)
4260 GOTO 4230
4290 GOTO 1000
5000 REM " --- Display"
5020 PRINT @(15,3),FNF$(B0$(3,P[0]),M0$),@(15,4),B1$(1,16),@(45,4),B1$(17,14),
5090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
