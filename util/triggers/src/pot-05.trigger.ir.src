rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem instead of key
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem with a non-modified Version 6 Addon. If there have been any modifications to
rem these systems, be sure to thoroughly examine this trigger and test changes.

rem --- Read V6 POT-05 instead of Barista Addon's pot_invhdr

	setopts $080872202c2e$
	seterr trigger_error

    info!=BBjAPI().getCurrentSessionInfo()
    grpID$=str(info!.getGroupID())   
    sessionID$=str(info!.getSessionID())
    nskey$="POT05"+"_"+grpID$+"_"+sessionID$

    rem ' trace
    goto skip_trace;rem this out to do the trace
    tfl$="C:/temp_downloads/pot05.ir."+grpID$+"_"+sessionID$+"_"+str(fpt(tim))+".txt"
    erase tfl$,err=*next
    string tfl$
    tfl=unt
    open(tfl)tfl$
    settrace(tfl,MODE="UNTIMED")
skip_trace:
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()
	
	wkdir$ = dir("")
	v6data_dir$=stbl("+V6DATA",err=std_exit)
    
    keyed_read=0
	ky$=td!.getKey(err=non_keyed_read)
    keyed_read=1
    
non_keyed_read: rem --- if read doesn't have key=k$, will need to get ky$ from namespace rather than td!.getKey()
    keyName$=td!.getKeyName()
    if !keyed_read
        rem --- get partial/trip-read or last full key from namespace
        gns!=BBjAPI().getGroupNamespace()
        ky$ = gns!.getValue(nskey$,err=*next)
        xPos=pos("::"=ky$)
        if xPos>1 then keyName$=ky$(1,xPos-1)
        if xPos>0 then ky$=ky$(xPos+2)
        
        rem --- if previous key read took us to EOF, namespace will contain special value to indicate that
        if ky$=nskey$+"**EOF"
            err_num=2
            goto trigger_error_forced
        endif
    endif

rem --- Template for the V6 POT-05 record being read

	intemp$="firm_id:c(2),ap_type:c(2),vendor_nbr:c(6),ap_inv_nbr:c(10),sequence_nbr:c(3*),"
    intemp$=intemp$+"inv_date:c(6),acct_date:c(6),due_date:c(6),po_disc_date:c(6),
:       hold_flag:c(1),terms_code:c(2),dist_code:c(2),payment_grp:c(2),reference:c(10),ap_inv_memo:c(16),"
    intemp$=intemp$+"rsrvd_str:c(7*),rsrvd_str_02:c(1*),dealer_str:c(1*),"
    intemp$=intemp$+"invoice_amt:n(7*),discount_amt:n(7*),retention:n(7*),"
    intemp$=intemp$+"rsrvd_num_01:n(1*),rsrvd_num_02:n(1*),rsrvd_num_03:n(1*),dealer_num_01:n(1*),dealer_num_02:n(1*),dealer_num_03:n(1*)"
	dim pot05a$:intemp$

rem --- Get template for Barista Addon's POT_INVHDR table

	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("POT_INVHDR",16),dom=trigger_error) *,*,pot_invhdr_tpl$
	dim pot_invhdr$:pot_invhdr_tpl$

rem --- Open V6 POT-05 file

	pot05_dev=unt
	open (pot05_dev,err=trigger_error)v6data_dir$+"POT-05"
	
rem --- Get the data

    next_ky$=nskey$+"**EOF";rem --- set default in case we take an end= error branch
    dom_flag=1

    if ky$="" and keyed_read=0
        switch (BBjAPI().TRUE)
            case keyName$="PRIMARY"
            case default
                read record (pot05_dev,end=trigger_error) pot05a$; dom_flag=0
                break
        swend
    else
        switch (BBjAPI().TRUE)
            case keyName$="PRIMARY"
            case default
                read record (pot05_dev,key=ky$,dom=*next) pot05a$; dom_flag=0
                break
        swend
    endif

    if dom_flag=0
        pot_invhdr.firm_id$=pot05a.firm_id$
        pot_invhdr.ap_type$=pot05a.ap_type$
        pot_invhdr.vendor_id$=pot05a.vendor_nbr$
        pot_invhdr.ap_inv_no$=pot05a.ap_inv_nbr$
        pot_invhdr.sequence_ref$=pot05a.sequence_nbr$
        pot_invhdr.reserved_str$=pot05a.rsrvd_str$
        pot_invhdr.hold_flag$=pot05a.hold_flag$
        pot_invhdr.ap_terms_code$=pot05a.terms_code$
        pot_invhdr.ap_dist_code$=pot05a.dist_code$
        pot_invhdr.payment_grp$=pot05a.payment_grp$
        pot_invhdr.reference$=pot05a.reference$
        pot_invhdr.ap_inv_memo$=pot05a.ap_inv_memo$
        pot_invhdr.reserved_str_02$=pot05a.rsrvd_str_02$
        value$=pot05a.inv_date$
        gosub fix_yymmdd_date
        pot_invhdr.inv_date$=value$
        value$=pot05a.acct_date$
        gosub fix_yymmdd_date
        pot_invhdr.acct_date$=value$
        value$=pot05a.due_date$
        gosub fix_yymmdd_date
        pot_invhdr.due_date$=value$
        value$=pot05a.po_disc_date$
        gosub fix_yymmdd_date
        pot_invhdr.po_disc_date$=value$
        pot_invhdr.reserved_str_03$=""
        pot_invhdr.dealer_str$=pot05a.dealer_str$
        pot_invhdr.invoice_amt=pot05a.invoice_amt
        pot_invhdr.discount_amt=pot05a.discount_amt
        pot_invhdr.retention=pot05a.retention
        pot_invhdr.reserved_num_01=pot05a.rsrvd_num_01
        pot_invhdr.reserved_num_02=pot05a.rsrvd_num_02
        pot_invhdr.reserved_num_03=pot05a.rsrvd_num_03
        pot_invhdr.dealer_num_01=pot05a.dealer_num_01
        pot_invhdr.dealer_num_02=pot05a.dealer_num_02
        pot_invhdr.dealer_num_03=pot05a.dealer_num_03
    endif
    
rem --- Get next key

    if !td!.getExtract() then
        next_ky$=nskey$+"**EOF";rem --- set default in case we take the end=
        
        switch (BBjAPI().TRUE)
            case keyName$="PRIMARY"
            case default
                next_ky$=key(pot05_dev,end=*next)
                break
        swend
    else
        next_ky$=ky$
    endif

    rem --- store key info in group namespace (for looping back to k$=key() in calling code)
    gns!=BBjAPI().getGroupNamespace()
    gns!.setValue(nskey$,keyName$+"::"+next_ky$)
    
    if dom_flag=1
        err_num=11
        goto trigger_error_forced
    else
        td!.setReadBuffer(pot_invhdr$)
        goto std_exit
    endif

trigger_error:

    rd_err_text$="", err_num=err
    
trigger_error_forced:

    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=11
        rem -- if we took the DOM above, store the partial key in group namespace for upcoming k$=key in calling code
        gns!=BBjAPI().getGroupNamespace()
        gns!.setValue(nskey$,keyName$+"::"+next_ky$)
	endif
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
    
fix_yymmdd_date: rem --- Convert yy dates to yyyy, and yymmdd dates to yyyymmdd
    rem ---     Input/Out: value$
    if len(cvs(value$,3)) then
        value$=fnyy_yy21$(value$)
        if asc(value$)>=65 then
            value$=str(200+asc(value$)-65)+value$(2)
        else
            value$="19"+value$
        endif
        value$=str(num(value$(1,4),err=*next)+add_year)+value$(5)
    else
        value$=pad("",8)
    endif
    return

rem --- Convert 2-Char Year to 21st Century 2-Char Year
    def fnyy_yy21$(q$)
        q3$=" ABCDE56789ABCDEFGHIJ"
        q$(1,1)=q3$(pos(q$(1,1)=" 0123456789ABCDEFGHIJ"))
        return q$
    fnend
	
std_exit: