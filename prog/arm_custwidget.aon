REM /**
REM  * V6demo --- arm_custwidget.aon
REM  * Displays a pie chart widget that shows AR aging information
REM  * @author bhipple
REM  *
REM  */


rem Use statements
use ::dashboard/dashboard.bbj::Dashboard
use ::dashboard/dashboard.bbj::DashboardCategory
use ::dashboard/dashboard.bbj::DashboardWidget
use ::dashboard/dashboard.bbj::WidgetControl
use ::dashboard/widget.bbj::Widget
use ::dashboard/widget.bbj::ChartWidget
use ::dashboard/widget.bbj::PieChartWidget
use ::BBUtils.bbj::BBUtils
use ::BBXImage.bbj::BBXImageFactory
use ::BBXImage.bbj::BBXImage
use ::bbwindowutils.bbj::BBWindowUtils

use ::sys/prog/bao_utilities.bbj::BarUtils

use java.awt.TexturePaint
use java.awt.geom.Rectangle2D
use java.awt.image.BufferedImage

seterr Finished

firmID$ = clientenv("firm_id",err=*next)
if (firmID$ = "" and argc > 1)
    firmID$ = argv(1)
endif

custNum$ = clientenv("cust_num",err=*next)
if (custNum$ = "" and argc > 2)
    custNum$ = argv(2)
endif

if (firmID$ <> "" and custNum$ <> "")

    REM Create the SQL string
    sql$ = "select V6_CUST_NAME,V6_AGING_FUTURE,V6_AGING_CUR,V6_AGING_30,V6_AGING_60,V6_AGING_90,V6_AGING_120 from ARM01,ARM02 "
    sql$ = sql$ + "where ARM01.V6_FIRM_ID = ARM02.V6_FIRM_ID and " 
    sql$ = sql$ + "ARM01.V6_CUSTOMER_NBR = ARM02.V6_CUSTOMER_NBR and "
    sql$ = sql$ + "V6_FIRM_ID = '" + firmID$ + "' and "
    sql$ = sql$ + "V6_CUSTOMER_NBR = '" + custNum$ + "' and "
    sql$ = sql$ + "V6_AR_TYPE = ''"

    r! = BarUtils.getResultSet(sql$)

    REM Get the  data
    if (r!.next())
        custName$ = r!.getObject(1)
        future = r!.getObject(2)
        current = r!.getObject(3)
        over30 = r!.getObject(4)
        over60 = r!.getObject(5)
        over90 = r!.getObject(6)
        over120 = r!.getObject(7)
        totalAging = future + current + over30 + over60 + over90 + over120

        if (totalAging > 0)
            rem Create the dashboard and category
            dashboard! = new Dashboard("ARAging","AR Aging")
            dashboardCategory! = dashboard!.addDashboardCategory("ARAging","AR Aging")
            dashboardCategory!.setWidgetTitlebarNameBackColor(bbjapi().makeColor(215,215,215))
            dashboardCategory!.setWidgetTitlebarDecorationBackColor(bbjapi().makeColor(225,225,225))    
            
            rem Create the widget
            widgetName$ = "ARAging"
            title$ = "Aging for "+custName$
            previewText$ = ""
            previewImage$ = ""
            chartTitle$ = ""
            flat = 1 
            legend = 0
            numSlices = 6
            dashboardWidget! = dashboardCategory!.addPieChartDashboardWidget(widgetName$,title$,previewText$,previewImage$,chartTitle$,flat,legend,numSlices)
            widget! = dashboardWidget!.getWidget()
            
            rem Get the background image for the AR Aging Chart
            declare BBXImage xImage!
            xImage! = BBXImageFactory.createImageFromServerFile(System.getProperty("basis.BBjHome") + "/utils/dashboard/images/chartBG.jpg")
            xImage!.setOpacity(20)
            tr! = new Rectangle2D.Double(0, 0, xImage!.getWidth(), xImage!.getHeight())
            chartTexturePaint! = new TexturePaint(xImage!.getBufferedImage(BufferedImage.TYPE_INT_ARGB), tr!)
            
            rem Customize the chart
            widget!.setChartBackground(chartTexturePaint!)
            widget!.setPlotPadding(.06)
            colors! = ChartWidget.getVectorOfJavaColorsFromHexColorStrings(4, "#f5bcbc", "#861313", .65, .65)
            colors!.insertItem(0, ChartWidget.getColorWithAlphaFromHexString("#169c16", 0.65))
            colors!.insertItem(0, ChartWidget.getColorWithAlphaFromHexString("#1d56c9", 0.65))
            widget!.setChartColors(colors!)
            widget!.setLegendPosition(ChartWidget.getSideLeft(), ChartWidget.getHAlignRight(), ChartWidget.getVAlignCenter())
            widget!.setLabelFormat("{0}: {1}", java.text.NumberFormat.getCurrencyInstance(), java.text.NumberFormat.getPercentInstance())
            rem Set the Widget data
            widget!.setDataSetValue("  Future", future)
            widget!.setDataSetValue(" Current", current)
            widget!.setDataSetValue(" 30 Days", over30)
            widget!.setDataSetValue(" 60 Days", over60)
            widget!.setDataSetValue(" 90 Days", over90)
            widget!.setDataSetValue("120 Days", over120)
            
            rem Create a window
            sysgui! = BBjAPI().openSysGui("X0")
            ScreenSize! = sysgui!.getSystemMetrics().getScreenBounds()
            width=400
            height=300
            winx=ScreenSize!.x+ScreenSize!.width-width-150
            winy=ScreenSize!.y+ScreenSize!.height-height-150

            window! = sysgui!.addWindow(winx,winy,width,height,"",$21000012$)
            widgetControl! = new WidgetControl(dashboardWidget!,window!,0,0,width,height,$$)
            widgetControl!.setMoveParentWindow(1)
            
            closeButton! = widgetControl!.getBBjControl(WidgetControl.getDASHBOARD_WIDGET_CLOSE_BUTTON_NAME())
            closeButton!.setCallback(BBjToolButton.ON_TOOL_BUTTON_PUSH,"Finished")
            window!.setCallback(window!.ON_CLOSE,"Finished")
            window!.setCallback(window!.ON_KEYPRESS,"window_keypress")
            
            rem BBWindowUtils.centerWindow(window!)
            window!.setVisible(1)
            window!.focus()

            process_events
       endif
    endif
    

window_keypress:rem --- Keypress event

    winEvent! = sysgui!.getLastEvent()
    key_code = winEvent!.getKeyCode()

    switch key_code
        case 334; rem <F4>
        case 27; rem <Escape>
            goto Finished
        break
        case default
        break
    swend

    return

Finished:
    REM Clean up
    r!.close(err=*next)

    release





