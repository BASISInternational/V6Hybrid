0010 REM "Work Order Conversion Update - Transaction Update            "
0020 REM "Program:  WOX.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Jun96 WGH: Not calculating unit cost and overhead rate correctly (
0031:460,505,1290)"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-05",FILES$[2]="WOT-01",FILES$[3]="WOT-21"
0120 LET FILES$[4]="WOT-31",FILES$[5]="WOE-02",FILES$[6]="WOM-10"
0125 LET FILES$[7]="SYS-01",FILES$[8]="WOM-07"
0130 LET OPTIONS$[1]="L"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE05_DEV=CHANNELS[1],WOT01_DEV=CHANNELS[2],WOT21_DEV=CHANNELS[3]
0165 LET WOT31_DEV=CHANNELS[4],WOE02_DEV=CHANNELS[5],WOM10_DEV=CHANNELS[6]
0170 LET SYS01_DEV=CHANNELS[7],WOM07_DEV=CHANNELS[8]
0200 REM " --- IOLISTs"
0210 WOE05A: IOLIST A0$,A[ALL]
0220 WOTRAN: IOLIST W0$,W1$,W[ALL]
0230 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0235 ARS01A: IOLIST *,*,*,R0$
0240 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0250 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0260 WOS01A: IOLIST X$,X$,X$,P9$
0270 WOE02A: IOLIST B0$,B1$
0280 WOM07A: IOLIST CROSSREF$
0400 REM " --- Parameters"
0405 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0410 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0415 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0420 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0425 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9800)IOL=WOS01A
0430 DIM P[6],G[4]
0435 LET G[4]=NUM(G2$(9,2))
0440 LET R9$="N"
0445 IF P9$(2,1)="N" THEN GOTO 0460
0450 FIND (SYS01_DEV,KEY=N0$+"AR"+"00",DOM=0460)IOL=ARS01A
0455 LET R9$=R0$(11,1)
0460 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),IVS01_PRECISION=NUM(P2$(5,1))
0465 LET P[3]=NUM(P2$(7,2)),P[5]=NUM(P4$(1,2)),P[6]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0505 PRECISION IVS01_PRECISION
0510 LET P9$=F0$(7,3)
0520 DIM J$(80,"-"),J1$(40)
0530 DIM A[5],W[11],CROSSREF$(18)
0700 REM " --- Background"
0710 FOR X=6 TO 10
0720 PRINT @(X,0),'CL'
0730 NEXT X
0740 FOR X=4 TO 4
0750 CALL "SYC.WC",1,0,80,0,0,X,0
0760 NEXT X
0800 REM " --- Options"
0805 FOR X=7 TO 16; PRINT @(0,X),'CE'; NEXT X
0810 CALL "SYC.YN",0,"Are You Ready To Update The "+F5$,0,V$,V3
0820 IF V$="NO" THEN GOTO 4000
0830 IF V$<>"YES" THEN GOTO 0800
0900 REM " --- Enter Conversion Date
0902 PRINT 'SB',@(23,3),"Conversion Date:"
0903 PRINT @(0,22),'CE',
0905 LET V0$="D",V1$="R",V2$=P9$,V3$="",V4$="",V0=8,V1=40,V2=3
0910 GOSUB 7000
0915 LET P9$=V$
0920 IF V3=4 THEN GOTO 4000
0925 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Is The Above Information Correct? 
0925:(Yes/No): ",V0=3,V1=FNV(V4$),V2=22
0930 GOSUB 7000
0935 IF V3=4 THEN GOTO 4000
0940 IF V$="NO" THEN GOTO 0900
0945 IF V$<>"YES" THEN GOTO 0925
0950 CALL "SYC.NB","Updating:",8,COLUMN
1000 REM " --- Do It Here
1010 READ (WOE05_DEV,KEY=N0$,DOM=1020)
1020 LET K$=KEY(WOE05_DEV,END=4000)
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1040 READ (WOE05_DEV)IOL=WOE05A
1050 PRINT @(COLUMN,11),K$(5)
1200 REM " --- Operations
1210 IF A[3]=0 THEN GOTO 1500
1220 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=1230)
1230 LET K3$=KEY(WOE02_DEV,END=1260)
1240 IF K3$(1,11)<>A0$(1,11) THEN GOTO 1260
1250 READ (WOE02_DEV,KEY=K3$)IOL=WOE02A
1260 DIM W0$(18),W1$(30),W[11]
1270 LET W0$(1)=A0$(1,11)+P9$+"O"+"001"
1280 LET W1$(1)=B0$(13,3)+B1$(1,3),W1$(16)="CONVERSION"
1290 LET W[0]=A[0],W[1]=A[1]+A[1]*A[2],W[2]=A[3],W[3]=A[1],W[4]=A[1]*A[2]
1300 GOSUB 5000
1500 REM " --- Materials
1510 IF A[4]=0 THEN GOTO 1700
1520 DIM W1$(43),W[11]
1530 LET W0$=A0$(1,11)+P9$+"M"+"001"
1540 LET W1$(1)="001",W1$(20,2)="CV",W1$(22,2)=P3$(6,2),W1$(24,20)="CNVERSION"
1550 LET W[0]=1,W[1]=A[4],W[2]=A[4]
1560 GOSUB 5000
1700 REM " --- Subcontracts
1710 IF A[5]=0 THEN GOTO 1770
1720 DIM W1$(31)
1730 LET W0$=A0$(1,11)+P9$+"S"+"001"
1740 LET W1$(1)="001",W1$(4,6)="000000",W1$(10)="CNVERSION"
1750 LET W[0]=1,W[1]=A[5],W[2]=A[5]
1760 GOSUB 5000
1770 REMOVE (WOE05_DEV,KEY=K$)
1780 GOTO 1020
4000 REM " --- All Done "
4010 CALL "SYC.BB",STATUS
4020 GOTO 9900
5000 REM " --- Post To WOT-01
5010 IF W0$(15,1)="O" THEN LET WOTRAN_DEV=WOT01_DEV
5020 IF W0$(15,1)="M" THEN LET WOTRAN_DEV=WOT21_DEV
5030 IF W0$(15,1)="S" THEN LET WOTRAN_DEV=WOT31_DEV
5040 FIND (WOTRAN_DEV,KEY=W0$,DOM=5070)
5050 LET W0$(16,3)=STR(NUM(W0$(16,3))+1:"000")
5060 GOTO 5040
5070 WRITE (WOTRAN_DEV,KEY=W0$)IOL=WOTRAN
5075 LET CROSSREF$=W0$(1)
5077 WRITE (WOM07_DEV,KEY=CROSSREF$)IOL=WOM07A
5080 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
