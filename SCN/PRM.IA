0010 REM "PRM - Tax Table Maintenance"
0020 REM "Program PRM.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Jul96 Caj: Corrected PRM-10(I) IO list because first numeric was d
0031:uplicated (210)
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4000
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-10",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 PRM10I: IOLIST A0$(1),A[ALL],B[ALL],C[ALL],D[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(7,1))
0500 REM " --- Init Data
0505 PRECISION P[2]
0520 DIM A0$(32),A[2]
0530 LET K0=1,K9=6,L0=10
0540 LET A0$(1,3)=N0$+"I"
0550 LET M1=LEN(M1$),M2=LEN(M2$)
0560 LET ARRAYLEN=10
0570 DIM B[ARRAYLEN],C[ARRAYLEN],D[ARRAYLEN]
0700 REM " --- Background
0720 CALL "SYC.WC",1,0,80,0,0,8,0
0740 PRINT 'SB',@(5,3),"Table ID:",@(2,4),"Description",@(2,5),"Lower Limit:",
0740:@(1,6),"Stand Deduct:",@(2,7),"Item Deduct:",@(M1-1,9),"Over",@(10+M1),"N
0740:ot Over",@(30+M1),"Tax",@(41+M1),"Percent",
0790 PRINT 'SF',
0900 REM " --- Init File
0920 READ (PRM10_DEV,KEY=A0$(1,3),DOM=0930)
1000 REM " --- Key
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="CEK",V3$="",V4$="Enter Up To 3 Characters, <F3>=Lookup, 
1050:<F4>=End",V0=3,V1=15,V2=3,O9=0
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6100
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Find Key
1220 FIND (PRM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=PRM10I
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record
1420 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup
1510 LET A0$(7)=""
1520 DIM A[2],B[10],C[10]
2000 REM " --- Ovelay
2020 LET O0=1,O9=1
2040 RUN "PRM.IB"
4000 REM " --- Correct?
4010 PRECISION P[2]
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " ---WRITE"
4120 WRITE (PRM10_DEV,KEY=A0$(1,K9))IOL=PRM10I
4190 GOTO 1000
4200 REM " --- Delete
4220 REMOVE (PRM10_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print
4520 CALL "SYC.YN",1,"Do You Wish To Print A List Of All Records",1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4560 LET O0=1
4580 RUN "PRM.IC"
5000 REM " --- Display
5020 PRINT @(15,4),A0$(7,20),@(15,5),A[0]:M1$,@(15,6),A[1]:M1$,@(15,7),A[2]:M1
5020:$,
5040 FOR X=0 TO ARRAYLEN
5050 IF X>0 THEN IF B[X]=0 THEN EXITTO 5100
5060 IF X>0 THEN PRINT @(20,L0+X-1),B[X]:M1$,
5070 PRINT @(5,L0+X),B[X]:M1$,@(35),C[X]:M1$,@(50),D[X]:M3$,
5090 NEXT X
5100 PRINT @(20,L0+X-1),"And Over.....",
5190 RETURN
6000 REM " --- Subroutines
6100 REM " --- Code Lookup
6110 LET V4=1,V6=4,V7=3,V8=7,V9=20,V5$="I"
6120 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
6190 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
