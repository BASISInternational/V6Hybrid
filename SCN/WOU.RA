0010 REM "WOU - Closed Recurring Work Order Update"
0020 REM "Program WOU.RA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.7           |
0026 REM "|         Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "03May00 KJS: Prevent error 13/6710 (150)
0032 REM "20Oct00 KJS: Don't exit when item or warehouse are missing (d3700)
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open Files"
0105 LET FILES=28
0107 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0110 LET FILES$[1]="WOE-04",FILES$[2]="WOE-01",FILES$[3]="WOE-02"
0115 LET FILES$[4]="WOE-22",FILES$[5]="WOE-32",FILES$[6]="IVM-01"
0120 LET FILES$[7]="IVM-02",FILES$[8]="SYS-01",FILES$[9]="WOT-01"
0125 LET FILES$[10]="WOT-21",FILES$[11]="WOT-31",FILES$[12]="IVT-04"
0130 LET FILES$[13]="WOM-10",FILES$[14]="WOE-13",FILES$[15]="WOE-23"
0135 LET FILES$[16]="WOE-15",FILES$[17]="WOE-25",FILES$[18]="ARM-10"
0145 LET FILES$[19]="WOT-03",FILES$[20]="WOT-23",FILES$[21]="WOT-33"
0147 LET FILES$[22]="WOM-05",FILES$[23]="IVM-04",FILES$[24]="IVT-02"
0148 LET FILES$[25]="WOM-06"
0149 LET OPTIONS$[1]="L"
0150 CALL "SYC.DA",1,1,25,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0152 IF STATUS THEN GOTO 9900
0155 LET WOE04_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],WOE02_DEV=CHANNELS[3]
0160 LET WOE22_DEV=CHANNELS[4],WOE32_DEV=CHANNELS[5],IVM01_DEV=CHANNELS[6]
0165 LET IVM02_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8],WOT01_DEV=CHANNELS[9]
0170 LET WOT21_DEV=CHANNELS[10],WOT31_DEV=CHANNELS[11],IVT04_DEV=CHANNELS[12]
0175 LET WOM10_DEV=CHANNELS[13],WOE13_DEV=CHANNELS[14],WOE23_DEV=CHANNELS[15]
0180 LET WOE15_DEV=CHANNELS[16],WOE25_DEV=CHANNELS[17],ARM10_DEV=CHANNELS[18]
0184 LET WOT03_DEV=CHANNELS[19],WOT23_DEV=CHANNELS[20],WOT33_DEV=CHANNELS[21]
0186 LET WOM05_DEV=CHANNELS[22],IVM04_DEV=CHANNELS[23],IVT02_DEV=CHANNELS[24]
0190 LET WOM06_DEV=CHANNELS[25]
0200 REM " --- IOLISTs"
0205 WOE01A: IOLIST A0$,A1$,A[ALL]
0210 IVM01A: IOLIST B0$,B1$,B2$,B3$,B4$,B5$,B6$,B[ALL]
0215 IVM02A: IOLIST C0$,C1$,C2$,C[ALL]
0220 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0225 WOS01A: IOLIST P0$,P1$,P2$,P9$,P4$,M0$,M1$,M2$,M3$
0230 ARS01A: IOLIST *,*,*,R3$
0235 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0240 IVS01A: IOLIST X$,X$,P2$,P3$,P4$
0245 WOREQA: IOLIST W0$,W1$,W[ALL]
0250 WOTRAN: IOLIST W0$,W1$,W[ALL]
0255 WOE23A: IOLIST W0$,W1$,W[ALL]
0260 WOHIST: IOLIST W0$,W1$,W[ALL]
0265 IVT04H: IOLIST L0$(1),L1$(1),L2$(1),L[ALL]
0270 WOM10A: IOLIST Y0$,Y1$
0400 REM " --- Parameters"
0405 DIM G[4],INFO$[20],DESC[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$,R9$="N",GL$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 LET P0=NUM(P2$(1,2)),P1=NUM(P2$(5,1)),P3=NUM(P4$(1,2))
0440 LET P4=FNYY_YEAR(P4$(3,2)),P9=NUM(P2$(7,2)),G[4]=NUM(G2$(9,2))
0445 CALL "SYC.VA","AR",INFO$[ALL]
0450 IF INFO$[20]<>"Y" THEN GOTO 0465
0455 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0465)IOL=ARS01A
0460 LET R9$=R3$(11,1)
0465 CALL "SYC.VA",N2$,INFO$[ALL]
0470 LET GL$=INFO$[9]
0475 FOR X=1 TO 3; LET DESC[X]=NUM(P2$((X-1)*2+9,2)); NEXT X
0500 REM " --- Init Data"
0510 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
0520 DIM J$(80,"-")
0600 REM " --- Open Additional Files"
0610 IF GL$<>"Y" THEN GOTO 0660
0620 LET FILES$[26]="GLM-01",FILES$[27]="GLT-04",FILES$[28]="GLT-05"
0630 CALL "SYC.DA",1,26,28,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATU
0630:S
0640 IF STATUS THEN GOTO 9900
0650 LET GLM01_DEV=CHANNELS[26],GLT04_DEV=CHANNELS[27],GLT05_DEV=CHANNELS[28]
0660 REM " --- Assign IVC.UA Channels "
0670 LET IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV
0680 LET IVFILES[4]=IVM04_DEV,IVFILES[42]=IVT02_DEV
0690 LET IVFILES[44]=IVT04_DEV
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Options"
0810 LET V4$="Are You Ready to Close These Work Orders?"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM 
0910 DIM A[11],B[12],W[11]
0920 CALL "SYC.NB","Updating",7,COLUMN
0950 REM " --- Disallow 'M'enu option in error routine
0960 LET EXIT_CTRL=1
1000 REM " --- Initial Read"
1020 READ (WOE04_DEV,KEY=N0$+"  "+"B",DOM=1100)
1100 LET K0$=KEY(WOE04_DEV,END=4000)
1110 IF K0$(1,5)<>N0$+"  "+"B" THEN GOTO 4000
1120 EXTRACT (WOE01_DEV,KEY=K0$(1,4)+K0$(6,7),DOM=3920)IOL=WOE01A
1130 IF A0$(14,1)="R" THEN GOTO 1160
1140 READ (WOE04_DEV)
1150 GOTO 1100
1160 PRINT @(COLUMN,11),A0$(5,7),
1200 REM " --- Calculate Actuals
1210 LET WOTRAN_DEV=WOT01_DEV
1220 DIM D[4],T[5]
1230 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=1240)
1240 LET K$=KEY(WOTRAN_DEV,END=1340)
1250 IF K$(1,11)<>A0$(1,11) THEN GOTO 1340
1260 READ (WOTRAN_DEV,KEY=K$)IOL=WOTRAN
1270 LET X=POS(W0$(15,1)="MS")+2
1280 IF X<=2 THEN GOTO 1310
1290 LET D[X]=D[X]+W[2]
1300 GOTO 1320
1310 LET D[1]=D[1]+W[0]*W[3],D[2]=D[2]+W[2]-W[0]*W[3]; REM "Split Direct & OH
1320 LET D[0]=D[0]+W[2]
1330 GOTO 1240
1340 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV; GOTO 1230
1350 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV; GOTO 1230
1360 LET T[0]=D[0]-A[3],T[0]=-T[0],T[1]=A[4]*A[5]; REM "CALC TOTALS
1370 DIM Y0$(40),Y1$(70,"0")
1380 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=2000)IOL=WOM10A
2000 IF A1$(96,1)<>"Y" THEN GOTO 2400
2400 REM " --- Do Variances Here
2410 IF A1$(94,1)<>"Y" THEN LET T[0]=-T[1]; GOTO 2900
2420 IF Y0$(27,1)="A" THEN LET T[1]=D[0]-A[3]; GOTO 2900
2430 REM " --- Calc Standards"
2440 LET WOREQ_DEV=WOE02_DEV
2450 DIM E[4]
2460 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=2470)
2470 LET K$=KEY(WOREQ_DEV,END=2600)
2480 IF K$(1,11)<>A0$(1,11) THEN GOTO 2600
2490 READ (WOREQ_DEV,KEY=K$)IOL=WOREQA
2500 LET X=POS(W0$(12,1)="BC")+2; IF X>2 THEN LET E[X]=E[X]+W[3]; GOTO 2520
2510 LET E[1]=E[1]+W[2]*W[6],E[2]=E[2]+W[3]-W[2]*W[6]; REM "Split Direct & OH
2520 LET E[0]=E[0]+W[3]
2530 GOTO 2470
2540 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 2460
2550 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 2460
2600 REM " --- Calc Variances
2620 PRECISION 2
2625 IF E[0]=0 THEN LET Y0=0; GOTO 2800
2630 IF A[0]=A[2]+A[4] OR E[0]=0 OR A1$(95,1)="N" THEN LET Y0=A[4]*A[5]+A[3]; 
2630:GOTO 2800
2700 REM " --- Pro-Rate Standards"
2710 IF A[0]<>0 THEN LET Y0=E[0]*(A[2]+A[4])/A[0] ELSE LET Y0=0
2720 IF Y0=E[0] THEN GOTO 2800
2725 IF E[0]=0 THEN LET E[1]=0,E[3]=0,E[4]=0; GOTO 2760
2730 LET E[1]=E[1]*Y0/E[0]
2740 LET E[3]=E[3]*Y0/E[0]
2750 LET E[4]=E[4]*Y0/E[0]
2760 LET E[2]=Y0-(E[1]+E[4]+E[3])
2800 REM " --- Calc Variance"
2820 LET T[2]=D[1]-E[1],T[4]=D[3]-E[3],T[5]=D[4]-E[4]
2830 LET T[3]=D[0]-(Y0+T[2]+T[4]+T[5]); REM "Any left overs goto OH VNCE
2900 REM " --- Post G/L Here"
2910 PRECISION 2
2915 LET Y1$=Y1$(1,20)+Y1$(51,40)
2920 FOR X=0 TO 5
2930 IF T[X]=0 THEN GOTO 2990
2940 LET ACCOUNT$=Y1$(X*10+1,10),WHEN$=A1$(91,3),AMOUNT=T[X],UNITS=0
2945 LET MEMO$=A1$(1,30)
2950 LET REF1$="WO "+A0$(5,7),REF2$="Whse "+A0$(54,2)
2955 LET REF3$="Type "+A0$(12,2)
2960 GOSUB GLPOST
2990 NEXT X
3000 REM " --- Complete It"
3010 IF A1$(94,1)="Y" THEN GOTO 3500
3020 IF A0$(31,3)<A1$(91,3) THEN LET A0$(31,3)=A1$(91,3)
3030 LET A[2]=A[2]+A[4],A[3]=A[3]+A[4]*A[5],A1$(91,5)="",A[4]=0,A[5]=0
3040 REM " --- Recurring Purge... - Move to History"
3050 LET WOTRAN_DEV=WOT01_DEV,WOHIST_DEV=WOT03_DEV
3060 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=3070)
3070 LET K$=KEY(WOTRAN_DEV,END=3140)
3080 IF K$(1,11)<>A0$(1,11) THEN GOTO 3140
3090 IF K$(12,3)>A0$(31,3) THEN GOTO 3140
3100 READ (WOTRAN_DEV,KEY=K$)IOL=WOTRAN
3110 WRITE (WOHIST_DEV,KEY=K$)IOL=WOHIST
3120 REMOVE (WOTRAN_DEV,KEY=K$,DOM=3070)
3130 GOTO 3070
3140 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV,WOHIST_DEV=WOT23_DE
3140:V; GOTO 3060
3150 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV,WOHIST_DEV=WOT33_DE
3150:V; GOTO 3060
3160 GOTO 3900
3500 REM " --- Complete, Complete"
3510 GOSUB 6700
3520 LET A[2]=A[2]+A[4],A[3]=A[3]+A[4]*A[5]
3530 LET A0$(15,1)="C",A0$(31,3)=A1$(91,3)
3540 REM " --- Post ARE-03 (Completed)
3596 LET A[4]=0,A[5]=0
3600 REM " --- Check on WOE-15/25 and WOE-13/23 (Materials)"
3610 REMOVE (WOE13_DEV,KEY=A0$(1,11)+"000",DOM=3620)
3620 READ (WOE23_DEV,KEY=A0$(1,11),DOM=3630)
3630 LET K$=KEY(WOE23_DEV,END=3750)
3640 IF K$(1,11)<>A0$(1,11) THEN GOTO 3750
3660 READ (WOE23_DEV,KEY=K$)IOL=WOE23A
3670 LET COMMIT_ADJ=W[0]-W[1]
3675 IF COMMIT_ADJ=0 THEN GOTO 3720
3680 LET ITEMS$[0]=N0$,ITEMS$[1]=W1$(19,2),ITEMS$[3]=W1$(21,20)
3685 LET ACTIONS$="UC",REFS[0]=COMMIT_ADJ
3690 CALL "IVC.UA",ACTIONS$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],
3690:REFS$[ALL],REFS[ALL],STATUS
3720 REMOVE (WOE23_DEV,KEY=K$,DOM=3730)
3730 GOTO 3630
3750 REMOVE (WOE15_DEV,KEY=A0$(1,11)+"000",DOM=3755)
3755 READ (WOE25_DEV,KEY=A0$(1,11),DOM=3760)
3760 LET K2$=KEY(WOE25_DEV,END=3900)
3770 IF K2$(1,11)<>A0$(1,11) THEN GOTO 3900
3780 REMOVE (WOE25_DEV,KEY=K2$)
3785 GOTO 3760
3900 WRITE (WOE01_DEV,KEY=A0$(1,11))IOL=WOE01A
3920 REMOVE (WOE04_DEV,KEY=K0$,DOM=3990)
3990 GOTO 1100
4000 REM " --- End"
4010 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4100 GOTO 9900
6600 REM " --- Item Distribution Code"
6620 DIM Q8$(80,"0")
6640 FIND (ARM10_DEV,KEY=N0$+"D"+C1$(13,2),DOM=6690)*,Q8$(1)
6660 LET Y2$=Q8$(61,10)
6690 RETURN
6700 REM " --- Remove Schedule Detail Records"
6710 READ (WOM06_DEV,KEY=A0$(1,2)+A0$(5,7),DOM=6720)
6720 LET K22$=KEY(WOM06_DEV,END=6790)
6730 IF POS(A0$(1,2)+A0$(5,7)=K22$)<>1 THEN GOTO 6790
6740 REMOVE (WOM06_DEV,KEY=K22$)
6750 REMOVE (WOM05_DEV,KEY=K22$(1,2)+K22$(13,6)+K22$(3,10),DOM=6760)
6760 GOTO 6720
6790 RETURN
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST:
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,RE
6950:F3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
