0010 REM "PRX - Convert 5.2 PR Files to 6.0 Format"
0020 REM "Program PRX60A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "17Jul96 Caj: Possible infinite loops (1023,1223,1423)
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="PRM-01",FILES$[3]="PRM-12"
0125 LET FILES$[4]="PRT-11",FILES$[5]="PRT-21",FILES$[6]="PRT-31"
0130 LET FILES$[7]="PRM-02",FILES$[8]="PRT-01"
0135 LET FILES$[9]="PRM02.TMP",FILES$[10]="PRT01.TMP"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0165 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],PRM01_DEV=CHANNELS[2],PRM12_DEV=CHANNELS[3]
0175 LET PRT11_DEV=CHANNELS[4],PRT21_DEV=CHANNELS[5],PRT31_DEV=CHANNELS[6]
0200 REM " --- IOLIST's
0210 PRM01A: IOLIST A0$(1),A1$(1)
0220 PRM02TMP: IOLIST X0$(1),X1$(1),X1[ALL],X2$(1),X2[ALL]
0230 PRM02A: IOLIST A0$(1),A[ALL]
0250 PRM12A: IOLIST B0$(1),B[ALL]
0260 PRT01A: IOLIST A0$(1),A[ALL]
0270 PRT11A: IOLIST B0$(1),B[ALL]
0280 PRT21A: IOLIST C0$(1),C[ALL]
0290 PRT31A: IOLIST D0$(1),D[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N1$=F4$,N2$="PR",N4$=F5$
0500 REM " --- Initializations"
0505 PRECISION 14
0800 REM " --- Display Warning"
0810 CALL "SYC60A",N2$,SYS01_DEV,STATUS
0820 IF STATUS THEN GOTO 9900
0900 REM " --- Additional file Opens"
0905 PRINT @(40,3),'CL',@(0,5),'CE',
0910 CALL "SYC.NB"," Making Temporary Files",0,COLUMN
0920 LET NUMBER=2
0930 DIM FROM$[NUMBER],TMPFILE$[NUMBER],FLAG$[NUMBER]
0940 LET FROM$[1]="PRM-02",TMPFILE$[1]="PRM02.TMP"
0945 LET FROM$[2]="PRT-01",TMPFILE$[2]="PRT01.TMP"
0950 CALL "SYC.RN",FROM$[ALL],TMPFILE$[ALL],FLAG$[ALL],NUMBER,STATUS
0955 IF STATUS<>0 THEN GOTO 9900
0970 CALL "SYC.DA",1,7,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0975 IF STATUS<>0 THEN GOTO 9900
0980 LET PRM02_DEV=CHANNELS[7],PRT01_DEV=CHANNELS[8]
0985 LET PRM02TMP_DEV=CHANNELS[9],PRT01TMP_DEV=CHANNELS[10]
1000 REM " --- PRM-02 and PRM-12"
1005 LET X$="Employee Pay Rate And Change Data",X=13
1010 GOSUB 6900
1020 LET K$=KEY(PRM02TMP_DEV,END=1100)
1023 IF LEN(K$)<11 OR POS("Z"=K$)=1 THEN READ (PRM02TMP_DEV); GOTO 1090
1025 PRINT @(COLUMN,11),K$(1,2)," ",K$(3),
1030 DIM A0$(13),A[1],B0$(17),B[1],X0$(11),X1$(10),X1[11],X2$(15),X2[9]
1040 READ (PRM02TMP_DEV)IOL=PRM02TMP
1050 FOR N=0 TO 4
1060 IF X1$(1+N*2,2)="  " THEN GOTO 1070 ELSE LET A0$(1)=X0$(1,11)+X1$(1+N*2,2
1060:),A[0]=X1[N],A[1]=X1[N+6]
1065 WRITE (PRM02_DEV,KEY=A0$)IOL=PRM02A
1070 IF X2$(1+N*3,3)="   " THEN GOTO 1080 ELSE LET B0$(1)=X0$(1,11)+FNYY_YY21$
1070:(FNM$(FNC$(X2$(1+N*3,3)))),B[0]=X2[N],B[1]=X2[N+5]
1075 WRITE (PRM12_DEV,KEY=B0$)IOL=PRM12A
1080 NEXT N
1090 GOTO 1020
1100 REM " --- Erase PRM02.TMP Temporary File"
1120 CLOSE (PRM02TMP_DEV,ERR=1140)
1140 ERASE "PRM02.TMP",ERR=1200
1200 REM " --- PRM-01
1205 LET X$="Employee Master File",X=13
1210 GOSUB 6900
1220 LET K$=KEY(PRM01_DEV,END=1400)
1223 IF LEN(K$)<11 OR POS("Z"=K$)=1 THEN REMOVE (PRM01_DEV,KEY=K$); GOTO 1290
1225 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,9),
1230 DIM A0$(11),A1$(240)
1235 READ (PRM01_DEV)IOL=PRM01A
1260 IF POS(A1$(156,1)="NS")=0 THEN GOTO 1220
1265 IF A1$(156,1)="S" THEN LET A1$(156,1)="H"
1270 IF A1$(156,1)="N" THEN LET A1$(156,1)="A"
1280 WRITE (PRM01_DEV,KEY=A0$)IOL=PRM01A
1290 GOTO 1220
1400 REM " --- PRT-01, PRT-11, PRT-21 and PRT-31"
1405 LET X$="Employee Detail History Data",X=13
1410 GOSUB 6900
1420 LET K$=KEY(PRT01TMP_DEV,END=1650)
1423 IF LEN(K$)<14 OR POS("Z"=K$)=1 THEN READ (PRT01TMP_DEV); GOTO 1490
1425 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,9),
1430 ON POS(K$(12,1)="ABCD") GOTO 1440,1450,1500,1550,1600
1440 READ (PRT01TMP_DEV)
1445 GOTO 1420
1450 REM " --- Old PRT-01 'A' Record"
1460 DIM A0$(14),A[16]
1470 READ (PRT01TMP_DEV)IOL=PRT01A
1480 WRITE (PRT01_DEV,KEY=A0$)IOL=PRT01A
1490 GOTO 1420
1500 REM " --- Old PRT-01 'B' Record"
1510 DIM B0$(21),B[7]
1520 READ (PRT01TMP_DEV)IOL=PRT11A
1530 WRITE (PRT11_DEV,KEY=B0$(1,14))IOL=PRT11A
1540 GOTO 1420
1550 REM " --- Old PRT-01 'C' Record" 
1560 DIM C0$(48),C[18]
1570 READ (PRT01TMP_DEV)IOL=PRT21A
1580 WRITE (PRT21_DEV,KEY=C0$(1,14))IOL=PRT21A
1590 GOTO 1420
1600 REM " --- Old PRT-01 'D' Record" 
1610 DIM D0$(19),D[19]
1620 READ (PRT01TMP_DEV)IOL=PRT31A
1630 WRITE (PRT31_DEV,KEY=D0$(1,14))IOL=PRT31A
1640 GOTO 1420
1650 REM " --- Erase PRT01.TMP Temporary File"
1660 CLOSE (PRT01TMP_DEV,ERR=1670)
1670 ERASE "PRT01.TMP",ERR=1800
1800 REM " --- PRT-03, PRT-04, PRT-05 and PRW-06"
1805 PRINT @(40,3),'CL',@(0,5),'CE',
1810 CALL "SYC.NB"," Renaming 'Report' Files",0,COLUMN
1820 LET NUMBER=4
1830 DIM FROM$[NUMBER],TMPFILE$[NUMBER],FLAG$[NUMBER]
1840 LET FROM$[1]="PRR-01",TMPFILE$[1]="PRT-03"
1845 LET FROM$[2]="PRR-02",TMPFILE$[2]="PRT-04"
1850 LET FROM$[3]="PRR-04",TMPFILE$[3]="PRT-05"
1855 LET FROM$[4]="PRR-06",TMPFILE$[4]="PRW-06"
1860 CALL "SYC.RN",FROM$[ALL],TMPFILE$[ALL],FLAG$[ALL],NUMBER,STATUS
1865 IF STATUS<>0 THEN GOTO 9900
4000 GOTO 9900
6900 REM " --- Update screen"
6910 PRINT @(40,3),X$,'CL',@(0,5),'CE',
6920 CALL "SYC.NB","Processing",X,COLUMN
6990 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9880 LET LINE_ERR$=LST(PGM(TCB(5)))
9890 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
