0010 REM "PRC - Generate Payroll G/L Postings"
0020 REM "Program PRC.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "09May01 KJS: Use the same caculation for accural taxes as PRU.CB (a4
0031:70,a480,a1615,m1620,a1625)
0040 REM 
0041 REM "ACTION      : 0=/Initialization/1=Header Posting/2=Detail Posting"
0045 REM "CHANNELS(0) : GLM01_DEV : GLM-01 Channel"
0046 REM "        (1) : GLT04_DEV : GLT-04 (or GLW-11) Channel"
0047 REM "        (2) : GLT05_DEV : GLT-05 Channel"
0048 REM "        (3) : PRM10_DEV : PRM-10 Channel"
0050 REM "INFO$   [0] : REF1$     : Formatted Employee Number"
0052 REM "        [1] : MEMO$     : Employee Name"
0053 REM "        [2] : ACCRUAL$  : Payroll Parameter Accrual Flag"
0054 REM "        [3] : PAYDATE$  : Pay Date"
0055 REM "        [4] : CHKDATE$  : Check Date"
0056 REM "        [5] : RECORD$   : PRE-01 Record (1st String)"
0057 REM "        [6] : DEPT$     : Department Code"
0058 REM "        [7] :           : Pay Period"
0059 REM "        [8] : ACCRDATE$ : Accrual Date"
0060 REM "        [9] : CASHACCT$ : Cash G/L Account Number (Returned)"
0061 REM "       [10] : ACCRACCT$ : Accrual G/L Account Number (Returned)"
0062 REM "INFO    [0] : G0        : G/L Account Number Length"
0063 REM "        [1] : DAYS      : Accrual Days"
0064 REM "        [2] : A         : Payroll/Posting Amount"
0065 REM "        [3] : U         : Payroll/Posting Units"
0066 REM "        [4] : PR_PREC   : P/R Precision"
0075 REM 
0080 SETESC 9900
0085 SETERR 9000
0090 ENTER ACTION,CHANNELS[ALL],INFO$[ALL],INFO[ALL]
0200 REM " --- IOLIST's"
0210 PRM10B: IOLIST A0$,A1$(1)
0220 PRM10E: IOLIST S0$,S1$(1),S2$,S3$,S4$,S5$,S6$,S7$,S8$
0400 REM " --- Parameters"
0410 LET GLM01_DEV=CHANNELS[0],GLT04_DEV=CHANNELS[1],GLT05_DEV=CHANNELS[2]
0420 LET PRM10_DEV=CHANNELS[3],REF1$=INFO$[0],MEMO$=INFO$[1]
0430 LET ACCRUAL$=INFO$[2],PAYDATE$=INFO$[3],CHKDATE$=INFO$[4]
0440 LET RECORD$=INFO$[5],DEPT$=INFO$[6],REF2$=RECORD$(15,7)
0450 LET ACCRDATE$=INFO$[8],CASHACCT$=INFO$[9],ACCRACCT$=INFO$[10]
0460 LET G0=INFO[0],DAYS=INFO[1],A=INFO[2],U=INFO[3]
0470 LET T1$="365052026024012004001",T2$="DWBSMQA",PR_PREC=INFO[4]
0480 LET DAYS_PER_PERIOD=NUM(T1$(POS(INFO$[7]=T2$)*3-2,3))
0500 REM " --- Initializations"
0510 DIM DEBIT$(G0,"0"),CREDIT$(G0,"0"),A1$(G0,"0"),S1$(G0*2,"0")
0550 LET N0$=RECORD$(1,2),AMOUNT=0,UNITS=0,WHEN$=CHKDATE$
0560 IF ACCRUAL$="Y" THEN LET WHEN$=PAYDATE$
0570 LET CODE$=RECORD$(25,2)
1000 REM " --- Retrieve department record"
1010 FIND (PRM10_DEV,KEY=N0$+"E"+DEPT$,DOM=1020)IOL=PRM10E
1080 ON ACTION GOTO 1100,1200,1090,4000
1090 ON POS(RECORD$(22,1)="ABCD") GOTO 4000,1500,2000,2200,2400
1100 REM " --- Store and return Cash/Accrual G/L Accounts"
1110 LET INFO$[9]=S1$(1,G0),INFO$[10]=S1$(G0+1,G0)
1190 GOTO 4000
1200 REM " --- Heading Record
1210 LET DEBIT$=CASHACCT$,CREDIT$=ACCRACCT$
1220 LET WHEN$=CHKDATE$,REF3$="Payroll"
1290 GOTO 3500
1500 REM " --- Earnings Record"
1510 LET CREDIT$=ACCRACCT$,WHEN$=PAYDATE$,X=POS(CODE$=S2$,2)
1520 LET REF3$=CODE$+" "+RECORD$(27,2)+" "+RECORD$(29,2)
1530 IF X=0 THEN GOTO 3500
1540 LET DEBIT$=S3$(INT(X/2)*G0+1,G0)
1590 IF DAYS=0 THEN GOTO 1900
1600 REM " --- Accrued payroll"
1610 LET ACCOUNT$=DEBIT$,GLDATE$=WHEN$
1615 PRECISION PR_PREC
1620 LET AMOUNT=A*DAYS*DAYS_PER_PERIOD/365,UNITS=U*DAYS*DAYS_PER_PERIOD/365
1625 PRECISION 2
1630 GOSUB GLPOST
1640 LET GLDATE$=ACCRDATE$,AMOUNT=-AMOUNT,UNITS=-UNITS
1650 GOSUB GLPOST
1660 LET ACCOUNT$=CREDIT$,AMOUNT=-AMOUNT,UNITS=-UNITS
1670 GOSUB GLPOST
1680 LET GLDATE$=WHEN$,AMOUNT=-AMOUNT,UNITS=-UNITS
1690 GOSUB GLPOST
1900 REM 
1910 LET UNITS=U
1990 GOTO 3500
2000 REM " --- Deduction Record"
2010 FIND (PRM10_DEV,KEY=N0$+"B"+CODE$,DOM=2020)IOL=PRM10B
2020 LET DEBIT$=A1$,CREDIT$=ACCRACCT$,REF3$="Deduct "+CODE$
2090 GOTO 3500
2200 REM " --- Tax Record"
2210 LET X=POS(CODE$=S4$,2),CREDIT$=ACCRACCT$
2220 IF X=0 THEN GOTO 3500
2230 LET DEBIT$=S5$(INT(X/2)*G0+1,G0),REF3$="Tax "+CODE$
2290 GOTO 3500
2400 REM " --- Contribution Record"
2410 LET X=POS(CODE$=S6$,2)
2420 IF X=0 THEN GOTO 3500
2430 LET CREDIT$=S8$(INT(X/2)*G0+1,G0),REF3$="Contrib "+CODE$
2440 LET DEBIT$=S7$(INT(X/2)*G0+1,G0)
2490 GOTO 3500
3500 REM " --- Generate G/L Postings"
3510 LET ACCOUNT$=DEBIT$,GLDATE$=WHEN$,AMOUNT=A
3520 IF ACTION=2 THEN IF POS(RECORD$(22,1)="BC")>0 THEN LET AMOUNT=-AMOUNT
3530 GOSUB GLPOST
3540 LET ACCOUNT$=CREDIT$,AMOUNT=-AMOUNT,UNITS=0
3550 GOSUB GLPOST
4000 REM " --- All done"
4090 GOTO 9900
6900 REM " --- Standard G/L Recap Routine"
6910 GLPOST:
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,
6950:REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to Calling Program"
9950 EXIT
9999 END
