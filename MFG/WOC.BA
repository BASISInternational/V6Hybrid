0010 REM "WOC - Operations Hours Between Days(Public/Called)"
0020 REM "Program WOC.BA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "V0$   : From Date 'YYMMDD' Or Packed 'YMD'
0051 REM "V1$   : Through Date 'YYMMDD' or Packed 'YMD'
0052 REM "V0    : # Of Hours From V0$ Date through V1$ Date
0053 REM "OP$   : Operation
0054 REM "FLAG$ : 'Y' If Calendar Found Non-Forecast Days
0070 SETESC 9000
0080 SETERR 9000
0090 ENTER V0$,V1$,V0,OP$,FLAG$
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="WOM-04",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET WOM04_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLISTs"
0210 WOM04A: IOLIST A0$,A1$,A2$,A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2)
0500 REM " --- Init Data"
0510 DIM A[31]
0520 LET FLAG$="",V0=0
1000 REM " --- Check Validity Of From Date"
1020 LET V$=V0$,X$="",X0=0
1030 CALL "SYC.CA",V$,X$,X0
1040 IF V$="N" THEN GOTO 4100
1100 REM " --- Check Validity Of Through Date"
1120 LET V$=V1$,X$="",X0=0
1130 CALL "SYC.CA",V$,X$,X0
1140 IF V$="N" THEN GOTO 4100
1200 REM " --- Check Forward Or Backward Calc"
1210 LET V$=FNYY_YY21$(FNM$(FNC$(V0$)))
1220 LET W$=FNYY_YY21$(FNM$(FNC$(V1$)))
1230 IF V0$>V1$ THEN GOSUB 5000 ELSE GOSUB 6000
4000 REM " --- Done - Successful"
4090 GOTO 9950
4100 REM " --- Done - Unsuccessful"
4120 GOTO 9900
5000 REM " --- Calculate Backwards"
5010 LET F_YR=FNYY_YEAR(V$(1,2)),F_MTH=NUM(V$(3,2)),F_DAY=NUM(V$(5,2))
5020 FIND (WOM04_DEV,KEY=N0$+OP$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=5900)I
5020:OL=WOM04A
5030 FOR X=F_DAY TO 1 STEP -1
5040 IF A[X]<0 THEN EXITTO 5900
5050 IF W$(1,4)=A0$(6,4) AND NUM(W$(5,2))>X THEN EXITTO 5490
5055 LET V0=V0-A[X]
5060 NEXT X
5100 LET X$=""
5110 CALL "SYC.CA",FNYEAR_YY21$(F_YR)+STR(F_MTH:"00")+"01",X$,-1
5120 LET F_YR=FNYY_YEAR(X$(1,2)),F_MTH=NUM(X$(3,2)),F_DAY=NUM(X$(5,2))
5130 IF W$(1,4)>X$(1,4) THEN GOTO 5490
5140 GOTO 5020
5490 RETURN
5900 REM " --- Operation Date Not Scheduled"
5910 LET FLAG$="Y"
5940 RETURN
6000 REM " --- Calculate Forwards"
6010 LET F_YR=FNYY_YEAR(V$(1,2)),F_MTH=NUM(V$(3,2)),F_DAY=NUM(V$(5,2))
6020 FIND (WOM04_DEV,KEY=N0$+OP$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=6500)I
6020:OL=WOM04A
6030 IF A[0]=0 THEN GOTO 6500
6040 FOR X=F_DAY TO A[0]
6050 IF A[X]<0 THEN EXITTO 6500
6060 IF W$(1,4)<A0$(6,4) THEN EXITTO 6590
6070 IF W$(1,4)=A0$(6,4) AND NUM(W$(5,2))<X THEN EXITTO 6590
6080 LET V0=V0+A[X]
6090 NEXT X
6100 LET F_MTH=F_MTH+1,F_DAY=1
6110 IF F_MTH>12 THEN LET F_YR=F_YR+1,F_MTH=1
6120 GOTO 6020
6500 REM " --- Date Not Scheduled"
6510 LET FLAG$="Y"
6590 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Exit - Unsuccessful"
9920 LET FLAG$="Y"
9950 REM " --- Return to caller"
9960 CLOSE (WOM04_DEV,ERR=9970); LET WOM04_DEV=0
9970 CLOSE (SYS01_DEV,ERR=9980); LET SYS01_DEV=0
9980 EXIT
9999 END
