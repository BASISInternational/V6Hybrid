0010 REM "WOC - Work Order SpeedSearch (Work Orders by Customer)"
0020 REM "Program WOC.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="IVM-01",FILES$[3]="IVM-10"
0130 LET FILES$[4]="WOE-01",FILES$[5]="WOE-03"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STA
0140:TUS
0150 IF STATUS THEN GOTO 9900
0160 LET ARM01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0170 LET WOE01_DEV=CHANNELS[4],WOE03_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 WOE01A: IOLIST B0$,B1$,B[ALL]
0230 IVM01A: IOLIST C0$(1),C1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,X$,X$,X$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0280 WOS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0300 IVM10C: IOLIST X0$(1)
0400 REM " --- Parameters"
0405 DIM P[1],I[5],X$(128)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8)),SNAME$=FNP$(X$(19,8))
0420 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0425 LET SHEIGHT=NUM(X$(45,2)),MAXOPT=NUM(X$(57,2))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0435 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0440 LET PAGES=INT((DSZ-5000)/4000),SYS01_DEV=NUM(STBL("!SYS01"))
0445 IF PAGES<3 THEN GOTO 9900
0450 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0455 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9900)IOL=ARS01A
0460 LET P[0]=NUM(P2$(1,2))
0465 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0470 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(3,2))
0475 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0480 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9900)IOL=WOS01A
0485 IF I3$(2,1)<>"Y" THEN GOTO 9900
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXOPT,6],A0$(8),A1$(320),B0$(75),B1$(141),B[11]
0515 DIM C0$(22),C1$(60),X0$(32),HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,M2=LEN(M2$),DONE=0,OPTION=0
0550 LET A0$(1)=N0$+OPTION$[3],P=WIDTH-13-M2,DW=P,PGM$=PGM(-2)
0560 LET NF$="(Not On File)"
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TIT
0610:LE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="Order   WO #    Type Status Started  Last Act Est Comp"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1200
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1030 PRINT @(1,0),'SB',"Customer:",'SF','CL',@(0,1),'CL',@(0,2),'CL',
1035 LET V2$="",V2$=KEY(ARM01_DEV,END=1050)
1040 IF POS(A0$(1,2)=V2$)<>1 THEN LET V2$=""
1045 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KE",V3$=M0$,V0=6,V1=11,V2=0,V4$=""
1055 PRINT @(V0+V1+1,V2),'CL',
1060 GOSUB 7000
1063 IF V3=6 THEN GOTO 4000
1065 IF V3=5 THEN GOTO 4000
1070 IF V3=4 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1080 IF V3=3 THEN CALL "SYC.LC",V$
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1200 REM " --- Retrieve records for display"
1210 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2]
1215 DIM DESCRIPTION$(I[2]+I[4]+I[5]),X$(WIDTH-2)
1220 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
1230 READ (ARM01_DEV,KEY=A0$,DOM=1000)IOL=ARM01A
1250 LET X$(1)='SB'+" Customer: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+A1$(1,30)
1260 LET X1$=A0$(3,6),X2$=PARAM$
1270 CALL "SYC.QB",X$,X1$,X2$
1280 PRINT @(0,0),X$,
1300 REM " --- Position WOE-03"
1310 DIM B0$(75),B1$(141),B[11]
1330 LET X$=A0$(1,2)+"B"+A0$(3)
1390 READ (WOE03_DEV,KEY=X$,DOM=1400)
1400 REM " --- Read next WOE-03 record"
1410 LET K$=KEY(WOE03_DEV,END=1900)
1420 IF POS(X$=K$)<>1 THEN GOTO 1900
1430 READ (WOE03_DEV)
1440 LET B0$(1)=K$(1,2)+K$(20)
1450 FIND (WOE01_DEV,KEY=B0$(1,11),DOM=1400)IOL=WOE01A
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G$(WIDTH-2)
1615 LET STATUS$=""
1620 IF B0$(15,1)="O" THEN LET STATUS$="Open"
1625 IF B0$(15,1)="P" THEN LET STATUS$="Plan'd"
1630 IF B0$(15,1)="Q" THEN LET STATUS$="Quote"
1635 IF B0$(15,1)="C" THEN LET STATUS$="Closed"
1650 LET G$(2)=B0$(40,7)+" "+B0$(5,7)+" "+B0$(12,2)+"   "+STATUS$
1660 IF FNP$(B0$(28,3))<>"" THEN LET G$(30)=FNB$(B0$(28,3))
1670 IF FNP$(B0$(25,3))<>"" THEN LET G$(39)=FNB$(B0$(25,3))
1680 IF FNP$(B0$(22,3))<>"" THEN LET G$(48)=FNB$(B0$(22,3))
1690 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=C0$(3)+B0$(5,7)
1700 REM " --- Retrieve inventory item and warehouse"
1710 DIM ITEM$(DW),WHSE$(DW),DESC$(DW)
1715 LET C0$(1)=N0$+B0$(56),C1$(1)=NF$,X0$(1)=N0$+"C"+B0$(54,2)+NF$
1720 IF B0$(14,1)<>"I" THEN GOTO 1750
1725 FIND (IVM01_DEV,KEY=C0$,DOM=1730)IOL=IVM01A
1730 LET ITEM$="{Item}"+C0$(3)
1735 LET DESC$="{Desc}"+FNITEM$(C1$,I[3],I[4],I[5])
1740 GOTO 1760
1750 LET ITEM$(1)="{Desc}"+B1$(1,30),DESC$(1)="      "+B1$(31,30)
1760 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1770)IOL=IVM10C
1770 LET WHSE$(1)="{Whse}"+X0$(4,2)+" "+X0$(6,24)
1800 REM " --- Bottom window record"
1810 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),G$(1)=""
1820 LET G$(1)=WHSE$,G$(P+1)="{Sched Qty}"+STR(B[0]:M2$)
1830 LET MORE$[PAGE,X1,1]=G$,G$(1)=ITEM$,G$(P)="{Qty Closed}"+STR(B[2]:M2$)
1840 LET MORE$[PAGE,X1,2]=G$,G$(1)=DESC$
1850 LET G$(P+3)="{Balance}"+STR(B[0]-B[2]:M2$),MORE$[PAGE,X1,3]=G$
1860 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1890 GOTO 1400
1900 REM " --- No more work orders"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No Work Orders Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1980 LET SELECTION$="PRIOR"
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 LET C0$(1)=N0$+ANSWER$
3040 IF LEN(ANSWER$)>20 THEN LET B0$(1)=N0$+"  "+ANSWER$(21)
3050 FIND (IVM01_DEV,KEY=C0$,DOM=3060)IOL=IVM01A
3060 FIND (WOE01_DEV,KEY=B0$(1,11),DOM=3070)IOL=WOE01A
3070 GOSUB 5000
3080 IF CONTROL=1 THEN GOTO 1000
3090 IF OPTION$[2]="ITEM/ORDER" THEN LET OPTION$[3]=C0$(3)+B0$(5,7)
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],W
5050:ININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR
5080:"
5090 RETURN
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),
8090:32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT
9999 END
