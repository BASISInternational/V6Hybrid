0010 REM "WOS - WO Issues Serial/Lot# Input"
0020 REM "Program WOE.DC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0085 SETERR 9000
0090 PRECISION IVS01_PRECISION
0095 ON O0 GOTO 0100,0100,0100,4800
0200 REM " --- IOLISTs"
0210 WOE14A: IOLIST T0$,T1$,T2$,T[ALL]
0220 IVM07A: IOLIST H0$,H1$,H[ALL]
0500 REM " --- Init Data"
0510 DIM T0$(17),T1$(20),T2$(20),T[2],H[11]
0520 LET T0$(1)=W0$(1,14),L9=19
0530 LET LOT$=P3$(17,1)
0540 IF LOT$="S" THEN LET P8$="Serial #"
0550 IF LOT$="L" THEN LET P8$="Lot #"
0560 LET O7=0
0570 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
0580 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$,PARAMS$[3]=P3$
0590 LET PARAMS$[4]=P4$
0910 GOSUB 5100
0920 LET O7=0
0930 READ (WOE14_DEV,KEY=W0$(1,14),DOM=0940)
0940 LET K1$=KEY(WOE14_DEV,END=0990)
0950 IF K1$(1,14)<>W0$(1,14) THEN GOTO 0990
0960 READ (WOE14_DEV)IOL=WOE14A
0970 LET O7=O7+T[0]
0980 GOTO 0940
0990 IF O7=W[3] THEN GOTO 4000
1000 REM " --- Overflow"
1020 IF L>L9 THEN PRINT @(0,20),'CE',@(0,L1+2),'LD'; LET L=L-1; GOTO 1050
1030 CALL "SYC.WC",1,0,80,0,0,20,0
1050 IF V$="N" THEN LET V$=""; GOTO 1420
1055 IF V3=4 THEN GOTO 4000
1100 REM " --- Next Key
1120 LET K1$=KEY(WOE14_DEV,END=1400)
1140 IF K1$(1,14)<>W0$(1,14) THEN GOTO 1400
1150 READ (WOE14_DEV)IOL=WOE14A
1160 GOTO 1490
1400 REM " --- Seq"
1410 LET T0=NUM(T0$(15,3))+1
1420 LET V0$="Z",V1$="ER",V2$=STR(T0),V3$="",V4$="Sequence #",V0=3,V1=4,V2=L,I
1420:0=0
1430 GOSUB 7000
1435 IF V3=2 THEN GOTO 1420
1440 IF V3=4 THEN GOTO 4000
1450 IF NUM(V$)=0 THEN GOTO 1400
1460 LET T0$(15,3)=V$
1480 FIND (WOE14_DEV,KEY=T0$(1,17),DOM=1600)IOL=WOE14A
1485 FIND (IVM07_DEV,KEY=N0$+W1$(19,22)+T1$,DOM=1490)IOL=IVM07A
1490 IF T0$(1,17)<=K9$ THEN GOSUB 5400
1495 LET O9$=T1$,O9=T[0],H[1]=H[1]-O9
1500 GOSUB 5000
1530 GOTO 3300
1600 REM " --- Setup"
1620 DIM T1$(20),T2$(20),T[2]
1630 IF LOT$="S" THEN LET T[0]=1 ELSE LET T[0]=W[3]-O7
1635 IF LOT$="S" AND W[3]<0 THEN LET T[0]=-1
1640 LET O9$="",O9=0
2000 REM " --- Enter Lot/Serial #
2010 IF LOT$="S" THEN PRINT @(9,L),'SB',"Serial #: ",'SF'
2020 LET V0$="S",V1$="C",V2$=T1$,V3$="",V4$="Enter Selected "+P8$+". ",V0=P2,V
2020:1=9,V2=L; REM   ,I0=1
2025 IF LOT$="S" THEN LET V1=19
2030 IF B2$(20,1)="Y" THEN LET V4$=V4$+" <F3>=Lookup"
2040 GOSUB 7000
2045 IF V3=2 THEN GOTO 3200
2050 IF V3=4 THEN GOTO 4000
2100 IF V3<>3 THEN GOTO 2200
2130 LET V8$="Z",H0$=N0$+W1$(19,22),H1$=""; DIM H[11]
2140 CALL "SYC.LL",V8$,H0$,H1$,H[ALL]
2160 IF H0$="" THEN GOTO 2000
2170 PRINT @(0,L+1),'CE'
2180 GOTO 2240
2200 REM " --- Find The Lots
2210 LET T1$(1)=V$
2220 FIND (IVM07_DEV,KEY=N0$+W1$(19,22)+T1$,DOM=2000)IOL=IVM07A
2225 IF O9$=T1$ THEN LET H[1]=H[1]-O9
2227 IF T[0]>0 THEN GOTO 2240
2230 IF T[0]<0 THEN IF LOT$="L" THEN GOTO 2275 ELSE IF H[0]-H[1]<=0 THEN GOTO 
2230:2240
2234 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Warning!! Serial # Is On Hand, <En
2234:ter> To Retry: ",V0=1,V1=FNV(V4$),V2=22; PRINT @(0,0),'RB',
2236 GOSUB 7000
2238 GOTO 2000
2240 IF H[0]-H[1]>0 THEN GOTO 2260
2245 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Quantity Not Available! <Enter> To
2245: Retry: ",V0=1,V1=FNV(V4$),V2=22; PRINT @(0,0),'RB',
2250 GOSUB 7000
2255 GOTO 2000
2260 LET T1$(1)=H0$(25,20)
2270 IF H[0]-H[1]<T[0] THEN LET T[0]=H[0]-H[1]
2275 LET T[1]=H[3]
2280 IF LOT$="L" THEN PRINT @(9,L),T1$,@(11+P2,L),T[0]:M2$,@(76-M3,L),T[1]:M3$
2290 IF LOT$="S" THEN PRINT @(19,L),T1$,@(70-M3,L),'SB',"Cost: ",'SF',@(76-M3,
2290:L),T[1]:M3$
2292 IF LOT$="S" THEN LET I0=4
2295 GOTO 3200
2400 REM " --- Order / Ship Quantity
2410 IF LOT$="S" THEN GOTO 3200
2420 LET V0$="N",V1$="C",V2$=STR(T[0]),V3$=M2$,V4$="Enter Qty Issued On This L
2420:ine.",V0=7,V1=11+P2,V2=L
2440 GOSUB 7000
2450 IF V3=2 OR V3=4 THEN GOTO 3200
2460 LET T[0]=V
2465 IF B2$(20,1)="Y" AND T[0]<0 THEN GOTO 2490
2470 IF B2$(20,1)="Y" AND T[0]>H[0]-H[1] THEN GOTO 2475 ELSE GOTO 2490
2475 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Quantity Not Available! <Enter> To
2475: Retry: ",V0=1,V1=FNV(V4$),V2=22; PRINT @(0,0),'RB',
2480 GOSUB 7000
2485 GOTO 2400
2490 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN LET I0=4; GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2400,3300
3300 REM " --- Correct"
3320 LET V0$="",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=T0$(1,17),V4$="<Enter>=OK, <
3320:F2>=Change, D=Delete"
3330 GOSUB 7000
3340 IF V3=2 THEN LET I0=3; GOTO 3200
3360 IF V$<>"" AND V$<>"D" THEN GOTO 3300
3370 IF V$="D" THEN GOSUB 3500; GOTO 1000
3400 REM " --- Write
3410 IF O9$=T1$ AND O9=T[0] THEN GOTO 3460
3415 IF O9$="" THEN GOTO 3440
3420 LET O$=O9$,O8=O9,I=-1
3430 GOSUB 3600
3440 LET O$=T1$,O8=T[0],I=1
3450 GOSUB 3600
3455 LET O9$=T1$,O9=T[0]
3460 LET L=L+1; PRINT @(0,21),'LD','LD'
3480 WRITE (WOE14_DEV,KEY=T0$(1,17))IOL=WOE14A
3485 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3500 REM " --- Delete"
3510 REMOVE (WOE14_DEV,KEY=T0$(1,17),DOM=3520)
3520 PRINT @(0,L),'CL',@(0,21),'LD','LD',
3540 IF O9$="" THEN GOTO 3590
3550 LET O$=O9$,O8=O9,I=-1
3570 GOSUB 3600
3590 RETURN
3600 REM " --- IVM-02 Commit / Uncommit to Offset IVM-07 Process Below "
3602 REM " --- IVC.UA commits/ Uncomits IVM-02 again when committing IVM-07
3604 REM " --- This pass negates the IVM-02 update "
3610 LET IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV,IVFILES[7]=IVM07_DEV
3620 LET IVFILES[17]=IVM17_DEV
3625 IF I=1 THEN LET ACTION$="UC" ELSE IF I=(-1) THEN LET ACTION$="CO"
3630 LET ITEMS$[0]=T0$(1,2),ITEMS$[1]=W1$(19,2),ITEMS$[2]=W1$(21,20)
3640 LET ITEMS$[3]=" ",REFS[0]=O8
3660 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],R
3660:EFS$[ALL],REFS[ALL],STATUS
3700 REM " --- IVM-07 Commit / Uncommit / Total
3710 LET IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV,IVFILES[7]=IVM07_DEV
3720 LET IVFILES[17]=IVM17_DEV
3730 IF I=1 THEN LET ACTION$="CO" ELSE IF I=(-1) THEN LET ACTION$="UC"
3740 LET ITEMS$[0]=T0$(1,2),ITEMS$[1]=W1$(19,2),ITEMS$[2]=W1$(21,20)
3750 LET ITEMS$[3]=T1$,REFS[0]=O8
3760 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],R
3760:EFS$[ALL],REFS[ALL],STATUS
3780 LET O7=O7+O8*I
3790 RETURN
4000 REM " --- Options"
4001 CALL "SYC.WC",1,0,80,0,0,20,0
4002 PRINT @(0,L),'CE',
4003 IF O7=W[3] THEN GOTO 4010
4004 PRINT 'RB',@(10,21),"Warning!!  Not Enough "+P8$,"'s Assigned.  Total: "+
4004:STR(O7),"  Not ",STR(W[3])
4006 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> To Retry Or <F1> To Accept
4006:: ",V0=1,V1=FNV(V4$),V2=22
4007 GOSUB 7000; PRINT @(0,21),'CL',
4008 IF V3<>1 THEN LET V$="N"; GOTO 1000
4010 PRINT @(8,22),"Is The "+P8$+" Information Correct? (Y/N/Review/List/Delet
4010:e): "
4020 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=6,V1=63+LEN(P8$),V2=22
4030 GOSUB 7000
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4060 IF V$="DELETE" THEN GOSUB 4200; GOTO 4100
4070 IF V$="" THEN LET V$="Y"
4080 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- OK Here
4110 PRINT @(0,L1+2),'CE',; LET L=L1+2
4120 LET L=L1,O0=1,K9$=W0$(1,14),L9=18,W0=NUM(W0$(12,3)),V3=0,T0=0,T1=0
4125 REM " --- Calc Unit Cost/Qty Shipped
4130 READ (WOE14_DEV,KEY=W0$(1,14),DOM=4140)
4140 LET K1$=KEY(WOE14_DEV,END=4185)
4150 IF K1$(1,14)<>W0$(1,14) THEN GOTO 4185
4160 READ (WOE14_DEV)IOL=WOE14A
4170 LET T0=T0+T[0],T1=T1+T[0]*T[1]
4175 GOTO 4140
4185 IF T0<>W[3] THEN LET W[3]=T0,O0=2,I0=5
4187 IF T0<>0 THEN IF W[4]<>T1/T0 THEN LET W[4]=T1/T0,O0=2,I0=5
4190 RUN "WOE.DB"
4200 REM " --- Delete All Assignments Here
4205 DIM T[2]
4210 READ (WOE14_DEV,KEY=W0$(1,14),DOM=4220)
4220 LET K1$=KEY(WOE14_DEV,END=4290)
4230 IF K1$(1,14)<>W0$(1,14) THEN GOTO 4290
4240 READ (WOE14_DEV)IOL=WOE14A
4250 LET O$=T1$,O8=T[0],I=-1
4260 GOSUB 3600
4270 REMOVE (WOE14_DEV,KEY=K1$)
4280 GOTO 4220
4290 RETURN
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (WOE14_DEV,KEY=W0$(1,14)+"000",DOM=4640)
4640 GOSUB 5400
4650 LET V3=0
4690 GOTO 1000
4800 REM " --- Delete (from WOE.DB) Here
4820 GOSUB 4200
4840 RUN "WOE.DB"
5000 REM " --- Display"
5020 IF LOT$="L" THEN PRINT @(4,L),T0$(15,3),@(9,L),'SB',"Lot #: ",'SF',T1$,@(
5020:18+P2,L),T[0]:M2$,@(76-M3,L),T[1]:M3$
5030 IF LOT$="S" THEN PRINT @(4,L),T0$(15,3),@(9,L),'SB',"Serial #: ",'SF',@(1
5030:9,L),T1$,@(70-M3,L),'SB',"Cost: ",'SF',@(76-M3,L),T[1]:M3$
5090 RETURN
5100 REM " --- List"
5105 LET Q5=0
5110 GOSUB 5400
5120 READ (WOE14_DEV,KEY=W0$(1,14),DOM=5130)
5130 LET K1$=KEY(WOE14_DEV,END=5290)
5135 IF LEN(K1$)<14 THEN GOTO 5290
5140 IF K1$(1,14)<>W0$(1,14) THEN GOTO 5290
5150 READ (WOE14_DEV)IOL=WOE14A
5160 LET Q5=1
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Continue? <Enter/End>: ",V0=1,V1=
5210:50,V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5290
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=T0$(1,17)
5280 GOTO 5130
5290 RETURN
5400 REM " --- Clear Screen"
5410 FOR X=L1+2 TO L9+1
5420 PRINT @(0,X),'CL',
5430 NEXT X
5440 LET L=L1+2
5490 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
