0010 REM "WOR - Batch Reschedule Work Orders"
0020 REM "Program:  WOR.UA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOE-01",FILES$[2]="SYS-01",FILES$[3]="BMM-08"
0130 LET FILES$[4]="WOM-02"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET WOE01_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 WOE01A: IOLIST A0$,A1$,A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0500 REM " --- Init Data"
0505 LET H0=80,HEADINGS=4,H=0,WHEN$=F0$(7,3),CLOCK$=""
0510 DIM HEADINGS$[HEADINGS]
0515 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0520 LET L9=59,L=L9+1
0530 DIM J$(80,"-"),J1$(40," "),J0$(20,"0"),A[11]
0540 LET R2$="9"
0600 REM " --- Open Additional Files"
0605 REM " --- Check For Bill Of Materials "
0610 IF P3$(1,1)<>"Y" THEN GOTO 0650
0615 REM " --- Use Bill Of Materials Code File "
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0635 LET CODFILE_DEV=CHANNELS[3]
0640 GOTO 0700
0650 REM " --- Use Work Order Code File "
0660 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS THEN GOTO 9900
0680 LET CODFILE_DEV=CHANNELS[4]
0700 REM " --- Background"
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(18,3),"Beginning Work Order: ",@(21,5),"Ending Work Order:",
0740:@(9,7),"Reschedule Priority 1 Through:",@(19,9),"Open/Planned/Quoted:",
0760 PRINT 'SF'
1000 REM " --- Beginning Work Order"
1005 LET I0=0
1010 PRINT 'CF',@(0,5),J1$(1,15)
1020 LET V0$="Z",V1$="C",V2$=R0$,V3$="",V4$="Enter Beginning Work Order (<F1>=
1020:First/<F3>=Lookup)",V0=7,V1=40,V2=3
1030 PRINT @(V1,V2),'CL',
1040 GOSUB 7000
1045 ON V3 GOTO 1060,1060,1090,1050,9900
1050 CALL "SYC.LW",V$,"B"
1060 IF V$="" OR V3=1 THEN LET R0$=""; PRINT @(V1,V2),"First",'CL',; GOTO 1090
1070 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=1000)
1075 PRINT @(V1,V2),V$,'CL',
1080 LET R0$=V$
1090 GOTO 3900
1200 REM " --- Ending Work Order"
1220 LET V0$="Z",V1$="C",V2$=R1$,V3$="",V4$="Enter Ending Work Order (<F1>=Las
1220:t/<F3>=Lookup)",V0=7,V1=40,V2=5
1230 PRINT @(V1,V2),'CL',
1240 GOSUB 7000
1245 ON V3 GOTO 1260,1260,1290,1250,9900
1250 CALL "SYC.LW",V$,"B"
1260 IF V$="" OR V3=1 THEN LET R1$=""; PRINT @(V1,V2),"Last",'CL',; GOTO 1290
1265 IF V$<R0$ THEN GOTO 1200
1270 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=1200)
1275 PRINT @(V1,V2),V$,'CL',
1280 LET R1$=V$
1290 GOTO 3900
1400 REM " --- Priority"
1420 LET V0$="S",V1$="RC",V2$=R2$,V3$="123456789",V4$="Enter Lowest Priority T
1420:o Reschedule",V0=1,V1=40,V2=7
1430 PRINT @(V1,V2),'CL',
1440 GOSUB 7000
1480 LET R2$=V$
1490 GOTO 3900
1600 REM " --- Open/Planned/Quoted"
1610 LET H3$=""
1620 LET V0$="S",V1$="CK^E",V2$=R3$,V3$="",V4$="Open, Planned, Quoted Or Any C
1620:ombination (O/P/Q/<F1>=All)",V0=3,V1=40,V2=9
1630 PRINT @(V1,V2),'CL'
1640 GOSUB 7000
1642 IF V3=4 THEN GOTO 9900
1645 IF V$="" OR V3=1 THEN LET V$="OPQ"
1650 FOR X=1 TO LEN(V$); IF POS(V$(X,1)="OPQ")=0 THEN EXITTO 1600
1655 NEXT X
1660 LET R3$=V$
1670 IF POS("O"=R3$)>0 THEN LET H3$="Open/"
1674 IF POS("P"=R3$)>0 THEN LET H3$=H3$+"Planned/"
1676 IF POS("Q"=R3$)>0 THEN LET H3$=H3$+"Quoted/"
1680 LET H3$=H3$(1,LEN(H3$)-1)
1685 PRINT @(V1,V2),V$+" "+H3$,'CL',
1690 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
4000 REM " --- Overlay"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct?
4020: (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4200 LET H4$="From "
4220 IF R0$<>"" THEN LET H4$=H4$+"Work Order "+R0$ ELSE LET H4$=H4$+"The First
4220: Work Order"
4240 LET H4$=H4$+" Through "
4260 IF R1$<>"" THEN LET H4$=H4$+"Work Order "+R1$ ELSE LET H4$=H4$+"The Last 
4260:Work Order"
4270 LET H3$="For All "+H3$+" Work Orders With A Priority Of 1"
4280 IF R2$>"1" THEN LET H3$=H3$+" Through "+R2$
4400 RUN "WOR.UB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
