0010 REM "Work Order Work In Process Conversion - Data Entry           "
0020 REM "Program:  WOX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=6
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="IVM-01"
0120 LET FILES$[4]="WOE-05",FILES$[5]="SYS-01",FILES$[6]="WOM-10"
0150 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0165 LET WOE05_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5],WOM10_DEV=CHANNELS[6]
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 WOE02A: IOLIST D0$,D1$,D[ALL]
0230 BMS01A: IOLIST *,*,M4$,M5$,M4,M5
0240 WOE05A: IOLIST W0$,W[ALL]
0250 IVM01A: IOLIST C0$,C1$(1)
0260 WOS01A: IOLIST X$,P1$,P2$,P9$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,X$,P2$,P3$
0290 ARS01A: IOLIST *,*,X$,*,*,P8$
0300 APS01A: IOLIST *,*,X$,*,*,P7$
0310 PRS01A: IOLIST *,*,X$,*,*,P6$
0320 WOM10A: IOLIST X1$
0400 REM " --- Parameters"
0405 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0410 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0415 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0420 IF P9$(1,1)<>"Y" THEN GOTO 0430
0425 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=0430)IOL=BMS01A
0430 IF P9$(2,1)<>"Y" THEN GOTO 0445
0435 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0445)IOL=ARS01A
0440 LET P8=NUM(X$(1,2))
0445 IF P9$(3,1)<>"Y" THEN GOTO 0460
0450 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=0460)IOL=APS01A
0455 LET P7=NUM(X$(1,2))
0460 IF P9$(4,1)<>"Y" THEN GOTO 0475
0465 FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=0475)IOL=PRS01A
0470 LET P6=NUM(X$(1,2))
0475 IF P9$(4,1)<>"Y" AND P9$(12,1)="P" THEN LET M5=NUM(P9$(19,6))
0480 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0485 DIM P[1],DESC[3]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1)),P0=P[0],P1=P[1]
0495 CALL "SYC.RM",M1$,M1,10,10
0500 CALL "SYC.RM",M2$,M2,10,10
0505 CALL "SYC.RM",M3$,M3,10,10
0510 REM " --- Init Data"
0515 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(75),A1$(138),A[11],D[7],W[5]
0525 LET K0=1,K9=11
0530 LET A0$(1)=N0$
0535 FOR X=1 TO 3
0540 LET DESC[X]=NUM(P2$((X-1)*2+9,2))
0545 NEXT X
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,10,0
0720 PRINT 'SB',@(0,3),"WO Number:",@(20),"Type:",@(54),"Status:",@(0,5),"Bill
0720: Number:",@(0,6),"Description:",@(52,4),"Category:",@(51,5),"Open Date:",
0720:@(57,6),"U/M:",@(52,7),"Revision:",@(49,8),"Closed Date:",@(0,9),"Sched P
0720:roduction:",@(30),"Closed Qty:",@(52),"Closed Value:",
0740 PRINT @(0,11)," Operations",@(5,12),"Hours:",@(23),"Rate:",@(40),"Ovhd:",
0740:@(57),"Total:"
0760 PRINT @(2,13),"Materials",@(0,15),"Subcontrcts",@(5,14),"Total:",@(5,16),
0760:"Total:"
0770 PRINT @(1,18),"WIP Total:"
0790 PRINT 'SF',
0800 REM " --- Options"
0900 REM " --- Init File"
0920 READ (WOE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(WOE01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF LEN(V2$)>5 THEN LET V2$=V2$(5) ELSE LET V2$=""
1050 LET V0$="Z",V1$="CER",V3$="",V4$="<F3>=Lookup",V0=7,V1=11,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1072 IF V3=3 THEN CALL "SYC.LW",V$,"?"
1075 IF V$="" THEN GOTO 1050
1080 IF V$="" OR POS("0"<>V$)=0 THEN GOTO 0900
1090 LET A0$(5)=V$
1095 PRINT @(V1,V2),A0$(5),
1200 REM " --- Find Key"
1205 DIM W0$(31),W[5]
1210 FIND (WOE01_DEV,KEY=A0$(1,K9),DOM=1000)IOL=WOE01A
1215 LET S9$=A0$(12,3)+A0$(34,16)+A0$(54,22),S9=A[0],O9=0,W0$(1,11)=A0$
1220 GOSUB 5000
1225 IF A0$(15,1)<>"C" THEN GOTO 1250
1230 DIM DESC$[1]
1235 LET DESC$[0]="Work Order Is Not Open. <Enter> To Continue."
1240 CALL "SYC.XA",2,DESC$[ALL],0,22,-1,V$,V3
1245 GOTO 1000
1250 FIND (WOE05_DEV,KEY=W0$(1,11),DOM=1400)IOL=WOE05A
1300 REM " --- Display"
1320 GOSUB 5160
1340 GOTO 4000
1400 REM " --- New Record"
1410 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=1420)
1420 LET K2$=KEY(WOE02_DEV,END=1450)
1430 IF K2$(1,11)<>A0$(1,11) THEN GOTO 1450
1440 GOTO 1490
1450 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Cannot Proceed, Work Order Operati
1450:ons Have Not Been Set Up, <Enter>: ",V0=1,V1=FNV(V4$),V2=22
1460 PRINT @(V1,V2),'RB',
1470 GOSUB 7000
1480 GOTO 1000
1490 LET V4$="Is This A New Record: "
1500 CALL "SYC.YN",1,V4$,1,V$,V3
1510 ON POS(V$="YN") GOTO 1490,1520,1000
1520 DIM D1$(30),D[7]
1530 LET D1$(1)="*** Not On File ***"
1540 READ (WOE02_DEV,KEY=K2$,DOM=1550)IOL=WOE02A
1550 GOSUB 5160
1560 LET I0=0,W[1]=D[6],W[2]=D[7]
1570 GOSUB 5160
2000 REM " --- Enter Here
2020 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M2$,V4$="Enter Direct Labor Hours",
2020:V0=9,V1=12,V2=12,I0=0,O9=1
2040 GOSUB 7000
2060 IF V3=2 OR V3=4 THEN GOTO 3900
2080 LET W[0]=V
2090 GOTO 2280
2100 REM " --- Rate
2120 LET V0$="N",V1$="C",V2$=STR(W[1]),V3$=M3$,V4$="Enter Direct Labor Rate",V
2120:0=7,V1=29,V2=12
2140 GOSUB 7000
2160 IF V3=2 OR V3=4 THEN GOTO 3900
2170 LET W[1]=V
2190 GOTO 2280
2200 REM " --- Overhead"
2220 LET V0$="N",V1$="C",V2$=STR(W[2]),V3$=M3$,V4$="Enter Overhead Factor",V0=
2220:7,V1=46,V2=12
2240 GOSUB 7000
2260 IF V3=2 OR V3=4 THEN GOTO 3900
2270 LET W[2]=V
2280 LET W[3]=W[0]*W[1]+W[0]*W[1]*W[2]; GOSUB 5160
2290 GOTO 3900
2300 REM " --- Total
2320 LET V0$="N",V1$="",V2$=STR(W[3]),V3$=M1$,V0=9,V1=64,V2=12
2340 GOSUB 7000
2360 IF V3=2 OR V3=4 THEN GOTO 3900
2370 LET W[3]=V
2380 GOSUB 5160
2390 GOTO 3900
2400 REM " --- Materials
2420 LET V0$="N",V1$="",V2$=STR(W[4]),V3$=M1$,V0=9,V1=12,V2=14
2440 GOSUB 7000
2460 IF V3=2 OR V3=4 THEN GOTO 3900
2470 LET W[4]=V
2480 GOSUB 5160
2490 GOTO 3900
2500 REM " --- Subcontracts
2520 LET V0$="N",V1$="",V2$=STR(W[5]),V3$=M1$,V0=9,V1=12,V2=16
2540 GOSUB 7000
2560 IF V3=2 OR V3=4 THEN GOTO 3900
2570 LET W[5]=V
2580 GOSUB 5160
2590 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 3970
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,2400,2500,3970
3970 LET I0=K0
4000 REM " --- Options"
4010 IF O9=0 THEN LET V2$="Y" ELSE LET V2$=""
4020 LET V0$="S",V1$="C",V3$="",V4$="Is The Above Information Correct? (Y/N/De
4020:lete): ",V0=6,V1=64,V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (WOE05_DEV,KEY=W0$(1,K9))IOL=WOE05A
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (WOE05_DEV,KEY=W0$(1,K9),DOM=4290)
4290 GOTO 1000
5000 REM " --- Display"
5010 DIM C1$(60)
5020 FIND (IVM01_DEV,KEY=N0$+A0$(56,20),DOM=5030)IOL=IVM01A
5030 DIM X1$(30)
5040 LET X1$(6,20)="** Not On File **",I9$="N"
5050 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=5070)IOL=WOM10A
5060 LET I9$=A0$(14,1)
5070 PRINT @(27,3),A0$(12,2),"  ",X1$(6,20),@(63),A0$(15,1),@(63,4),A0$(14,1),
5070:@(63,5),FNB$(A0$(16,3)),@(63,6),A0$(50,2),@(63,7),A0$(52,2)
5080 IF A0$(15,1)="O" THEN PRINT @(65,3),"**Open**" ELSE IF A0$(15,1)="C" THEN
5080: PRINT @(65,3),"**Closed**" ELSE IF A0$(15,1)="P" THEN PRINT @(65,3),"*Pl
5080:anned*" ELSE PRINT @(65,3),"*Quoted*"
5090 IF I9$="I" THEN PRINT @(65,4),"Inventory",@(14,5),A0$(56) ELSE IF I9$="N"
5090: THEN PRINT @(65,4),"Non-Stocking", ELSE IF I9$="R" THEN PRINT @(65,4),"R
5090:ecurring"
5100 IF A0$(15,1)="C" OR POS(" "<>A0$(31,3))>0 THEN PRINT @(65,8),FNB$(A0$(31,
5100:3))
5110 IF I9$<>"I" THEN PRINT @(13,6),A1$(1,30),@(13,7),A1$(31,30),
5120 IF I9$="I" THEN PRINT @(13,6),C1$(1,MIN(DESC[1],42)),@(13,7),C1$(DESC[1]+
5120:1,MIN(DESC[2],37))
5130 PRINT @(11,8),A1$(65,25),@(42),A1$(86,5)
5140 PRINT @(18,9),A[0]:M2$,@(42,9),A[2]:M2$,@(66,9),A[3]:M1$
5150 RETURN
5160 PRINT @(12,12),W[0]:M2$,@(29,12),W[1]:M3$,@(46,12),W[2]:M3$,@(64,12),W[3]
5160::M1$
5170 PRINT @(12,14),W[4]:M1$,@(12,16),W[5]:M1$
5180 PRINT @(12,18),W[3]+W[4]+W[5]:M1$
5190 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
