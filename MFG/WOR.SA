0010 REM "WOR - Production Exception Report"
0020 REM "Program:  WOR.SA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOE-32"
0130 LET FILES$[4]="WOM-02",FILES$[5]="WOT-01",FILES$[6]="SYS-01"
0140 LET FILES$[7]="APM-01",FILES$[8]="ARM-01",FILES$[9]="BMM-08"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOE32_DEV=CHANNELS[3]
0180 LET WOM02_DEV=CHANNELS[4],WOT01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 LET NO_OPS=NUM(P3$(26,2))
0450 DIM INFO$[20]
0460 CALL "SYC.VA","PO",INFO$[ALL]
0470 LET PO$=INFO$[20]
0480 CALL "SYC.VA","AR",INFO$[ALL]
0490 LET AR$=INFO$[20]
0500 REM " --- Init Data
0510 LET H1$=N1$,H2$=N3$
0520 LET L9=59,L=L9+1,R0=100
0540 DIM A[11],D[13],W[11]
0550 LET M1$="###0.00-",M2$="##0.0",M3$="##0.00-"
0560 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0570 LET R0$=F0$(7,3),R2$="N"
0600 REM " --- Open Additional Files
0610 IF AR$<>"Y" THEN LET FILES$[8]="      "
0620 IF PO$<>"Y" THEN LET FILES$[7]="      "
0630 IF P3$(1,1)<>"Y" THEN LET FILES$[9]="      "
0660 CALL "SYC.DA",1,7,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS<>0 THEN GOTO 9900
0675 LET APM01_DEV=CHANNELS[7],ARM01_DEV=CHANNELS[8],BMM08_DEV=CHANNELS[9]
0700 REM " --- Background
0705 IF PO$="N" THEN LET NUMBER=6 ELSE LET NUMBER=8
0710 FOR X=4 TO NUMBER STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(30,3),"Run Date:",@(10,5),"Include All Incomplete Steps:",
0750 IF PO$="Y" THEN PRINT @(34,7),"Type: ",
0760 PRINT 'SF'
1000 REM " --- Run Date
1020 LET V0$="D",V1$="CR",V2$=R0$,V3$="",V4$="Enter Run Date",V0=6,V1=40,V2=3,
1020:I0=0
1030 PRINT @(V1,V2),'CL',
1050 GOSUB 7000
1060 ON V3 GOTO 1080,1080,1080,1080,9900
1080 LET R0$=V$
1090 GOTO 3900
1100 REM " --- Include Past Due Only
1120 LET V0$="Y",V1$="EC",V2$=R2$,V3$="",V4$="Y=Print All Incomplete Steps For
1120: Past Due WOs, N=Print Only Past Due Steps",V0=1,V1=40,V2=5
1140 GOSUB 7000
1150 ON V3 GOTO 1160,1160,1160,1160,9900
1160 LET R2$=V$
1190 GOTO 3900
1200 REM " --- Type
1210 IF PO$="N" THEN LET R1$="L"; GOTO 1320
1220 LET V0$="S",V1$="C^K",V2$=R1$,V3$="",V4$="L=Past Due Labor, P=Past Due PO
1220:s, <F1>=Both",V0=2,V1=40,V2=7
1230 PRINT @(V1,V2),'CL',
1240 GOSUB 7000
1250 ON V3 GOTO 1270,1260,1320,1270,9900
1260 LET V$="LP"
1270 IF V$="" THEN GOTO 1260
1280 FOR X=1 TO LEN(V$); IF POS(V$(X,1)="LP")=0 THEN EXITTO 1200
1290 NEXT X
1300 PRINT @(V1,V2),V$,'CL',
1310 LET R1$=V$
1320 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,4000
4000 REM " --- Overlay
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct?
4020: (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4190 RUN "WOR.SB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
