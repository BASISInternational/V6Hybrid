0010 REM "MPM - Forecast Type Maintenance"
0020 REM "Program MPM.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files "
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="MPM-01",FILES$[2]="MPE-01",FILES$[3]="MPE-02"
0120 LET FILES$[4]="SYS-01"
0150 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET MPM01_DEV=CHANNELS[1],MPE01_DEV=CHANNELS[2],MPE02_DEV=CHANNELS[3]
0165 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLISTS "
0210 MPM01A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 MPE01A: IOLIST B0$,B1$,B[ALL]
0230 MPE02A: IOLIST C0$,C[ALL]
0240 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,P5
0250 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0260 IVS01A: IOLIST X$,P5$,P6$,X$,X$,X$,P7$,P8$,P9$
0270 WOM10A: IOLIST TYPE$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 DIM P[2]
0460 LET P[0]=NUM(P6$(1,2))
0465 LET IVS01_PRECISION=NUM(P6$(5,1))
0470 DIM INFO$[20]
0480 CALL "SYC.VA","WO",INFO$[ALL]
0490 LET WO$=INFO$[20]
0500 REM " --- Initialize Data "
0510 LET HEADINGS=4
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$
0540 LET HEADINGS$[2]="Forecast Type Maintenance Listing "
0550 PRECISION IVS01_PRECISION
0570 DIM A0$(5),A1$(83),A2$(26*6),A[5],B[26],C[25],TYPE$(30)
0580 LET A0$(1,2)=N0$,K9=5
0590 LET P9$="  ##",P8$="-###",P9=LEN(P9$),P8=LEN(P8$)
0600 REM " --- Additional file opens"
0610 IF WO$<>"Y" THEN GOTO 0700
0620 LET FILES$[5]="WOM-10"
0650 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0670 LET WOM10_DEV=CHANNELS[5]
0700 REM " --- Background "
0710 CALL "SYC.WC",1,0,80,0,0,7,0
0720 CALL "SYC.WC",1,0,80,0,0,9,0
0730 PRINT 'SB',@(0,3),"Forecast Type:",@(38),"Include Committed In Forecast Q
0730:ty:",@(2,4),"Description:",@(50),"Number Of MRP Periods:",@(13,5),":",@(5
0730:2),"Days Per MRP Period:",@(30,8),"Period Ending Dates",
0735 IF WO$="Y" THEN PRINT @(48,6),"Planned Work Order Type:"
0740 FOR X=1 TO 9
0750 PRINT @(11,X+9),X:"##:",@(34),X+9:"##:",
0760 IF X+18<=26 THEN PRINT @(57),X+18:"##:",
0770 NEXT X
0790 PRINT 'SF',
0900 REM " --- Initialize File "
0920 READ (MPM01_DEV,KEY=A0$(1,2),DOM=1000)
1000 REM " --- Key Read "
1010 PRINT 'CF'
1020 LET V2$=""
1025 LET V2$=KEY(MPM01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="S",V1$="KEC",V4$="Enter A Valid Forecast Type (<F3>=Lookup)",V0=
1050:3,V1=15,V2=3,I0=0,O8=0,O9=0
1060 GOSUB 7000
1065 IF V3=3 THEN GOSUB 6900
1070 IF V3=4 THEN GOTO 4500
1080 IF V$="" THEN GOTO 0900
1085 LET A0$(3,3)=V$
1090 PRINT @(V1,V2),A0$(3,3),
1200 REM " --- Find Key "
1220 FIND (MPM01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=MPM01A
1240 LET A0=A[0]
1250 IF WO$<>"Y" THEN GOTO 1300
1270 FIND (WOM10_DEV,KEY=N0$+"A"+A1$(62,2),DOM=1300)IOL=WOM10A
1280 IF TYPE$(26,1)<>"I" THEN GOSUB 5400; GOTO 1270
1300 REM " --- Display "
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record "
1420 LET V4$="Is This A New Record"
1430 CALL "SYC.YN",1,V4$,2,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set Up "
1510 LET A1$(1)="",A2$(1)=""
1520 DIM A[5]
1530 GOTO 2000
1800 REM " --- Work Order Type "
1810 IF WO$<>"Y" THEN GOTO 1980
1820 LET V0$="S",V1$="KC",V2$=A1$(62,2),V3$="",V4$="Enter Work Order Type For 
1820:Planned Work Orders, <F3>=Lookup, <F1>=None",V0=2,V1=73,V2=6
1830 PRINT @(V1,V2),'CL',
1840 GOSUB 7000
1850 IF V3=1 THEN LET A1$(62,2)=""; GOTO 1970
1860 IF V3<>3 THEN GOTO 1930
1870 LET NUMBER=2
1880 DIM DESC$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
1890 LET CHANNEL=WOM10_DEV,RECORD$="A",TITLE$="Work Order Type Lookup",COLUMN=
1890:5,ROW=5
1900 LET DESC$[0]="Code",DESC$[1]="Description",DESC$[2]="Cat",FIELD[0]=1,FIEL
1900:D[1]=1,FIELD[2]=1,POSITION[0]=4,POSITION[1]=6,POSITION[2]=26,LENGTH[0]=2,
1900:LENGTH[1]=20,LENGTH[2]=1
1910 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESC$[ALL],FIELD[ALL],POSITIO
1910:N[ALL],LENGTH[ALL],COLUMN,ROW,V$
1920 IF V$="  " AND A1$(62,2)="  " THEN GOTO 1970
1930 LET V$=V$+"  ",V$=V$(1,2)
1940 FIND (WOM10_DEV,KEY=N0$+"A"+V$,DOM=1800)IOL=WOM10A
1950 IF TYPE$(26,1)<>"I" THEN GOSUB 5400; GOTO 1800
1960 LET A1$(62,2)=V$
1970 IF A1$(62,2)<>"  " THEN PRINT @(V1,V2),A1$(62,2),'CL' ELSE PRINT @(V1,V2)
1970:,"None"
1980 GOTO 3900
2000 REM " --- Description "
2010 LET X=I0,O9=1,O8=1
2020 LET V0$="S",V1$="",V2$=A1$(30*X+1,30),V4$="",V0=30,V1=15,V2=4+X
2040 GOSUB 7000
2060 LET A1$(30*X+1,30)=V$
2090 GOTO 3900
2100 REM " --- Number Of Periods "
2120 LET V0$="N",V1$="",V2$=STR(A[0]),V3$=P9$,V0=2,V1=73,V2=4
2140 GOSUB 7000
2145 ON V3 GOTO 2150,2150,3900
2150 IF V<=0 OR V>26 THEN GOTO 2100
2160 LET A[0]=V
2190 GOTO 3900
2200 REM " --- Days Per Period "
2220 LET V0$="N",V1$="",V2$=STR(A[1]),V3$=P8$,V0=3,V1=73,V2=5
2240 GOSUB 7000
2245 ON V3 GOTO 2250,2250,3900
2250 IF V<=0 THEN GOTO 2200
2260 LET A[1]=V
2290 GOTO 3900
2300 REM " --- Clear Date Display "
2310 LET I0=6
2320 IF A[0]=A0 THEN GOTO 2380
2330 LET X0=A[0]+1,X1=26
2340 IF X0>X1 THEN GOTO 2390
2350 GOSUB 6020
2360 GOSUB 6120
2380 LET A0=A[0]
2390 GOTO 3900
2400 REM " --- Dates "
2410 LET X=I0-7
2420 IF X+1>A[0] THEN GOTO 2610
2430 LET V0$="D",V1$="C6N",V2$=A2$(6*X+1,6),V4$="Enter Date (<F1>=Insert Or De
2430:lete/<F3>=Auto Calculation)",V0=6,V1=15,V2=X+10
2440 IF X>8 THEN LET V1=38,V2=X+1
2450 IF X>17 THEN LET V1=61,V2=X-8
2460 GOSUB 7000
2470 LET SAV$=V$
2480 IF V3=4 THEN GOTO 3990
2490 IF V3<>2 AND V3<>4 THEN IF X=0 AND POS(" "<>V$)=0 THEN GOTO 2400
2500 IF V3=3 AND POS(" "<>A2$(6*X+1,6))=0 THEN LET A2$(6*X+1,6)=V$
2510 IF V3=1 THEN GOTO 2800
2520 IF V3=3 AND LEN(V$)=6 THEN LET A2$(6*X+1,6)=V$; GOTO 3100
2530 IF V3=2 THEN GOTO 3900
2540 IF X=0 OR X=A[0]-1 THEN GOTO 2590 ELSE IF X>0 AND V$>A2$(6*(X-1)+1,6) THE
2540:N GOTO 2590
2550 PRINT 'RB',
2560 LET SAV$=V$,V0$="S",V1$="C",V2$="",V3$="",V4$="Date Entered Is Earlier Th
2560:an Preceding Date! <Enter>=Continue, <F3>=OK: ",V0=1,V1=76,V2=22
2570 GOSUB 7000
2580 IF V3=3 THEN GOTO 2590 ELSE GOTO 2400
2590 LET A2$(6*X+1,6)=SAV$
2600 IF V3<>2 AND V3<>4 THEN IF X=0 AND A[0]<>1 AND POS(" "<>A2$(7))=0 THEN GO
2600:SUB 6800
2610 GOTO 3900
2700 REM " --- Include Committed In Availability "
2710 PRINT @(6,20),"Should The Forecast Be Consumed By Future Shipments Of Ope
2710:n Orders?",@(3,21),"(Y=Increase Or Consume Demand Based On Orders,/N=Igno
2710:re Future Shipments)"
2720 LET V0$="Y",V1$="C",V2$=A1$(61,1),V3$="",V4$="",V0=1,V1=73,V2=3
2730 IF POS(V2$="YN")=0 THEN LET V2$="Y"
2740 GOSUB 7000
2750 PRINT @(0,20),'CL',@(0,21),'CL'
2760 LET A1$(61,1)=V$
2770 GOTO 3900
2800 REM " --- Special Date Entry Functions "
2810 LET Y1=V1,Y2=V2,Y9=X+1
2820 PRINT 'SB',@(Y1+10,Y2),"<<<",'SF',
2830 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<F1>=Insert Date   <F2>=Delete Da
2830:te   <F4>=Exit",V0=1,V1=78,V2=22
2840 GOSUB 7000
2850 ON V3 GOSUB 2830,2900,3000,2830,2860
2860 PRINT @(Y1+10,Y2),FILL(3," "),@(Y1),
2870 GOTO 2400
2900 REM " --- Insert Date "
2910 IF Y9=1 THEN LET A2$(1)=FILL(6," ")+A2$(1)
2920 IF Y9>1 AND Y9<26 THEN LET A2$(1)=A2$(1,6*(Y9-1))+FILL(6," ")+A2$(6*(Y9-1
2920:)+1)
2930 LET A2$(1)=A2$(1,6*A[0])
2940 LET X0=Y9,X1=A[0]
2950 GOSUB 5200
2960 LET DATE$="I"
2970 GOSUB 7600
2980 RETURN
3000 REM " --- Delete Date "
3010 IF Y9=1 THEN LET A2$(1)=A2$(7)
3020 IF Y9>1 AND Y9<26 THEN LET A2$(1)=A2$(1,6*(Y9-1))+A2$(6*Y9+1)
3030 IF Y9=26 THEN LET A2$(1)=A2$(1,LEN(A2$)-6)
3040 LET X0=Y9,X1=A[0]
3050 GOSUB 5200
3060 LET DATE$="D"
3070 GOSUB 7600
3080 RETURN
3100 REM " --- Auto Calc Of Remaining Dates "
3110 IF POS(" "<>V$)=0 THEN GOTO 2400
3120 LET Y9=X+1
3130 PRINT @(Y1+10,Y2),FILL(3," "),@(Y1),
3140 LET O9=1,O8=1,V9=X+1
3150 GOSUB 6400
3160 LET X0=V9+1,X1=A[0]
3170 GOSUB 5200
3180 GOTO 3990
3900 REM " --- Next Field "
3905 IF V3=4 AND I0<=5 THEN GOTO 2300 ELSE IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2000,2700,2100,2200,1800,2300,2400,2400,2400,2400,2400,24
3960:00,2400,2400,2400,2400,2400,2400,2400,2400,2400,2400,2400,2400,2400,2400,
3960:2400,2400,2400,2400,2400,2400,3990
3990 LET I0=0
4000 REM " --- Options "
4005 IF O9=1 THEN GOTO 6600
4010 IF O8=0 THEN LET X0$="/Copy" ELSE LET X0$=""
4015 LET V4$="Is The Above Information Correct (Y/N/#"+X0$+"/Delete)?"
4020 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 ON V3 GOTO 4035,4000,4000,4000,9900
4035 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="COPY" AND O8=0 THEN GOTO 4300
4050 IF V$="D" OR V$="DATE" THEN GOTO 4400
4060 IF O9=0 AND V$="" THEN LET V$="Y"
4070 LET I=NUM(V$,ERR=4090)
4080 IF I>0 THEN GOTO 4400
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write "
4120 WRITE (MPM01_DEV,KEY=A0$(1,K9))IOL=MPM01A
4190 GOTO 1000
4200 REM " --- Delete "
4205 LET V4$="All related information will also be deleted. OK to continue"
4210 CALL "SYC.YN",0,V4$,0,V$,V3
4215 IF V$<>"YES" THEN GOTO 4000
4220 LET CHANNEL_DEV=UNT,ALIAS$="MPE-03"
4225 GOSUB 6300
4230 IF STATUS>0 THEN GOTO 4000
4235 LET CHANNEL_DEV=UNT,ALIAS$="MPE-04"
4240 GOSUB 6300
4245 IF STATUS>0 THEN GOTO 4000
4250 LET CHANNEL_DEV=UNT,ALIAS$="MPE-05"
4255 GOSUB 6300
4260 IF STATUS>0 THEN GOTO 4000
4265 LET CHANNEL_DEV=MPE01_DEV,X$="Clearing Production Entries"
4270 GOSUB 6200
4275 LET CHANNEL_DEV=MPE02_DEV,X$="Clearing Resource Entries"
4280 GOSUB 6200
4285 REMOVE (MPM01_DEV,KEY=A0$(1,K9),DOM=4290)
4290 GOTO 1000
4300 REM " --- Copy "
4310 LET X$=A0$(3,3)
4320 LET V0$="S",V1$="EC",V2$="",V3$="",V4$="Enter The New Forecast Type To Be
4320: Created:",V0=3,V1=FNV(V4$),V2=22
4330 GOSUB 7000
4335 ON V3 GOTO 4340,4300,4000,4300,4000
4340 LET A0$(3)=V$
4360 PRINT @(15,3),A0$(3,3),
4380 LET O8=1,O9=1
4390 GOTO 4000
4400 REM " --- Go To Date "
4420 LET I0=7,O8=1,O9=1,I=0
4430 LET I=NUM(V$,ERR=4490)
4435 LET I=I-1
4440 LET I0=I0+I
4490 GOTO 2400
4500 REM " --- Print "
4520 LET V0$="Y",V1$="C",V2$="N",V3$="",V4$="Do You Wish To Print A List Of Al
4520:l Records? <Y/'Enter'>: ",V0=1,V1=67,V2=22
4530 GOSUB 7000
4540 ON POS(V$="YN") GOTO 4500,4580,9900
4580 RUN "MPM.AB"
4600 REM " --- Invalid WO Type Code
4620 PRINT 'RB',
4640 LET V0$="S",V1$="C",V2$="",V3$="",V4$="This WO Type is NOT an Inventory C
4640:ategory. <Enter> to Correct. ",V0=1,V1=FNV(V4$),V2=22
4660 GOSUB 7000
4690 RETURN
5000 REM " --- Display "
5020 PRINT @(73,3),A1$(61,1),@(15,4),A1$(1,30),@(73,4),A[0]:P9$,@(73,5),A[1]:P
5020:8$,@(15,5),A1$(31,30),
5040 GOSUB 5200
5070 IF WO$="Y" THEN IF A1$(62,2)<>"  " THEN PRINT @(73,6),A1$(62,2) ELSE PRIN
5070:T @(73,6),"None"
5090 RETURN
5200 REM " --- Display Dates "
5210 LET X0=1,X1=A[0]
5220 FOR X=1 TO 26
5240 IF X>0 AND X<10 AND (X>=X0 AND X<=X1) THEN PRINT @(15,X+9),FNB6$(A2$(6*(X
5240:-1)+1,6)),
5250 IF X>9 AND X<19 AND (X>=X0 AND X<=X1) THEN PRINT @(38,X),FNB6$(A2$(6*(X-1
5250:)+1,6)),
5260 IF X>18 AND X<27 AND (X>=X0 AND X<=X1) THEN PRINT @(61,X-9),FNB6$(A2$(6*(
5260:X-1)+1,6)),
5280 NEXT X
5290 RETURN
5400 REM " --- Wrong WO Type here
5410 DIM MESS$[1]
5420 LET MESS$[0]="This Work Order Type is NOT a Inventory Category."
5425 LET MESS$[1]="An Inventory Work Order Type Code MUST be Selected"
5430 CALL "SYC.XA",2,MESS$[ALL],1,22,-1,V$,V3
5490 RETURN
6000 REM " --- Clear Date Display "
6010 LET X0=1,X1=26
6020 FOR X=1 TO 26
6040 IF X>=0 AND X<10 AND (X>=X0 AND X<=X1) THEN PRINT @(15,X+9),FILL(8," "),
6050 IF X>9 AND X<19 AND (X>=X0 AND X<=X1) THEN PRINT @(38,X),FILL(8," "),
6060 IF X>18 AND X<27 AND (X>=X0 AND X<=X1) THEN PRINT @(61,X-9),FILL(8," "),
6070 NEXT X
6080 PRINT
6090 RETURN
6100 REM " --- Clear Date Data "
6110 LET X0=1,X1=26
6120 FOR X=X0 TO X1
6140 LET A2$(6*(X-1)+1,6)=""
6160 NEXT X
6190 RETURN
6200 REM " --- Remove MPE-01/MPE-02 records for forecast being deleted"
6210 PRINT @(0,22),'CL',@(39-LEN(X$)),X$,
6215 READ (CHANNEL_DEV,KEY=N0$+A0$(3,3),DOM=6220)
6220 LET K$=KEY(CHANNEL_DEV,END=6290)
6225 IF POS(N0$+A0$(3,3)=K$)<>1 THEN GOTO 6290
6230 PRINT @(40,22),K$(6,2)," ",K$(8),
6240 REMOVE (CHANNEL_DEV,KEY=K$)
6245 GOTO 6220
6290 RETURN
6300 REM " --- Erase MPE-03/04/05 files for forecast being deleted"
6310 CALL "MPC.FA",CHANNEL_DEV,ALIAS$,N0$,A0$(3,3),FILENAME$,STATUS
6315 DIM MESSAGE$[1]
6320 IF STATUS>0 THEN GOTO 6380
6330 LOCK (CHANNEL_DEV,ERR=6350)
6335 CLOSE (CHANNEL_DEV,ERR=6340)
6340 ERASE FILENAME$,ERR=6380
6345 GOTO 6390
6350 LET MESSAGE$[0]=$22$+ALIAS$+$22$+" is in use by another user (<Enter>=Con
6350:tinue)"
6355 CALL "SYC.XA",2,MESSAGE$[ALL],0,V$,V3
6360 LET STATUS=99
6380 CLOSE (CHANNEL,ERR=6390)
6390 RETURN
6400 REM " --- Date Calculations "
6420 LET X0=V9+1,X1=26
6440 IF X0>X1 THEN GOTO 6590
6460 GOSUB 6020
6480 GOSUB 6120
6500 FOR X=V9 TO A[0]-1
6520 CALL "SYC.CA",A2$(6*(X-1)+1,6),NEXT_DATE$,A[1]
6540 LET A2$(6*X+1,6)=NEXT_DATE$
6560 IF X>=0 AND X<9 THEN LET V1=15,V2=X+10 ELSE IF X>8 AND X<18 THEN LET V1=3
6560:8,V2=X+1 ELSE IF X>17 THEN LET V1=61,V2=X-8
6570 PRINT @(V1,V2),FNB6$(NEXT_DATE$),
6580 NEXT X
6590 RETURN
6600 REM " --- Check Date Order "
6620 FOR X=1 TO A[0]
6640 IF X=1 THEN GOTO 6680
6650 IF A2$(6*(X-1)+1,6)>=A2$(6*(X-2)+1,6) THEN GOTO 6680
6660 IF X>0 AND X<10 THEN LET PV1=15,PV2=X+9 ELSE IF X>9 AND X<19 THEN LET PV1
6660:=38,PV2=X ELSE IF X>18 THEN LET PV1=61,PV2=X-9
6665 PRINT 'SB',@(PV1+10,PV2),"<<<",'SF','RB',
6670 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Date Flagged Is Earlier Than Prece
6670:ding Date! <Enter>=Continue, <F3>=OK: ",V0=1,V1=76,V2=22
6671 GOSUB 7000
6672 PRINT @(PV1+10,PV2),"   ",
6673 IF V3=3 THEN GOTO 6680
6674 LET X=X+1,I0=X+5
6676 EXITTO 2400
6680 NEXT X
6690 GOTO 4010
6800 REM " --- Initial Date Calculation "
6820 LET V4$="Do You Wish To Automatically Calculate Remaining Dates"
6830 CALL "SYC.YN",1,V4$,2,V$,V3
6840 IF V$="N" THEN RETURN
6850 IF V$<>"Y" THEN GOTO 6800
6860 LET O9=1,O8=1
6870 LET V9=1
6875 GOSUB 6400
6880 LET X0=V9,X1=A[0]
6885 GOSUB 5200
6890 EXITTO 3990
6900 REM " --- Forecast Lookup"
6910 LET RECORD$="",TITLE$="MRP Forecast Types",NUMBER=1,CHANNEL=MPM01_DEV
6920 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6930 LET DESCRIPTION$[0]="Type",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
6940 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1
6950 LET LENGTH[1]=30,ROW=V2+2,V$="SKIP"
6960 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6980 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],
6980:POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6990 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
7600 REM " --- Adjust Related Production/Resource Entries"
7610 LET V4$="Adjust Corresponding Production/Resource Records"
7620 CALL "SYC.YN",0,V4$,0,V$,V3
7660 IF V$<>"YES" THEN RETURN
7690 PRINT 'SB',@(29,22),"Updating: ",'SF',
7700 REM " --- Read MPE-01 "
7705 READ (MPE01_DEV,KEY=A0$(1,5),DOM=7710)
7710 LET K0$=KEY(MPE01_DEV,END=7750)
7715 IF K0$(1,5)<>A0$(1,5) THEN GOTO 7750
7720 EXTRACT (MPE01_DEV,KEY=K0$)IOL=MPE01A
7725 IF DATE$="I" THEN GOSUB 7800 ELSE IF DATE$="D" THEN GOSUB 7900
7730 WRITE (MPE01_DEV,KEY=K0$)IOL=MPE01A
7735 PRINT @(40,22),B0$(6,2)," ",B0$(8),'CL',
7740 GOTO 7710
7750 REM " --- Read MPE-02 "
7755 READ (MPE02_DEV,KEY=A0$(1,5),DOM=7760)
7760 LET K0$=KEY(MPE02_DEV,END=7790)
7765 IF K0$(1,5)<>A0$(1,5) THEN GOTO 7790
7770 EXTRACT (MPE02_DEV,KEY=K0$)IOL=MPE02A
7775 IF DATE$="I" THEN GOSUB 7800 ELSE IF DATE$="D" THEN GOSUB 7900
7780 WRITE (MPE02_DEV,KEY=K0$)IOL=MPE02A
7782 PRINT @(40,22),C0$(6,2)," ",C0$(8),'CL',
7785 GOTO 7760
7790 PRINT @(0,22),'CL'
7795 RETURN
7800 REM " --- Insert Amount "
7820 FOR X=A[0] TO Y9+1 STEP -1
7830 IF X>1 THEN LET B[X-1]=B[X-2],C[X-1]=C[X-2]
7840 NEXT X
7850 LET B[Y9-1]=0,C[Y9-1]=0
7890 RETURN
7900 REM " --- Delete Amount "
7920 FOR X=Y9-1 TO A[0]
7930 IF X<25 THEN LET B[X]=B[X+1],C[X]=C[X+1]
7940 NEXT X
7990 RETURN
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8040 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
