0010 REM "MPU-MRP Generation - Purge WO & Req Data, create MRP work files"
0020 REM "Program MPU.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES$[31]="MPW-06",FILES$[32]="MPW-02"
0120 LET OPTIONS$[31]="C",OPTIONS$[32]="C"
0200 REM " --- IOLISTS"
0205 MPW02A: IOLIST COMMITTED$,COMMITTED
0210 MPW06A: IOLIST PW0$(1),PW1$(1),PW[ALL]
0215 MPM01A: IOLIST *,DESC$(1)
0220 POE01A: IOLIST PO0$(1),PO1$(1),PO2$(1),PO3$(1),PO[ALL]
0225 POE11A: IOLIST PD0$(1),PD1$(1),PD2$(1),PD3$,PD4$,PD5$,PD6$,PD[ALL]
0230 WOE01A: IOLIST WOHDR$(1),WO1$(1),WO[ALL]
0235 WOE02A: IOLIST WA0$(1),WA1$(1)
0240 WOREQ: IOLIST WA0$(1),WA1$(1)
0245 WOE22A: IOLIST WB0$(1),WB1$(1),WB[ALL]
0250 WOE32A: IOLIST WC0$(1),WC1$(1)
0255 WOE23A: IOLIST W30$,W31$,W3[ALL]
0260 WOM05A: IOLIST WM50$,WM51$,WM52$,WM5[ALL]
0265 IVM02A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0270 POM02A: IOLIST PM0$(1),PM1$(1)
0400 REM " --- Parameters "  
0500 REM " --- Initialize "
0510 PRECISION IVS01_PRECISION
0520 DIM PO0$(15),PO1$(160),PO2$(1),PO3$(1),PO[8],PM0$(4),PM1$(32)
0530 DIM PD0$(18),PD1$(48),PD2$(32),PD3$(22),PD4$(40),PD5$(1),PD6$(1),PD[12]
0540 LET LAST_REQ=0
0550 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],IVFILES[44],REFS[5]
0560 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=I2$,PARAMS$[3]=I3$
0560:,PARAMS$[4]=I4$
0570 LET IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV
0600 REM " --- Additional File Opens "
0610 IF PURCH_REQ$="N" AND PURGE_REQ$="N" THEN LET OPTIONS$[31]=""
0620 IF WO$<>"Y" THEN LET FILES$[32]="",OPTIONS$[32]=""
0630 CALL "SYC.DA",1,31,32,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATU
0630:S
0640 IF STATUS THEN GOTO 9999
0650 LET MPW06_DEV=CHANNELS[31]
0660 IF CHANNELS[32] THEN LET MPW02_DEV=CHANNELS[32]
0700 REM " --- Background "
0710 CALL "SYC.NB","Reviewing Purchase Requisitions:",17,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0980 REM " --- Initial Read "
0990 READ (POE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Process Purchase Requisitions"
1010 LET K$=KEY(POE01_DEV,END=1600)
1020 IF POS(N0$=K$(1,2))=0 THEN GOTO 1600
1030 READ (POE01_DEV,KEY=K$)IOL=POE01A
1040 PRINT @(COLUMN,11),K$(3,6)+" "+K$(9,7)
1050 READ (POE11_DEV,KEY=PO0$(1,15),DOM=1060)
1060 LET K2$=KEY(POE11_DEV,END=1410)
1070 IF POS(K2$(1,15)=PO0$(1,15))=0 THEN GOSUB CHECK_REQ_HEADER
1080 READ (POE11_DEV,KEY=K2$,DOM=1060)IOL=POE11A
1085 READ (POM02_DEV,KEY=N0$+PD1$(1,2),DOM=1060)IOL=POM02A
1090 IF PM1$(21,1)<>"S" THEN GOTO 1060
1100 IF PURGE_REQ$="N" THEN GOTO 1120
1110 IF POS(PD1$(35,3)=FORECAST_TYPE$,3)>0 THEN GOTO PURGE_REQ_DETAIL
1120 IF PURCH_REQ$="N" THEN GOTO 1060
1200 REM " --- Rebuild Purchase Requisition Work File "
1210 DIM PW0$(30),PW1$(63),PW[2]
1220 LET PW0$(1)=PD0$(1,2)+PD3$(1,22),PW0$(25,6)=PD1$(3,6)
1230 LET PW1$(21,6)=PD0$(3,6),PW1$(27,7)=PD0$(9,7)
1240 FIND (MPW06_DEV,KEY=PW0$,DOM=1250)IOL=MPW06A
1250 IF PD[0]=0 THEN LET PD[0]=1
1260 LET PW[0]=PW[0]+PD[2]*PD[0]
1270 WRITE (MPW06_DEV,KEY=PW0$)IOL=MPW06A
1280 GOTO 1060
1300 REM " --- Purge Planned Requisition Detail "
1310 PURGE_REQ_DETAIL:
1320 REMOVE (POE11_DEV,KEY=K2$)
1330 GOTO 1060
1400 REM " --- Check For Orphan Header Record "
1410 LET LAST_REQ=1
1420 CHECK_REQ_HEADER:
1430 READ (POE11_DEV,KEY=PO0$(1,15),DOM=1440)
1440 LET K22$=KEY(POE11_DEV,END=1460)
1450 IF POS(PO0$(1,15)=K22$)>0 THEN GOTO 1470
1460 REMOVE (POE01_DEV,KEY=PO0$(1,15),DOM=1470)
1470 IF LAST_REQ THEN GOTO 1600
1480 READ (POE01_DEV,KEY=K2$(1,15))IOL=POE01A
1490 RETURN
1600 REM " --- Process Work Orders"
1610 IF WO$<>"Y" THEN GOTO 4000
1620 CALL "SYC.NB","Updating Committed Workfile:    ",17,COLUMN
1630 REM " --- Process Work Order Committed File - WOE-23"
1640 READ (WOE23_DEV,KEY=N0$,DOM=1650)
1650 DIM W3[4]
1660 LET K$=KEY(WOE23_DEV,END=1900)
1670 IF K$(1,2)<>N0$ THEN GOTO 1900
1680 PRINT @(COLUMN,11),K$(5,7)+" "+K$(12,3)
1690 READ (WOE23_DEV,KEY=K$)IOL=WOE23A
1700 LET DATE$=FNYY_YY21$(FNM$(FNC$(W31$(4,3))))
1710 LET COMMITTED=0
1720 DIM COMMITTED$(37)
1730 LET COMMITTED$(1)=N0$+W31$(19,2)+W31$(21,20)+DATE$+W30$(5,7)
1740 READ (MPW02_DEV,KEY=COMMITTED$(1,30),DOM=1750,ERR=1750)IOL=MPW02A
1750 LET COMMITTED=COMMITTED+W3[0]-W3[1]
1760 WRITE (MPW02_DEV,KEY=COMMITTED$(1,30))IOL=MPW02A
1770 GOTO 1650
1900 REM " --- Process Work Order File"
1910 IF INCLUDE_QUOTE$="N" AND INCLUDE_PLANNED$="N" AND PURGE_PLANNED$="N" THE
1910:N GOTO 4000
1920 CALL "SYC.NB","Reviewing Work Orders:          ",17,COLUMN
1930 DIM WOHDR$(75),WO1$(141),WO[11],W0$(24),W1$(64),W2$(1),W[16]
1940 READ (WOE01_DEV,KEY=N0$,DOM=1950)
1950 READ (WOE01_DEV,END=4000)IOL=WOE01A
1960 IF POS(N0$=WOHDR$)<>1 THEN GOTO 4000
1970 IF POS(WOHDR$(15,1)="OC")>0 THEN GOTO 1950
1980 PRINT @(COLUMN,11),WOHDR$(5,7)
1990 IF WOHDR$(15,1)="Q" AND INCLUDE_QUOTE$<>"Y" THEN GOTO 1950
2000 IF WOHDR$(15,1)="P" AND INCLUDE_PLANNED$="N" AND PURGE_PLANNED$="N" THEN 
2000:GOTO 1950
2010 LET WOKEY$=WOHDR$(1,11)
2100 REM " --- Process Work Order "
2110 IF WOHDR$(15,1)="P" AND PURGE_PLANNED$="Y" AND POS(WO1$(99,3)=FORECAST_TY
2110:PE$,3)>0 THEN GOTO PURGE_PLANNED
2120 IF WOHDR$(15,1)="P" AND INCLUDE_PLANNED$="N" THEN GOTO 1950
2200 REM " --- Include Planned or Quoted WO Material Requirements " 
2210 DIM WB0$(15),WB1$(151),WB[13]
2220 READ (WOE22_DEV,KEY=WOKEY$,DOM=2230)
2230 LET K2$=KEY(WOE22_DEV,END=1950)
2240 IF POS(WOKEY$+"B"=K2$)<>1 THEN GOTO 1950
2250 READ (WOE22_DEV)IOL=WOE22A
2260 IF WB1$(4,3)<>"   " THEN LET DATE$=FNYY_YY21$(FNM$(FNC$(WB1$(4,3)))) ELSE
2260: LET DATE$=FNYY_YY21$(FNM$(FNC$(WOHDR$(22,3))))
2270 LET COMMITTED=0
2280 DIM COMMITTED$(37)
2290 LET COMMITTED$(1)=WB0$(1,2)+WB1$(25,22)+DATE$+WB0$(5,7)
2300 READ (MPW02_DEV,KEY=COMMITTED$(1,30),DOM=2310)IOL=MPW02A
2310 LET COMMITTED=COMMITTED+WB[2]
2320 WRITE (MPW02_DEV,KEY=COMMITTED$(1,30))IOL=MPW02A
2330 GOTO 2230
2500 REM " --- Purge Planned Work Order "
2510 PURGE_PLANNED:
2520 LET WOREQ_DEV=WOE02_DEV
2530 READ (WOREQ_DEV,KEY=WOKEY$,DOM=2600)
2600 REM " --- Remove All Requirements & Cross References "
2610 LET K24$=KEY(WOREQ_DEV,END=2930)
2620 IF POS(WOKEY$=K24$)<>1 THEN GOTO 2930
2630 DIM WA0$(15),WA1$(151),CROSSREF$(37)
2640 READ (WOREQ_DEV)IOL=WOREQ
2700 REM " --- Create Cross Reference Key "
2710 ON POS(K24$(12,1)="ABC") GOTO 2900,2720,2750,2780
2720 LET WOP$=WA1$(1,3),WDATE$=WA1$(4,3)
2730 LET CROSSREF$=WA0$(1,2)+"D"+WA0$(3,2)+WOP$+WDATE$+WA0$(5,7)+WA0$(13,3)
2740 GOTO 2810
2750 LET WITEM$=WA1$(25,22)
2760 LET CROSSREF$=WA0$(1,2)+"E"+WA0$(3,2)+WITEM$+WA0$(5,7)+WA0$(13,3)
2770 GOTO 2810
2780 LET WVNDR$=WA1$(7,6)
2790 LET CROSSREF$=WA0$(1,2)+"F"+WA0$(3,2)+WVNDR$+WA0$(5,7)+WA0$(13,3)
2800 REM " --- Delete Cross Reference "
2810 REMOVE (WOE03_DEV,KEY=CROSSREF$,DOM=2900)
2900 REM " --- Delete Requirement Record "
2910 REMOVE (WOREQ_DEV,KEY=K24$,DOM=2920)
2920 GOTO 2610
2930 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 2530
2940 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 2530
3000 REM " --- Delete Work Order Comments "            
3010 READ (WOE07_DEV,KEY=WOKEY$,DOM=3020)
3020 LET K25$=KEY(WOE07_DEV,END=3100)
3030 IF POS(WOKEY$=K25$)<>1 THEN GOTO 3100
3040 REMOVE (WOE07_DEV,KEY=K25$,DOM=3020)
3100 REM " --- Delete Schedule Detail & Cross Reference "
3110 READ (WOM06_DEV,KEY=WOKEY$(1,2)+WOKEY$(5,7),DOM=3120)
3120 LET K27$=KEY(WOM06_DEV,END=3200)
3130 IF POS(WOKEY$(1,2)+WOKEY$(5,7)=K27$)<>1 THEN GOTO 3200
3140 REMOVE (WOM05_DEV,KEY=K27$(1,2)+K27$(13,6)+K27$(3,10),DOM=3150)
3150 REMOVE (WOM06_DEV,KEY=K27$)
3160 GOTO 3120
3200 REM " --- Delete Transaction Header Records "
3210 REMOVE (WOE04_DEV,KEY=WOKEY$(1,4)+"A"+WOKEY$(5),DOM=3220)
3220 REMOVE (WOE04_DEV,KEY=WOKEY$(1,4)+"B"+WOKEY$(5),DOM=3230)
3230 REMOVE (WOE04_DEV,KEY=WOKEY$(1,4)+"C"+WOKEY$(5),DOM=3240)
3240 REMOVE (WOE04_DEV,KEY=WOKEY$(1,4)+"D"+WOKEY$(5),DOM=3310)
3300 REM " --- Delete Work Order Header Cross References "
3310 REMOVE (WOE03_DEV,KEY=WOKEY$(1,2)+"A"+WOHDR$(12,2)+WOKEY$(3),DOM=3320)
3320 REMOVE (WOE03_DEV,KEY=WOKEY$(1,2)+"B"+WOHDR$(34,16)+WOKEY$(3),DOM=3330)
3330 REMOVE (WOE03_DEV,KEY=WOKEY$(1,2)+"C"+WOHDR$(54,22)+WOKEY$(3),DOM=3350)
3340 REM " --- Delete Work Order Master Record "
3350 REMOVE (WOE01_DEV,KEY=WOKEY$)
3450 GOTO 1950
4000 REM " --- Overlay "        
4010 RUN "MPU.AC"
8000 REM "--- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9999 END
