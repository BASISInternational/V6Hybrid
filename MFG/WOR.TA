0010 REM "WOR - Bottleneck Analysis Report"
0020 REM "Program:  WOR.TA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOM-02"
0130 LET FILES$[4]="WOM-04",FILES$[5]="WOM-05",FILES$[6]="WOM-05"
0140 LET FILES$[7]="WOT-01",FILES$[8]="SYS-01"
0145 LET FILES$[9]="ARM-01",FILES$[10]="BMM-08"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOM02_DEV=CHANNELS[3]
0180 LET WOM04_DEV=CHANNELS[4],WOM05_DEV=CHANNELS[5],WOM05_DEV1=CHANNELS[6]
0185 LET WOT01_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8]
0200 REM " --- IOLists
0220 BMM08A: IOLIST X$
0250 WOM02A: IOLIST X$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 LET NO_OPS=NUM(P3$(26,2))
0500 REM " --- Init Data
0510 LET H1$=N1$,H2$=N3$
0520 LET L9=59,L=L9+1,UTILPERC=100
0530 DIM DASHES$(80,"-")
0540 DIM A[9],B[31],C[11],D[13],Z[9]
0550 LET M1$="###0.00-",M2$="##0.0",M3$="##0.00-"
0560 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0600 REM " --- Additional File Opens
0610 IF P3$(1,1)<>"Y" THEN GOTO 0650
0620 CALL "SYC.DA",1,9,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET ARM01_DEV=CHANNELS[9]
0650 IF P3$(2,1)<>"Y" THEN GOTO 0700
0660 CALL "SYC.DA",1,10,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATU
0660:S
0670 IF STATUS THEN GOTO 9900
0680 LET BMM08_DEV=CHANNELS[10]
0700 REM " --- Background
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(18,3),"From Operation Code: ",@(15,5),"Through Operation Cod
0740:e: ",@(28,7),"From Date:",@(25,9),"Through Date: ",@(15,11),"Percent of U
0740:tilization: ",
0760 PRINT 'SF'
1000 REM " --- From Op Code
1010 LET V0$="S",V1$="KEC",V2$=OPFROM$,V3$="",V4$="Enter Valid Operation Code,
1010: <F1>=First, <F3>=Lookup",V0=3,V1=40,V2=3,I0=0
1020 PRINT @(V1,V2),'CL',
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1050 IF V3=3 THEN GOSUB 6000
1060 IF V$<>"" AND V3<>1 THEN GOTO 1100
1070 LET OPFROM$=""
1080 PRINT @(V1,V2),'CL',"First",
1090 GOTO 1150
1100 LET V$=V$+"   ",V$=V$(1,3)
1110 IF P3$(1,1)="Y" THEN FIND (BMM08_DEV,KEY=N0$+V$,DOM=1000)IOL=BMM08A
1120 IF P3$(1,1)<>"Y" THEN FIND (WOM02_DEV,KEY=N0$+V$,DOM=1000)IOL=WOM02A
1130 PRINT @(V1,V2),V$," ",X$(6,20),
1140 LET OPFROM$=V$
1150 GOTO 3900
1200 REM " --- Through Op Code
1210 LET V0$="S",V1$="KEC",V2$=OPTHRU$,V3$="",V4$="Enter Valid Operation Code,
1210:  <F1>=Last",V0=3,V1=40,V2=5
1220 PRINT @(V1,V2),'CL',
1230 GOSUB 7000
1240 IF V3=4 THEN GOTO 9900
1250 IF V3=3 THEN GOSUB 6000
1260 IF V$<>"" AND V3<>1 THEN GOTO 1300
1270 LET OPTHRU$=""
1280 PRINT @(V1,V2),'CL',"Last",
1290 GOTO 1360
1300 LET V$=V$+"   ",V$=V$(1,3)
1310 IF OPFROM$<>"" THEN IF V$<OPFROM$ THEN GOTO 1200
1320 IF P3$(1,1)="Y" THEN FIND (BMM08_DEV,KEY=N0$+V$,DOM=1200)IOL=BMM08A
1330 IF P3$(1,1)<>"Y" THEN FIND (WOM02_DEV,KEY=N0$+V$,DOM=1200)IOL=WOM02A
1340 PRINT @(V1,V2),V$," ",X$(6,20),
1350 LET OPTHRU$=V$
1360 GOTO 3900
1400 REM " --- Starting
1410 LET V0$="D",V1$="C",V2$=DATEFROM$,V3$="",V4$="Enter Starting Date, <F1>=F
1410:irst",V0=6,V1=40,V2=7
1420 PRINT @(V1,V2),'CL',
1430 GOSUB 7000
1440 IF V$<>"" AND V3<>1 THEN GOTO 1480
1450 LET DATEFROM$=""
1460 PRINT @(V1,V2),'CL',"First",
1470 GOTO 1490
1480 LET DATEFROM$=V$
1490 GOTO 3900
1600 REM " --- Ending
1610 LET V0$="D",V1$="C",V2$=DATETHRU$,V3$="",V4$="Enter Ending Date, <F1>=Las
1610:t",V0=6,V1=40,V2=9
1620 PRINT @(V1,V2),'CL',
1630 GOSUB 7000
1640 IF V$<>"" AND V3<>1 THEN GOTO 1675
1650 LET DATETHRU$=""
1660 PRINT @(V1,V2),'CL',"Last",
1670 GOTO 1690
1675 IF DATEFROM$<>"" THEN IF V$<DATEFROM$ THEN GOTO 1600
1680 LET DATETHRU$=V$
1690 GOTO 3900
1800 REM " --- % of Utilization         
1820 LET V0$="N",V1$="C",V2$=STR(UTILPERC),V3$="##0.0",V4$="Enter Minimum Perc
1820:ent of Utilization To Report",V0=3,V1=40,V2=11
1825 PRINT @(V1,V2),'CL',
1830 GOSUB 7000
1840 IF V<0 THEN GOTO 1800
1850 LET UTILPERC=V
1890 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,4000
4000 REM " --- Overlay
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct?
4020: (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4190 RUN "WOR.TB"
6000 REM " --- Op Code Lookup
6010 IF P3$(1,1)="Y" THEN LET V5=BMM08_DEV ELSE LET V5=WOM02_DEV
6020 LET V4=1,V6=3,V7=3,V8=6,V9=20
6030 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
6090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
