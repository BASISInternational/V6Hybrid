0010 REM "WOS - Copy Scheduling Calendar"
0020 REM "Program WOM.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOM-04",FILES$[2]="WOM-04",FILES$[3]="SYS-01"
0130 LET FILES$[4]="BMM-08",FILES$[5]="WOM-02"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET WOM041_DEV=CHANNELS[1],WOM042_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 WOM04A1: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 WOM04A2: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 CODFILE: IOLIST X$
0260 WOS01A: IOLIST X$,X$,X$,P9$,X$,X$,X$,X$,X$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="WO"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0500 REM " --- Init Data"
0520 DIM A0$(9),A1$(10),A2$(10),A[31],TOCODE$(99)
0600 REM " --- Open Additional Files
0605 REM " --- Check For Bill Of Materials "
0610 IF P9$(1,1)<>"Y" THEN GOTO 0650
0615 REM " --- Use Bill Of Materials Op Code File "
0620 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0635 LET CODFILE_DEV=CHANNELS[4]
0640 GOTO 0700
0650 REM " --- Use Work Order Op Code File "
0660 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS THEN GOTO 9900
0680 LET CODFILE_DEV=CHANNELS[5]
0700 REM " --- Background"
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(22,3),"Operation Code:",@(0,5),"First Day Currently Schedule
0740:d:",@(40,5),"Last Day Currently Scheduled:",@(20,7),"Date To Copy From:",
0740:@(17,9),"Date To Copy Through:",
0790 PRINT 'SF',
0900 REM " --- Init File"
1000 REM " --- Operation Code"
1010 PRINT 'CF'
1020 LET V0$="S",V1$="KEC",V2$=OPCODE$,V3$="",V4$="Enter Operation Code To Cop
1020:y From, <F3>=Lookup",V0=3,V1=39,V2=3,I0=0
1040 GOSUB 7000
1045 IF V3=2 THEN GOTO 1090
1050 IF V3=4 THEN GOTO 9900
1055 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
1057 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description",FIELD[0]=1,FIELD
1057:[1]=1,POSITION[0]=3,POSITION[1]=6,LENGTH[0]=3,LENGTH[1]=20
1060 IF V3=3 THEN CALL "SYC.LK",CODFILE_DEV,"",1,"Operation Code",DESCRIPTION$
1060:[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],50,7,V$
1065 LET V$=V$+"   ",V$=V$(1,3)
1070 FIND (CODFILE_DEV,KEY=N0$+V$,DOM=1000)IOL=CODFILE
1075 PRINT @(V1,V2),V$," ",X$(6,20),'CL'
1080 LET OPCODE$=V$
1085 GOSUB 6200
1087 IF FIRST_DATE$="   " THEN GOTO 1000
1090 GOTO 3900
1100 REM " --- Day To Copy Through
1110 IF LEN(R1$)=0 THEN LET R1$=LAST_DATE$
1120 LET V0$="D",V1$="KEC",V2$=R1$,V3$="",V4$="Enter Date To Copy Through",V0=
1120:8,V1=39,V2=9
1140 GOSUB 7000
1145 IF V3=2 THEN GOTO 1190
1150 IF V3=4 THEN GOTO 9900
1160 IF V$>LAST_DATE$ THEN GOTO 1100
1170 IF V$<R0$ THEN GOTO 1100
1180 LET R1$=V$
1185 GOSUB 6400
1190 GOTO 3900
1200 REM " --- Operation Code to Copy To"
1205 LET V1=0,V2=I0+8; IF V2>21 THEN LET V2=V2-11,V1=V1+26
1210 IF V2>21 THEN LET V2=V2-11,V1=V1+26
1212 IF TOCODE$((I0-3)*3+1,3)=$000000$ THEN LET V2$="   " ELSE LET V2$=TOCODE$
1212:((I0-3)*3+1,3)
1215 LET V0$="S",V1$="KEC",V3$="",V4$="Enter Operation Code To Copy To, <F1>=D
1215:elete, <F3>=Lookup, <F4>=End",V0=3
1220 GOSUB 7000
1225 IF V3=2 THEN GOTO 1290
1230 IF V3=4 THEN LET I0=50; GOTO 1290
1235 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
1240 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description",FIELD[0]=1,FIELD
1240:[1]=1,POSITION[0]=3,POSITION[1]=6,LENGTH[0]=3,LENGTH[1]=20
1245 IF V3=3 THEN CALL "SYC.LK",CODFILE_DEV,"",1,"Operation Code",DESCRIPTION$
1245:[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],50,7,V$
1250 IF V3=1 THEN LET TOCODE$=TOCODE$(1,(I0-3)*3)+TOCODE$((I0-3)*3+4)+"   "; G
1250:OSUB 6400; GOTO 1200
1255 LET V$=V$+"   ",V$=V$(1,3)
1260 FIND (CODFILE_DEV,KEY=N0$+V$,DOM=1200)IOL=CODFILE
1265 IF V$="   " THEN LET V$=$000000$
1270 IF POS(V$=TOCODE$(1,(I0-3)*3+1),3)>0 OR V$=OPCODE$ OR (V$=$000000$ AND OP
1270:CODE$="   ") THEN GOTO 1200
1275 LET TOCODE$((I0-3)*3+1,3)=V$
1280 IF POS(V$=TOCODE$((I0-3)*3+4),3)>0 THEN LET TOCODE$=TOCODE$(1,POS(V$=TOCO
1280:DE$,3,2)-1)+TOCODE$(POS(V$=TOCODE$,3,2)+3)+"   "; GOSUB 6400; GOTO 1290
1285 IF V$<>$000000$ THEN PRINT @(V1,V2),V$," ",X$(6,20), ELSE PRINT @(V1,V2),
1285:"    ",X$(6,20),
1290 GOTO 3900
1400 REM " --- 1st Day To Copy From"
1410 IF LEN(R0$)=0 THEN LET R0$=FIRST_DATE$
1420 LET V0$="D",V1$="KEC",V2$=R0$,V3$="",V4$="Enter Date To Copy From",V0=8,V
1420:1=39,V2=7
1440 GOSUB 7000
1445 IF V3=2 THEN GOTO 1490
1450 IF V3=4 THEN GOTO 9900
1460 IF V$<FIRST_DATE$ THEN GOTO 1400
1480 LET R0$=V$
1490 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1400,1100,1200,1200,1200,1200,1200,1200,1200,1200,1200,12
3960:00,1200,1200,1200,1200,1200,1200,1200,1200,1200,1200,1200,1200,1200,1200,
3960:1200,1200,1200,1200,1200,1200,1200,1200,1200,3990
3990 LET I0=0
4000 REM " --- Options"
4020 LET V0$="Y",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct
4020:? (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Initialize For Write"
4105 SETESC 7090
4110 CALL "SYC.NB","Copying:",9,COL
4120 LET FIRST$=FNYY_YY21$(FNM$(FNC$(R0$)))
4130 LET LAST$=FNYY_YY21$(FNM$(FNC$(R1$)))
4150 LET YR$=FIRST$(1,2),MON$=FIRST$(3,2)
4160 DIM B0$(9),B1$(1),B2$(10),B[31]
4170 READ (WOM041_DEV,KEY=N0$+OPCODE$+YR$+MON$,DOM=4200)IOL=WOM04A1
4180 GOTO 4240
4200 REM " --- Read Loop"
4210 LET K$=KEY(WOM041_DEV,END=9900)
4220 IF K$>N0$+OPCODE$+LAST$(1,4) THEN GOTO 9900
4230 READ (WOM041_DEV,KEY=K$)IOL=WOM04A1
4300 REM " --- Write Loop"
4310 FOR LOOP=1 TO LEN(TOCODE$) STEP 3
4315 IF TOCODE$(LOOP,3)="   " THEN GOTO 4600
4320 IF TOCODE$(LOOP,3)=$000000$ THEN LET TO$="   " ELSE LET TO$=TOCODE$(LOOP,
4320:3)
4325 PRINT @(COL,11),A0$(8,2),"/",FNYY21_YY$(A0$(6,2))," ",TO$,
4330 FIND (WOM042_DEV,KEY=N0$+TO$+A0$(6,4),DOM=4355)IOL=WOM04A2
4340 IF B0$(6,4)=FIRST$(1,4) THEN GOTO 4400
4350 IF B0$(6,4)=LAST$(1,4) THEN GOTO 4500
4355 FOR X=1 TO 31; LET B[X]=-1; NEXT X
4360 LET B0$(1)=A0$(1,2)+TO$+A0$(6),B1$(1)=A1$,B2$(1)=A2$,B[0]=A[0]
4365 IF B0$(6,4)=FIRST$(1,4) THEN GOTO 4400
4370 IF B0$(6,4)=LAST$(1,4) THEN GOTO 4500
4375 FOR X=0 TO 31; LET B[X]=A[X]; NEXT X
4380 GOSUB 6000
4390 GOTO 4600
4400 REM " --- Found An Existing Record For The First Month"
4405 IF B0$(6,4)=LAST$(1,4) THEN LET LAST=NUM(LAST$(5,2)) ELSE LET LAST=31
4410 FOR X=NUM(FIRST$(5,2)) TO LAST
4420 LET B[X]=A[X]
4430 NEXT X
4440 GOSUB 6000
4450 GOTO 4600
4500 REM " --- Found An Existing Record For The Last Month"
4510 FOR X=1 TO NUM(LAST$(5,2))
4520 LET B[X]=A[X]
4530 NEXT X
4540 GOSUB 6000
4600 NEXT LOOP
4610 GOTO 4200
6000 REM " --- Write Record"
6050 WRITE (WOM042_DEV,KEY=B0$(1,9))IOL=WOM04A2
6090 RETURN
6200 REM " --- Find First and Last Date For This Op Code"
6210 LET FIRST_DATE$="   "
6220 READ (WOM041_DEV,KEY=N0$+OPCODE$,DOM=6230)
6230 LET K$=KEY(WOM041_DEV,END=6280)
6235 IF POS(N0$+OPCODE$=K$)<>1 THEN GOTO 6280
6240 READ (WOM041_DEV,KEY=K$)IOL=WOM04A1
6250 LET X$="01"
6255 FOR X=1 TO A[0]
6260 IF A[X]>=0 THEN LET X$=STR(X:"00"); EXITTO 6270
6265 NEXT X
6270 LET FIRST_DATE$=FND$(A0$(8,2)+X$+A0$(6,2))
6280 PRINT @(31,5),'SB',FNB$(FIRST_DATE$),'SF',
6310 LET LAST_DATE$="   "
6320 READ (WOM041_DEV,KEY=N0$+OPCODE$,DOM=6330)
6330 LET K$=KEY(WOM041_DEV,END=6380)
6335 IF POS(N0$+OPCODE$=K$)<>1 THEN GOTO 6380
6340 READ (WOM041_DEV,KEY=K$)IOL=WOM04A1
6345 LET X$=STR(A[0]:"00")
6350 FOR X=A[0] TO 1 STEP -1
6360 IF A[X]>=0 THEN LET X$=STR(X:"00"); EXITTO 6370
6365 NEXT X
6370 LET LAST_DATE$=FND$(A0$(8,2)+X$+A0$(6,2))
6375 GOTO 6330
6380 PRINT @(70,5),'SB',FNB$(LAST_DATE$),'SF',
6390 RETURN
6400 REM " --- Redisplay 'TO' Op Codes
6410 LET Z=1; PRINT @(0,11),'CE',
6420 FOR X=0 TO 52 STEP 26
6430 FOR Y=11 TO 21
6435 IF TOCODE$(Z,3)="   " THEN GOTO 6460
6440 IF TOCODE$(Z,3)=$000000$ THEN LET TO$="   " ELSE LET TO$=TOCODE$(Z,3)
6445 FIND (CODFILE_DEV,KEY=N0$+TO$,DOM=6460)IOL=CODFILE
6450 PRINT @(X,Y),TO$," ",X$(6,20),
6460 LET Z=Z+3
6470 NEXT Y
6480 NEXT X
6490 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
