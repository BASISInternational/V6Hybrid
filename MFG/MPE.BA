0010 REM "MPE - Resource Forecast Entry"
0020 REM "Program MPE.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="MPE-02",FILES$[2]="MPM-01",FILES$[3]="BMM-08"
0120 LET FILES$[4]="IVM-10",FILES$[5]="SYS-01"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET MPE02_DEV=CHANNELS[1],MPM01_DEV=CHANNELS[2],BMM08_DEV=CHANNELS[3]
0165 LET IVM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 MPE02A: IOLIST A0$(1),A[ALL]
0220 MPM01A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 BMM08A: IOLIST C0$(1)
0260 BMS01A: IOLIST X$,M4$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVM10C: IOLIST X0$(1)
0290 IVS01A: IOLIST X$,X$,P2$,P3$,X$,X$,M1$,M2$,M3$
0400 REM " --- Parameters"
0405 DIM P[0]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=9800)IOL=BMS01A
0450 LET P[0]=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),NUMBER=1,MP=26
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM J1$(30),A0$(10),A[MP-1],B0$(5),B1$(60),B2$(MP*26),B[5],C0$(32)
0520 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0530 DIM X0$(32)
0550 LET L0=10
0580 LET A0$(1,2)=N0$
0590 CALL "SYC.RM",M4$,M4,10,10
0600 REM " --- Print positions"
0610 LET X0=INT((80-(39+3*M4))/4),X1=13+M4,P0=X0,P1=P0+X1+2,P2=P1+X1+2
0620 LET P3=P0+4,P4=P1+4,P5=P2+4,P6=P3+9,P7=P4+9,P8=P5+9,P9=80-M4
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-4,0
0720 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0730 PRINT 'SB',@(1,3),"Forecast Type:",@(0,4),"Warehouse Code:",@(0,5),"Opera
0730:tion Code:",@(P3+1,L0-3),"Period",@(P6+M4-8),"Resource",@(P4+1),"Period",
0730:@(P7+M4-8),"Resource",@(P5+1),"Period",@(P8+M4-8),"Resource",@(P3,L0-2),"
0730:End Date",@(P6+M4-8),"Capacity",@(P4),"End Date",@(P7+M4-8),"Capacity",@(
0730:P5),"End Date",@(P8+M4-8),"Capacity",
0740 FOR X=1 TO 9
0750 PRINT @(P0,X+(L0-1)),X:"##:",@(P1),X+9:"##:",
0760 IF X+18<=26 THEN PRINT @(P2),X+18:"##:",
0770 NEXT X
0790 PRINT 'SF',
0900 REM " --- Position File"
0990 READ (MPE02_DEV,KEY=A0$(1,2),DOM=1000)
1000 REM " --- Forecast Type"
1005 PRINT 'CF',
1010 LET V2$="",FORECAST$="",WHSE$="",OPERATION$=""
1013 LET V2$=KEY(MPE02_DEV,END=1030)
1015 IF POS(A0$(1,2)=V2$)<>1 THEN LET V2$=""
1020 IF V2$="" THEN GOTO 1030
1025 LET OPERATION$=V2$(8,3),WHSE$=V2$(6,2),V2$=V2$(3,3),FORECAST$=V2$
1030 LET V0$="S",V1$="KC",V3$="",V0=3,V1=16,V2=3,I0=0,O8=0,O9=0
1035 LET V4$="Enter A Valid Forecast Type (<F3>=Lookup)"
1040 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1045 GOSUB 7000
1050 ON V3 GOTO 1065,1000,1000,1055,9900
1055 GOSUB 6000
1060 IF V$="" THEN GOTO 1000
1065 IF V$="" THEN GOTO 0900
1070 IF V$<>FORECAST$ THEN LET WHSE$="",OPERATION$=""
1073 LET B0$(1)=N0$+V$
1075 FIND (MPM01_DEV,KEY=B0$,DOM=1000)IOL=MPM01A
1080 PRINT @(V1,V2),B0$(3,3)," ",B1$(1,30)," ",B1$(31,28),
1085 LET A0$(3,3)=B0$(3,3),FORECAST$=A0$(3,3)
1090 GOSUB 5200
1100 REM " --- Warehouse"
1105 PRINT @(16,4),'CL',@(16,5),'CL',
1110 IF P3$(1,1)<>"Y" THEN LET V$=P3$(6,2); GOTO 1190
1115 LET V0$="S",V1$="C",V2$=WHSE$,V3$="",V0=2,V1=16,V2=4
1120 IF V2$="" THEN LET V2$=P3$(6,2)
1125 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
1130 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1135 GOSUB 7000
1140 ON V3 GOTO 1155,1100,1000,1145,1000
1145 GOSUB 6100
1150 IF V$="" THEN GOTO 1100
1155 IF V$<>WHSE$ THEN LET OPERATION$=""
1160 LET X0$(1)=N0$+"C"+V$
1165 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1100)IOL=IVM10C
1170 PRINT @(V1,V2),X0$(4,2),"  ",X0$(6,24),
1175 LET A0$(6,2)=X0$(4,2),WHSE$=X0$(4,2)
1180 GOSUB 6500
1200 REM " --- Operation"
1205 GOSUB 5500
1210 LET V0$="S",V1$="KC",V3$="",V0=3,V1=16,V2=5,O7=0,O8=0,O9=0
1215 LET V4$="Enter A Valid Operation (<F3>=Lookup)"
1220 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1225 GOSUB 7000
1230 ON V3 GOTO 1245,1200,1100,1235,1100
1235 GOSUB 6200
1240 IF V$="" THEN GOTO 1200
1245 LET C0$(1)=N0$+V$
1250 FIND (BMM08_DEV,KEY=C0$(1,5),DOM=1200)IOL=BMM08A
1270 LET A0$(8)=C0$(3),OPERATION$=C0$(3)
1290 PRINT @(V1,V2),C0$(3,3)," ",C0$(6,20),
1300 REM " --- Retrieve MPE-02 record"
1310 READ (MPE02_DEV,KEY=A0$,DOM=1400)IOL=MPE02A
1320 GOSUB 5100
1390 GOTO 4000
1400 REM " --- New record?"
1410 LET V4$="Is This A New Record"
1420 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON V3 GOTO 1450,1400,1200,1400,1200
1490 ON POS(V$="YN") GOTO 1400,1500,1200
1500 REM " --- Initialize new record"
1510 DIM A[MP]
1520 LET I0=0
1590 GOSUB 5100
2000 REM " --- Quantities"
2010 IF I0+1>B[0] THEN GOTO 3900
2015 LET V0$="N",V1$="C",V2$=STR(A[I0]),V3$=M4$,V1=P6,V2=L0+I0
2020 IF I0>8 THEN LET V1=P7,V2=L0+(I0-9)
2025 IF I0>17 THEN LET V1=P8,V2=L0+(I0-18)
2030 LET V4$="Enter The Number Of Hours For Period "+STR(I0+1)+" (<F1>=Insert 
2030:Or Delete/<F3>=Duplicate)"
2035 GOSUB 7000
2040 ON V3 GOTO 2045,2200,3900,2800,3900
2045 LET A[I0]=V
2050 PRINT @(V1,V2),A[I0]:M4$,
2090 GOTO 3900
2200 REM " --- Insert Or Delete?"
2210 LET Y1=V1+M2,Y2=V2
2215 PRINT @(Y1,Y2),'SB',"<",'SF',
2220 LET V4$="Insert At This Position Or Delete This Quantity (<F1>=Insert/<F3
2220:>=Delete)?"
2230 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
2240 GOSUB 7000
2250 ON V3 GOSUB 2260,2400,2260,2600,2260
2260 PRINT @(Y1,Y2)," ",
2290 GOTO 2000
2400 REM " --- <F1>=Insert"
2410 FOR X=B[0] TO I0+1 STEP -1
2420 IF X>1 THEN LET A[X-1]=A[X-2]
2430 NEXT X
2440 LET A[I0]=0
2490 GOTO 2850
2600 REM " --- <F3>=Delete"
2610 FOR X=I0-1 TO B[0]
2620 IF X>-1 THEN LET A[X]=A[X+1]
2630 NEXT X
2640 LET A[25]=0
2690 GOTO 2850
2800 REM " --- Duplicate current amount"
2810 LET O8=1,O9=1
2820 FOR X=I0+1 TO B[0]-1
2830 LET A[X]=A[I0]
2840 NEXT X
2850 GOSUB 5100
2860 PRINT @(Y1,Y2)," ",
2890 GOTO 2000
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 IF I0<26 THEN GOTO 2000
3990 LET I0=0
4000 REM " --- Correct?"
4005 LET V4$="Is The Above Information Correct (Y/N/#/"
4010 IF O8=0 THEN LET V4$=V4$+"Copy/"
4015 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$=V4$+"Delete)"
4020 LET V0=6,V1=FNV(V4$),V2=22,I0=0
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4000,4000,1200
4040 IF V$="DELETE" THEN GOTO 4200
4045 IF (V$="C" OR V$="COPY") AND O8=0 THEN GOTO 4300
4070 LET V=NUM(V$,ERR=4090)
4080 IF V>0 THEN GOTO 4800
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (MPE02_DEV,KEY=A0$)IOL=MPE02A
4120 GOSUB 6600
4190 GOTO 1200
4200 REM " --- Delete"
4210 REMOVE (MPE02_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1200
4300 REM " --- Copy"
4310 LET WIDTH=60,HEIGHT=5,TITLE$="Copy "+N4$
4330 LET ROW=L0+2,COLUMN=INT((80-WIDTH)/2)
4340 CALL "SYC.WA",0,WIDTH,HEIGHT,COLUMN,ROW,TITLE$,NAME$
4350 PRINT 'SB',@(1,0),"Warehouse Code:",@(1,1),"Operation Code:",'SF',
4390 LET COPYWHSE$=WHSE$,COPYOPER$=""
4400 REM " --- Copy to Warehouse"
4410 LET V0$="S",V1$="C",V2$=COPYWHSE$,V3$="",V0=2,V1=17,V2=0
4420 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
4430 GOSUB 7000
4440 ON V3 GOTO 4460,4400,4700,4445,4700
4445 GOSUB 6100
4450 IF V$="" THEN GOTO 4400
4460 LET X0$(1)=N0$+"C"+V$
4465 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=4400)IOL=IVM10C
4470 PRINT @(V1,V2),X0$(4,2),"  ",X0$(6,24),
4475 LET COPYWHSE$=X0$(4,2)
4500 REM " --- Operation"
4510 LET V0$="S",V1$="KC",V2$=COPYOPER$,V3$="",V0=3,V1=17,V2=1
4520 LET V4$="Enter A Valid Operation (<F3>=Lookup)"
4525 GOSUB 7000
4530 ON V3 GOTO 4545,4500,4400,4535,4400
4535 GOSUB 6200
4540 IF V$="" THEN GOTO 4500
4545 LET C0$(1)=N0$+V$
4550 FIND (BMM08_DEV,KEY=C0$(1,5),DOM=4500)IOL=BMM08A
4560 PRINT @(V1,V2),C0$(3,3)," ",C0$(6,20)
4570 LET COPYOPER$=C0$(3)
4600 REM " --- Copy record"
4610 LET A0$(6,2)=COPYWHSE$,A0$(8)=COPYOPER$
4620 WRITE (MPE02_DEV,KEY=A0$)IOL=MPE02A
4650 LET V0$="S",V1$="C",V2$="",V4$="Copy Completed (<Enter>=Continue)"
4660 LET V3$="",V0=1,V1=INT(WIDTH/2)+INT(LEN(V4$)/2)+2,V2=HEIGHT-2
4670 GOSUB 7000
4680 ON V3 GOTO 4690,4400,4700,4400,4700
4690 GOTO 4400
4700 REM " --- Copy Completed"
4710 CALL "SYC.WD",NAME$
4720 LET A0$(1)=N0$+FORECAST$+WHSE$+OPERATION$
4790 GOTO 4000
4800 REM " --- Jump directly to quantity field"
4810 IF V<1 THEN GOTO 4000
4820 IF V>B[0] THEN GOTO 4000
4880 LET I0=V-2
4890 GOTO 3900
5100 REM " --- Display quantities"
5110 FOR X=1 TO B[0]
5120 LET V1=P6,V2=L0+(X-1)
5130 IF X>9 THEN LET V1=P7,V2=L0+(X-10)
5140 IF X>18 THEN LET V1=P8,V2=L0+(X-19)
5150 PRINT @(V1,V2),A[X-1]:M4$,
5160 NEXT X
5190 RETURN
5200 REM " --- Display Dates"
5210 FOR X=1 TO B[0]
5220 LET V1=P3,V2=L0+(X-1)
5230 IF X>9 THEN LET V1=P4,V2=L0+(X-10)
5250 IF X>18 THEN LET V1=P5,V2=L0+(X-19)
5260 PRINT @(V1,V2),FNB6$(B2$(6*(X-1)+1,6)),
5280 NEXT X
5290 RETURN
5500 REM " --- Clear operation specific information"
5510 FOR X=21 TO L0 STEP -1
5520 PRINT @(P6,X),FILL(M4+1," "),@(P7,X),FILL(M4+1," "),@(P8,X),FILL(M4+1," "
5520:),
5530 NEXT X
5570 PRINT @(17,5),'CL',
5590 RETURN
6000 REM " --- Forecast Lookup"
6010 LET RECORD$="",TITLE$="MRP Forecast Types"
6030 LET DESCRIPTION$[0]="Type",CHANNEL=MPM01_DEV,FIELD[1]=2
6040 LET POSITION[0]=3,LENGTH[0]=3,POSITION[1]=1,LENGTH[1]=30
6090 GOTO 6400
6100 REM " --- Warehouse Lookup"
6110 LET RECORD$="C",TITLE$="Warehouse Lookup"
6130 LET DESCRIPTION$[0]="ID",CHANNEL=IVM10_DEV,FIELD[1]=1
6140 LET POSITION[0]=4,LENGTH[0]=2,POSITION[1]=6,LENGTH[1]=24
6190 GOTO 6400
6200 REM " --- Operation Lookup"
6210 LET RECORD$="",TITLE$="Operations Lookup"
6230 LET DESCRIPTION$[0]="Code",CHANNEL=BMM08_DEV,FIELD[1]=1
6240 LET POSITION[0]=3,LENGTH[0]=3,POSITION[1]=6,LENGTH[1]=20
6400 REM " --- Call Windowed Lookup"
6410 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6440 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6450 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],
6450:POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN
6500 REM " --- Retrieve first item for forecast/whse combination"
6590 READ (MPE02_DEV,KEY=A0$(1,7),DOM=6600)
6600 REM " --- Next Item"
6610 LET V2$="",V2$=KEY(MPE02_DEV,END=6690)
6620 IF POS(A0$(1,6)=V2$)<>1 THEN LET V2$=""
6630 IF V2$<>"" THEN LET V2$=V2$(8)
6690 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
