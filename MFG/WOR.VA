0010 REM "WOR - Wor Order Job Status Summary "
0020 REM "Program:  WOR.VA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=15
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOT-01"
0125 LET FILES$[4]="IVM-10",FILES$[5]="WOM-10",FILES$[6]="SYS-01"
0130 LET FILES$[7]="IVM-01",FILES$[8]="WOE-03",FILES$[9]="WOT-03"
0135 LET FILES$[10]="WOT-21",FILES$[11]="WOT-31",FILES$[12]="WOT-23"
0140 LET FILES$[13]="WOT-33",FILES$[14]="WOE-22"
0149 LET FILES$[15]="ARM-01"
0150 CALL "SYC.DA",1,1,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOT01_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],WOM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0185 LET IVM01_DEV=CHANNELS[7],WOE03_DEV=CHANNELS[8],WOT03_DEV=CHANNELS[9]
0190 LET WOT21_DEV=CHANNELS[10],WOT31_DEV=CHANNELS[11],WOT23_DEV=CHANNELS[12]
0195 LET WOT33_DEV=CHANNELS[13],WOE22_DEV=CHANNELS[14]
0200 REM " --- IOLISTs"
0210 WOM10A: IOLIST X0$
0220 IVM10A: IOLIST X0$
0230 IVM01A: IOLIST *,X0$
0240 ARM01A: IOLIST *,X0$
0250 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVS01A: IOLIST I0$,I1$,I2$,I3$
0290 ARS01A: IOLIST X$,X$,C0$,X$,X$,C1$
0400 REM " --- Parameters"
0405 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0410 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0415 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0417 LET I9=NUM(P3$(26,2)),AR$=P3$(2,1),BM$=P3$(1,1)
0420 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 IF AR$="N" THEN GOTO 0445
0435 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0440 LET C0=NUM(C0$(1,2)),C1=LEN(C1$)
0445 DIM DESC[3]
0450 LET ITEM_LEN=NUM(I2$(1,2)),PRECISION=NUM(I2$(5,1))
0455 FOR X=1 TO 3
0460 LET DESC[X]=NUM(I2$((X-1)*2+9,2)),DESCLEN=DESCLEN+DESC[X]
0465 NEXT X
0470 IF DESCLEN>30 THEN LET DESCLEN=30
0500 REM " --- Init Data"
0505 LET H0=132,H=0,HEADINGS=6,WHEN$=F0$(7,3),CLOCK$=""
0510 DIM HEADINGS$[HEADINGS]
0515 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0520 LET L9=59,L=L9+1
0525 DIM J$(80,"-"),J1$(40," "),J0$(20,"0")
0530 DIM A0$(75),A1$(141),A[11]
0535 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),G5=LEN(G5$)
0540 DIM J2$(M1,"-")
0545 LET P0=NUM(P4$(1,2)),P1=FNYY_YEAR(P4$(3,2)),P0=P0-1
0550 IF P0=0 THEN LET P0=NUM(G2$(1,2)),P1=P1-1; REM "Find Last Period End Date
0555 CALL "SYC.PA",SYS01_DEV,P0,P1,V0$,G9$,STATUS
0560 IF STATUS THEN GOTO 9000
0565 LET P4$=""
0600 REM " --- Check For Accounts Receivable
0610 IF AR$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,15,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATU
0620:S
0630 IF STATUS THEN GOTO 9900
0640 LET ARM01_DEV=CHANNELS[15]
0700 REM " --- Background"
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(13,3),"Selected Report Sequence: ",@(28,5),"Beginning: ",@(3
0740:1,7),"Ending:",@(15,9),"Units Or Dollars (U/D):",@(27,11),"As Of Date:",@
0740:(11,13),"Open/Closed/Planned/Quoted: "
0760 PRINT 'SF'
1000 REM " --- Options"
1005 LET I0=0
1010 PRINT 'CF',@(0,5),J1$(1,15)
1015 LET V4$="W=Work Order, T=WO Type"
1016 LET V3$="WT"
1017 IF BM$="Y" THEN LET V4$=V4$+", B=Bill Number",V3$=V3$+"B"
1018 IF AR$="Y" THEN LET V4$=V4$+", C=Customer",V3$=V3$+"C"
1020 LET V0$="S",V1$="KREC",V2$=P9$,V0=1,V1=40,V2=3
1037 IF V2$="" THEN LET V2$="W"
1040 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1070 IF V$<>P9$ THEN LET P7$="",P8$=""
1080 LET P9$=V$
1100 ON POS(P9$="WBCT")-1 GOTO 1110,1120,1130,1140
1110 LET H3$="Work Order Sequence",X9$="  ",FILEX9_DEV=WOE01_DEV,X8=0; GOTO 11
1110:80
1120 LET H3$="Bill Number Sequence",X9$="C",FILEX9_DEV=WOE03_DEV,X8=22; GOTO 1
1120:180
1130 LET H3$="Customer Sequence",X9$="B",FILEX9_DEV=WOE03_DEV,X8=6; GOTO 1180
1140 LET H3$="Work Order Type Sequence",X9$="A",FILEX9_DEV=WOE03_DEV,X8=2
1180 LET H9$=H3$(1,POS("Seq"=H3$)-2)
1190 GOTO 3900
1200 REM " --- Beginning
1220 LET V1$="CE",V2$=P8$,V3$="",V4$="Enter Beginning "+H9$+", <F1>=First, <F3
1220:>=Lookup",V1=40,V2=5
1230 PRINT @(V1,V2),'CL'
1260 ON POS(P9$="WBCT")-1 GOTO 1300,1310,1320,1330
1300 LET V0$="Z",V0=7; GOTO 1355
1310 LET V0$="S",V0=ITEM_LEN; GOTO 1355
1320 LET V0$="M",V3$=C1$,V0=C0; GOTO 1355
1330 LET V0$="S",V0=2
1355 GOSUB 7000
1360 IF V3=4 THEN GOTO 9900
1370 IF V3=3 THEN GOSUB 6000
1380 IF V3=2 THEN GOTO 1490
1390 IF V3=1 OR V$="" THEN LET V$=""; PRINT @(V1,V2),'CL',"First",; GOTO 1420
1400 GOSUB 6300
1410 IF OK$="N" THEN GOTO 1200
1420 LET P8$=V$
1490 GOTO 3900
1500 REM " --- Ending"
1520 LET V1$="CE",V2$=P7$,V3$="",V4$="Enter Ending "+H9$+", <F1>=Last, <F3>=Lo
1520:okup",V1=40,V2=7
1530 PRINT @(V1,V2),'CL'
1560 ON POS(P9$="WBCT")-1 GOTO 1600,1610,1620,1630
1600 LET V0$="Z",V0=7; GOTO 1650
1610 LET V0$="S",V0=ITEM_LEN; GOTO 1650
1620 LET V0$="M",V3$=C1$,V0=C0; GOTO 1650
1630 LET V0$="S",V0=2
1650 GOSUB 7000
1660 IF V3=4 THEN GOTO 9900
1670 IF V3=3 THEN GOSUB 6000
1680 IF V3=2 THEN GOTO 1790
1690 IF V3=1 OR V$="" THEN LET V$=""; PRINT @(V1,V2),'CL',"Last",; GOTO 1750
1710 GOSUB 6300
1720 IF OK$="N" THEN GOTO 1500
1750 IF P8$<>"" AND V$<>"" THEN IF V$<P8$ THEN GOTO 1500
1760 LET P7$=V$
1790 GOTO 3900
1900 REM " --- Units or Dollars "        
1910 LET H4$="",HOW$=""
1920 LET V0$="S",V1$="CK^E",V2$="",V3$="",V4$="U=Units, D=Dollars",V0=1,V1=40,
1920:V2=9
1930 GOSUB 7000
1950 IF V$="" THEN GOTO 1900
1970 IF POS(V$="UD")=0 THEN GOTO 1900
1980 LET UNIT$=V$
1990 IF POS("U"=UNIT$)>0 THEN LET H4$="Units",HOW$="Units"
2000 IF POS("D"=UNIT$)>0 THEN LET H4$="Dollars",HOW$="Dollars"
2040 PRINT @(V1,V2),V$+" "+H4$,'CL',
2050 GOTO 3900
2400 REM " --- Open/Closed/Planned/Quoted
2410 LET H4$=""
2420 LET V0$="S",V1$="CK^E",V2$=P6$,V3$="",V4$="O=Open, C=Closed, P=Planned, Q
2420:=Quoted, or Any Combination, <F1>=All",V0=4,V1=40,V2=13
2430 GOSUB 7000
2440 IF V3=4 THEN GOTO 9900
2450 IF V$="" OR V3=1 THEN LET V$="OCPQ"
2460 FOR X=1 TO LEN(V$); IF POS(V$(X,1)="OCPQ")=0 THEN EXITTO 2400
2470 NEXT X
2480 LET P6$=V$
2490 IF POS("O"=P6$)>0 THEN LET H4$="Open/"
2500 IF POS("C"=P6$)>0 THEN LET H4$=H4$+"Closed/"
2510 IF POS("P"=P6$)>0 THEN LET H4$=H4$+"Planned/"
2520 IF POS("Q"=P6$)>0 THEN LET H4$=H4$+"Quoted/"
2530 LET H4$=H4$(1,LEN(H4$)-1)
2540 PRINT @(V1,V2),V$+" "+H4$,'CL',
2550 GOTO 3900
2800 REM " --- Warehouse
2810 IF P9$<>"B" THEN GOTO 2940
2820 IF P5$="" THEN LET P5$=I3$(6,2)
2830 PRINT 'SB',@(0,5),"Whse Code:",'SF'
2840 LET V0$="S",V1$="CE",V2$=P5$,V3$="",V4$="Enter A Valid Warehouse Code, <F
2840:3>=Lookup",V0=2,V1=12,V2=5
2850 GOSUB 7000
2860 IF V3=4 THEN GOTO 9900
2870 IF V3<>3 THEN GOTO 2900
2880 LET V4=1,V5=5,V6=4,V7=2,V8=6,V9=20,V5$="C"
2890 CALL "SYC.LZ",V1,V2,V4,IVM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
2900 IF LEN(V$)<2 THEN LET V$=V$+"  ",V$=V$(1,2)
2910 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=2800)IOL=IVM10A
2920 PRINT @(V1+3,V2),X0$(6,10)
2930 LET P5$=V$
2940 GOTO 3900
3700 REM " --- As Of Date
3710 IF P4$="" THEN LET P4$=F0$(7,3)
3720 LET V0$="D",V1$="CE",V2$=P4$,V3$="",V4$="Enter Closing Date To Calculate 
3720:Costs, <F1>=All Dates",V0=3,V1=40,V2=11
3730 GOSUB 7000
3740 IF V3=4 THEN GOTO 9900
3750 IF V3=1 THEN PRINT @(V1,V2),"Last",'CL'; LET P4$=""; GOTO 3780
3760 LET P4$=V$
3770 IF P4$<>"" THEN LET H6$="Work In Process As Of "+FNB$(P4$) ELSE LET H6$="
3770:"
3780 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,2800,1200,1500,1900,3700,2400,4000
4000 REM " --- Overlay"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N)?:  ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4140 IF P9$="B" THEN LET P8$=P5$+P8$; IF P7$<>"" THEN LET P7$=P5$+P7$
4200 LET H4$=H4$+" Beginning With "
4220 IF P8$<>"" THEN LET H4$=H4$+P8$ ELSE LET H4$=H4$+"The First"
4230 LET H4$=H4$(1,POS("   "=H4$+"   ")-1)
4240 LET H4$=H4$+" And Ending With "
4260 IF P7$<>"" THEN LET H4$=H4$+P7$ ELSE LET H4$=H4$+"The Last"
4270 LET H4$=H4$(1,POS("   "=H4$+"   ")-1)
4400 RUN "WOR.VB"
6000 REM " --- Item Lookup
6004 PRINT @(V1,V2),'CL'
6005 ON POS(P9$="BWCT")-1 GOTO 6010,6050,6100,6200
6010 CALL "SYC.LB",V$
6030 GOTO 6290
6050 REM " --- Work Order Lookup"
6060 CALL "SYC.LW",V$,"?"
6090 GOTO 6290
6100 REM " --- Customer Lookup
6130 CALL "SYC.LC",V$
6190 GOTO 6290
6200 REM " --- Lookup Type
6210 IF V3=1 THEN GOTO 6290
6220 LET V4=1,V5=6,V6=4,V7=2,V8=6,V9=20,V5$="A"
6230 CALL "SYC.LZ",V1,V2,V4,WOM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
6290 RETURN
6300 REM " --- Verify Entry"
6305 LET OK$=""
6310 ON POS(P9$="BWCT")-1 GOTO 6320,6360,6390,6430
6320 LET V$=V$+J1$,V$=V$(1,20)
6330 FIND (IVM01_DEV,KEY=N0$+V$,DOM=6480)IOL=IVM01A
6340 PRINT @(V1,V2),V$," ",X0$(1,MIN(LEN(X0$),38-ITEM_LEN)),'CL',
6350 GOTO 6490
6360 REM " --- Work Order Verify"
6370 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=6480)
6375 PRINT @(V1,V2),V$,'CL',
6380 GOTO 6490
6390 REM " --- Customer Verify"
6395 LET V$=V$+"000000",V$=V$(1,6)
6400 FIND (ARM01_DEV,KEY=N0$+V$,DOM=6480)IOL=ARM01A
6410 PRINT @(V1,V2),FNF$(V$(1,C0),C1$)," ",X0$(1,30),'CL',
6420 GOTO 6490
6430 REM " --- Type Verify"
6435 LET V$=V$+"  ",V$=V$(1,2)
6440 FIND (WOM10_DEV,KEY=N0$+"A"+V$,DOM=6480)IOL=WOM10A
6450 PRINT @(V1,V2),V$," ",X0$(6,20),'CL',
6460 GOTO 6490
6480 LET OK$="N"
6490 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
