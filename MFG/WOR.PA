0010 REM "WOR - Shop Floor Dispatch Report"
0020 REM "Program WOR.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOM-02"
0130 LET FILES$[4]="WOM-05",FILES$[5]="WOT-01",FILES$[6]="SYS-01"
0140 LET FILES$[7]="WOE-03",FILES$[8]="ARM-01",FILES$[9]="BMM-08"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOM02_DEV=CHANNELS[3]
0180 LET WOM05_DEV=CHANNELS[4],WOT01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0185 LET WOE03_DEV=CHANNELS[7]
0200 REM " --- IOLists
0240 BMM08A: IOLIST D0$(1),D[ALL]
0250 WOM02A: IOLIST D0$(1),D[ALL]
0260 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0265 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0405 DIM INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$,BM$="N"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0435 LET OPNMAX=NUM(P3$(26,2))
0440 LET BM$=P3$(1,1)
0450 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0485 IF BM$<>"Y" THEN GOTO 0500
0490 CALL "SYC.VA","BM",INFO$[ALL]
0495 LET BM$=INFO$[20]
0500 REM " --- Initializations
0510 DIM A0$(75),A1$(138),A[11],B0$(15),B1$(151),B[13]
0520 DIM C0$(9),C[31],D0$(90),D[13],L[9],BLANK$(30),DASHES$(80,"-")
0530 LET OPERATION$="",L0=10,PR_CODE$="9"
0545 LET M2$="##0.0-",M2=LEN(M2$),UMASK$="-######0.000",UMASK=LEN(UMASK$)
0550 CALL "SYC.RM",M3$,M3,8,10
0560 LET SCHEDATE$=F0$(7,3)
0570 LET L9=59,L=L9+1
0590 LET LINES=21-L0
0600 REM " --- Addition file opens
0610 IF AR$<>"Y" THEN GOTO 0660
0620 CALL "SYC.DA",1,8,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS<>0 THEN GOTO 9900
0640 LET ARM01_DEV=CHANNELS[8]
0660 IF BM$<>"Y" THEN GOTO 0700
0670 CALL "SYC.DA",1,9,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS<>0 THEN GOTO 9900
0690 LET BMM08_DEV=CHANNELS[9]
0700 REM " --- Background
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(27,3),"W.O. Status:",@(15,5),"Beginning Analysis Date:",@(18
0740:,7),"Ending Analysis Date:",@(19,9),"Schedule As Of Date:",@(25,11),"Prio
0740:rity Code:",@(24,13),"Operation Code:",
0790 PRINT 'SF',
1000 REM 1000 " --- W.O. Status
1010 PRINT @(0,0),'CF',
1020 LET V0$="S",V1$="CK^",V2$=STATUS$,V3$="",V4$="O=Open, P=Planned, Q=Quoted
1020:, Or Any Combination, <F1>=All",V0=3,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1060,1050,1120,1060,9900
1050 LET V$=""
1060 IF V$="" THEN LET V$="OPQ"
1070 FOR X=1 TO LEN(V$)
1080 IF POS(V$(X,1)="OPQ")=0 THEN EXITTO 1000
1090 NEXT X
1100 PRINT @(V1,V2),V$,"   ",
1110 LET STATUS$=V$
1120 GOTO 3900
1200 REM 1200 " --- Beginning Date
1210 LET V4$="Enter The Beginning Date, <F1>=First"
1220 LET V0$="D",V1$="CK",V2$=BEGDATE$,V3$="",V0=3,V1=40,V2=5
1230 GOSUB 7000
1240 ON V3 GOTO 1280,1250,1310,1280,9900
1250 LET BEGDATE$=""
1260 PRINT @(V1,V2),"First   "
1270 GOTO 1300
1280 IF V$="" THEN GOTO 1250
1290 LET BEGDATE$=V$
1300 IF BM$="Y" THEN READ (BMM08_DEV,KEY=N0$,DOM=1310)
1305 IF BM$<>"Y" THEN READ (WOM02_DEV,KEY=N0$,DOM=1310)
1310 GOTO 3900
1400 REM 1400 " --- Operation Code
1410 LET V0$="S",V1$="CRK",V2$=OP_CODE$,V3$="",V4$="Enter A Valid Operation Co
1410:de, <F1>=All, <F3>=Lookup",V0=3,V1=40,V2=13
1420 GOSUB 7000
1430 ON V3 GOTO 1480,1440,1520,1470,9900
1440 LET OP_CODE$=""
1450 PRINT @(V1,V2),"All Operation Codes",'CL',
1460 GOTO 1520
1470 GOSUB 6000
1480 LET V$=V$+"   ",V$=V$(1,3)
1485 IF BM$="Y" THEN FIND (BMM08_DEV,KEY=N0$+V$,DOM=1400)IOL=BMM08A
1490 IF BM$<>"Y" THEN FIND (WOM02_DEV,KEY=N0$+V$,DOM=1400)IOL=WOM02A
1500 PRINT @(V1,V2),V$," ",D0$(6,20),'CL',
1510 LET OP_CODE$=V$
1520 GOTO 3900
1600 REM 1600 " --- Priority Code
1610 LET V0$="S",V1$="CRK",V2$=PR_CODE$,V3$="123456789",V4$="Enter Minimum Pri
1610:ority Code To Include (1=High, 9=Low)",V0=1,V1=40,V2=11
1620 GOSUB 7000
1630 ON V3 GOTO 1640,1600,1650,1640,9900
1640 LET PR_CODE$=V$
1650 GOTO 3900
1800 REM 1800 " --- Through Date
1810 LET V4$="Enter The Ending Date, <F1>=Last"
1820 LET V0$="D",V1$="CK",V2$=LASTDATE$,V3$="",V0=3,V1=40,V2=7
1830 GOSUB 7000
1840 ON V3 GOTO 1870,1850,1900,1870,9900
1850 LET LASTDATE$=""; PRINT @(V1,V2),"Last    "
1860 GOTO 1900
1870 IF V$="" THEN GOTO 1850
1880 IF BEGDATE$<>"" THEN IF V$<BEGDATE$ THEN GOTO 1800
1890 LET LASTDATE$=V$
1900 GOTO 3900
2000 REM 2000 " --- Schedule Date
2010 LET V4$="Enter The Date To Schedule For"
2020 LET V0$="D",V1$="C",V2$=SCHEDATE$,V3$="",V0=3,V1=40,V2=9
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2050,2060,2050,9900
2050 LET SCHEDATE$=V$
2060 GOTO 3900
3900 REM  " --- Next Field
3910 IF V3=4 THEN GOTO 2000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1800,2000,1600,1400,4000
4000 REM " --- Correct?
4050 LET V4$="Is The Above Information Correct (Y/N)?"
4060 LET V0$="Y",V1$="CR",V2$="Y",V3$="",V4$="Is The Above Information Correct
4060: (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4070 GOSUB 7000
4080 ON V3 GOTO 4090,4090,4090,4090,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Setup Headings
4110 IF POS("O"=STATUS$)>0 THEN LET H0$=H0$+"Open/"
4120 IF POS("P"=STATUS$)>0 THEN LET H0$=H0$+"Planned/"
4130 IF POS("Q"=STATUS$)>0 THEN LET H0$=H0$+"Quoted/"
4140 LET H0$="Includes All "+H0$(1,LEN(H0$)-1)+" Work Orders"
4150 LET H1$="From "
4160 IF BEGDATE$="" THEN LET H1$=H1$+"The First Date" ELSE LET H1$=H1$+FNB$(BE
4160:GDATE$)
4170 LET H1$=H1$+" Through "
4180 IF LASTDATE$="" THEN LET H1$=H1$+"The Last Date" ELSE LET H1$=H1$+FNB$(LA
4180:STDATE$)
4190 IF OP_CODE$="" THEN LET H2$="For All Op Codes" ELSE LET H2$="For Op Code 
4190:"+OP_CODE$
4200 LET H2$=H2$+" With Priority Code Of 1"
4210 IF PR_CODE$>"1" THEN LET H2$=H2$+" Through "+PR_CODE$
4220 LET H2$=H2$+" Scheduled As Of "+FNB$(SCHEDATE$)
4290 RUN "WOR.PB"
6000 REM " --- Operation Lookup
6010 LET CHANNEL=WOM02_DEV,RECORD$="",NUMBER=1,TITLE$="Operation Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6035 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
6040 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6055 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6060 LET V$="SKIP"
6070 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],
6070:POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
