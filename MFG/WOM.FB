0010 REM "WOS - OP Code Calendar Change Maintnance"
0020 REM "Program WOM.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9900
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLIST's"
0210 WOM04A: IOLIST A0$,A1$,A2$,A[ALL]
0220 CODFILE: IOLIST OPDESC$
0400 REM " --- Create main window"
0410 LET WIDTH=78,HEIGHT=21,COLUMN=1,ROW=2,TITLE$=""
0420 CALL "SYC.WA",0,WIDTH,HEIGHT,COLUMN,ROW,TITLE$,NAME$
0500 REM " --- Initializations"
0510 DIM L[12]
0520 LET M1$="##0.0",M1=LEN(M1$)
0560 LET DAYS$="SunMonTueWedThuFriSat"
0580 LET L[1]=31,L[2]=28,L[3]=31,L[4]=30,L[5]=31,L[6]=30
0585 LET L[7]=31,L[8]=31,L[9]=30,L[10]=31,L[11]=30,L[12]=31
0600 REM " --- Load options"
0800 REM " --- Fill in calendar block"
0810 FOR X=10 TO 65 STEP 11
0820 CALL "SYC.WC",1,1,0,HEIGHT-2,X,0,0
0830 NEXT X
0840 CALL "SYC.WC",1,0,WIDTH-2,0,0,1,0
0850 FOR X=4 TO 16 STEP 3
0860 CALL "SYC.WC",1,0,WIDTH-2,0,0,X,0
0870 NEXT X
0880 PRINT @(22,HEIGHT-4),'CL',@(22,HEIGHT-3),'CL',
1000 REM " --- Month/Year?"
1010 LET YEAR$=A0$(6,2),MONTH$=A0$(8,2)
1020 DIM TITLE$(52)
1100 REM " --- Set window title"
1110 LET MONTH=NUM(MONTH$),TITLE$(1)=FNP$(MONTHS$((MONTH-1)*9+1,9))+" "+STR(19
1110:00+FNYY_YEAR(YEAR$)),TITLE$(16)=OPCODE$+" "+OPDESC$(6,20)+"<F1>=Op Code"
1120 PRINT @(23,17),TITLE$,@(23,18),"<Enter>=Change hrs, PgUp/PgDn=Change Mont
1120:hs, <F4>=End",
1400 REM " --- Days"
1410 LET COLUMN=0,ROW=0
1420 FOR X=1 TO 7
1430 PRINT @(COLUMN,ROW),DAYS$((X-1)*3+1,3),
1440 LET COLUMN=COLUMN+11
1450 NEXT X
1500 REM " --- Determine starting day of the week"
1510 DIM DAYS[42],ROW[42],COLUMN[42]
1515 LET FIRST$=YEAR$+MONTH$+"01",X$="",FIRST=0,D=0,DATE=0
1520 CALL "SYC.CB",FIRST$,X$,FIRST
1530 LET FIRST=FIRST+1,L[2]=28,YY=FNYY_YEAR(YEAR$),MM=NUM(MONTH$)
1540 IF FIRST>7 THEN LET FIRST=1
1550 IF MOD(YY,4)=0 THEN LET L[2]=29
1560 FOR X=FIRST TO 42
1570 LET DATE=DATE+1
1580 IF DATE<=L[MM] THEN LET D=D+1,DAYS[X]=D
1590 NEXT X
1900 REM " --- Display dates on calendar"
1910 LET COLUMN=0,ROW=2
1920 FOR X=1 TO 42
1930 IF DAYS[X]>0 THEN PRINT @(COLUMN,ROW),DAYS[X],
1940 LET COLUMN=COLUMN+11
1950 IF COLUMN<77 THEN GOTO 1990
1960 LET COLUMN=0,ROW=ROW+3
1990 NEXT X
3000 REM " --- Display data"
3010 LET COLUMN=0,ROW=3,DATE=1,FIRST_COL=0,FIRST_ROW=0,LAST_LINE$=""
3020 FOR X=1 TO 42
3030 IF DAYS[X]=0 THEN GOTO 3060
3032 IF FIRST_ROW=0 THEN LET FIRST_ROW=ROW,FIRST_COL=COLUMN
3035 LET X$=" "+STR(A[DATE]:M1$)+" Hr",ROW[DATE]=ROW,COLUMN[DATE]=COLUMN
3037 IF A[DATE]=-1 THEN LET X$=" Unsched "
3040 PRINT @(COLUMN,ROW),X$,
3045 IF ROW=18 THEN LET LAST_LINE$=LAST_LINE$+X$+"  "
3050 LET DATE=DATE+1
3060 LET COLUMN=COLUMN+11
3070 IF COLUMN<77 THEN GOTO 3090
3080 LET COLUMN=0,ROW=ROW+3
3090 NEXT X
3095 LET LAST_COL=COLUMN,LAST_ROW=ROW
3400 REM " --- Initialize for modification"
3410 LET V0=1,COLUMN=FIRST_COL,ROW=FIRST_ROW,L_COL=COLUMN,L_ROW=ROW,DAYS=1
3500 REM " --- Modify data"
3505 PRINT @(L_COL,L_ROW)," ",@(L_COL+9)," ",@(COLUMN,ROW),"[",@(COLUMN+9),"]"
3505:,
3510 LET V0$="Sa",V1$="a",V2$="",V3$="",V4$="",V0=1,V1=COLUMN+4,V2=ROW-1,L_COL
3510:=COLUMN,L_ROW=ROW
3520 GOSUB 7000
3525 PRINT @(V1,V2)," ",
3530 IF V3=4 THEN GOTO 4000
3535 IF V3=1 THEN GOSUB 6300; GOTO 1000
3540 IF V3=-1 THEN IF DAYS+1<=L[MM] THEN LET DAYS=DAYS+1,COLUMN=COLUMN[DAYS],R
3540:OW=ROW[DAYS]
3550 IF V3=-2 THEN IF DAYS-1>=1 THEN LET DAYS=DAYS-1,COLUMN=COLUMN[DAYS],ROW=R
3550:OW[DAYS]
3560 IF V3=-3 THEN IF DAYS+7<=L[MM] THEN LET DAYS=DAYS+7,COLUMN=COLUMN[DAYS],R
3560:OW=ROW[DAYS]
3570 IF V3=2 THEN IF DAYS-7>=1 THEN LET DAYS=DAYS-7,COLUMN=COLUMN[DAYS],ROW=RO
3570:W[DAYS]
3580 IF V3=-17 THEN GOSUB 6000; PRINT 'CF'; GOTO 1000
3590 IF V3=-16 THEN GOSUB 6100; PRINT 'CF'; GOTO 1000
3600 IF V3<>0 THEN GOTO 3500
3610 LET V0$="N",V1$="",V2$=STR(A[DAYS]:M1$),V3$=M1$,V4$="",V0=M1,V1=COLUMN+4,
3610:V2=ROW-1
3615 IF A[DAYS]=-1 THEN LET V2$=STR(0:M1$)
3620 GOSUB 7000
3625 IF V<0 OR V>999.5 THEN GOTO 3610
3630 LET A[DAYS]=V
3632 IF ROW=18 THEN LET LAST_LINE$(COLUMN+2,8)=STR(A[DAYS]:M1$)+" Hr"
3635 PRINT @(0,18),LAST_LINE$,
3640 PRINT @(V1,V2),FILL(M1," "),@(COLUMN+1,ROW),STR(A[DAYS]:M1$)+" Hr",
3650 PRINT @(23,18),"<Enter>=Change hrs, PgUp/PgDn=Change Months, <F4>=End",
3660 FOR X=10 TO 21 STEP 11
3670 CALL "SYC.WC",1,1,0,2,X,HEIGHT-4,0
3680 NEXT X
3690 GOTO 3500
4000 REM " --- End
4010 LET A[0]=L[MM]
4020 GOSUB 6200; IF SCHED$="Y" THEN WRITE (WOM04_DEV,KEY=A0$)IOL=WOM04A
4090 GOTO 9900
6000 REM " --- Go Back a Page"
6010 LET A[0]=L[MM]
6020 GOSUB 6200; IF SCHED$="Y" THEN WRITE (WOM04_DEV,KEY=A0$)IOL=WOM04A
6030 LET K$=A0$
6040 LET MNTH=NUM(K$(8,2))-1,YEAR=FNYY_YEAR(K$(6,2))
6045 IF MNTH<1 AND YEAR<1 THEN GOTO 6090
6050 IF MNTH<1 THEN LET MNTH=12,K$(6,2)=FNYEAR_YY21$(YEAR-1)
6060 LET K$(8,2)=STR(MNTH:"00")
6070 FIND (WOM04_DEV,KEY=K$,DOM=6071)IOL=WOM04A; GOTO 6090
6080 LET A0$=K$; DIM A[31]
6085 FOR X=1 TO 31; LET A[X]=-1; NEXT X
6090 RETURN
6100 REM " --- Go Forward a Page"
6110 LET A[0]=L[MM]
6120 GOSUB 6200; IF SCHED$="Y" THEN WRITE (WOM04_DEV,KEY=A0$)IOL=WOM04A
6130 LET K$=A0$
6140 LET MNTH=NUM(K$(8,2))+1,YEAR=FNYY_YEAR(K$(6,2))
6150 IF MNTH>12 THEN LET MNTH=1,K$(6,2)=FNYEAR_YY21$(YEAR+1)
6160 LET K$(8,2)=STR(MNTH:"00")
6170 FIND (WOM04_DEV,KEY=K$,DOM=6171)IOL=WOM04A; GOTO 6190
6180 LET A0$=K$; DIM A[31]
6185 FOR X=1 TO 31; LET A[X]=-1; NEXT X
6190 RETURN
6200 REM " --- Check For Scheduled Day In Month
6210 LET SCHED$="Y"
6220 FOR X=1 TO A[0]
6230 IF A[X]>=0 THEN EXITTO 6290
6240 NEXT X
6250 LET SCHED$="N"
6290 RETURN
6300 REM " --- Get New Op Code"
6310 GOSUB 6200; IF SCHED$="Y" THEN WRITE (WOM04_DEV,KEY=A0$)IOL=WOM04A
6320 CALL "SYC.WA",0,26,4,27,10,"",ONAME$
6330 PRINT @(1,0),'SB',"Enter New Op Code:",'SF',
6340 LET V0$="S",V1$="E",V2$=OPCODE$,V3$="",V4$="<F3>=Lookup",V0=3,V1=20,V2=0
6350 GOSUB 7000
6360 IF V3=3 THEN GOSUB 6500
6370 IF V3=4 THEN GOTO 6480
6380 LET V$=V$+"   ",V$=V$(1,3)
6390 FIND (CODFILE_DEV,KEY=N0$+V$,DOM=6330)IOL=CODFILE
6400 LET OPCODE$=V$,A0$(3,3)=V$,K$=A0$
6410 FIND (WOM04_DEV,KEY=K$,DOM=6411)IOL=WOM04A; GOTO 6480
6420 DIM A[31]
6430 FOR X=1 TO 31; LET A[X]=-1; NEXT X
6480 CALL "SYC.WD",ONAME$
6490 RETURN
6500 REM " --- Op Code Lookup"
6510 LET NUMBER=1
6520 DIM DESC$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6530 LET DESC$[0]="Code",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
6540 LET DESC$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6550 CALL "SYC.LK",CODFILE_DEV,"",NUMBER,"Op Code Lookup",DESC$[ALL],FIELD[ALL
6550:],POSITION[ALL],LENGTH[ALL],5,4,V$
6590 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
