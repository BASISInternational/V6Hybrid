0010 REM "WOS - Employee Maintenance"
0020 REM "Program:  WOM.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOM-01",FILES$[2]="WOM-03",FILES$[3]="SYS-01"
0130 LET FILES$[4]="SYM-06"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET WOM01_DEV=CHANNELS[1],WOM03_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0180 LET SYM06_DEV=CHANNELS[4]
0200 REM " --- IOLists
0210 WOM01A: IOLIST A0$,A1$
0230 WOM03B: IOLIST B0$
0260 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0435 LET PR$=P3$(4,1)
0450 DIM P[1]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(11),A1$(41)
0530 LET K0=1,K9=11
0540 LET A0$(1)=N0$
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,7,0
0720 PRINT 'SB',@(2,3),"Employee No:",@(4,4),"Last Name:",@(33),"First Name:",
0720:@(5,6),"Alt Seq:",
0790 PRINT 'SF',
0800 REM " --- Options"
0810 IF PR$="N" THEN GOTO 0900
0820 DIM MESS$[2]
0825 LET MESS$[0]="Shop Floor System Paramaters Indicate That"
0830 LET MESS$[1]="Payroll Is Installed. Please Maintain Employees"
0840 LET MESS$[2]="In Payroll. Press <Enter> To Continue: "
0850 CALL "SYC.XA",0,MESS$[ALL],2,22,-1,V$,V3
0870 GOTO 9900
0900 REM " --- Init File"
0920 READ (WOM01_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF'
1020 LET V2$="",V2$=KEY(WOM01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KCE",V3$=P1$,V0=9,V1=15,V2=3,V4$="<F3>=Employee Lookup"
1060 GOSUB 7000
1065 IF V3=4 THEN GOTO 4500
1070 IF V3<>3 THEN GOTO 1080
1075 CALL "WOC.LP",V$
1080 IF V$="" THEN GOTO 1000
1090 LET A0$(3)=V$
1200 REM " --- Find Key"
1220 FIND (WOM01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=WOM01A
1240 LET S9$=A1$(31,10)
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1420 LET V4$="Is This A New Record: "
1430 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up"
1510 LET A1$(1)=""
2000 REM " --- Last Name
2020 LET V0$="S",V1$="",V2$=A1$(1,16),V3$="",V0=16,V1=15,V2=4,I0=0
2040 GOSUB 7000
2060 LET A1$(1,16)=V$
2090 GOTO 3900
2200 REM " --- First Name
2220 LET V0$="S",V1$="",V2$=A1$(17,14),V3$="",V0=14,V1=45,V2=4
2240 GOSUB 7000
2260 LET A1$(17,14)=V$
2290 GOTO 3900
2400 REM " --- Alt Sequence
2410 IF POS(" "<>A1$(31,10))=0 THEN LET A1$(31,10)=A1$(1,16)
2420 LET V0$="S",V1$="",V2$=A1$(31,10),V3$="",V0=10,V1=15,V2=6
2440 GOSUB 7000
2460 LET A1$(31,10)=V$
2490 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,3990
3990 LET I0=K0
4000 REM " --- Options"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct 
4020:(Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (WOM01_DEV,KEY=A0$(1,K9))IOL=WOM01A
4120 IF S9$=A1$(31,10) THEN GOTO 4190
4130 LET B0$=N0$+"B"+A1$(31,10)+A0$(3)
4140 WRITE (WOM03_DEV,KEY=B0$)IOL=WOM03B
4160 REMOVE (WOM03_DEV,KEY=N0$+"B"+S9$+A0$(3),DOM=4190)
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (WOM01_DEV,KEY=A0$(1,K9),DOM=4225)
4225 REMOVE (WOM03_DEV,KEY=N0$+"B"+A1$(31,10)+A0$(3),DOM=4290)
4290 GOTO 1000
4500 REM " --- Print?"
4510 LET V4$="Print A List Of All Records"
4530 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4910 LET PROGRAM$="",TOTAL$="Employees",HEADINGS=3,NUMBER=3,WIDTH=80
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Employee",FIELD[0]=1,POSITION[0]=3
4940 LET LENGTH[0]=P[0],TYPE$[0]="M",MASK$[0]=M0$
4945 LET FIELD$[1]="Last Name",FIELD[1]=2,POSITION[1]=1
4950 LET LENGTH[1]=16,TYPE$[1]="S",MASK$[1]=""
4955 LET FIELD$[2]="First Name",FIELD[2]=2,POSITION[2]=17
4960 LET LENGTH[2]=14,TYPE$[2]="S",MASK$[2]=""
4965 LET FIELD$[3]="Alt Sequence",FIELD[3]=2,POSITION[3]=31
4970 LET LENGTH[3]=10,TYPE$[3]="S",MASK$[3]="",RECORD$=""
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing",CHANNEL=WOM01_DEV
4990 RUN "SYR.AA"
5000 REM " --- Display"
5020 PRINT @(15,3),FNF$(A0$(3,P[0]),M0$),@(15,4),A1$(1,16),@(45,4),A1$(17,14),
5020:@(15,6),A1$(31,10)
5090 RETURN
5520 PRINT @(15,3),FNF$(A0$(3,P[0]),M0$),@(15,4),A1$(1,16),@(45,4),A1$(17,14)
6000 REM " --- Subroutines"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
