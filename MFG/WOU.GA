0010 REM "WOU - Work Order Cost Adjustments Update"
0020 REM "Program WOU.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open Files"
0105 LET FILES=11
0107 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0110 LET FILES$[1]="WOE-12",FILES$[2]="WOE-42",FILES$[3]="WOT-01"
0115 LET FILES$[4]="WOT-31",FILES$[5]="WOE-01",FILES$[6]="WOM-10"
0120 LET FILES$[7]="SYS-01",FILES$[8]="WOM-07"
0145 LET OPTIONS$[1]="L",OPTIONS$[2]="L"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE12_DEV=CHANNELS[1],WOE42_DEV=CHANNELS[2],WOT01_DEV=CHANNELS[3]
0165 LET WOT31_DEV=CHANNELS[4],WOE01_DEV=CHANNELS[5],WOM10_DEV=CHANNELS[6]
0170 LET SYS01_DEV=CHANNELS[7],WOM07_DEV=CHANNELS[8]
0200 REM " --- IOLISTs"
0210 WOADJA: IOLIST A0$,A1$,A[ALL]
0220 WOE01A: IOLIST B0$,B1$,B[ALL]
0230 WOE01B: IOLIST C0$,C1$,C[ALL]
0240 WOTRAN: IOLIST W0$,W1$,W[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P9$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 WOM10A: IOLIST *,Y0$(1)
0295 WOM10A2: IOLIST *,Z0$(1)
0300 WOM07A: IOLIST CROSSREF$
0400 REM " --- Parameters"
0405 DIM P[6],G[4],INFO$[20],CROSSREF$(18)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0455 LET G[4]=NUM(G2$(9,2))
0460 CALL "SYC.VA",N2$,INFO$[ALL]
0465 LET GL$=INFO$[9]
0500 REM " --- Init Data"
0510 DIM A[11],W[11],B[11],C[11],D[5]
0520 DIM Y0$(10*G[4],"0"),Z0$(10*G[4],"0")
0560 LET LM$="Labor Cost Adjustment",SM$="Subcontract Cost Adjustment"
0565 LET CM$="Closed Cost Adjustment"
0570 LET FULL$=""
0580 PRECISION 4
0600 REM " --- Additional File Opens"
0625 REM " --- Assign Adjustment and Transaction Channels "
0630 LET WOADJ_DEV=WOE12_DEV
0635 LET WOTRAN_DEV=WOT01_DEV
0640 IF GL$<>"Y" THEN GOTO 0700
0650 LET FILES$[8]="GLM-01",FILES$[9]="GLT-04",FILES$[10]="GLT-05"
0660 CALL "SYC.DA",1,8,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS THEN GOTO 9900
0680 LET GLM01_DEV=CHANNELS[8],GLT04_DEV=CHANNELS[9],GLT05_DEV=CHANNELS[10]
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Options"
0810 LET V4$="Are You Ready To Update The "+N3$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 LET EXIT_CTRL=1
0900 REM 
0910 CALL "SYC.NB","Updating",16,COLUMN
1000 REM " --- Initial File Read "
1010 READ (WOADJ_DEV,KEY=N0$,DOM=1020)
1020 REM " --- Main Read "
1030 LET K$=KEY(WOADJ_DEV,END=4000)
1040 IF POS(N0$=K$)<>1 THEN GOTO 4000
1050 READ (WOADJ_DEV,KEY=K$)IOL=WOADJA
1060 PRINT @(COLUMN,11),A0$(5,7)," ",FNB$(A0$(12)),
1200 REM " --- Find Original Transaction "
1220 EXTRACT (WOTRAN_DEV,KEY=A0$,DOM=3900)IOL=WOTRAN
1300 REM " --- Find Header(S)
1310 FIND (WOE01_DEV,KEY=A0$(1,11))IOL=WOE01A
1320 IF NUM(A1$(1,7))=0 THEN LET A1$(1,7)=A0$(5,7)
1330 IF POS(" "=A1$(8,3))<>0 THEN LET A1$(8,3)=A0$(12,3)
1340 LET C0$=B0$
1350 IF A1$(1,7)=A0$(5,7) THEN GOTO 1400
1360 FIND (WOE01_DEV,KEY=N0$+"  "+A1$(1,7),DOM=1400)IOL=WOE01B
1400 REM " --- Find The Distribution "
1410 FIND (WOM10_DEV,KEY=N0$+"A"+B0$(12,2))IOL=WOM10A
1420 LET Z0$=Y0$
1430 IF C0$(12,2)=B0$(12,2) THEN GOTO 1440
1435 FIND (WOM10_DEV,KEY=N0$+"A"+C0$(12,2),DOM=1440)IOL=WOM10A2
1440 REMOVE (WOADJ_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Back Out Distribution " 
2030 LET WHEN$=A0$(12,3),REF1$="WO "+B0$(5,7),REF2$="Seq "+A0$(16,3)
2040 LET REF3$=FNB$(A1$(8,3)),MEMO$=CM$
2050 LET ACCOUNT$=Y0$(1,G[4]),AMOUNT=-W[2],UNITS=0
2060 GOSUB GLPOST
2090 ON POS(A0$(15,1)="OS")-1 GOTO 2100,2200,3900
2100 REM " --- Labor "    
2120 LET ACCOUNT$=Y0$(G[4]*2+1,G[4]),AMOUNT=(W[0]+W[6])*W[3],MEMO$=LM$
2130 LET AMOUNT1=AMOUNT*1
2140 GOSUB GLPOST
2160 LET ACCOUNT$=Y0$(G[4]*3+1,G[4]),AMOUNT=W[2]-AMOUNT1
2180 GOSUB GLPOST
2190 GOTO 2500
2200 REM " --- Subcontracts "    
2220 LET ACCOUNT$=Y0$(41,10),AMOUNT=W[2],MEMO$=SM$
2240 GOSUB GLPOST
2500 REM " --- Add It Back In "                
2520 LET WHEN$=A1$(8,3),REF1$="WO "+C0$(5,7),REF3$=FNB$(A0$(12,3))
2540 LET ACCOUNT$=Z0$(1,10),AMOUNT=(A[0]+A[3])*(A[1]+A[2]),UNITS=0,MEMO$=CM$
2550 GOSUB GLPOST
2590 ON POS(A0$(15,1)="OS")-1 GOTO 2600,2700
2600 REM " --- Labor "     
2610 LET ACCOUNT$=Z0$(21,10),AMOUNT=((-A[0])-A[3])*A[1],MEMO$=LM$
2620 GOSUB GLPOST
2650 LET ACCOUNT$=Z0$(31,10),AMOUNT=((-A[0])-A[3])*A[2]
2660 GOSUB GLPOST
2690 GOTO 3000
2700 REM " --- Subcontracts "    
2710 LET ACCOUNT$=Z0$(41,10),AMOUNT=(-A[0])*A[1],MEMO$=SM$
2720 GOSUB GLPOST
3000 REM " --- Update The Transactions"
3010 LET NEWWO$=""
3020 IF A0$(5,7)=A1$(1,7) AND A0$(12,3)=A1$(8,3) THEN GOTO 3080
3030 LET NEWWO$="Y"
3040 REMOVE (WOTRAN_DEV,KEY=W0$)
3050 REMOVE (WOM07_DEV,KEY=W0$)
3060 LET W0$(5,7)=A1$(1,7)
3070 LET W0$(12,3)=A1$(8,3)
3080 LET W[0]=A[0],W[1]=A[1]+A[2],W[2]=(W[0]+A[3])*W[1]
3090 IF W0$(15,1)="O" THEN LET W[3]=A[1],W[4]=A[2],W[5]=A[4],W[6]=A[3]
3100 IF NEWWO$="Y" THEN GOTO 3195
3110 WRITE (WOTRAN_DEV,KEY=W0$)IOL=WOTRAN
3120 LET CROSSREF$=W0$
3130 WRITE (WOM07_DEV,KEY=CROSSREF$)IOL=WOM07A
3140 GOTO 3800
3195 REM " --- Add transaction to WOT-01 or WOT-31 "
3200 IF POS(W0$(1,15)=FULL$,15)>0 THEN GOTO 0010
3205 LET K2$=W0$(1,15)+$FF$
3210 READ (WOTRAN_DEV,KEY=K2$,DOM=3215)
3215 LET K9$=KEYP(WOTRAN_DEV,END=3235)
3220 LET V2=1
3225 IF POS(K2$(1,15)=K9$)=1 THEN LET V2=NUM(K9$(16,3))+1
3230 IF V2<1000 THEN LET K2$=K2$(1,15)+STR(V2:"000"); GOTO 3270
3235 REM " --- Any free sequences left between 000 and 999"
3240 LET V2=-1
3245 LET V2=V2+1,K2$=K2$(1,15)+STR(V2:"000")
3250 IF V2>999 THEN GOTO 3295
3255 READ (WOTRAN_DEV,KEY=K2$,DOM=3270)
3260 GOTO 3245
3265 REM " --- Write record using free sequence number"
3270 LET W0$=K2$
3275 WRITE (WOTRAN_DEV,KEY=K2$)IOL=WOTRAN
3280 LET CROSSREF$=K2$
3285 WRITE (WOM07_DEV,KEY=CROSSREF$)IOL=WOM07A
3290 GOTO 3800
3295 LET FULL$=FULL$+K2$(1,15)
3400 REM " --- Summary posting when no free records exist"
3410 LET K2$=K2$(1,15)+"999"
3420 EXTRACT (WOTRAN_DEV,KEY=K2$)IOL=WOTRAN
3430 IF W0$(15,1)="S" THEN LET W1$(20)="Summary Transaction",W[0]=W[0]+A[0],W[
3430:2]=W[2]+A[0]*A[1]; IF W[0]<>0 THEN LET W[1]=W[2]/W[0] ELSE LET W[1]=0
3440 IF W0$(15,1)="O" THEN LET W1$(7,9)="",W0=W[0]+A[0],W2=W[2]+A[0]*(A[1]+A[2
3440:]); IF W0<>0 THEN LET W1=W2/W0,W3=(W[3]*W[0]+A[1]*A[0])/W0,W4=(W[4]*W[0]+
3440:A[2]*A[0])/W0 ELSE LET W1=0,W2=0,W3=0,W4=0
3450 IF W0$(15,1)="O" THEN LET W[0]=W0,W[1]=W1,W[2]=W2,W[3]=W3,W[4]=W4
3460 WRITE (WOTRAN_DEV,KEY=K2$)IOL=WOTRAN
3470 LET CROSSREF$=K2$
3480 WRITE (WOM07_DEV,KEY=CROSSREF$)IOL=WOM07A
3800 REM " --- Update The Last Activity Date
3810 IF A0$(5,7)=A1$(1,7) AND A0$(12,3)=A1$(8,3) THEN GOTO 3900
3820 EXTRACT (WOE01_DEV,KEY=N0$+"  "+A1$(1,7),DOM=3900)IOL=WOE01A
3840 IF POS(" "<>B0$(25,3))=0 THEN LET B0$(25,3)=A1$(8,3)
3860 IF B0$(28,3)<A1$(8,3) THEN LET B0$(28,3)=A1$(8,3)
3880 WRITE (WOE01_DEV,KEY=B0$(1,11))IOL=WOE01A
3900 REM " --- Remove Record"
3920 REMOVE (WOADJ_DEV,KEY=K$,DOM=3990)
3990 GOTO 1020
4000 REM " --- Overlay " 
4010 IF WOADJ_DEV=WOE42_DEV THEN GOTO 4050
4020 LET WOADJ_DEV=WOE42_DEV
4030 LET WOTRAN_DEV=WOT31_DEV
4040 GOTO 1010
4050 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4060 CALL "SYC.BB",STATUS
4100 GOTO 9900
6900 REM " --- Special G/L Posting Routine"
6910 GLPOST:
6920 IF GL$<>"Y" THEN GOTO 6990
6930 PRECISION 2
6940 LET AMOUNT=AMOUNT*1
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,RE
6950:F3$,MEMO$,AMOUNT,UNITS,STATUS
6960 PRECISION 4
6990 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
