0010 REM "WOX - Work Order Cross Reference Rebuild"
0020 REM "Program:  WOX.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION 9
0100 REM " --- Open/Lock Files"
0105 LET FILES=10
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOE-22"
0120 LET FILES$[4]="WOE-32",FILES$[5]="WOM-10",FILES$[6]="SYS-01"
0125 LET FILES$[7]="WOM-10",FILES$[8]="WOM-01"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9000
0160 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOE22_DEV=CHANNELS[3]
0165 LET WOE32_DEV=CHANNELS[4],WOM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0170 LET WOM10_DEV=CHANNELS[7],WOM01_DEV=CHANNELS[8]
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 WOE03X: IOLIST CROSSREF$
0230 WOS01A: IOLIST X$,X$,X$,P9$,X$,M0$,M1$,M2$,M3$
0240 IVS01A: IOLIST X$,X$,P2$,X$
0250 WOREQA: IOLIST W0$,W1$,W[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOM03B: IOLIST CROSSREF$
0280 WOM01A: IOLIST A0$,A1$(1)
0290 WOM10A: IOLIST Y0$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9000)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=6000)IOL=WOS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=6400)IOL=IVS01A
0450 LET P0=NUM(P2$(1,2))
0500 REM " --- Init Data"
0510 DIM A0$(75),A1$(141),A[11],W[13]
0520 LET A0$(1)=N0$
0600 REM " --- Additional File Opens "
0610 LET V4$="Rebuild Work Order Cross References For Firm "+N0$
0620 CALL "SYC.YN",0,V4$,0,V$,V3
0630 IF V$="NO" OR V3=4 THEN GOTO 9900
0640 IF V$<>"YES" THEN GOTO 0600
0650 LET FILES$[9]="WOE-03",FILES$[10]="WOM-03"
0655 LET OPTIONS$[9]="C",OPTIONS$[10]="C"
0660 CALL "SYC.DA",1,9,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS THEN GOTO 9000
0690 LET WOE03_DEV=CHANNELS[9],WOM03_DEV=CHANNELS[10]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.NB","Updating:",13,COLUMN
0900 REM " --- Initial Read "
0910 READ (WOE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Now Do It
1010 LET K$=KEY(WOE01_DEV,END=3000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 3000
1030 READ (WOE01_DEV,END=3000)IOL=WOE01A
1031 DIM CROSSREF$(14)
1035 LET CROSSREF$=N0$+"A"+A0$(12,2)+A0$(3,9)
1040 WRITE (WOE03_DEV,KEY=CROSSREF$)IOL=WOE03X
1050 PRINT @(COLUMN,11),A0$(5,7)+"      "
1200 REM " --- Sort By Customer
1210 IF P9$(2,1)<>"Y" THEN GOTO 1300
1220 IF NUM(A0$(34,6),ERR=1300)=0 THEN GOTO 1300
1221 DIM CROSSREF$(28)
1225 LET CROSSREF$=N0$+"B"+A0$(34,16)+A0$(3,9)
1230 WRITE (WOE03_DEV,KEY=CROSSREF$)IOL=WOE03X
1300 REM " --- Sort By Item
1310 DIM Y0$(30),CROSSREF$(34)
1320 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=1330)IOL=WOM10A
1330 IF Y0$(26,1)=A0$(14,1) THEN GOTO 1390
1340 DIM MESS$[1]
1350 LET MESS$[0]="WO Category Mismatch For Work Order "+A0$(5,7)+". <Enter> T
1350:o Correct."
1360 CALL "SYC.XA",1,MESS$[ALL],0,-1,22,V$,V3
1370 LET A0$(14,1)=Y0$(26,1)
1380 WRITE (WOE01_DEV,KEY=A0$(1,11))IOL=WOE01A
1390 IF Y0$(26,1)<>"I" THEN GOTO 2000
1395 LET CROSSREF$=N0$+"C"+A0$(54,22)+A0$(3,9)
1400 WRITE (WOE03_DEV,KEY=CROSSREF$)IOL=WOE03X
2000 REM " --- Now Do Details
2005 LET WOREQ_DEV=WOE02_DEV
2010 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=2020)
2020 LET K2$=KEY(WOREQ_DEV,END=2540)
2030 IF POS(A0$(1,11)=K2$)<>1 THEN GOTO 2540
2040 READ (WOREQ_DEV,KEY=K2$)IOL=WOREQA
2050 IF W1$(91,1)<>"S" THEN GOTO 2020
2060 PRINT @(COLUMN,11),W0$(5,7)," ",W0$(12,1)," ",W0$(13,3),
2070 ON POS(W0$(12,1)="ABC")-1 GOSUB 2300,2400,2500
2080 WRITE (WOE03_DEV,KEY=CROSSREF$)IOL=WOE03X
2090 GOTO 2020
2300 REM " --- Operations Here
2310 DIM CROSSREF$(21)
2320 LET CROSSREF$=N0$+"D  "+W1$(1,6)+W0$(5,7)+W0$(13,3)
2330 RETURN
2400 REM " --- Materials
2410 DIM CROSSREF$(37)
2420 LET CROSSREF$=N0$+"E  "+W1$(25,22)+W0$(5,7)+W0$(13,3)
2430 RETURN
2500 REM " --- Subcontracts
2510 DIM CROSSREF$(21)
2520 LET CROSSREF$=N0$+"F  "+W1$(7,6)+W0$(5,7)+W0$(13,3)
2530 RETURN
2540 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 2010
2550 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 2010
2560 GOTO 1000
3000 REM " --- Employee X-ref (If Required)
3010 DIM A1$(40),CROSSREF$(22)
3015 READ (WOM01_DEV,KEY=N0$,DOM=3020)
3020 READ (WOM01_DEV,END=3070)IOL=WOM01A
3022 IF A1$(1,2)<>N0$ THEN GOTO 9900
3025 LET CROSSREF$=A0$(1,2)+"B"+A1$(31,10)+A0$(3,9)
3030 WRITE (WOM03_DEV,KEY=CROSSREF$)IOL=WOM03B
3040 PRINT @(COLUMN,11),A0$(3,9)+"    "
3050 GOTO 3020
3070 REM " --- End Of File
3080 GOTO 9900
6000 REM " --- Setup Warning for No WO parameters "
6010 DIM MESS$[1]
6020 LET MESS$[0]="Missing WO Parameter Record For Firm "+N0$+"."
6030 LET MESS$[1]="Rebuild For This Firm Cannot Proceed. <Enter>."
6040 GOTO 6600
6400 REM " --- Setup Warning for No IV parameters "
6410 DIM MESS$[1]
6420 LET MESS$[0]="Missing IV Parameter Record For Firm "+N0$+"."
6430 LET MESS$[1]="Rebuild For This Firm Cannot Proceed. <Enter>."
6600 REM " --- Display Warning"
6610 CALL "SYC.XA",2,MESS$[ALL],1,22,-1,V$,V3
6620 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
