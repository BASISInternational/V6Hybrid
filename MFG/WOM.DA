0010 REM "WOS - OP Code Scheduling Calendar Create"
0020 REM "Program WOM.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOM-04",FILES$[2]="SYS-01",FILES$[3]="BMM-08"
0130 LET FILES$[4]="WOM-02"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET WOM04_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 WOM04A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 WOS01A: IOLIST X$,X$,X$,P9$,X$,X$,X$,X$,X$
0230 CODFILEA: IOLIST X$
0240 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="WO"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0500 REM " --- Init Data"
0510 DIM A0$(9),A1$(10),A2$(10),A[31]
0520 FOR X=1 TO 31
0530 LET A[X]=-1
0540 NEXT X
0550 LET R9$="SunMonTueWedThuFriSat"
0560 LET K9=9
0600 REM " --- Open Additional Files
0605 REM " --- Check For Bill Of Materials "
0610 IF P9$(1,1)<>"Y" THEN GOTO 0650
0615 REM " --- Use Bill Of Materials Code File "
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0625 IF STATUS THEN GOTO 9900
0630 LET CODFILE_DEV=CHANNELS[3]
0640 GOTO 0700
0645 REM " --- Use Work Order Code File "
0650 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET CODFILE_DEV=CHANNELS[4]
0700 REM " --- Background"
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(23,3),"Operation Code:",@(9,5),"Last Day Currently Scheduled
0740::",@(18,7),"1st Day To Schedule:",@(22,9),"Number Of Weeks:",@(22,11),"Nu
0740:mber Of Hours:",@(25,13),"Days To Omit:",
0790 PRINT 'SF',
0900 REM " --- Init File"
1000 REM " --- Operation Code"
1010 PRINT 'CF'
1020 LET V0$="S",V1$="KEC",V2$=OPCODE$,V3$="",V4$="Enter Operation Code To Sch
1020:edule, <F3>=Lookup",V0=3,V1=39,V2=3,I0=0
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1050 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
1060 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description",FIELD[0]=1,FIELD
1060:[1]=1,POSITION[0]=3,POSITION[1]=6,LENGTH[0]=3,LENGTH[1]=20
1070 IF V3=3 THEN CALL "SYC.LK",CODFILE_DEV,"",1,"Operation Code",DESCRIPTION$
1070:[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],50,7,V$
1080 LET V$=V$+"   ",V$=V$(1,3)
1090 FIND (CODFILE_DEV,KEY=N0$+V$,DOM=1000)IOL=CODFILEA
1100 PRINT @(V1,V2),V$," ",X$(6,20),'CL'
1110 LET OPCODE$=V$
1120 GOSUB 6200
1130 GOTO 3900
1300 REM " --- Number of Weeks"
1310 LET V0$="N",V1$="KEC",V2$=STR(R0),V3$="##0",V4$="Enter Number Of Weeks To
1310: Schedule Ahead",V0=3,V1=39,V2=9
1320 GOSUB 7000
1330 IF V3=4 THEN GOTO 9900
1340 IF V<=0 THEN GOTO 1300
1350 LET R0=V
1360 GOTO 3900
2000 REM " --- Days To Omit"
2010 LET V0$="S",V1$="KEC",V2$=R1$,V3$="",V4$="Enter Days To Omit (1=Sun, 2=Mo
2010:n, ... 7=Sat)",V0=7,V1=39,V2=13,R2$="",R3$=""
2020 GOSUB 7000
2030 IF V3=4 THEN GOTO 9900
2040 IF LEN(V$)=0 OR POS(" "<>V$)=0 THEN LET V$=""; GOTO 2140
2050 FOR X=1 TO LEN(V$)
2060 IF V$(X,1)<"1" OR V$(X,1)>"7" THEN EXITTO 2000
2070 LET R2$=R2$+R9$((NUM(V$(X,1))-1)*3+1,3)+", "
2080 LET R3$=R3$+R9$((NUM(V$(X,1))-1)*3+1,3)
2090 NEXT X
2100 FOR X=1 TO 7
2110 IF POS(STR(X:"0")=V$,1,2)>0 THEN EXITTO 2000
2120 NEXT X
2130 LET R2$=R2$(1,LEN(R2$)-2)
2140 LET R1$=V$
2150 PRINT @(V1,V2),'CL',V$," ",R2$,
2160 GOTO 3900
2300 REM " --- Number of Default Hours"
2310 LET V0$="N",V1$="KEC",V2$=STR(R1),V3$="#0.0",V4$="Enter Number Of Hours P
2310:er Day",V0=4,V1=39,V2=11
2320 GOSUB 7000
2330 IF V3=4 THEN GOTO 9900
2340 IF V<0 THEN GOTO 2300
2350 LET R1=V
2360 GOTO 3900
2500 REM " --- 1st Day to Schedule"
2510 IF LEN(R0$)=0 THEN LET R0$=F0$(7,3)
2520 LET V0$="D",V1$="KEC",V2$=R0$,V3$="",V4$="Enter 1st Date To Schedule",V0=
2520:8,V1=39,V2=7
2530 GOSUB 7000
2540 IF V3=4 THEN GOTO 9900
2550 LET R0$=V$
2560 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,2500,1300,2300,2000,3990
3990 LET I0=0
4000 REM " --- Options"
4020 LET V0$="Y",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct
4020:? (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Write"
4105 CALL "SYC.NB","Creating:",7,COLUMN
4110 LET X$=FNYY_YY21$(FNM$(FNC$(R0$))),OLD_DATE$=X$
4120 GOSUB 6100
4210 FOR WEEK_COUNT=1 TO R0
4220 FOR DAY_COUNT=1 TO 7
4230 CALL "SYC.CB",X$,X0$,X0
4240 IF X$(1,4)<>OLD_DATE$(1,4) THEN GOSUB 6000
4250 IF POS(X0$=R3$,3)>0 THEN LET A[NUM(X$(5,2))]=0; GOTO 4270
4260 LET A[NUM(X$(5,2))]=R1
4270 LET X9$=X$
4280 PRINT @(COLUMN,11),FNB6$(X$),
4285 LET X0$=""
4290 CALL "SYC.CA",X$,X0$,1
4295 LET X$=X0$
4300 NEXT DAY_COUNT
4310 NEXT WEEK_COUNT
4320 GOSUB 6000
4390 GOTO 9900
6000 REM " --- Write Record"
6010 LET A0$=N0$+OPCODE$+OLD_DATE$(1,2)+OLD_DATE$(3,2)
6020 LET A[0]=32
6030 LET A[0]=A[0]-1
6040 LET X=JUL(1900+FNYY_YEAR(X9$(1,2)),NUM(X9$(3,2)),A[0],ERR=6030)
6050 WRITE (WOM04_DEV,KEY=A0$(1,K9))IOL=WOM04A
6060 DIM A[31]
6065 FOR X9=1 TO 31
6066 LET A[X9]=-1
6067 NEXT X9
6070 LET OLD_DATE$=X$
6080 GOSUB 6100
6090 RETURN
6100 REM " --- Get existing record"
6110 LET A0$=N0$+OPCODE$+X$(1,2)+X$(3,2)
6120 DIM A[31]
6125 FOR X9=1 TO 31; LET A[X9]=-1; NEXT X9
6130 READ (WOM04_DEV,KEY=A0$(1,K9),DOM=6190)IOL=WOM04A
6190 RETURN
6200 REM " --- Find Last Date For This Op Code"
6210 LET LAST_DATE$="   "
6220 READ (WOM04_DEV,KEY=N0$+OPCODE$,DOM=6230)
6230 LET K$=KEY(WOM04_DEV,END=6280)
6235 IF POS(N0$+OPCODE$=K$)<>1 THEN GOTO 6280
6240 READ (WOM04_DEV,KEY=K$)IOL=WOM04A
6255 FOR X=A[0] TO 1 STEP -1
6260 IF A[X]>=0 THEN LET X$=STR(X:"00"); EXITTO 6270
6265 NEXT X
6270 LET LAST_DATE$=FND$(A0$(8,2)+X$+A0$(6,2))
6275 GOTO 6230
6280 PRINT @(39,5),'SB',FNB$(LAST_DATE$),'SF',
6290 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
