0010 REM "WOC - Work Order SpeedSearch (Work Order Cost Summary)"
0020 REM "Program WOC.QD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0105 LET FILES=12
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOE-22"
0120 LET FILES$[4]="WOE-32",FILES$[5]="WOT-01",FILES$[6]="WOT-21"
0125 LET FILES$[7]="WOT-31",FILES$[8]="WOM-05",FILES$[9]="WOM-06"
0130 LET FILES$[10]="IVM-01",FILES$[11]="WOM-02"
0140 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOE22_DEV=CHANNELS[3]
0170 LET WOE32_DEV=CHANNELS[4],WOT01_DEV=CHANNELS[5],WOT21_DEV=CHANNELS[6]
0180 LET WOT31_DEV=CHANNELS[7],WOM05_DEV=CHANNELS[8],WOM06_DEV=CHANNELS[9]
0190 LET IVM01_DEV=CHANNELS[10]
0200 REM " --- IOLIST's"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 WOE02A: IOLIST B0$(1),B1$(1),B[ALL]
0230 WOT01A: IOLIST C0$(1),C1$(1),C[ALL]
0240 WOM02A: IOLIST D0$(1),D[ALL]
0250 IVM01A: IOLIST H0$(1),H1$(1)
0255 APS01A: IOLIST X$,P1$,P2$,X$,X$,M0$,X$,X$,X$
0260 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0270 WOS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0280 APM01A: IOLIST L0$(1),L1$(1)
0320 WOE22A: IOLIST B0$(1),B1$(1),B[ALL]
0330 WOT21A: IOLIST C0$(1),C1$(1),C[ALL]
0340 WOE32A: IOLIST B0$(1),B1$(1),B[ALL]
0350 WOT31A: IOLIST C0$(1),C1$(1),C[ALL]
0400 REM " --- Parameters"
0405 DIM I[5],P[1],X$(128),INFO$[20]
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8)),SNAME$=FNP$(X$(19,8))
0420 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0425 LET SHEIGHT=NUM(X$(45,2)),MAXOPT=NUM(X$(57,2)),PO$="N"
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0435 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0440 LET PAGES=INT((DSZ-5000)/4000),SYS01_DEV=NUM(STBL("!SYS01",ERR=9900))
0445 IF PAGES<3 THEN GOTO 9900
0450 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0455 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0460 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0465 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0470 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9900)IOL=WOS01A
0473 LET BM$=I3$(1,1),PO$=I3$(3,1),MAXROW=HEIGHT-5
0475 IF PO$="N" THEN GOTO 0500
0477 CALL "SYC.VA","PO",INFO$[ALL]
0480 LET PO$=INFO$[20]
0485 IF PO$="N" THEN GOTO 0500
0490 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9900)IOL=APS01A
0495 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(75),A1$(138),A[11],B0$(15),B1$(151),B[13],D0$(32),D[2]
0515 DIM C0$(18),C1$(28),C[11],H0$(22),H1$(60),MESSAGE$[1],TOTAL[1]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),OPTLIST$[2,MAXOPT,6]
0525 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2]
0530 DIM G1$(WIDTH-2),G2$(WIDTH-2),G3$(WIDTH-2),L0$(8),L1$(30)
0550 LET PAGE=1,MAXOPS=100,MAXSUB=100,WO$=OPTION$[3]
0555 IF LEN(WO$)>6 THEN LET WO$=WO$(LEN(WO$)-6)
0560 LET A0$(1,2)=N0$,A0$(5)=WO$,WARNED=0,D_STATUS=0
0565 LET PGM$=PGM(-2),M4$="-##0.00%",M4=LEN(M4$),NF$="(Not On File)"
0570 LET NUMOPS=0,NUMSUB=0,LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
0580 CALL "SYC.RM",M1$,M1,8,15
0590 CALL "SYC.RM",M2$,M2,10,12
0600 REM " --- Other File Opens"
0610 IF BM$="Y" THEN LET FILES$[11]="BMM-08"
0620 IF PO$="Y" THEN LET FILES$[12]="APM-01"
0625 CALL "SYC.DA",1,11,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATU
0625:S
0630 IF STATUS THEN GOTO 9900
0640 LET WOM02_DEV=CHANNELS[11],APM01_DEV=CHANNELS[12]
0650 REM " --- Option Window"
0660 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TIT
0660:LE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET W3=WIDTH-2-M4,W2=W3-M2,W1=W2-M2,W=WIDTH-1-M2-15
0730 LET HEADING$(2)="Hours Or Units",HEADING$(W1+M2-9)="Standard"
0740 LET HEADING$(W2+M2-7)="Actual",HEADING$(WIDTH-10)="Variance"
0750 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0760 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0770 GOSUB 6000
0800 REM " --- Read work order and finished good"
0810 FIND (WOE01_DEV,KEY=A0$(1,11),DOM=0815)IOL=WOE01A
0815 LET H0$(1)=N0$+A0$(56),H1$(1)=NF$
0820 FIND (IVM01_DEV,KEY=H0$,DOM=0825)IOL=IVM01A
0825 REM " --- Display top window"
0830 DIM G$((WIDTH-2)+LEN('SF')+LEN('SB'))
0835 CALL "SYC.WB",TNAME$,0
0840 LET X$='SB'+" WO #: "+'SF'+A0$(5,7)
0845 IF FNP$(A0$(25,3))<>"" THEN GOTO 0860
0850 LET X$=X$+" (Not Started"
0855 GOTO 0870
0860 LET X$=X$+" (Started "+FNB$(A0$(25,3))
0865 IF FNB$(A0$(28,3))<>"" THEN LET X$=X$+" Last Activity "+FNB$(A0$(28,3))
0870 LET G$(1)=X$+")",X1$=A0$(5,7),X2$=SUBPARAM$
0880 CALL "SYC.QB",G$,X1$,X2$
0885 LET G$(1)='SB'+" Item: "+'SF'+H0$(3,I[0])+" "+FNITEM$(H1$,I[3],I[4],I[5])
0890 LET X1$=H0$(3,I[0]),X2$=PARAM$
0895 CALL "SYC.QB",G$,X1$,X2$
0900 REM " --- Initializations for accumulating standards"
0910 DIM STDOPS[MAXOPS,1],ACTOPS[MAXOPS,1],STDSUB[MAXSUBS,1],ACTSUB[MAXSUBS,1]
0920 LET OPERATIONS$="",OPSREQ$=""
0950 CALL "SYC.WA",0,30,3,26,10,"",GNAME$
0960 PRINT @(1,0),"Generating Cost Summary...",
0990 READ (WOE02_DEV,KEY=A0$(1,11),DOM=1000)
1000 REM " --- Accumulate operation standards"
1010 LET K$=KEY(WOE02_DEV,END=1200)
1020 IF POS(A0$(1,11)=K$)<>1 THEN GOTO 1200
1030 READ (WOE02_DEV)IOL=WOE02A
1040 IF B1$(91,1)="M" THEN GOTO 1000
1050 LET X=POS(B0$(13,3)=OPERATIONS$,6)
1060 IF X>0 THEN GOTO 1150
1070 IF INT(LEN(OPERATIONS$)/6)>=MAXOPS THEN GOTO 1100
1080 LET OPERATIONS$=OPERATIONS$+B0$(13,3)+B1$(1,3),OPSREQ$=OPSREQ$+B1$(4,3)
1090 GOTO 1050
1100 REM " --- Exceeded Number of Operations Parameter"
1110 IF WARNED THEN GOTO 1190
1120 LET MESSAGE$[0]="Work Order Number Of Operations Parameter Needs"
1130 LET MESSAGE$[1]="To Be Expanded (<Enter>=Continue)",WARNED=1
1140 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1145 GOTO 1190
1150 REM " --- Store standards for this operation"
1160 LET X=INT(X/6),STDOPS[X,0]=STDOPS[X,0]+B[2],STDOPS[X,1]=STDOPS[X,1]+B[3]
1190 GOTO 1000
1200 REM " --- Done with operation standards"
1290 READ (WOT01_DEV,KEY=A0$(1,11),DOM=1300)
1300 REM " --- Accumulate operation actuals"
1310 LET K$=KEY(WOT01_DEV,END=1400)
1320 IF POS(A0$(1,11)=K$)<>1 THEN GOTO 1400
1330 READ (WOT01_DEV)IOL=WOT01A
1340 LET X=POS(C1$(1,3)=OPERATIONS$,6)
1350 IF X=0 THEN GOTO 1300
1360 LET X=INT(X/6),ACTOPS[X,0]=ACTOPS[X,0]+C[0],ACTOPS[X,1]=ACTOPS[X,1]+C[2]
1390 GOTO 1300
1400 REM " --- Initializations for accumulating material standards"
1450 LET STDUNITS=0,STDCOST=0,ACTUNITS=0,ACTCOST=0,COMPONENTS=0
1490 READ (WOE22_DEV,KEY=A0$(1,11),DOM=1500)
1500 REM " --- Accumulate material standards"
1510 LET K$=KEY(WOE22_DEV,END=1700)
1520 IF POS(A0$(1,11)=K$)<>1 THEN GOTO 1700
1530 READ (WOE22_DEV)IOL=WOE22A
1540 IF B1$(91,1)="M" THEN GOTO 1500
1550 LET STDUNITS=STDUNITS+B[2],STDCOST=STDCOST+B[3]
1560 LET COMPONENTS=COMPONENTS+1
1590 GOTO 1500
1700 REM " --- Done with material standards"
1790 READ (WOT21_DEV,KEY=A0$(1,11),DOM=1800)
1800 REM " --- Accumulate material actuals"
1810 LET K$=KEY(WOT21_DEV,END=1900)
1820 IF POS(A0$(1,11)=K$)<>1 THEN GOTO 1900
1830 READ (WOT21_DEV)IOL=WOT21A
1840 LET ACTUNITS=ACTUNITS+C[0],ACTCOST=ACTCOST+C[2]
1890 GOTO 1800
1900 REM " --- Initializations for accumulating subcontract standards"
1910 DIM STDSUB[MAXSUB,1],ACTSUB[MAXSUB,1]
1920 LET SUBCONTRACTS$="",SUBREQ$="",VENDOR$=""
1980 IF PO$="N" THEN GOTO 2400
1990 READ (WOE32_DEV,KEY=A0$(1,11),DOM=2000)
2000 REM " --- Accumulate subcontract standards"
2010 LET K$=KEY(WOE32_DEV,END=2200)
2020 IF POS(A0$(1,11)=K$)<>1 THEN GOTO 2200
2030 READ (WOE32_DEV)IOL=WOE32A
2040 IF B1$(91,1)="M" THEN GOTO 2000
2050 LET X=POS(B0$(13,3)=SUBCONTRACTS$,3)
2060 IF X>0 THEN GOTO 2100
2070 IF INT(LEN(SUBCONTRACTS$)/3)>=MAXSUB THEN GOTO 2000
2080 LET SUBCONTRACTS$=SUBCONTRACTS$+B0$(13,3)
2085 LET VENDOR$=VENDOR$+B1$(7,6),SUBREQ$=SUBREQ$+B1$(4,3)
2090 GOTO 2050
2100 REM " --- Store standards for this subcontract"
2110 LET X=INT(X/3),STDSUB[X,0]=STDSUB[X,0]+B[2],STDSUB[X,1]=STDSUB[X,1]+B[3]
2190 GOTO 2000
2200 REM " --- Done with subcontract standards"
2290 READ (WOT31_DEV,KEY=A0$(1,11),DOM=2300)
2300 REM " --- Accumulate subcontract actuals"
2310 LET K$=KEY(WOT31_DEV,END=2400)
2320 IF POS(A0$(1,11)=K$)<>1 THEN GOTO 2400
2330 READ (WOT31_DEV)IOL=WOT31A
2340 LET X=POS(C1$(1,3)=SUBCONTRACTS$,3)
2350 IF X=0 THEN GOTO 2300
2360 LET X=INT(X/6),ACTSUB[X,0]=ACTSUB[X,0]+C[0],ACTSUB[X,1]=ACTSUB[X,1]+C[2]
2390 GOTO 2300
2400 REM " --- Accumulate operation totals"
2410 LET NUMOPS=INT(LEN(OPERATIONS$)/6)
2420 IF NUMOPS=0 THEN GOTO 2500
2430 FOR X=0 TO NUMOPS-1
2440 FOR Y=0 TO 1
2450 LET STDOPS[NUMOPS,Y]=STDOPS[NUMOPS,Y]+STDOPS[X,Y]
2460 LET ACTOPS[NUMOPS,Y]=ACTOPS[NUMOPS,Y]+ACTOPS[X,Y]
2470 NEXT Y
2480 NEXT X
2500 REM " --- Accumulate subcontract totals"
2510 LET NUMSUB=INT(LEN(SUBCONTRACTS$)/3)
2520 IF NUMSUB=0 THEN GOTO 2600
2530 FOR X=0 TO NUMSUB-1
2540 FOR Y=0 TO 1
2550 LET STDSUB[NUMSUB,Y]=STDSUB[NUMSUB,Y]+STDSUB[X,Y]
2560 LET ACTSUB[NUMSUB,Y]=ACTOPS[NUMSUB,Y]+ACTSUB[X,Y]
2570 NEXT Y
2580 NEXT X
2600 REM " --- Display initializations"
2610 CALL "SYC.WD",GNAME$
2620 CALL "SYC.WB",NAME$,0
2700 REM " --- Top of operations loop"
2710 IF NUMOPS=0 THEN GOTO 3000
2720 FOR X=0 TO NUMOPS
2730 IF X=NUMOPS THEN GOTO 2800
2740 LET D0$(1,5)=N0$+OPERATIONS$(X*6+4,3),D0$(6)=NF$
2750 FIND (WOM02_DEV,KEY=D0$(1,5),DOM=2800)IOL=WOM02A
2800 REM " --- Display page?"
2810 GOSUB 5200
2820 IF DONE THEN GOTO 4000
2830 IF ANSWER$<>"" THEN GOTO 3900
2850 REM " --- Middle window record"
2860 LET G$(1)=" O "+D0$(3,3)+" "+D0$(6,20)
2870 LET STANDARD=STDOPS[X,0],ACTUAL=ACTOPS[X,0]
2880 IF X=NUMOPS THEN LET G$(1)=" O     Total Hours"
2890 GOSUB 5400
2900 REM " --- Bottom window record"
2910 LET G1$(1)="",G2$(1)="",G3$(1)=""
2920 IF X<NUMOPS THEN LET G1$(1)="{Required}"+FNB$(OPSREQ$(X*3+1,3))
2930 LET STANDARD=STDOPS[X,1],ACTUAL=ACTOPS[X,1]
2940 IF X=NUMOPS THEN LET TOTAL[0]=TOTAL[0]+STANDARD,TOTAL[1]=TOTAL[1]+ACTUAL
2950 GOSUB 5600
2990 NEXT X
3000 REM " --- Display page?"
3010 GOSUB 5200
3020 IF DONE THEN GOTO 4000
3030 IF ANSWER$<>"" THEN GOTO 3900
3100 REM " --- Middle window record"
3110 LET G$(1)=" M Total Materials",STANDARD=STDUNITS,ACTUAL=ACTUNITS
3120 GOSUB 5400
3150 REM " --- Bottom window record"
3160 LET G1$(1)="{Number Of Components}"+STR(COMPONENTS),G2$(1)="",G3$(1)=""
3170 LET STANDARD=STDCOST,ACTUAL=ACTCOST
3180 LET TOTAL[0]=TOTAL[0]+STANDARD,TOTAL[1]=TOTAL[1]+ACTUAL
3190 GOSUB 5600
3200 REM " --- Top of subcontract loop"
3210 IF NUMSUB=0 THEN GOTO 3500
3220 FOR X=0 TO NUMSUB
3230 IF X=NUMSUB THEN GOTO 3300
3240 LET B0$(1)=A0$(1,11)+"C"+SUBCONTRACTS$(X*3+1,3),B1$(13)=NF$
3250 FIND (WOE32_DEV,KEY=B0$,DOM=3300)IOL=WOE32A
3300 REM " --- Display page?"
3310 GOSUB 5200
3320 IF DONE THEN GOTO 4000
3330 IF ANSWER$<>"" THEN GOTO 3900
3350 REM " --- Middle window record"
3360 LET G$(1)=" S "+B1$(13,30),STANDARD=STDSUB[X,0],ACTUAL=ACTSUB[X,0]
3370 IF X=NUMSUB THEN LET G$(1)=" S Total Units"
3390 GOSUB 5400
3400 REM " --- Bottom window record"
3410 LET G1$(1)="",G2$(1)="",G3$(1)="",STANDARD=STDSUB[X,1],ACTUAL=ACTSUB[X,1]
3420 IF X=NUMSUB THEN GOTO 3470
3430 LET L0$(1)=N0$+VENDOR$(X*6+1,6),L1$(1)=NF$
3435 FIND (APM01_DEV,KEY=L0$,DOM=3440)IOL=APM01A
3440 LET G1$(1)="{Vendor}"+FNF$(L0$(3,P[0]),M0$)+" "+L1$
3450 LET G2$(1)=" {Req'd}"+FNB$(SUBREQ$(X*3+1,3))
3460 GOTO 3480
3470 LET TOTAL[0]=TOTAL[0]+STANDARD,TOTAL[1]=TOTAL[1]+ACTUAL
3480 GOSUB 5600
3490 NEXT X
3500 REM " --- Display page?"
3510 GOSUB 5200
3520 IF DONE THEN GOTO 4000
3530 IF ANSWER$<>"" THEN GOTO 3900
3600 REM " --- Middle window record"
3610 LET G$(1)="   Grand Total",LLIST$[PAGE,X1,1]=G$
3650 REM " --- Bottom window record"
3660 LET G1$(1)="",G2$(1)="",G3$(1)="",STANDARD=TOTAL[0],ACTUAL=TOTAL[1]
3670 GOSUB 5600
3700 REM " --- No more records"
3710 IF X0>0 THEN GOTO 3800
3720 LET MESSAGE$[0]="No Operation Or Material Information Found (<Enter>=Cont
3720:inue)"
3730 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
3740 LET SELECTION$="PRIOR"
3790 GOTO 3900
3800 REM " --- Display last page"
3810 LET ACTION$="M",CHOICE1$=H0$(3)+A0$(5,7),CHOICE2$=""
3820 GOSUB 5000
3830 IF DONE THEN GOTO 4000
3900 REM " --- Process option window"
3910 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3920 LET ACTION$="O",CHOICE1$=H0$(3)+A0$(5,7),CHOICE2$=""
3930 GOSUB 5000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],W
5050:ININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR
5080:"
5090 RETURN
5200 REM " --- Display page?"
5210 IF L<MAXROW THEN GOTO 5290
5220 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
5230 GOSUB 5000
5240 LET L=1,X1=1
5290 RETURN
5400 REM " --- Middle window record"
5410 LET G$(W1)=STR(STANDARD:M2$),G$(W2)=STR(ACTUAL:M2$)
5420 LET G$(W3)=STR(FNVARIANCE(STANDARD,ACTUAL):M4$)
5430 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=H0$(3)+A0$(5,7)
5490 RETURN
5600 REM " --- Bottom window record"
5610 LET G1$(W)="{Standard Cost}"+STR(STANDARD:M2$)
5620 LET G2$(W+2)="{Actual Cost}"+STR(ACTUAL:M2$)
5630 LET G3$(W+5)="{Variance}",MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
5640 LET G3$(WIDTH-1-M4)=STR(FNVARIANCE(STANDARD,ACTUAL):M4$)
5650 LET MORE$[PAGE,X1,1]=G1$,MORE$[PAGE,X1,2]=G2$,MORE$[PAGE,X1,3]=G3$
5660 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
5690 RETURN
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CS',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),
8090:32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8300 REM " --- Calculate variance percentage"
8310 DEF FNVARIANCE(QSTANDARD,QACTUAL)
8320 LET QVARIANCE=QACTUAL-QSTANDARD,QPERCENT=100
8330 IF QSTANDARD<>0 THEN LET QPERCENT=(QVARIANCE*100)/QSTANDARD
8340 LET QPERCENT=MIN(999.99,MAX(QPERCENT,-999.99))
8380 RETURN QPERCENT
8390 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9940 IF GNAME$<>"" THEN CALL "SYC.WD",GNAME$
9950 EXIT
9999 END
