0010 REM "WOR - WORK ORDER DETAIL REPORT"
0020 REM "Program:  WOR.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOE-03",FILES$[2]="WOE-01",FILES$[3]="IVM-01"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="SYS-01"
0135 LET FILES$[7]="WOM-10",FILES$[8]="WOE-02",FILES$[9]="ARM-01"
0140 LET FILES$[10]="ARM-04"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0165 IF STATUS THEN GOTO 9900
0170 LET WOE03_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0175 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0180 LET WOM10_DEV=CHANNELS[7],WOE02_DEV=CHANNELS[8]
0200 REM " --- IOLists
0210 ARS01A: IOLIST *,*,C0$,*,*,C1$
0215 IVM01A: IOLIST *,X0$
0220 APS01A: IOLIST *,*,X$,*,*,P7$
0225 ARM01A: IOLIST *,X0$
0230 PRS01A: IOLIST *,*,X$,*,*,P6$
0235 WOM10A: IOLIST X0$
0240 GLS01A: IOLIST *,*,G2$,G3$
0250 BMS01A: IOLIST *,*,M4$,M5$,M4,M5
0260 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,X$,P2$,I3$
0290 IVM10C: IOLIST X0$
0400 REM " --- Parameters"
0405 LET BM$="",AR$="",PO$="",PR$=""
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0432 LET BM$=P3$(1,1),AR$=P3$(2,1),PO$=P3$(3,1),PR$=P3$(4,1)
0435 LET P6$=P1$,P6=NUM(P2$(1,2))
0437 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0440 IF BM$<>"Y" THEN GOTO 0450
0445 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=0450)IOL=BMS01A
0450 IF AR$<>"Y" THEN GOTO 0460
0455 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0460)IOL=ARS01A
0457 LET C0=NUM(C0$(1,2)),C1=LEN(C1$),P8=C0,P8$=C1$
0460 IF PO$<>"Y" THEN GOTO 0470
0465 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=0470)IOL=APS01A
0467 LET P7=NUM(X$(1,2))
0470 IF PR$<>"Y" THEN GOTO 0480
0475 FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=0480)IOL=PRS01A
0477 LET P6=NUM(X$(1,2))
0480 IF PR$<>"Y" AND P3$(12,1)="P" THEN LET M5=NUM(P3$(19,6))
0485 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0487 DIM P[1],DESC[3]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1)),IVS01_PRECISION=P[1]
0495 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0497 FOR X=1 TO 3; LET DESC[X]=NUM(P2$((X-1)*2+9,2)); NEXT X
0500 REM " --- Init Data"
0510 LET H0=60,H1=115,H=0,H1$=N1$,H2$=N4$
0520 LET L9=59,L=L9+1
0530 DIM J$(80,"-"),J1$(40," "),J0$(20,"0")
0540 DIM A0$(55+P[0]),A1$(138),A[11]
0550 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),G5=LEN(G5$)
0560 DIM J2$(M1,"-")
0570 LET P0=NUM(P4$(1,2)),P1=FNYY_YEAR(P4$(3,2)),P0=P0-1; IF P0=0 THEN LET P0=
0570:NUM(G2$(1,2)),P1=P1-1; REM "Find LAST PERIOD end date
0572 CALL "SYC.PA",SYS01_DEV,P0,P1,V0$,G9$,STATUS
0578 LET P4$=""
0630 IF AR$<>"Y" THEN GOTO 0700
0640 CALL "SYC.DA",1,9,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS THEN GOTO 9900
0660 LET ARM01_DEV=CHANNELS[9],ARM04_DEV=CHANNELS[10]
0700 REM " --- Background"
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(13,3),"Selected Report Sequence: ",@(28,5),"Beginning: ",@(3
0740:1,7),"Ending:",@(11,9),"Open/Closed/Planned/Quoted: "
0760 PRINT 'SF'
1000 REM " --- Options"
1005 LET I0=0
1010 PRINT 'CF',@(0,5),J1$(1,15)
1015 LET V4$="W=Work Order, T=WO Type"
1016 LET V3$="WT"
1017 IF BM$="Y" THEN LET V4$=V4$+", B=Bill Number",V3$=V3$+"B"
1018 IF AR$="Y" THEN LET V4$=V4$+", C=Customer",V3$=V3$+"C"
1020 LET V0$="S",V1$="KREC^",V2$=P9$,V0=1,V1=40,V2=3
1037 IF V2$="" THEN LET V2$="W"
1040 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1070 IF V$<>P9$ THEN LET END$="",BEGIN$=""
1080 LET P9$=V$
1090 IF P9$="W" THEN LET FILEIN_DEV=WOE01_DEV ELSE LET FILEIN_DEV=WOE03_DEV
1100 ON POS(P9$="WBCT")-1 GOTO 1110,1120,1130,1140
1110 LET H3$="Work Order Sequence",SEQTYPE$="  ",W9=2,SEQLEN=0; GOTO 1150
1120 LET H3$="Bill Number Sequence",SEQTYPE$="C",W9=1,SEQLEN=22; GOTO 1150
1130 LET H3$="Customer Sequence",SEQTYPE$="B",W9=1,SEQLEN=6; GOTO 1150
1140 LET H3$="Work Order Type Sequence",SEQTYPE$="A",W9=1,SEQLEN=2
1180 LET H9$=H3$(1,POS("Seq"=H3$)-2)
1190 GOTO 3900
1200 REM " --- Beginning
1220 LET V1$="CE",V2$=BEGIN$,V3$="",V4$="Enter Beginning "+H9$+", <F1>=First, 
1220:<F3>=Lookup",V1=40,V2=5
1230 PRINT @(V1,V2),'CL'
1260 ON POS(P9$="WBCT")-1 GOTO 1300,1310,1320,1330
1300 LET V0$="Z",V0=7; GOTO 1340
1310 LET V0$="S",V0=P[0]; GOTO 1340
1320 LET V0$="M",V3$=C1$,V0=C0; GOTO 1340
1330 LET V0$="S",V0=2
1350 GOSUB 7000
1360 IF V3=4 THEN GOTO 9900
1370 IF V3=3 THEN GOSUB 6000
1380 IF V3=2 THEN GOTO 1490
1390 IF V3=1 OR V$="" THEN LET V$=""; PRINT @(V1,V2),'CL',"First",; GOTO 1420
1400 GOSUB 6300
1410 IF OK$="N" THEN GOTO 1200
1420 LET BEGIN$=V$
1490 GOTO 3900
1500 REM " --- Ending"
1520 LET V1$="CE",V2$=END$,V3$="",V4$="Enter Ending "+H9$+", <F1>=Last, <F3>=L
1520:ookup",V1=40,V2=7
1530 PRINT @(V1,V2),'CL'
1560 ON POS(P9$="WBCT")-1 GOTO 1600,1610,1620,1630
1600 LET V0$="Z",V0=7; GOTO 1640
1610 LET V0$="S",V0=P[0]; GOTO 1640
1620 LET V0$="M",V3$=C1$,V0=C0; GOTO 1640
1630 LET V0$="S",V0=2
1650 GOSUB 7000
1660 IF V3=4 THEN GOTO 9900
1670 IF V3=3 THEN GOSUB 6000
1680 IF V3=2 THEN GOTO 1790
1690 IF V3=1 OR V$="" THEN LET V$=""; PRINT @(V1,V2),'CL',"Last",; GOTO 1730
1710 GOSUB 6300
1720 IF OK$="N" THEN GOTO 1500
1730 IF BEGIN$<>"" AND V$<>"" THEN IF V$<BEGIN$ THEN GOTO 1500
1760 LET END$=V$
1790 GOTO 3900
1900 REM " --- Open/Closed/Planned/Quoted
1910 LET H4$=""
1920 LET V0$="S",V1$="CKE^",V2$=WOTYPE$,V3$="",V4$="O=Open, C=Closed, P=Planne
1920:d, Q=Quoted, or Any Combination, <F1>=All",V0=4,V1=40,V2=9
1940 GOSUB 7000
1942 IF V3=4 THEN GOTO 9900
1944 IF V3=1 THEN LET V$=""
1945 IF V$="" THEN LET V$="OCPQ"
1950 FOR X=1 TO LEN(V$); IF POS(V$(X,1)="OCPQ")=0 THEN EXITTO 1900
1955 NEXT X
1960 LET WOTYPE$=V$
1970 IF POS("O"=WOTYPE$)>0 THEN LET H4$="Open/"
1972 IF POS("C"=WOTYPE$)>0 THEN LET H4$=H4$+"Closed/"
1974 IF POS("P"=WOTYPE$)>0 THEN LET H4$=H4$+"Planned/"
1976 IF POS("Q"=WOTYPE$)>0 THEN LET H4$=H4$+"Quoted/"
1980 LET H4$=H4$(1,LEN(H4$)-1)
1985 PRINT @(V1,V2),V$+" "+H4$,'CL',
1990 GOTO 3900
2000 REM " --- Warehouse
2020 IF P9$<>"B" THEN GOTO 2190
2030 IF P5$="" THEN LET P5$=I3$(6,2)
2040 PRINT 'SB',@(0,5),"Whse Code:",'SF'
2060 LET V0$="S",V1$="CE",V2$=P5$,V3$="",V4$="Enter A Valid Warehouse Code,  <
2060:F3>=Lookup",V0=2,V1=12,V2=5
2080 GOSUB 7000
2085 IF V3=4 THEN GOTO 9900
2090 IF V3<>3 THEN GOTO 2140
2100 LET V4=1,V5=5,V6=4,V7=2,V8=6,V9=20,V5$="C"
2120 CALL "SYC.LZ",V1,V2,V4,IVM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
2140 IF LEN(V$)<2 THEN LET V$=V$+"  ",V$=V$(1,2)
2150 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=2000)IOL=IVM10C
2160 PRINT @(V1+3,V2),X0$(6,10)
2180 LET P5$=V$
2190 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,2000,1200,1500,1900,4000
4000 REM " --- Overlay"
4010 LET V4$="Is The Above Information Correct: "
4030 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4140 IF P9$="B" THEN LET BEGIN$=P5$+BEGIN$; IF END$<>"" THEN LET END$=P5$+END$
4200 LET H4$=H4$+" Beginning With "
4220 IF BEGIN$<>"" THEN LET H4$=H4$+BEGIN$ ELSE LET H4$=H4$+"The First"
4230 LET H4$=H4$(1,POS("   "=H4$+"   ")-1)
4240 LET H4$=H4$+" And Ending With "
4260 IF END$<>"" THEN LET H4$=H4$+END$ ELSE LET H4$=H4$+"The Last"
4270 LET H4$=H4$(1,POS("   "=H4$+"   ")-1)
4480 LET Z1$="Y",Z2$="Y",Z3$="",Z4$="",Z5$="OMS",Z6$="Y"
4490 RUN "WOR.IB"
6000 REM " --- Item Lookup
6004 PRINT @(V1,V2),'CL'
6005 ON POS(P9$="BWCT")-1 GOTO 6010,6050,6100,6200
6010 CALL "SYC.LB",V$
6030 GOTO 6290
6050 REM " --- Work Order Lookup"
6060 CALL "SYC.LW",V$,"?"
6090 GOTO 6290
6100 REM " --- Customer Lookup
6130 CALL "SYC.LC",V$
6190 GOTO 6290
6200 REM " --- Lookup Type
6210 IF V3=1 THEN GOTO 6290
6220 LET V4=1,V5=6,V6=4,V7=2,V8=6,V9=20,V5$="A"
6230 CALL "SYC.LZ",V1,V2,V4,WOM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
6290 RETURN
6300 REM " --- Verify Entry"
6305 LET OK$=""
6310 ON POS(P9$="BWCT")-1 GOTO 6320,6360,6390,6430
6320 LET V$=V$+J1$,V$=V$(1,P[0])
6330 FIND (IVM01_DEV,KEY=N0$+V$,DOM=6480)IOL=IVM01A
6340 PRINT @(V1,V2),V$," ",X0$(1,MIN(LEN(X0$),38-P[0])),'CL',
6350 GOTO 6490
6360 REM " --- Work Order Verify"
6370 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=6480)
6375 PRINT @(V1,V2),V$,'CL',
6380 GOTO 6490
6390 REM " --- Customer Verify"
6395 LET V$=V$+"000000",V$=V$(1,6)
6400 FIND (ARM01_DEV,KEY=N0$+V$,DOM=6480)IOL=ARM01A
6410 PRINT @(V1,V2),FNF$(V$(1,C0),C1$)," ",X0$(1,30),'CL',
6420 GOTO 6490
6430 REM " --- Type Verify"
6435 LET V$=V$+"  ",V$=V$(1,2)
6440 FIND (WOM10_DEV,KEY=N0$+"A"+V$,DOM=6480)IOL=WOM10A
6450 PRINT @(V1,V2),V$," ",X0$(6,20),'CL',
6460 GOTO 6490
6480 LET OK$="N"
6490 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
