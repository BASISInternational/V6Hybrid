0010 REM "MPX Convert MRP to 6.0"
0020 REM "Program MPX60A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0060 BEGIN
0070 SETESC 9000
0080 SETERR 9000
0100 REM " --- Open/Lock Files "   
0105 LET NUMBER=3
0110 DIM FILES$[NUMBER],OPTIONS$[NUMBER],CHANNELS[NUMBER]
0115 LET FILES$[1]="SYS-01"
0120 LET FILES$[2]="MPE-01",FILES$[3]="MPE01.TMP"
0150 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLISTS      
0210 MPE01A: IOLIST B0$,B1$,B[ALL]
0220 MPE01T: IOLIST A0$,A1$,A[ALL]
0230 IVS01A: IOLIST I0$,I1$,I2$
0240 SYS01T: IOLIST X$,F0$,X1$,X2$,X4$,X5$,X6$
0400 REM " --- Parameters      
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=1240)IOL=SYS01T
0420 LET N1$=F4$,N2$="MP",N3$=F4$
0500 REM " --- Initialize Data 
0510 PRECISION 14
0520 LET N0$=""
0800 REM " --- Display Warning"
0810 CALL "SYC60A",N2$,SYS01_DEV,STATUS
0820 IF STATUS THEN GOTO 9900
0850 PRINT @(40,3),"Materials Requirement Planning",
0900 REM " --- Notification Box     
0910 CALL "SYC.NB","Processing",20,COLUMN
1000 REM " --- Initialize For MPE-01 Conversion 
1005 LET NUMB=1
1010 GOSUB DIMENSION_RENAME
1015 LET FROM$[1]="MPE-01",TMPFILE$[1]="MPE01.TMP"
1020 GOSUB RENAME_FILES
1025 IF STATUS THEN GOTO 9900
1030 CALL "SYC.DA",1,2,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1035 IF STATUS THEN GOTO 9900
1040 LET MPE01_DEV=CHANNELS[2],MPE01TMP_DEV=CHANNELS[3]
1050 READ (MPE01TMP_DEV,KEY="",DOM=1100)
1100 REM " --- Process MPE-01 Data "
1110 DIM A0$(27),A1$(100),A[26],B0$(27),B1$(100),B[26]
1130 LET K$=KEY(MPE01TMP_DEV,END=1260)
1140 IF POS(K$(1,2)=N0$)=1 THEN GOTO 1180
1150 GOSUB NEW_FIRM_ID
1160 IF VALID_FIRM THEN GOTO 1180
1170 READ (MPE01TMP_DEV,KEY=N0$+$FF$,DOM=1130)
1180 READ (MPE01TMP_DEV,KEY=K$)IOL=MPE01T
1190 LET B0$(1,7)=A0$(1,7)
1200 LET B0$(8,20)=A0$(8,ITEM_LENGTH)
1210 LET B1$(1)=A1$(1)
1220 LET B[ALL]=A[ALL]
1230 WRITE (MPE01_DEV,KEY=B0$)IOL=MPE01A
1240 PRINT @(COLUMN,11),B0$(8,20)
1250 GOTO 1130
1260 CLOSE (MPE01TMP_DEV,ERR=1270)
1270 ERASE "MPE01.TMP",ERR=4000
4000 REM " --- Done "
4010 GOTO 9900
6000 REM " --- Rename Data Files "
6010 RENAME_FILES:
6020 CALL "SYC.RN",FROM$[ALL],TMPFILE$[ALL],FLAG$[ALL],NUMB,STATUS
6030 RETURN
6100 REM " --- Dimension File Rename Variables "
6105 DIMENSION_RENAME:
6110 DIM FROM$[NUMB],TMPFILE$[NUMB],FLAG$[NUMB]
6120 RETURN
6200 REM " --- Firm ID Change "
6205 NEW_FIRM_ID:
6210 LET N0$=K$(1,2),VALID_FIRM=1
6220 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=6250)IOL=IVS01A
6230 LET ITEM_LENGTH=NUM(I2$(1,2))
6240 GOTO 6290
6250 LET VALID_FIRM=0
6290 RETURN
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
