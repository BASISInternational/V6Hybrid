0010 REM "WOR - Calendar Print - Parameters                            "
0020 REM "Program WOR.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOM-04",FILES$[2]="SYS-01",FILES$[3]="BMM-08"
0130 LET FILES$[4]="WOM-02"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET WOM04_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 WOM04A: IOLIST A0$(1),A1$,A2$,A[ALL]
0220 CODFILE: IOLIST OPDESC$
0260 WOS01A: IOLIST X$,X$,X$,P9$,X$,X$,X$,X$,X$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="WO"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0500 REM " --- Intialize Data "
0520 DIM A0$(9),A[31]
0530 LET M1$="##0.0"
0540 LET MONTHS$="January  February March    April    May      June     July  
0540:   August   SeptemberOctober  November December "
0550 LET R9$="SunMonTueWedThuFriSat"
0560 LET K9=9
0570 LET NUMBER=1
0600 REM " --- Additional File Opens "
0605 REM " --- Check For Bill Of Materials "
0610 IF P9$(1,1)<>"Y" THEN GOTO 0650
0615 REM " --- Use Bill Of Materials Code File "
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0635 LET CODFILE_DEV=CHANNELS[3]
0640 GOTO 0700
0650 REM " --- Use Work Order Code File "
0660 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS THEN GOTO 9900
0680 LET CODFILE_DEV=CHANNELS[4]
0700 REM " --- Background "
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(23,3),"Operation Code:",@(8,5),"First Day Currently Schedule
0740:d",@(9,7),"Last Day Currently Scheduled:",@(27,9),"From Month:",@(28,11),
0740:"From Year:",@(24,13),"Through Month:",@(25,15),"Through Year:",
0790 PRINT 'SF',
1000 REM " --- Operation Code To Print"
1005 PRINT 'CF'
1010 LET V0$="S",V1$="KEC",V2$=OPCODE$,V3$="",V4$="Enter Operation Code To Pri
1010:nt, <F1>=All, <F3>=Lookup",V0=3,V1=39,V2=3,I0=0
1015 GOSUB 7000
1020 IF V3=2 THEN GOTO 1090
1025 IF V3=4 THEN GOTO 9900
1030 IF V3=1 THEN LET OPCODE$=""; PRINT @(V1,V2),"All",'CL',; GOTO 1090
1035 IF V3<>3 THEN GOTO 1065
1040 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
1045 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description",FIELD[0]=1,FIELD
1045:[1]=1,POSITION[0]=3,POSITION[1]=6,LENGTH[0]=3,LENGTH[1]=20
1050 CALL "SYC.LK",CODFILE_DEV,"",NUMBER,"Operation Code",DESCRIPTION$[ALL],FI
1050:ELD[ALL],POSITION[ALL],LENGTH[ALL],5,4,V$
1065 LET V$=V$+"   ",OPCODE$=V$(1,3)
1070 FIND (CODFILE_DEV,KEY=N0$+OPCODE$,DOM=1000)IOL=CODFILE
1075 PRINT @(V1,V2),OPCODE$," ",OPDESC$(6,20),
1080 GOSUB 6200
1085 IF FIRST_DATE$="   " THEN GOSUB 6000; GOTO 1000
1090 GOTO 3900
1100 REM " --- 1st Year To Print"
1110 IF R0=0 THEN LET R1$="  "; GOTO 1190
1120 LET V0$="D",V1$="KECY",V2$=R1$,V3$="",V4$="Enter Year To Print From",V0=2
1120:,V1=39,V2=11
1140 GOSUB 7000
1145 IF V3=2 THEN GOTO 1190
1150 IF V3=4 THEN GOTO 9900
1160 LET R1$=V$
1190 GOTO 3900
1200 REM " --- Last Month To Print"
1220 LET V0$="N",V1$="KEC",V2$=STR(R2),V3$="#0",V4$="Enter Last Month To Print
1220:, <F1>=Last",V0=2,V1=39,V2=13
1240 GOSUB 7000
1245 IF V3=2 THEN GOTO 1290
1250 IF V3=4 THEN GOTO 9900
1255 IF V3=1 OR V=0 THEN LET R2=0; PRINT @(V1,V2),"Last",'CL',@(V1,V2+2),'CL',
1255:; GOTO 1290
1260 IF V<1 OR V>12 THEN GOTO 1200
1270 PRINT @(V1,V2),V:"#0"," ",MONTHS$((V-1)*9+1,9),'CL',
1280 LET R2=V
1290 GOTO 3900
1300 REM " --- Last Year To Print"
1310 IF R2=0 THEN LET R3$="  "; GOTO 1390
1320 LET V0$="D",V1$="KECY",V2$=R3$,V3$="",V4$="Enter Year To Print Through",V
1320:0=2,V1=39,V2=15
1340 GOSUB 7000
1345 IF V3=2 THEN GOTO 1390
1350 IF V3=4 THEN GOTO 9900
1360 IF V$+STR(R2:"00")<R1$+STR(R0:"00") THEN PRINT @(V1,V2),'CL',; LET V3=2; 
1360:GOTO 1390
1370 LET R3$=V$
1390 GOTO 3900
1400 REM " --- 1st Month To Print"
1420 LET V0$="N",V1$="KEC",V2$=STR(R0),V3$="#0",V4$="Enter 1st Month To Print,
1420: <F1>=First",V0=2,V1=39,V2=9
1440 GOSUB 7000
1445 IF V3=2 THEN GOTO 1490
1450 IF V3=4 THEN GOTO 9900
1455 IF V3=1 OR V=0 THEN LET R0=0; PRINT @(V1,V2),"First",'CL',@(V1,V2+2),'CL'
1455:,; GOTO 1490
1460 IF V<1 OR V>12 THEN GOTO 1400
1470 PRINT @(V1,V2),V:"#0"," ",MONTHS$((V-1)*9+1,9),'CL',
1480 LET R0=V
1490 GOTO 3900
3900 REM "NEXT FIELD"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1400,1100,1200,1300,3990
3990 LET I0=0
4000 REM " --- Options "
4020 LET V0$="Y",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct
4020:? (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 RUN "WOR.QB"
6000 REM " --- No Calendar scheduled for this Op Code"
6010 DIM MESSAGE$[1]
6020 LET MESSAGE$[0]="There is no calendar scheduled for this",MESSAGE$[1]="Op
6020:eration Code. <Enter> to continue. "
6030 CALL "SYC.XA",0,MESSAGE$[ALL],1,22,-1,V$,V3
6090 RETURN
6200 REM "FIND FIRST AND LAST DATE FOR THIS OP CODE"
6210 LET FIRST_DATE$="   "
6220 READ (WOM04_DEV,KEY=N0$+OPCODE$,DOM=6230)
6230 LET K$=KEY(WOM04_DEV,END=6280)
6235 IF POS(N0$+OPCODE$=K$)<>1 THEN GOTO 6280
6240 READ (WOM04_DEV,KEY=K$)IOL=WOM04A
6250 LET X$="01"
6255 FOR X=1 TO A[0]
6260 IF A[X]>=0 THEN LET X$=STR(X:"00"); EXITTO 6270
6265 NEXT X
6270 LET FIRST_DATE$=FND$(A0$(8,2)+X$+A0$(6,2))
6280 PRINT @(39,5),'SB',FNB$(FIRST_DATE$),'SF',
6310 LET LAST_DATE$="   "
6320 READ (WOM04_DEV,KEY=N0$+OPCODE$,DOM=6330)
6330 LET K$=KEY(WOM04_DEV,END=6380)
6335 IF POS(N0$+OPCODE$=K$)<>1 THEN GOTO 6380
6340 READ (WOM04_DEV,KEY=K$)IOL=WOM04A
6345 LET X$=STR(A[0]:"00")
6350 FOR X=A[0] TO 1 STEP -1
6360 IF A[X]>=0 THEN LET X$=STR(X:"00"); EXITTO 6370
6365 NEXT X
6370 LET LAST_DATE$=FND$(A0$(8,2)+X$+A0$(6,2))
6375 GOTO 6330
6380 PRINT @(39,7),'SB',FNB$(LAST_DATE$),'SF',
6390 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
