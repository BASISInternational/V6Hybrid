0010 REM "WOR - Work Order Transaction History Report - Parameter Entry"
0020 REM "Program:  WOR.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=21
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-03",FILES$[2]="WOE-01",FILES$[3]="IVM-01"
0120 LET FILES$[4]="IVM-10",FILES$[5]="IVM-03",FILES$[6]="SYS-01"
0125 LET FILES$[7]="WOM-10",FILES$[8]="WOT-01",FILES$[9]="WOT-21"
0130 LET FILES$[10]="WOT-31",FILES$[11]="WOT-03",FILES$[12]="WOT-23"
0135 LET FILES$[13]="WOT-33",FILES$[14]="WOM-07"
0150 CALL "SYC.DA",1,1,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE03_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0165 LET IVM10_DEV=CHANNELS[4],IVM03_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0170 LET WOM10_DEV=CHANNELS[7],WOT01_DEV=CHANNELS[8],WOT21_DEV=CHANNELS[9]
0175 LET WOT31_DEV=CHANNELS[10],WOT03_DEV=CHANNELS[11],WOT23_DEV=CHANNELS[12]
0180 LET WOT33_DEV=CHANNELS[13],WOM07_DEV=CHANNELS[14]
0200 REM " --- IOLISTs"
0210 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0220 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0230 IVS01A: IOLIST I0$,I1$,I2$,I3$
0240 ARS01A: IOLIST X$,X$,C0$,X$,X$,C1$
0250 APS01A: IOLIST X$,X$,C2$,X$,X$,C3$
0260 GLS01A: IOLIST X$,X$,G2$,G3$
0270 IVM10C: IOLIST X0$
0280 IVM01A: IOLIST *,X0$
0290 ARM01A: IOLIST *,X0$
0300 WOM10A: IOLIST X0$
0310 PRS01A: IOLIST *,*,C4$,*,*,C5$
0400 REM " --- Parameters"
0405 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0410 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0415 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0420 LET BM$=P3$(1,1),AR$=P3$(2,1),PO$=P3$(3,1),PR$=P3$(4,1)
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 LET I9=NUM(I2$(1,2)),IVS01_PRECISION=NUM(I2$(5,1)),I2=NUM(I2$(7,2))
0440 IF AR$="N" THEN GOTO 0455
0445 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0450 LET C0=NUM(C0$(1,2)),C1=LEN(C1$)
0455 IF PO$="N" THEN GOTO 0470
0460 FIND (SYS01_DEV,KEY=N0$+"AP00")IOL=APS01A
0465 LET C2=NUM(C2$(1,2)),C3=LEN(C3$)
0470 IF PR$="N" THEN LET C4=NUM(P2$(1,2)),C5$=M0$; GOTO 0500
0475 FIND (SYS01_DEV,KEY=N0$+"PR00")IOL=PRS01A
0480 LET C4=NUM(C4$(1,2))
0500 REM " --- Init Data"
0505 LET H0=132,HEADINGS=4,H=0,WHEN$=F0$(7,3),CLOCK$=""
0510 DIM HEADINGS$[HEADINGS]
0515 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0520 LET L9=59,L=L9+1
0525 DIM J$(80,"-"),J1$(40," "),J0$(20,"0")
0530 DIM A1$(138),A[11],DESC[3]
0535 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0540 DIM J2$(M1,"-")
0545 LET P0=NUM(P4$(1,2)),P1=FNYY_YEAR(P4$(3,2)),P0=P0-1
0550 IF P0=0 THEN LET P0=NUM(G2$(1,2)),P1=P1-1; REM "Find LAST PERIOD end date
0555 CALL "SYC.PA",SYS01_DEV,P0,P1,V0$,G9$,STATUS
0560 IF STATUS THEN GOTO 9900
0565 LET P4$=""
0570 FOR X=1 TO 3
0575 LET DESC[X]=NUM(I2$((X-1)*2+9,2))
0580 NEXT X
0600 REM " --- Additional File Opens "
0605 IF AR$<>"Y" THEN GOTO 0615
0610 LET FILES$[15]="ARM-01",FILES$[16]="ARM-04"
0615 IF PO$="Y" THEN LET FILES$[17]="APM-01"
0620 IF BM$="Y" THEN LET FILES$[18]="BMM-08"
0625 IF BM$="N" THEN LET FILES$[18]="WOM-02"
0630 IF PR$="Y" THEN LET FILES$[19]="PRM-01"
0635 IF PR$="N" THEN LET FILES$[19]="WOM-01"
0640 IF POS(I3$(17,1)="LS")>0 THEN LET FILES$[20]="WOT-11"
0645 IF POS(I3$(17,1)="LS")>0 THEN LET FILES$[21]="WOT-12"
0650 CALL "SYC.DA",1,15,21,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATU
0650:S
0655 IF STATUS THEN GOTO 9900
0660 LET ARM01_DEV=CHANNELS[15],ARM04_DEV=CHANNELS[16],APM01_DEV=CHANNELS[17]
0665 LET OPCODE_DEV=CHANNELS[18],EMPCODE_DEV=CHANNELS[19]
0670 LET WOT11_DEV=CHANNELS[20],WOT12_DEV=CHANNELS[21]
0700 REM " --- Background"
0710 FOR X=4 TO 18 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(14,3),"Selected Report Sequence:",@(29,5),"Whse Code:",@(29,
0740:7),"Beginning:",@(32,9),"Ending:",@(22,11),"Open/Closed/Both:",@(24,13),"
0740:Beginning Date:",@(27,15),"Ending Date:",@(22,17),"Transaction Type:",
0760 PRINT 'SF'
1000 REM " --- Options"
1005 LET I0=0
1010 PRINT 'CF',@(0,5),J1$(1,15),
1015 LET V4$="W=Work Order, T=WO Type"
1016 LET V3$="WT"
1017 IF BM$="Y" THEN LET V4$=V4$+", B=Bill Number",V3$=V3$+"B"
1018 IF AR$="Y" THEN LET V4$=V4$+", C=Customer",V3$=V3$+"C"
1020 LET V0$="S",V1$="KREC^",V2$=P9$,V0=1,V1=40,V2=3
1037 IF V2$="" THEN LET V2$="W"
1040 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1070 IF V$<>P9$ THEN LET P7$="",P8$=""
1080 LET P9$=V$
1100 ON POS(P9$="WBCT")-1 GOTO 1110,1120,1130,1140
1110 LET H3$="Work Order Sequence",REC$="  ",CHAN_DEV=WOE01_DEV,X8=0; GOTO 118
1110:0
1120 LET H3$="Bill Number Sequence",REC$="C",CHAN_DEV=WOE03_DEV,X8=22; GOTO 11
1120:80
1130 LET H3$="Customer Sequence",REC$="B",CHAN_DEV=WOE03_DEV,X8=6; GOTO 1180
1140 LET H3$="Work Order Type Sequence",REC$="A",CHAN_DEV=WOE03_DEV,X8=2
1180 LET H9$=H3$(1,POS("Seq"=H3$)-2)
1190 GOTO 3900
1200 REM " --- Beginning
1220 LET V1$="CE",V2$=P8$,V3$="",V4$="Enter Beginning "+H9$+", <F1>=First, <F3
1220:>=Lookup",V1=40,V2=7
1230 PRINT @(V1,V2),'CL'
1260 ON POS(P9$="WBCT")-1 GOTO 1300,1310,1320,1330
1300 LET V0$="Z",V0=7; GOTO 1350
1310 LET V0$="S",V0=I9; GOTO 1350
1320 LET V0$="M",V3$=C1$,V0=C0; GOTO 1350
1330 LET V0$="S",V0=2
1350 GOSUB 7000
1360 IF V3=4 THEN GOTO 9900
1370 IF V3=3 THEN GOSUB 6000
1380 IF V3=2 THEN GOTO 1490
1390 IF V3=1 OR V$="" THEN LET V$=""; PRINT @(V1,V2),'CL',"First",; GOTO 1420
1400 GOSUB 6300
1410 IF OK$="N" THEN GOTO 1200
1420 LET P8$=V$
1490 GOTO 3900
1500 REM " --- Ending"
1520 LET V1$="CE",V2$=P7$,V3$="",V4$="Enter Ending "+H9$+", <F1>=Last, <F3>=Lo
1520:okup",V1=40,V2=9
1530 PRINT @(V1,V2),'CL'
1560 ON POS(P9$="WBCT")-1 GOTO 1600,1610,1620,1630
1600 LET V0$="Z",V0=7; GOTO 1650
1610 LET V0$="S",V0=I9; GOTO 1650
1620 LET V0$="M",V3$=C1$,V0=C0; GOTO 1650
1630 LET V0$="S",V0=2
1650 GOSUB 7000
1660 IF V3=4 THEN GOTO 9900
1670 IF V3=3 THEN GOSUB 6000
1680 IF V3=2 THEN GOTO 1790
1690 IF V3=1 OR V$="" THEN LET V$=""; PRINT @(V1,V2),'CL',"Last",; GOTO 1720
1710 GOSUB 6300
1720 IF OK$="N" THEN GOTO 1500
1740 IF P8$<>"" AND V$<>"" THEN IF V$<P8$ THEN GOTO 1500
1750 LET P7$=V$
1790 GOTO 3900
1800 REM " --- Open/Closed/Both
1820 LET V0$="S",V1$="CE",V2$=P6$,V3$="OCB",V4$="<O>=Open Work Orders Only, <C
1820:>=Closed Only, <B>=Both Open & Closed",V0=1,V1=40,V2=11
1830 IF V2$="" THEN LET V2$="B"
1840 GOSUB 7000
1850 IF V3=4 THEN GOTO 9900
1860 LET P6$=V$
1870 IF P6$="O" THEN LET H4$="Open Work Orders" ELSE IF P6$="C" THEN LET H4$="
1870:Closed Work Orders" ELSE IF P6$="B" THEN LET H4$="All Work Orders" ELSE G
1870:OTO 1800
1880 PRINT @(V1+3,V2),H4$
1890 GOTO 3900
2000 REM " --- Warehouse
2020 IF P9$<>"B" THEN GOTO 2190
2030 IF P5$="" THEN LET P5$=I3$(6,2)
2060 LET V0$="S",V1$="CE",V2$=P5$,V3$="",V4$="Enter A Valid Warehouse Code, <F
2060:3>=Lookup",V0=2,V1=40,V2=5
2080 GOSUB 7000
2085 IF V3=4 THEN GOTO 9900
2090 IF V3<>3 THEN GOTO 2140
2120 CALL "IVC.LK",3,12,5,V$
2140 IF LEN(V$)<2 THEN LET V$=V$+"  ",V$=V$(1,2)
2150 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=2000)IOL=IVM10C
2160 PRINT @(V1+3,V2),X0$(6,24)
2180 LET P5$=V$
2190 GOTO 3900
2200 REM " --- Begin Date
2220 LET V0$="D",V1$="CE",V2$=D9$,V3$="",V4$="Enter Beginning Transaction Date
2220:, <F1>=First",V0=8,V1=40,V2=13
2240 GOSUB 7000
2245 IF V3=4 THEN GOTO 9900
2250 IF V3=1 THEN LET V$=""
2260 LET D9$=V$
2280 IF D9$="" THEN PRINT @(V1,V2),'CL',"First"
2290 GOTO 3900
2400 REM " --- End Date
2420 LET V0$="D",V1$="CE",V2$=D8$,V3$="",V4$="Enter Ending Transaction Date, <
2420:F1>=Last",V0=8,V1=40,V2=15
2440 GOSUB 7000
2445 IF V3=4 THEN GOTO 9900
2450 IF V3=1 THEN LET V$=""
2455 IF D9$<>"" AND V$<>"" THEN IF V$<D9$ THEN GOTO 2400
2460 LET D8$=V$
2480 IF D8$="" THEN PRINT @(V1,V2),'CL',"Last"
2490 GOTO 3900
2600 REM " --- Transaction Type"
2610 LET V0$="S",V1$="C^E",V2$=T6$,V3$="",V4$="M=Materials, O=Operations, S=Su
2610:bcontracts, Any Combination, <F1>=All",V0=3,V1=40,V2=17
2620 GOSUB 7000
2630 IF V3=4 THEN GOTO 9900
2640 IF V3=1 THEN LET V$=""
2650 IF V$="" THEN LET T6$="MOS"; GOTO 2710
2660 IF LEN(V$)=0 OR LEN(T6$)>3 THEN GOTO 2600
2670 FOR X=1 TO LEN(V$)
2680 IF POS(V$(X,1)="MOS")=0 THEN EXITTO 2600
2690 NEXT X
2700 LET T6$=V$
2710 PRINT @(V1,V2),T6$,'CL',
2720 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,2000,1200,1500,1800,2200,2400,2600,4000
4000 REM " --- Overlay"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct?
4020: (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4140 IF P9$="B" THEN LET P8$=P5$+P8$; IF P7$<>"" THEN LET P7$=P5$+P7$
4200 LET H4$=H4$+" Beginning With "
4220 IF P8$<>"" THEN LET H4$=H4$+P8$ ELSE LET H4$=H4$+"The First"
4230 LET H4$=H4$(1,POS("   "=H4$+"   ")-1)
4240 LET H4$=H4$+" And Ending With "
4260 IF P7$<>"" THEN LET H4$=H4$+P7$ ELSE LET H4$=H4$+"The Last"
4270 LET H4$=H4$(1,POS("   "=H4$+"   ")-1)
4280 LET H7$="With A Transaction Type Of "+T6$(1,1); IF LEN(T6$)=2 THEN LET H7
4280:$=H7$+" AND "+T6$(2,1)
4290 IF LEN(T6$)=3 THEN LET H7$=H7$+", "+T6$(2,1)+" AND "+T6$(3,1)
4400 RUN "WOR.JB"
6000 REM " --- Item Lookup
6004 PRINT @(V1,V2),'CL'
6005 ON POS(P9$="BWCT")-1 GOTO 6010,6050,6100,6200
6010 CALL "SYC.LB",V$
6030 GOTO 6290
6050 REM " --- Work Order Lookup"
6060 CALL "SYC.LW",V$,"?"
6090 GOTO 6290
6100 REM " --- Customer Lookup
6130 CALL "SYC.LC",V$
6190 GOTO 6290
6200 REM " --- Work Order Type Lookup "
6205 LET NUMBER=1,RECORD$="A",TITLE$="WO Type Lookup"
6210 DIM DESC$[NUMBER],POSITION[NUMBER],LENGTH[NUMBER],FIELD[NUMBER]
6220 LET DESC$[0]="WO Type",DESC$[1]="Description"
6230 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6240 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=24
6250 LET COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,ROW=7
6260 CALL "SYC.LK",WOM10_DEV,RECORD$,NUMBER,TITLE$,DESC$[ALL],FIELD[ALL],POSIT
6260:ION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN
6300 REM " --- Verify Entry"
6305 LET OK$=""
6310 ON POS(P9$="BWCT")-1 GOTO 6320,6360,6390,6430
6320 LET V$=V$+J1$,V$=V$(1,20)
6330 FIND (IVM01_DEV,KEY=N0$+V$,DOM=6480)IOL=IVM01A
6340 PRINT @(V1,V2),V$(1,I9)," ",X0$(1,MIN(LEN(X0$),38-I9)),'CL',
6350 GOTO 6490
6360 REM " --- Work Order Verify"
6370 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=6480)
6375 PRINT @(V1,V2),V$,'CL',
6380 GOTO 6490
6390 REM " --- Customer Verify"
6395 LET V$=V$+"000000",V$=V$(1,6)
6400 FIND (ARM01_DEV,KEY=N0$+V$,DOM=6480)IOL=ARM01A
6410 PRINT @(V1,V2),FNF$(V$(1,C0),C1$)," ",X0$(1,30),'CL',
6420 GOTO 6490
6430 REM " --- Type Verify"
6435 LET V$=V$+"  ",V$=V$(1,2)
6440 FIND (WOM10_DEV,KEY=N0$+"A"+V$,DOM=6480)IOL=WOM10A
6450 PRINT @(V1,V2),V$," ",X0$(6,20),'CL',
6460 GOTO 6490
6480 LET OK$="N"
6490 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
