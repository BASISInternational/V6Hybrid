0010 REM "WOR - Calendar Print - Detail Print                          "
0020 REM "Program: WOR.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9900
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLIST's"
0210 WOM04A: IOLIST A0$,A1$,A2$,A[ALL]
0220 CODFILE: IOLIST OPDESC$
0500 REM " --- Initializations"
0510 DIM L[12],HEADINGS$[6],J$(80,"-"),A1$(31),A0$(9)
0520 LET HEADINGS$[1]=N1$,HEADINGS$[2]=N4$
0530 IF OPCODE$="" THEN GOTO 0560
0540 LET L_DATE$=FNB$(LAST_DATE$)
0550 LET L_DATE$=FNYY_YY21$(L_DATE$(7,2))+L_DATE$(1,2)
0560 LET MONTHS$="January  February March    April    May      June     July  
0560:   August   SeptemberOctober  November December "
0570 LET DAYS$="Sunday   Monday   Tuesday  WednesdayThursday Friday   Saturday
0570: "
0580 LET L[1]=31,L[2]=28,L[3]=31,L[4]=30,L[5]=31,L[6]=30
0585 LET L[7]=31,L[8]=31,L[9]=30,L[10]=31,L[11]=30,L[12]=31
0590 CALL "SYC.NB","Printing:",15,COLUMN2
0600 REM " --- Load options"
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS THEN GOTO 9900
0630 LET A0$(1,5)=N0$+OPCODE$
0700 REM " --- Initialize Op Code for All Op Code selection"
0710 IF OPCODE$<>"" THEN GOTO 0800
0720 READ (WOM04_DEV,KEY=A0$(1,2),DOM=0721)
0730 LET K$=KEY(WOM04_DEV,END=4000)
0740 IF POS(N0$=K$)<>1 THEN GOTO 4000
0750 LET A0$(3,3)=K$(3,3)
0800 REM " --- Get first month"
0810 IF R0>0 THEN LET A0$(6,4)=R1$+STR(R0:"00"); GOTO 1000
0820 READ (WOM04_DEV,KEY=A0$(1,5),DOM=0821)
0830 LET K$=KEY(WOM04_DEV,END=4000)
0840 IF POS(N0$+OPCODE$=K$)<>1 THEN GOTO 4000
0850 LET A0$=K$
1000 REM " --- Month/Year?"
1010 DIM A[31]
1015 FOR X=1 TO 31; LET A[X]=-1; NEXT X
1020 READ (WOM04_DEV,KEY=A0$(1,9),DOM=1021)IOL=WOM04A
1030 LET YEAR$=A0$(6,2),MONTH$=A0$(8,2)
1100 REM " --- Set calendar title"
1110 DIM OPDESC$(30)
1120 FIND (CODFILE_DEV,KEY=N0$+A0$(3,3),DOM=1121)IOL=CODFILE
1130 LET HEADINGS$[4]=FNP$(MONTHS$((NUM(MONTH$)-1)*9+1,9))+" "+STR(1900+FNYY_Y
1130:EAR(YEAR$))
1140 LET HEADINGS$[5]=FNP$(A0$(3,3)+" "+OPDESC$(6,20))
1150 GOSUB 5000
1160 PRINT @(COLUMN2,11),HEADINGS$[4],'CL',
1200 REM " --- Print Days of the Week"
1210 PRINT (7)@(1),"+",J$(1,76),"+"
1220 LET COLUMN=1
1230 FOR X=1 TO 7
1240 PRINT (7)@(COLUMN),"|",DAYS$((X-1)*9+1,9),
1250 LET COLUMN=COLUMN+11
1260 NEXT X
1270 PRINT (7)@(COLUMN),"|"
1280 PRINT (7)@(1),"|",J$(1,76),"|"
1500 REM " --- Determine starting day of the week"
1510 DIM DAYS[42]
1515 LET FIRST$=YEAR$+MONTH$+"01",X$="",FIRST=0,D=0,DATE=0
1520 CALL "SYC.CB",FIRST$,X$,FIRST
1530 LET FIRST=FIRST+1,L[2]=28,YY=FNYY_YEAR(YEAR$),MM=NUM(MONTH$)
1540 IF FIRST>7 THEN LET FIRST=1
1550 IF MOD(YY,4)=0 THEN LET L[2]=29
1560 FOR X=FIRST TO 42
1570 LET DATE=DATE+1
1580 IF DATE<=L[MM] THEN LET D=D+1,DAYS[X]=D
1590 NEXT X
1900 REM " --- Display dates on calendar"
1910 LET DATE=1
1920 FOR Y=0 TO 5
1930 LET COLUMN=1
1940 FOR X=1 TO 7
1945 PRINT (7)@(COLUMN),"|",
1950 IF DAYS[Y*7+X]>0 THEN PRINT (7)@(COLUMN+1),DAYS[Y*7+X],
1960 LET COLUMN=COLUMN+11
1970 NEXT X
1980 PRINT (7)@(COLUMN),"|"
1990 LET COLUMN=1
2000 FOR X=1 TO 7
2005 PRINT (7)@(COLUMN),"|",
2010 IF DAYS[Y*7+X]=0 THEN GOTO 2050
2020 LET X$=" "+STR(A[DATE]:M1$)+" Hr"
2025 IF A[DATE]=-1 THEN LET X$=" Unsched"
2030 PRINT (7)@(COLUMN+1),X$,
2040 LET DATE=DATE+1
2050 LET COLUMN=COLUMN+11
2060 NEXT X
2070 PRINT (7)@(COLUMN),"|"
2080 IF Y<5 THEN PRINT (7)@(1),"|",J$(1,76),"|"
2090 NEXT Y
2100 PRINT (7)@(1),"+",J$(1,76),"|"
3000 REM " --- Get Next Month"
3010 LET MON=NUM(A0$(8,2)),YR=FNYY_YEAR(A0$(6,2))
3020 LET MON=MON+1
3030 IF MON>12 THEN LET MON=1,YR=YR+1
3040 LET A0$(6,4)=FNYEAR_YY21$(YR)+STR(MON:"00")
3050 IF R2>0 THEN IF A0$(6,4)>R3$+STR(R2:"00") THEN IF OPCODE$<>"" THEN GOTO 4
3050:000 ELSE READ (WOM04_DEV,KEY=A0$(1,5)+$FF$,DOM=3100)
3060 IF R2=0 AND OPCODE$<>"" AND A0$(6,4)>L_DATE$ THEN GOTO 4000
3070 IF R2=0 AND OPCODE$="" THEN GOTO 3200
3090 GOTO 1000
3100 REM " --- Get next Op Code when printing all Codes with specific end date
3100:"
3110 LET K$=KEY(WOM04_DEV,END=4000)
3120 IF POS(N0$=K$)<>1 THEN GOTO 4000
3130 LET A0$=K$
3190 GOTO 0800
3200 REM " --- Get next record when printing all codes with last date"
3210 LET K$=KEY(WOM04_DEV,END=4000)
3220 IF POS(N0$=K$)<>1 THEN GOTO 4000
3230 IF K$(3,3)=A0$(3,3) THEN GOTO 1000
3240 LET A0$=K$
3250 IF R0>0 THEN LET A0$(6,4)=R1$+STR(R0:"00")
3290 GOTO 1000
4000 REM " --- End
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],5,PAGE,80,F0$(7,3),CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
