0010 REM "WOX - Rebuild Work Order Employee Cross-References"
0020 REM "Program WOX.KA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=3
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOM-01",FILES$[2]="SYS-01"
0150 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOM01_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLISTs"
0210 WOM01A: IOLIST A0$,A1$
0220 WOM03B: IOLIST CROSSREF$
0260 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 IF P3$(4,1)<>"Y" THEN GOTO 0800
0450 DIM MESS$[1]
0460 LET MESS$[0]="Payroll Processing Is Installed.  Please Rebuild Employee"
0465 LET MESS$[1]="Cross Reference From Payroll. <Enter> To Continue:"
0470 CALL "SYC.XA",3,MESS$[ALL],1,-1,-1,V$,V3
0480 GOTO 9900
0500 REM " --- Initialize Data "
0510 DIM CROSSREF$(22)
0600 REM " --- Additional File Opens "
0610 LET FILES$[3]="WOM-03",OPTIONS$[3]="C"
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET WOM03_DEV=CHANNELS[3]
0650 RETURN
0800 REM " --- Options
0810 LET V4$="Rebuild Work Order Employee Cross Reference For Firm "+N0$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Clear Files"
0910 GOSUB 0600
0990 CALL "SYC.NB","Updating",12,COLUMN
1000 REM " --- Employee X-Refs"
1090 READ (WOM01_DEV,KEY="",DOM=1100)
1100 REM " --- Main Read"
1120 READ (WOM01_DEV,END=4000)IOL=WOM01A
1140 IF A1$="" THEN GOTO 1100
1160 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3,9),
1170 IF A0$(1,2)<>N0$ THEN GOTO 9900
1215 LET CROSSREF$=A0$(1,2)+"B"+A1$(31,10)+A0$(3,9)
1220 WRITE (WOM03_DEV,KEY=CROSSREF$)IOL=WOM03B
1900 GOTO 1100
4000 REM " --- End"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
