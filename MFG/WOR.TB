0010 REM "WOR - Bottleneck Analysis Report"
0020 REM "Program: WOR.TB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 ARM09A: IOLIST *,NAME$(1)
0220 BMM08A: IOLIST X0$(1)
0230 WOE01A: IOLIST C0$,C1$,C[ALL]
0240 WOE02A: IOLIST D0$,D1$,D[ALL]
0250 WOM02A: IOLIST X0$(1)
0260 WOM04A: IOLIST B0$,B1$,B2$,B[ALL]
0270 WOM05A1: IOLIST A0$,A1$,A2$,A[ALL]
0280 WOM05A2: IOLIST Z0$,Z1$,Z2$,Z[ALL]
0290 WOT01A: IOLIST W0$,W1$,W[ALL]
0300 ARM01A: IOLIST *,NAME$(1)
0500 REM " --- Init Data
0510 LET HEADINGS=5
0520 DIM W[11],T[2],X0$(30),HEADINGS$[HEADINGS],W$[9]
0530 IF OPFROM$="" THEN LET H4$="The First Op Code thru " ELSE LET H4$="From O
0530:p Code "+OPFROM$+" thru "
0540 IF OPTHRU$="" THEN LET H4$=H4$+"The Last Op Code " ELSE LET H4$=H4$+"Op C
0540:ode "+OPTHRU$+" "
0550 IF DATEFROM$="" THEN LET H4$=H4$+"From The First Date thru " ELSE LET H4$
0550:=H4$+"From "+FNB$(DATEFROM$)+" thru "
0560 IF DATETHRU$="" THEN LET H4$=H4$+"The Last Date" ELSE LET H4$=H4$+FNB$(DA
0560:TETHRU$)
0565 LET H5$="Based On Utilization Percent Of "+STR(UTILPERC:"##0.0")
0570 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$
0580 LET HEADINGS$[3]=H4$,HEADINGS$[4]=H5$,H0=132,H=0,WHEN$=F0$(7,3),CLOCK$=""
0600 REM " --- Open Printer
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS THEN GOTO 9900
0700 REM " --- Background
0720 CALL "SYC.NB","Calculating:",34,COLUMN
0730 PRINT 'SB',@(COLUMN+13,11),"Printing:",'SF',
0740 LET COLUMN1=COLUMN+23
1000 REM " --- Init Read
1020 READ (WOM05_DEV,KEY=N0$+OPFROM$,DOM=1100)
1100 REM " --- Main Read
1120 LET K5$=KEY(WOM05_DEV,END=4000)
1140 IF POS(N0$=K5$)<>1 THEN GOTO 4000
1150 IF OPTHRU$<>"" THEN IF K5$(3,3)>OPTHRU$ THEN GOTO 4000
1160 IF DATEFROM$<>"" THEN IF K5$(6,3)<DATEFROM$ THEN READ (WOM05_DEV,KEY=K5$(
1160:1,5)+DATEFROM$,DOM=1120)
1170 IF DATETHRU$<>"" THEN IF K5$(6,3)>DATETHRU$ THEN READ (WOM05_DEV,KEY=K5$(
1170:1,8)+$FF$,DOM=1120)
1180 READ (WOM05_DEV,KEY=K5$)IOL=WOM05A1
1200 REM " --- Breaks
1210 PRINT @(COLUMN,11),A0$(3,3)," ",FNB$(A0$(6,3)),
1220 IF OPCODE$="" THEN LET OPCODE$=A0$(3,3),TRANSDATE$=A0$(6,3)
1230 IF OPCODE$<>A0$(3,3) THEN GOSUB 2000; LET L=L9+1
1240 IF TRANSDATE$<>A0$(6,3) THEN GOSUB 2000
1400 REM " --- Additional Reads
1410 FIND (WOE01_DEV,KEY=N0$+"  "+A0$(9,7),DOM=1100)IOL=WOE01A
1420 IF POS(C0$(15,1)="QC")>0 THEN READ (WOM05_DEV,KEY=A0$(1,15)+$FF$,DOM=1100
1420:)
1500 REM " --- Include?
1510 GOSUB 6100
1515 IF OPIND$="" THEN GOTO 1100
1520 IF A0$(16,3)<NOW_SEQ$ THEN GOTO 1100
1900 REM " --- Add It Here
1905 IF NUM(C1$(97,1))=0 THEN LET C1$(97,1)="9"
1910 LET W$[NUM(C1$(97,1))]=W$[NUM(C1$(97,1))]+A0$(3,16)
1930 LET T0=T0+A[1]+A[2]
1990 GOTO 1100
2000 REM " --- Print Data
2010 IF T0=0 THEN GOTO 2900
2020 DIM B[31]
2030 FIND (WOM04_DEV,KEY=N0$+OPCODE$+FNYY_YY21$(FNA$(TRANSDATE$(1,1),TRANSDATE
2030:$))+FNA$(TRANSDATE$(2,1),TRANSDATE$),DOM=2031)IOL=WOM04A
2040 LET S_DAY=NUM(FNA$(TRANSDATE$(3,1),TRANSDATE$))
2050 IF B[S_DAY]<0 THEN LET S_HRS=0 ELSE LET S_HRS=B[S_DAY]
2060 IF S_HRS>0 THEN IF 100*T0/S_HRS<UTILPERC THEN GOTO 2900
2080 IF L+5>L9 THEN LET L=L9+1
2090 GOSUB 5200
2100 FOR X=1 TO 9
2110 IF LEN(W$[X])=0 THEN GOTO 2290
2120 PRINT @(COLUMN1,11),W$[X](1,3)," ",FNB$(W$[X](4,3)),
2130 FOR Y=1 TO LEN(W$[X]) STEP 16
2140 READ (WOM05_DEV1,KEY=N0$+W$[X](Y,16))IOL=WOM05A2
2150 READ (WOE01_DEV,KEY=N0$+"  "+Z0$(9,7),DOM=2151)IOL=WOE01A
2160 IF P3$(2,1)<>"Y" THEN GOTO 2200
2170 DIM NAME$(30)
2180 READ (ARM01_DEV,KEY=N0$+C0$(34,6),DOM=2181)IOL=ARM01A
2200 IF L+2>L9 THEN GOSUB 5200
2210 PRINT (7)@(1),C1$(97,1),@(4),Z0$(9,7),@(12),Z0$(16,3),@(17),C0$(14,1),@(2
2210:0),
2220 IF C0$(14,1)="I" THEN PRINT (7)C0$(56), ELSE PRINT (7)C1$(1,20),
2230 PRINT (7)@(41),FNB$(C0$(19,3)),@(50),FNB$(C0$(25,3)),@(59),FNB$(C0$(22,3)
2230:),@(70),C1$(98,1),@(74),NOW_OP$,@(79),Z[3]:M3$,@(85),Z[1]:M1$,Z[2]:M1$,Z[
2230:1]+Z[2]:M1$
2240 PRINT (7)@(20),NAME$,@(74),FROM_OP$,@(79),Z[0]:M3$
2250 LET L=L+2
2260 LET T[1]=T[1]+Z[1],T[2]=T[2]+Z[2]
2280 NEXT Y
2290 NEXT X
2300 REM " --- End of Day
2310 IF L+3>L9 THEN GOSUB 5200
2320 PRINT (7)@(85),DASHES$(1,3*M1)
2330 PRINT (7)@(4),"Total For Date: ",FNB$(TRANSDATE$),@(85),T[1]:M1$,T[2]:M1$
2330:,T[1]+T[2]:M1$,S_HRS:M1$,@(118),S_HRS-(T[1]+T[2]):M1$,
2340 IF S_HRS=0 THEN PRINT (7)"*****" ELSE PRINT (7)100*(T[1]+T[2])/S_HRS:M2$
2350 PRINT (7)""
2360 LET L=L+3
2900 REM " --- Reset for next day
2970 DIM W$[9],T[2]
2980 LET OPCODE$=A0$(3,3),TRANSDATE$=A0$(6,3),T0=0
2990 RETURN
4000 REM " --- End Of File Here
4005 IF LEN(A0$)=0 THEN GOSUB 5000; GOTO 4100
4010 GOSUB 2000
4100 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5020 IF P3$(1,1)="Y" THEN FIND (BMM08_DEV,KEY=N0$+OPCODE$,DOM=5025)IOL=BMM08A
5025 IF P3$(1,1)<>"Y" THEN FIND (WOM02_DEV,KEY=N0$+OPCODE$,DOM=5030)IOL=WOM02A
5030 LET HEADINGS$[2]=OPCODE$+" "+FNP$(X0$(6,20))
5040 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5050 IF STATUS>0 THEN EXITTO 9900
5080 PRINT (7)""
5100 PRINT (7)@(0),"Pri W.O.",@(20),"Description",@(44),"Est",@(53),"Act",@(61
5100:),"Est",@(68),"Schd Now At  Move",@(88),"Setup",@(98),"Run",@(104),"Total
5100:",@(111),"Avail"
5110 PRINT (7)@(0),"Cod Number",@(12),"Seq Cat Customer",@(43),"Start",@(52),"
5110:Start",@(59),"Complete",@(68),"Mthd  From  Queue",@(89),"Time",@(97),"Tim
5110:e",@(105),"Time",@(112),"Time",@(121),"Diff",@(128),"Pct"
5140 LET L=L+3
5190 RETURN
5200 REM " --- Sub Heading
5210 IF L+3>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)@(0),"Date: ",FNB$(TRANSDATE$)
5240 PRINT (7)""
5250 LET L=L+3
5290 RETURN
6100 REM " --- Find Current Location of W.O.
6110 LET OPIND$="",X=0,FROM_OP$="",NOW_OP$=""
6120 DIM OPS$[NO_OPS,1],OPS[NO_OPS,3]
6130 READ (WOE02_DEV,KEY=A0$(1,2)+"  "+A0$(9,7)+"A",DOM=6131)
6200 REM " --- Read Loop for Standards
6210 LET K4$=KEY(WOE02_DEV,END=6290)
6220 IF POS(A0$(1,2)+"  "+A0$(9,7)=K4$)<>1 THEN GOTO 6290
6230 READ (WOE02_DEV,KEY=K4$)IOL=WOE02A
6240 IF D[2]=0 THEN GOTO 6200; REM "Must have hours to work!
6250 LET OPIND$=OPIND$+D0$(13,3),OPS$[X,0]=D1$(1,3),OPS$[X,1]=D1$(4,3),OPS[X,0
6250:]=D[2],OPS[X,2]=D[8]
6260 LET X=X+1
6270 GOTO 6200
6280 IF OPIND$="" THEN GOTO 6590; REM "No labor required!
6290 READ (WOT01_DEV,KEY=A0$(1,2)+"  "+A0$(9,7),DOM=6300)
6300 REM " --- Read Loop for Actuals
6310 LET K5$=KEY(WOT01_DEV,END=6400)
6320 IF POS(A0$(1,2)+"  "+A0$(9,7)=K5$)<>1 THEN GOTO 6400
6330 READ (WOT01_DEV,KEY=K5$)IOL=WOT01A
6350 LET X=POS(W1$(1,3)=OPIND$,3)
6360 IF X=0 THEN GOTO 6300
6370 LET X=INT(X/3)
6380 LET OPS[X,1]=OPS[X,1]+W[0]+W[6],OPS[X,3]=OPS[X,3]+W[5]
6390 GOTO 6300
6400 REM " --- Setup Current Op Code
6410 LET NOW=0
6420 FOR X=0 TO LEN(OPIND$)/3-1
6430 IF OPS[X,1]>0 THEN LET NOW=X
6435 IF OPS[X,3]>=C[0] THEN LET NOW=X+1
6440 NEXT X
6450 IF OPS[NOW,1]>=OPS[NOW,0] THEN LET NOW=NOW+1
6460 IF NOW>LEN(OPIND$)/3-1 THEN GOTO 6590
6470 LET NOW_SEQ$=OPIND$(NOW*3+1,3)
6500 REM " --- Now setup NOW_OP$ and FROM_OP$
6530 LET NOW_OP$=OPS$[NOW,0]
6540 IF NOW>0 THEN LET FROM_OP$=OPS$[NOW-1,0] ELSE LET FROM_OP$=OPS$[NOW,0]
6590 RETURN
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
