0010 REM "MPR - Resource Forecast Listing (Report Overlay)"
0020 REM "Program MPR.HB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 MPE02A: IOLIST A0$,A[ALL]
0230 BMM08A: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM A[MP-1],B[MP-1]
0560 LET PREFIX$=FNP$(N0$+FORECAST$+WHSE$),NUMBER=ENDPER-BEGPER+2
0570 LET NUMCOL=4,BEGPER=BEGPER-1,ENDPER=ENDPER-1,WIDTH=80
0575 IF NUMCOL>NUMBER THEN LET NUMCOL=NUMBER
0590 CALL "SYC.RM",M4$,M4,10,10
0600 REM " --- Titles/Headings"
0610 DIM H9$((NUMCOL+1)*M4-1,"-"),DATES$[NUMBER]
0620 DIM AMOUNTS[NUMBER],TOTALS[NUMBER]
0640 LET X$=" Hours ",H9$(FNC(X$,LEN(H9$)),LEN(X$))=X$,N=0
0650 FOR X=BEGPER TO ENDPER
0660 LET N=N+1,DATES$[N]=FNB6$(S2$(X*6+1,6))
0670 NEXT X
0680 LET DATES$[NUMBER]="   Totals"
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",3,COLUMN
0900 REM " --- Position file"
0990 READ (MPE02_DEV,KEY=PREFIX$,DOM=1000)
1000 REM " --- Read next MPE-03 record"
1010 READ (MPE02_DEV,END=4000)IOL=MPE02A
1020 IF POS(PREFIX$=A0$)<>1 THEN GOTO 4000
1050 PRINT @(COLUMN,11),A0$(8,3),
1200 REM " --- Additional reads"
1210 DIM X0$(32)
1220 LET X0$(1)=N0$+A0$(8,3),X0$(6,20)="Unknown Operation"
1230 FIND (BMM08_DEV,KEY=X0$(1,5),DOM=1240)IOL=BMM08A
2000 REM " --- Build detail"
2010 DIM AMOUNTS[NUMBER]
2050 LET N=0,LAST=0
2100 REM 
2110 FOR X=BEGPER TO ENDPER
2120 LET N=N+1,AMOUNTS[N]=A[X]
2130 NEXT X
2200 REM " --- Accumulate operation totals"
2210 FOR X=1 TO NUMBER-1
2220 LET AMOUNTS[NUMBER]=AMOUNTS[NUMBER]+AMOUNTS[X]
2230 NEXT X
2500 REM " --- Print detail"
2510 LET FIRST=LAST+1,LAST=FIRST+NUMCOL-1
2520 IF FIRST>NUMBER THEN GOTO 3000
2530 IF LAST>NUMBER THEN LET LAST=NUMBER
2540 GOSUB 5500
2590 GOTO 2500
3000 REM " --- Accumulate report totals"
3010 FOR X=1 TO NUMBER
3020 LET TOTALS[X]=TOTALS[X]+AMOUNTS[X]
3030 NEXT X
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 DIM X0$(32)
4020 LET X0$(6,20)="Totals For Report",LAST=0
4030 FOR X=1 TO NUMBER
4040 LET AMOUNTS[X]=TOTALS[X]
4050 NEXT X
4100 REM " --- Print totals"
4110 LET FIRST=LAST+1,LAST=FIRST+NUMCOL-1
4120 IF FIRST>NUMBER THEN GOTO 4900
4130 IF LAST>NUMBER THEN LET LAST=NUMBER
4140 GOSUB 5500
4190 GOTO 4100
4900 REM 
4990 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+4
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)P9$,@(WIDTH-LEN(H6$)),H6$
5050 PRINT (7)""
5060 PRINT (7)"Operation"
5070 PRINT (7)"Code Description",@(30),H9$
5090 RETURN
5500 REM " --- Print detail block"
5510 DIM BUFFER$(WIDTH)
5520 LET L=L+3+LINES,N=41,BUFFER$(1)=X0$(3,3),BUFFER$(6)=X0$(6,20)
5530 FOR X=FIRST TO LAST
5540 LET BUFFER$(N)=DATES$[X],N=N+M4
5550 NEXT X
5560 IF L>L9 THEN GOSUB 5000
5570 PRINT (7)""
5580 PRINT (7)BUFFER$
5590 DIM X0$(32)
5600 REM 
5610 DIM BUFFER$(WIDTH)
5620 LET N=40,BUFFER$(30)="Capacity"
5630 FOR X=FIRST TO LAST
5640 LET BUFFER$(N)=STR(AMOUNTS[X]:M4$),N=N+M4
5650 NEXT X
5660 PRINT (7)BUFFER$
5690 RETURN
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8060 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
