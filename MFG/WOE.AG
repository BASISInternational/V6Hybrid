0010 REM "WOS - Work Order entry - Status Summary"
0020 REM "Program:  WOE.AG"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0100 REM " --- Open/Lock Files
0200 REM " --- IOLISTs"
0210 WOE02A: IOLIST W0$(1),W1$(1),W[ALL]
0220 WOTRAN: IOLIST R0$,R1$,R[ALL]
0500 REM " --- Init Data
0510 LET TRAN_PASS$="0"
0511 IF A0$(15,1)="C" AND A0$(31,3)<=G9$ THEN LET WO_STATUS$="0"
0512 IF A0$(15,1)<>"C" OR A0$(31,3)>G9$ THEN LET WO_STATUS$="1"
0520 LET L0=11,L1=L0+1,L9=20
0540 DIM W0$(15),W1$(151),W[13],R[11]
0560 LET W0$(1)=A0$(1,11)+"A",L=L1
0570 LET M6$="##0.00-",M6=LEN(M6$)
0580 LET M9$="####.00-",M9=LEN(M9$); REM "% VARIANCE MASK
0590 LET M3=LEN(M3$)
0595 LET I9=NUM(P9$(26,2))
0700 REM " --- Background
0710 PRINT @(0,L0),'CE',
0720 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0730 PRINT 'SB',@(2,L0-1),"Job Status Summary",@(79-M6*7-M9),"+",@(79-M6*6-M9-
0730:4),"Date Req",@(79-M6*5-M9),"+",@(79-M6*4-M9-4),"Standard",@(79-M6*3-M9),
0730:"+",@(79-M6*2-M9-3),"Actual",@(79-M6-M9),"+",@(79-M6-6),"% Vnce",@(79-M6)
0730:,"+",@(79-6),"Cmp Qty",@(79-M6*4-M9-5,L0),"Setup",@(79-M6*3-M9-3),"Run",@
0730:(79-M6*2-M9-5),"Setup",@(79-M6-M9-3),"Run",'SF',
1000 REM " --- Calc
1010 LET X0$="",T0=0,T1=0,T2=0,T3=0,T4=0,T5=0,T6=0,T7=0,S0=0,S1=0,S2=0,S3=0,Z0
1010:=0,Z1=0,X1$="",X2$="",WARN$="",X3$=""
1020 DIM X0[I9],X1[I9],X2[I9],X3[I9],X4[I9],X5[I9],X6[I9],X7[I9],Z0[I9],Z1[I9]
1020:,Z2[I9]
1100 REM " --- Calc Standards - Operations
1110 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=1120)
1120 LET K$=KEY(WOE02_DEV,END=1300)
1130 IF POS(K$(1,11)=A0$(1,11))<>1 THEN GOTO 1300
1140 READ (WOE02_DEV,KEY=K$)IOL=WOE02A
1150 IF W1$(91,1)<>"S" THEN GOTO 1120
1160 LET Y=POS(W0$(13,3)=X0$,3)
1170 IF Y=0 AND LEN(X0$)=I9*3+3 THEN GOTO 1180 ELSE GOTO 1230
1180 IF WARN$<>"" THEN GOTO 1220
1190 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Warning!! # Operations Parameter S
1190:hould Be Expanded!! <Enter>: ",V0=1,V1=69,V2=22
1200 PRINT @(V1,V2),'RB',
1210 GOSUB 7000
1220 LET Y=LEN(X0$),WARN$="Y"; GOTO 1250
1230 IF Y=0 THEN LET X0$=X0$+W0$(13,3),X1$=X1$+W1$(7,20),X2$=X2$+W1$(1,3),X3$=
1230:X3$+W1$(4,3); GOTO 1160
1240 LET X=INT(Y/3)
1250 LET X0[X]=X0[X]+W[3],X1[X]=X1[X]+W[2]-W[8]
1260 LET X4[X]=X4[X]+W[2]*W[6],X5[X]=X5[X]+W[3]-W[2]*W[6]
1270 LET Z0[X]=Z0[X]+W[8]
1280 GOTO 1120
1300 REM " --- Get Actuals "        
1310 GOSUB 5600
1400 REM " --- Now Display Units
1410 IF X0$="" THEN GOTO 4000
1420 LET L=L0+1
1430 PRINT @(0,L),"Operation Units:",
1440 LET L=L+1
1450 FOR Y=1 TO LEN(X0$)/3
1460 IF L>L9 THEN GOSUB 5000
1470 PRINT @(2,L),X2$(3*Y-2,3)," ",X1$(20*Y-19,20),
1480 LET X=INT((3*Y-2)/3)
1490 PRINT @(80-M6*5-M9-11),FNB$(X3$(X*3+1,3)),@(80-M6*5-M9),Z0[X]:M6$,X1[X]:M
1490:6$,Z1[X]:M6$,X3[X]:M6$,
1500 IF Z0[X]+X1[X]<>0 THEN LET Y0=(Z0[X]+X1[X]-(Z1[X]+X3[X]))*100/(Z0[X]+X1[X
1500:]) ELSE LET Y0=0
1510 PRINT @(80-M6-M9),Y0:M9$,Z2[X]:M6$,
1520 LET L=L+1,T0=T0+X0[X],T1=T1+X2[X],T2=T2+X1[X],T3=T3+X3[X]
1530 LET S0=S0+X4[X],S1=S1+X5[X],S2=S2+X6[X],S3=S3+X7[X]
1540 LET Z0=Z0+Z0[X],Z1=Z1+Z1[X]
1550 NEXT Y
1600 REM " --- Labor Unit Totals Here - Setup & Run
1610 IF L+2>L9 THEN GOSUB 5000
1620 LET L=L+1
1630 PRINT @(2,L),"Total Units:",@(80-M6*5-M9),Z0:M6$,T2:M6$,Z1:M6$,T3:M6$,
1640 IF Z0+T2<>0 THEN PRINT @(80-M6-M9),(Z0+T2-(Z1+T3))*100/(Z0+T2):M9$, ELSE 
1640:PRINT @(80-M6-M9),0:M9$,
4000 REM " --- All Done
4020 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> To Continue Or <F2> To Res
4020:tart: ",V0=1,V1=59,V2=22
4030 GOSUB 7000
4040 IF V3=2 THEN GOTO 0520
4100 REM " --- All Done!
4140 RUN "WOE.AA"
5000 REM " --- Next Screen Here
5020 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> To Continue Or <F4> To End
5020:: ",V0=1,V1=57,V2=22
5030 GOSUB 7000
5040 IF V3=4 THEN EXITTO 4100
5060 GOSUB 5400
5080 RETURN
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN
5600 REM " --- Calculate Actuals "
5620 LET TRAN_PASS$="0"
5630 GOSUB SELECT_TRANSACTION_FILE
5635 IF TRAN_PASS$="4" THEN GOTO 5780
5640 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=5650)
5650 DIM R[11]
5660 LET K7$=KEY(WOTRAN_DEV,END=5630)
5670 IF K7$(1,11)<>A0$(1,11) THEN GOTO 5630
5680 READ (WOTRAN_DEV,KEY=K7$)IOL=WOTRAN
5690 ON POS(R0$(15,1)="OMS") GOTO 5660,5700,5740,5760
5700 REM " --- Actual Operations
5710 LET Y=POS(R1$(1,3)=X0$,3),Y=INT(Y/3)
5720 LET X2[Y]=X2[Y]+R[2],X3[Y]=X3[Y]+R[0],X6[Y]=X6[Y]+R[0]*R[3],X7[Y]=X7[Y]+R
5720:[2]-R[0]*R[3],Z1[Y]=Z1[Y]+R[6],Z2[Y]=Z2[Y]+R[5]
5730 GOTO 5660
5740 LET T5=T5+R[2]
5750 GOTO 5660
5760 LET T7=T7+R[2]
5770 GOTO 5660
5780 PRINT @(0,L),'CL'
5790 RETURN
6000 REM " --- Select Transaction File "
6010 SELECT_TRANSACTION_FILE:
6020 ON POS(WO_STATUS$="01") GOTO DONE,CLOSED_WO,OPEN_WO
6030 CLOSED_WO:
6040 ON POS(TRAN_PASS$="0123") GOTO DONE,FILE_03,FILE_23,FILE_33,DONE
6050 FILE_03:
6060 LET WOTRAN_DEV=WOT03_DEV
6070 LET TRAN_PASS$="1"
6080 GOTO 6160
6090 FILE_23:
6100 LET WOTRAN_DEV=WOT23_DEV
6110 LET TRAN_PASS$="2"
6120 GOTO 6160
6130 FILE_33:
6140 LET WOTRAN_DEV=WOT33_DEV
6150 LET TRAN_PASS$="3"
6160 RETURN
6200 OPEN_WO:
6210 ON POS(TRAN_PASS$="0123") GOTO DONE,FILE_01,FILE_21,FILE_31,DONE
6220 FILE_01:
6230 LET WOTRAN_DEV=WOT01_DEV
6240 LET TRAN_PASS$="1"
6250 GOTO 6330
6260 FILE_21:
6270 LET WOTRAN_DEV=WOT21_DEV
6280 LET TRAN_PASS$="2"
6290 GOTO 6330
6300 FILE_31:
6310 LET WOTRAN_DEV=WOT31_DEV
6320 LET TRAN_PASS$="3"
6330 RETURN
6400 DONE:
6405 LET TRAN_PASS$="4"
6410 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 5000
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
