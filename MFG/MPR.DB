0010 REM "MPR - Planned Resources Report (Report Overlay)"
0020 REM "Program MPR.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 CALL "MPC.FA",MPE04_DEV,"MPE-04",N0$,FORECAST$,FILENAME$,STATUS
0115 IF STATUS THEN GOTO 9900
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 MPE02A: IOLIST A0$,A[ALL]
0220 MPE04A: IOLIST B0$,B[ALL]
0230 BMM08A: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM A[MP-1],B[MP-1]
0550 LET HEADINGS$[2]=H5$
0560 LET PREFIX$=FNP$(N0$+FORECAST$+WHSE$),NUMBER=ENDPER-BEGPER+2
0570 LET LINES=2,MAXCOL=9,NUMCOL=MAXCOL,BEGPER=BEGPER-1,ENDPER=ENDPER-1
0575 IF NUMCOL>NUMBER THEN LET NUMCOL=NUMBER
0590 CALL "SYC.RM",M4$,M4,10,10
0600 REM " --- Titles/Headings"
0610 DIM TITLES$[LINES],H9$((MAXCOL+1)*M4-1,"-")
0615 DIM TOTALS[NUMBER,LINES],AMOUNTS[NUMBER,LINES],DATES$[NUMBER]
0620 LET TITLES$[1]=" Required",TITLES$[0]=" Capacity"
0630 LET TITLES$[2]="Available",X$=" Hours "
0640 LET H9$(FNC(X$,LEN(H9$)),LEN(X$))=X$,N=0
0650 FOR X=BEGPER TO ENDPER
0660 LET N=N+1,DATES$[N]=FNB6$(S2$(X*6+1,6))
0670 NEXT X
0680 LET DATES$[NUMBER]="   Totals"
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",3,COLUMN
0900 REM " --- Position file"
0990 READ (MPE02_DEV,KEY=PREFIX$,DOM=1000)
1000 REM " --- Read next MPE-02 record"
1010 READ (MPE02_DEV,END=4000)IOL=MPE02A
1020 IF POS(PREFIX$=A0$)<>1 THEN GOTO 4000
1050 PRINT @(COLUMN,11),A0$(8,3),
1200 REM " --- Additional reads"
1210 DIM B[MP-1],X0$(32)
1220 LET X0$(1)=N0$+A0$(8,3),X0$(6,20)="Unknown Operation"
1230 FIND (MPE04_DEV,KEY=A0$,DOM=1240)IOL=MPE04A
1240 FIND (BMM08_DEV,KEY=X0$(1,5),DOM=1250)IOL=BMM08A
2000 REM " --- Build detail"
2010 DIM AMOUNTS[NUMBER,LINES]
2050 LET N=0,LAST=0
2100 REM 
2110 FOR X=BEGPER TO ENDPER
2120 LET N=N+1,AMOUNTS[N,0]=A[X],AMOUNTS[N,1]=B[X]
2130 LET AMOUNTS[N,LINES]=AMOUNTS[N,0]-AMOUNTS[N,1]
2190 NEXT X
2200 REM " --- Accumulate operation totals"
2210 FOR Y=0 TO LINES
2220 FOR X=1 TO NUMBER-1
2230 LET AMOUNTS[NUMBER,Y]=AMOUNTS[NUMBER,Y]+AMOUNTS[X,Y]
2240 NEXT X
2250 NEXT Y
2500 REM " --- Print detail"
2510 LET FIRST=LAST+1,LAST=FIRST+NUMCOL-1
2520 IF FIRST>NUMBER THEN GOTO 3000
2530 IF LAST>NUMBER THEN LET LAST=NUMBER
2540 GOSUB 5500
2590 GOTO 2500
3000 REM " --- Accumulate report totals"
3010 FOR Y=0 TO LINES
3020 FOR X=1 TO NUMBER
3030 LET TOTALS[X,Y]=TOTALS[X,Y]+AMOUNTS[X,Y]
3040 NEXT X
3050 NEXT Y
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 DIM X0$(32)
4020 LET X0$(6,20)="Totals For Report",LAST=0
4030 FOR Y=0 TO LINES
4040 FOR X=1 TO NUMBER
4050 LET AMOUNTS[X,Y]=TOTALS[X,Y]
4060 NEXT X
4070 NEXT Y
4100 REM " --- Print totals"
4110 LET FIRST=LAST+1,LAST=FIRST+NUMCOL-1
4120 IF FIRST>NUMBER THEN GOTO 4900
4130 IF LAST>NUMBER THEN LET LAST=NUMBER
4140 GOSUB 5500
4190 GOTO 4100
4900 REM 
4990 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+6
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)P9$,@(WIDTH/2-INT(LEN(H6$)/2)),H6$
5050 PRINT (7)""
5060 PRINT (7)"Operation"
5070 PRINT (7)"Code Description",@(30),H9$
5090 RETURN
5500 REM " --- Print detail block"
5510 DIM BUFFER$(WIDTH)
5520 LET L=L+3+LINES,N=41,BUFFER$(1)=X0$(3,3),BUFFER$(6)=X0$(6,20)
5530 FOR X=FIRST TO LAST
5540 LET BUFFER$(N)=DATES$[X],N=N+M4
5550 NEXT X
5560 IF L+3>L9 THEN GOSUB 5000
5570 PRINT (7)""
5580 PRINT (7)BUFFER$
5590 DIM X0$(32)
5600 REM 
5610 FOR Y=0 TO LINES
5620 DIM BUFFER$(WIDTH)
5630 LET N=40,BUFFER$(30)=TITLES$[Y]
5640 FOR X=FIRST TO LAST
5650 LET BUFFER$(N)=STR(AMOUNTS[X,Y]:M4$),N=N+M4
5660 NEXT X
5670 PRINT (7)BUFFER$
5680 NEXT Y
5690 RETURN
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8060 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=FNP$(Q$(POS(" "<>Q$)))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
