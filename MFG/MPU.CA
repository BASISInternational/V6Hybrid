0010 REM "MPU - Purchase Requisition Update"
0020 REM "Program MPU.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLIST's"
0210 MPW04A: IOLIST A0$(1),A1$(1),A[ALL]
0220 POE01A: IOLIST E0$(1),E1$(1),E2$,E3$,E[ALL]
0230 POE11A: IOLIST D0$(1),D1$(1),D2$(1),D3$,D4$,D5$,D6$,D[ALL]
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM A0$(39),A1$(35),A[6],D0$(18),D1$(48),D2$(32),CHANS[11]
0520 DIM E0$(16),E1$(160),E2$(1),E3$(1),E[8]
0550 LET CHANS[1]=APM02_DEV,CHANS[2]=POE01_DEV,CHANS[3]=POE02_DEV
0560 LET CHANS[4]=POE11_DEV,CHANS[5]=POE52_DEV,CHANS[6]=POM02_DEV
0570 LET CHANS[7]=POS10_DEV,CHANS[8]=POE21_DEV,CHANS[9]=POE31_DEV
0580 LET CHANS[10]=POE41_DEV,CHANS[11]=SYS01_DEV
0800 REM " --- Options"
0810 LET V4$="Are You Ready to Update the Purchase Requisition Register"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0840 REM " --- Disallow 'M'enu option in Error Routine
0850 LET EXIT_CTRL=1
0900 REM " --- Position File"
0910 LOCK (MPW04_DEV)
0920 PRINT @(COLUMN-9,11),'SB',"Updating",'SF',J1$(1,P[0]+10),
0990 READ (POE01_DEV,KEY=N0$,DOM=1000)
1900 REM " --- Position file"
1910 PRINT @(COLUMN-9,11),'SB',"Updating",'SF',J1$(1,P[0]+10),
1920 LET LAST$="",ITEM$="",VENDOR$=""
1990 READ (MPW04_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next MPW-04 record"
2010 LET K$=KEY(MPW04_DEV,END=4000)
2020 IF POS(PREFIX$=K$)<>1 THEN GOTO 4000
2030 READ (MPW04_DEV)IOL=MPW04A
2040 IF K$(8,28)=LAST$ THEN GOTO 2200
2050 PRINT @(COLUMN,11),K$(8,AP)," ",K$(14,P[0]),
2060 LET LAST$=K$(8,28)
2200 REM " --- New Vendor?"
2210 IF K$(8,6)=VENDOR$ THEN GOTO 2500
2220 LET K$=K$(1,2)+K$(8,6),VENDOR$=""
2230 FIND (APM01_DEV,KEY=K$,DOM=3900)
2240 LET VENDOR$=K$(3)
2500 REM " --- Generate Purchase Requisition"
2510 DIM INFO$[14],INFO[2]
2520 LET INFO$[0]=N0$,INFO$[1]=A0$(8,6),INFO$[2]="S",INFO$[3]=A0$(6,2)
2530 LET INFO$[5]=FNYY_YY21$(FNM$(FNC$(A1$(3,3)))),INFO$[6]=A1$(1,2)
2540 LET INFO$[7]="M",INFO$[8]=A0$(3,3),INFO$[13]=A0$(14,20)
2550 LET INFO$[4]=F0$(18,6),INFO[0]=A[0],INFO[1]=A[1],INFO[2]=A[2]
2590 CALL "POC.BA",CHANS[ALL],INFO$[ALL],INFO[ALL],DETAIL_KEY$,STATUS
3900 REM " --- Loop back for next record"
3990 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END
