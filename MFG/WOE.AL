0010 REM "WOS - Work Order entry - Cost Summary"
0011 REM 0
0020 REM "Program:  WOE.AL"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0100 REM " --- Open/Lock Files
0110 GOSUB ASSIGN_WO_CHANNELS
0200 REM " --- IOLISTs"
0210 WOE02A: IOLIST W0$(1),W1$(1),W[ALL]
0220 WOE22A: IOLIST W0$(1),W1$(1),W[ALL]
0230 WOE32A: IOLIST W0$(1),W1$(1),W[ALL]
0240 WOTRAN: IOLIST R0$,R1$,R[ALL]
0500 REM " --- Initialize Data "
0505 LET LAST_PERIOD_END$=G9$
0510 LET L0=11,L1=L0+1,L9=20
0520 DIM W0$(15),W1$(151),W[13],R[11]
0530 LET W0$(1)=A0$(1,11)+"A",L=L1
0540 LET M9$="####.00-",M9=LEN(M9$); REM "% VARIANCE MASK
0550 LET M3=LEN(M3$)
0560 LET I9=NUM(P9$(26,2))
0580 LET RECORD$=" "
0590 LET X0$="",T0=0,T1=0,T4=0,T5=0,T6=0,T7=0,S0=0,S1=0,S2=0,S3=0,Z0=0,Z1=0,X1
0590:$="",X2$="",WARN$=""
0595 DIM X0[I9],X2[I9],X4[I9],X5[I9],X6[I9],X7[I9],Z0[I9],Z1[I9],Z2[I9]
0700 REM " --- Background
0705 PRINT @(0,L0),'CE',
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(2,L0-1),"Cost Summary",@(71-M3*5),"+",@(71-M3*4-5,L0-1),"Sta
0720:ndard",@(71-M3*3),"+",@(71-M3*2-3),"Actual",@(71-M3),"+",@(71-M3*4-7,L0),
0720:"Direct",@(71-M3*3-5),"Ovhd",@(71-M3*2-7),"Direct",@(71-M3-5),"Ovhd",@(71
0720:-9,L0-1),"Variance",@(71),"% Vn'ce"
0790 PRINT 'SF',
1000 REM " --- Calculate Standards "
1010 REM " --- Operations "
1020 READ (WOE02_DEV,KEY=A0$(1,11),DOM=1030)
1030 LET K$=KEY(WOE02_DEV,END=1200)
1040 IF K$(1,11)<>A0$(1,11) THEN GOTO 1200
1050 READ (WOE02_DEV,KEY=K$)IOL=WOE02A
1060 IF W1$(91,1)<>"S" THEN GOTO 1030
1070 LET Y=POS(W0$(13,3)=X0$,3)
1080 IF Y=0 AND LEN(X0$)=I9*3+3 THEN GOTO 1090 ELSE GOTO 1140
1090 IF WARN$<>"" THEN GOTO 1130
1100 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Warning!! # Operations Parameter S
1100:hould Be Expanded!! <Enter>: ",V0=1,V1=69,V2=22
1110 PRINT @(V1,V2),'RB',
1120 GOSUB 7000
1130 LET Y=LEN(X0$),WARN$="Y"; GOTO 1160
1140 IF Y=0 THEN LET X0$=X0$+W0$(13,3),X1$=X1$+W1$(7,20),X2$=X2$+W1$(1,3); GOT
1140:O 1070
1150 LET X=INT(Y/3)
1160 LET X0[X]=X0[X]+W[3]
1170 LET X4[X]=X4[X]+W[2]*W[6],X5[X]=X5[X]+W[3]-W[2]*W[6]
1180 LET Z0[X]=Z0[X]+W[8]
1190 GOTO 1030
1200 REM " --- Get Actuals "   
1210 GOSUB 6000
1300 REM " --- Calculate Unit Totals"
1310 IF X0$="" THEN GOTO 1900
1320 LET L=L0+1
1330 FOR Y=1 TO LEN(X0$)/3
1340 LET X=INT((3*Y-2)/3)
1350 LET T0=T0+X0[X],T1=T1+X2[X]
1360 LET S0=S0+X4[X],S1=S1+X5[X],S2=S2+X6[X],S3=S3+X7[X]
1370 LET Z0=Z0+Z0[X],Z1=Z1+Z1[X]
1380 NEXT Y
1400 REM " --- Labor Unit Totals Here - Setup & Run
1410 PRINT @(0,L),"Operations:"
1420 LET L=L+1
1430 REM " --- Now Display Dollars"
1440 FOR Y=1 TO LEN(X0$)/3
1450 IF L>L9 THEN GOSUB 4100
1460 PRINT @(2,L),X2$(3*Y-2,3)," ",X1$(20*Y-19,20),
1470 LET X=INT((3*Y-2)/3)
1480 PRINT @(71-M3*5),X4[X]:M3$,X5[X]:M3$,X6[X]:M3$,X7[X]:M3$,X0[X]-X2[X]:M3$,
1490 IF X0[X]<>0 THEN LET Y0=(X0[X]-X2[X])*100/X0[X] ELSE LET Y0=0
1500 PRINT @(71,L),Y0:M9$,
1510 LET L=L+1
1520 NEXT Y
1600 REM " --- Labor Totals Here - Direct & Overhead
1610 IF L+3>L9 THEN GOSUB 4100
1620 LET L=L+1
1630 PRINT @(57-M3*5,L),"Direct Labor: ",@(71-M3*5),S0:M3$,@(71-M3*3),S2:M3$,@
1630:(71-M3),S0-S2:M3$,
1640 IF S0<>0 THEN PRINT @(71,L),(S0-S2)*100/S0:M9$, ELSE PRINT @(71,L),0:M9$,
1650 LET L=L+1
1660 PRINT @(55-M3*5,L),"Overhead Labor: ",@(71-M3*4),S1:M3$,@(71-M3*2),S3:M3$
1660:,@(71-M3),S1-S3:M3$,
1670 IF S1<>0 THEN PRINT @(71,L),(S1-S3)*100/S1:M9$, ELSE PRINT @(71,L),0:M9$,
1680 GOSUB 4100
1690 PRINT @(0,L0),'CL',; LET L1=L1-1,L=L1
1800 REM " --- Labor Totals
1810 PRINT @(0,L),"Labor Totals: ",@(71-M3*4),T0:M3$,@(71-M3*2),T1:M3$,@(71-M3
1810:),T0-T1:M3$
1820 IF T0<>0 THEN PRINT @(71,L),(T0-T1)*100/T0:M9$ ELSE PRINT @(71,L),0:M9$
1830 IF A[0]<>1 AND A[0]<>0 THEN PRINT @(14,L+1),"Per Unit Total: ",@(71-M3*4)
1830:,T0/A[0]:M3$,@(71-M3*2),T1/A[0]:M3$,@(71-M3),(T0-T1)/A[0]:M3$,; LET L=L+1
1840 LET L=L+1
1900 REM " --- Calculate Standards "
1905 IF X0$="" THEN PRINT @(0,L0),'CL',; LET L1=L1-1,L=L1
1910 REM " --- Materials "
1920 READ (WOE22_DEV,KEY=A0$(1,11),DOM=1930)
1930 LET K$=KEY(WOE22_DEV,END=1980)
1940 IF K$(1,11)<>A0$(1,11) THEN GOTO 1980
1950 READ (WOE22_DEV,KEY=K$)IOL=WOE22A
1960 LET T4=T4+W[3]
1970 GOTO 1930
1980 IF L+1>L9 THEN GOSUB 4100
1990 PRINT @(0,L),"Materials: ",@(71-M3*4),T4:M3$,@(71-M3*2),T5:M3$,@(71-M3),T
1990:4-T5:M3$,
2000 IF T4<>0 THEN LET Y0=(T4-T5)*100/T4 ELSE LET Y0=0
2010 PRINT @(71,L),Y0:M9$
2020 IF A[0]<>0 AND A[0]<>1 THEN PRINT @(14,L+1),"Per Unit Total: ",@(71-M3*4)
2020:,T4/A[0]:M3$,@(71-M3*2),T5/A[0]:M3$,@(71-M3),(T4-T5)/A[0]:M3$,; LET L=L+1
2030 LET L=L+1,T0=T0+T4,T1=T1+T5
2100 REM " --- Calculate Standards "
2110 REM " --- Subcontracts "
2120 IF P9$(3,1)<>"Y" THEN GOTO 2250
2130 READ (WOE32_DEV,KEY=A0$(1,11),DOM=2140)
2140 LET K$=KEY(WOE32_DEV,END=2190)
2150 IF K$(1,11)<>A0$(1,11) THEN GOTO 2190
2160 READ (WOE32_DEV,KEY=K$)IOL=WOE32A
2170 LET T6=T6+W[3]
2180 GOTO 2140
2190 IF L>L9 THEN GOSUB 4100
2200 PRINT @(0,L),"Subcontracts:",@(71-M3*4),T6:M3$,@(71-M3*2),T7:M3$,@(71-M3)
2200:,T6-T7:M3$
2210 IF T6<>0 THEN LET Y0=(T6-T7)*100/T6 ELSE LET Y0=0
2220 PRINT @(71,L),Y0:M9$
2230 IF A[0]<>0 AND A[0]<>1 THEN PRINT @(14,L+1),"Per Unit Total: ",@(71-M3*4)
2230:,T6/A[0]:M3$,@(71-M3*2),T7/A[0]:M3$,@(71-M3),(T6-T7)/A[0]:M3$,; LET L=L+1
2240 LET L=L+1,T0=T0+T6,T1=T1+T7
2250 REM " --- Now Print Totals
2260 IF L+2>L9 THEN GOSUB 4100
2270 CALL "SYC.WC",1,0,M3,0,71-M3*4,L,0
2280 CALL "SYC.WC",1,0,80-(71-M3*2),0,71-M3*2,L,0
2290 PRINT @(19,L+1),"WO Totals: ",@(71-M3*4),T0:M3$,@(71-M3*2),T1:M3$,@(71-M3
2290:),T0-T1:M3$
2300 IF T0<>0 THEN LET Y0=(T0-T1)*100/T0 ELSE LET Y0=0
2310 PRINT @(71,L+1),Y0:M9$
2320 LET L=L+2
2400 REM " --- Unit Totals
2410 IF A[0]=1 OR A[0]=0 THEN GOTO 4000
2420 IF L+1>L9 THEN GOSUB 4100
2430 PRINT @(14,L),"Per Unit Total: ",@(71-M3*4),T0/A[0]:M3$,@(71-M3*2),T1/A[0
2430:]:M3$,@(71-M3),(T0-T1)/A[0]:M3$,
2440 IF T0<>0 THEN LET Y0=(T0-T1)*100/T0 ELSE LET Y0=0
2450 PRINT @(71,L),Y0:M9$
4000 REM " --- All Done
4010 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> To Continue Or <F2> To Res
4010:tart: ",V0=1,V1=59,V2=22
4020 GOSUB 7000
4030 IF V3=2 THEN GOTO 0510
4040 REM " --- All Done!
4050 RUN "WOE.AA"
4100 REM " --- Next Screen Here
4110 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> To Continue Or <F4> To End
4110:: ",V0=1,V1=57,V2=22
4120 GOSUB 7000
4130 IF V3=4 THEN EXITTO 4040
4140 GOSUB 5400
4150 RETURN
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN
6000 REM " --- Calculate Work Order Actuals "
6010 PRINT @(25,L),"Now Calculating Actuals...."
6020 LET RECORD$=" "
6030 GOSUB ASSIGN_CHANNEL
6040 IF RECORD$="DONE" THEN GOTO 6190
6050 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=6060)
6060 DIM R[11]
6070 LET K7$=KEY(WOTRAN_DEV,END=6030)
6080 IF K7$(1,11)<>A0$(1,11) THEN GOTO 6030
6090 READ (WOTRAN_DEV,KEY=K7$)IOL=WOTRAN
6100 ON POS(R0$(15,1)="OMS") GOTO 6070,6110,6150,6170
6110 REM " --- Actual Operations
6120 LET Y=POS(R1$(1,3)=X0$,3),Y=INT(Y/3)
6130 LET X2[Y]=X2[Y]+R[2],X6[Y]=X6[Y]+(R[0]+R[6])*R[3],X7[Y]=X7[Y]+R[2]-(R[0]+
6130:R[6])*R[3],Z1[Y]=Z1[Y]+R[6],Z2[Y]=Z2[Y]+R[5]
6140 GOTO 6070
6150 LET T5=T5+R[2]
6160 GOTO 6070
6170 LET T7=T7+R[2]
6180 GOTO 6070
6190 PRINT @(0,L),'CL'
6200 RETURN
6300 REM " --- Assign Proper WO Transaction File "
6330 ASSIGN_CHANNEL:
6360 ON POS(RECORD$="OMS") GOTO WO_OPERATIONS,WO_MATERIALS,WO_SUBCONTRACTS,DON
6360:E
6370 WO_OPERATIONS:
6380 LET WOTRAN_DEV=OPERATIONS_DEV
6390 LET RECORD$="O"
6400 GOTO 6480
6410 WO_MATERIALS:
6420 LET WOTRAN_DEV=MATERIALS_DEV
6430 LET RECORD$="M"
6440 GOTO 6480
6450 WO_SUBCONTRACTS:
6460 LET WOTRAN_DEV=SUBCONTRACTS_DEV
6470 LET RECORD$="S"
6480 RETURN
6640 DONE:
6650 LET RECORD$="DONE"
6660 GOTO 6480
6700 REM " --- Assign Proper WO Transaction File "
6710 REM " --- Open/Planned WOs = WOT-01/WOT-21/WOT-32
6720 REM " --- Closed WOs = WOT-03/WOT-23/WOT-33
6730 ASSIGN_WO_CHANNELS:
6740 ON POS(A0$(15,1)="QPOC") GOTO 9900,NOT_CLOSED,NOT_CLOSED,NOT_CLOSED,CLOSE
6740:D
6750 NOT_CLOSED:
6760 LET OPERATIONS_DEV=WOT01_DEV
6770 LET MATERIALS_DEV=WOT21_DEV
6780 LET SUBCONTRACTS_DEV=WOT31_DEV
6790 GOTO 6850
6800 CLOSED:
6805 REM " --- Is WO Close Date in a Closed Period? If no use "Open" files."
6810 IF LAST_PERIOD_END$<=A0$(31,3) THEN GOTO 6760
6820 LET OPERATIONS_DEV=WOT03_DEV
6830 LET MATERIALS_DEV=WOT23_DEV
6840 LET SUBCONTRACTS_DEV=WOT33_DEV
6850 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 4040
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
