0010 REM "WOE - Dispatch Inquiry"
0020 REM "Program WOE.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOM-05"
0125 LET FILES$[4]="WOM-04",FILES$[5]="WOT-01",FILES$[6]="SYS-01"
0130 LET FILES$[7]="WOE-03",FILES$[8]=""
0150 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOM05_DEV=CHANNELS[3]
0165 LET WOM04_DEV=CHANNELS[5],WOT01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0170 LET WOE03_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 WOE02A: IOLIST B0$(1),B1$(1),B[ALL]
0230 WOM04A: IOLIST C0$(1),C1$,C2$,C[ALL]
0240 WOM05A: IOLIST L0$,L1$,L2$,L[ALL]
0250 OPCODEA: IOLIST D0$(1),D[ALL]
0260 WOT01A: IOLIST W0$(1),W1$(1),W[ALL]
0270 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0290 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[5],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$,BM$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 LET OPNMAX=NUM(P3$(26,2))
0430 LET BM$=P3$(1,1)
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(7,2)),P[2]=NUM(P2$(5,1))
0445 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0450 IF BM$<>"Y" THEN GOTO 0500
0455 CALL "SYC.VA","BM",INFO$[ALL]
0460 LET BM$=INFO$[20]
0500 REM " --- Initializations"
0510 DIM A0$(76),A1$(138),A[11],B0$(15),B1$(151),B[13],J1$(80)
0520 DIM C0$(9),C[31],D0$(90),D[13],L[9],BLANK$(30)
0530 LET OPERATION$="",L0=10,PR_CODE$="9"
0540 LET M2=LEN(M2$),M3=LEN(M3$),MINMASK=MAX(M2,M3)
0550 CALL "SYC.RM",M2$,M2,MIN(MINMASK,9),9
0560 CALL "SYC.RM",M3$,M3,MIN(MINMASK,9),9
0570 LET AMTCOL=80-MINMASK
0580 LET LINES=21-L0
0590 LET UMASK$="-#####0.000",UMASK=LEN(UMASK$)
0600 REM " --- Build footing"
0610 IF BM$="Y" THEN LET FILES$[8]="BMM-08"
0620 IF BM$<>"Y" THEN LET FILES$[8]="WOM-02"
0630 CALL "SYC.DA",1,8,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS THEN GOTO 9900
0650 LET OPCODE_DEV=CHANNELS[8]
0700 REM " --- Background"
0720 PRINT 'SB',@(3,3),"W.O. Status:",@(34),"From Date:",@(AMTCOL-11),"Queue T
0720:ime",@(1,4),"Priority Code:",@(31),"Through Date:",@(AMTCOL-13),"Pcs Per 
0720:Hour",
0730 PRINT @(0,5),"Operation Code:",
0740 CALL "SYC.WC",1,0,80,0,0,6,0
0760 PRINT 'SB',@(0,7),"Date",@(7,7),"Pri WO WO",@(80-M3*3-8),"At From",@(80-M
0760:3*2-6),"Setup",@(80-M3-4),"Run",@(80-5),"Move",
0770 PRINT @(0,8),"Req'd",@(7,8),"Cde St Number  Description",@(80-M3*3-8),"Op
0770:n Opn",@(80-M3*2-5),"Time",@(80-M3-5),"Time",@(80-5),"Time",
0780 CALL "SYC.WC",1,0,80,0,0,9,0
0790 PRINT 'SF',
0995 REM " --- W.O. Status"
1000 PRINT @(0,0),'CF',
1005 LET V0$="S",V1$="CK^",V2$=STATUS$,V3$="",V4$="O=Open, P=Planned, Q=Quote,
1005: Or Any Combination, <F1>=All",V0=3,V1=16,V2=3,I0=0
1010 GOSUB 7000
1015 ON V3 GOTO 1025,1020,1050,1025,9900
1020 LET V$=""
1025 IF V$="" THEN LET V$="OPQ"
1030 FOR X=1 TO LEN(V$); IF POS(V$(X,1)="OPQ")=0 THEN EXITTO 0995
1035 NEXT X
1040 PRINT @(V1,V2),V$,"   ",
1045 LET STATUS$=V$
1050 GOTO 3900
1100 REM " --- Beginning Date"
1110 LET V4$="Enter The Beginning Date For The Display, <F1>=First"
1120 LET V0$="D",V1$="CK",V2$=BEGDATE$,V3$="",V0=3,V1=45,V2=3
1130 GOSUB 7000
1140 ON V3 GOTO 1170,1150,1195,1170,9900
1150 LET BEGDATE$=""; PRINT @(V1,V2),"First   "
1160 GOTO 1190
1170 IF V$="" THEN GOTO 1150
1180 LET BEGDATE$=V$
1190 READ (OPCODE_DEV,KEY=N0$,DOM=1195)
1195 GOTO 3900
1200 REM " --- Operation Code"
1205 PRINT @(0,L0),'CE',@(16,5),'CL',
1210 LET V2$="",V2$=KEY(OPCODE_DEV,END=1240)
1220 IF V2$(1,2)<>N0$ THEN LET V2$=""
1230 IF V2$<>"" THEN LET V2$=V2$(3)
1240 LET V0$="S",V1$="CRK",V3$="",V0=3,V1=16,V2=5
1250 LET V4$="Enter The Operation Code To Display (<F3>=Lookup)"
1260 GOSUB 7000
1270 ON V3 GOTO 1290,1200,1297,1280,9900
1280 GOSUB 6000
1290 LET OP_CODE$=V$
1295 GOSUB 5000
1297 GOTO 3900
1300 REM " --- Priority Code"
1340 LET V0$="S",V1$="CRK",V2$=PR_CODE$,V3$="123456789",V4$="Enter Minimum Pri
1340:ority Code To Display (1=High, 9=Low)",V0=1,V1=16,V2=4
1360 GOSUB 7000
1370 ON V3 GOTO 1390,1300,1395,1390,9900
1390 LET PR_CODE$=V$
1395 GOTO 3900
1400 REM " --- Through Date"
1410 LET V4$="Enter The Ending Date For The Display, <F1>=Last"
1420 LET V0$="D",V1$="CK",V2$=LASTDATE$,V3$="",V0=3,V1=45,V2=4
1430 GOSUB 7000
1440 ON V3 GOTO 1470,1450,1490,1470,9900
1450 LET LASTDATE$=""; PRINT @(V1,V2),"Last    "
1460 GOTO 1490
1470 IF V$="" THEN GOTO 1450
1475 IF BEGDATE$<>"" THEN IF V$<BEGDATE$ THEN GOTO 1400
1480 LET LASTDATE$=V$
1490 GOTO 3900
1500 REM " --- Correct?"
1550 LET V4$="Is The Above Information Correct: "
1570 CALL "SYC.YN",1,V4$,2,V$,V3
1580 ON V3 GOTO 1590,1500,1500,1500,9900
1590 ON POS(V$="YN") GOTO 1500,1600,0995
1600 REM " --- Calculate Actual Run & Setup Time From Transactions "
1605 DIM W0$(18),W1$(30),W[11],K3$(21),KT$(18)
1607 LET WOSTR$=""
1610 READ (WOE03_DEV,KEY=N0$+"D  "+OP_CODE$,DOM=1615)
1615 CALL "SYC.NB","Calculating Actuals:",12,COLUMN
1620 LET K3$=KEY(WOE03_DEV,END=1900)
1625 IF K3$(3,1)<>"D" THEN GOTO 1900
1630 IF K3$(6,LEN(OP_CODE$))<>OP_CODE$ THEN GOTO 1900
1635 READ (WOE03_DEV)
1640 READ (WOT01_DEV,KEY=N0$+K3$(4,2)+K3$(12,7),DOM=1645)
1645 LET KT$=KEY(WOT01_DEV,END=1800)
1650 PRINT @(COLUMN,11),KT$(5,7)+" "+KT$(16,3)
1660 IF KT$(5,7)<>K3$(12,7) THEN GOTO 1800
1665 READ (WOT01_DEV)IOL=WOT01A
1670 IF W0$(15,1)<>"O" THEN GOTO 1645
1680 IF W1$(4,LEN(OP_CODE$))<>OP_CODE$ THEN GOTO 1645
1690 LET UNITS=UNITS+W[0],SET=SET+W[6]
1700 GOTO 1645
1800 REM " --- Done With This Work Order "
1810 IF UNITS=0 AND SET=0 THEN GOTO 1620
1820 LET WOSTR$=WOSTR$+K3$(12,7)+STR(UNITS:UMASK$)+STR(SET:UMASK$)
1830 LET UNITS=0,SET=0
1840 GOTO 1620
1900 REM " --- Position WOM-05 detail file"
1905 PRINT @(0,10),'CE'
1910 DIM SCHED[DAYS],A0$(76),A1$(138),A[11]
1920 LET L=0,TOTSET=0,TOTRUN=0
1980 LET PREFIX$=N0$+D0$(3,3),K$=PREFIX$
1990 READ (WOM05_DEV,KEY=K$,DOM=2000)
2000 REM " --- Read next WOM-05 detail record"
2005 LET RUNT=0,SET=0,MOVE=0
2010 LET K$=KEY(WOM05_DEV,END=3000)
2020 IF POS(PREFIX$=K$)<>1 THEN GOTO 3000
2040 READ (WOM05_DEV)IOL=WOM05A
2050 PRINT @(27,22),"Processing..."+K$(9,7)+" "+K$(16,3)
2060 LET THISSEQ$=K$(16,3),THISWO$=K$(9,7)
2100 REM " --- Retrieve WOE-02 operations record"
2110 DIM B0$(15),B1$(151),B[13]
2120 LET B0$(1)=K$(1,2)+"  "+K$(9,7)+"A"+K$(16,3)
2130 FIND (WOE02_DEV,KEY=B0$,DOM=2900)IOL=WOE02A
2140 LET THISCOD$=B1$(1,3)
2200 REM " --- Work order still open?"
2210 IF B0$(5,7)=A0$(5,7) THEN GOTO 2235
2220 LET A0$(1)=B0$(1,11),MOVE=0
2230 FIND (WOE01_DEV,KEY=A0$(1,11),DOM=2900)IOL=WOE01A
2235 IF A0$(15,1)="P" AND POS("P"=STATUS$)>0 THEN GOTO 2260
2240 IF A0$(15,1)="Q" AND POS("Q"=STATUS$)>0 THEN GOTO 2260
2245 IF A0$(15,1)="O" AND POS("O"=STATUS$)>0 THEN GOTO 2260
2250 GOTO 2900
2260 DIM DESC$(80-M3*2-8-23)
2270 LET DESC$(1)=A1$(1,30); IF A0$(14,1)="I" THEN LET DESC$(1)=A0$(56)+" "+DE
2270:SC$
2280 LET MOVE=L[3]
2300 REM "--- Shall we print it?"
2320 GOSUB 6200
2325 GOSUB 6800
2330 IF RUNT=0 AND SET=0 AND MOVE=0 THEN GOTO 2900
2332 IF A1$(97,1)>PR_CODE$ THEN GOTO 2900
2333 IF BEGDATE$<>"" THEN IF K$(6,6)<BEGDATE$ THEN GOTO 2900
2334 IF LASTDATE$<>"" THEN IF K$(6,6)>LASTDATE$ THEN GOTO 2900
2335 LET V3=0
2340 GOSUB 5200
2345 LET TOTSET=TOTSET+SET,TOTRUN=TOTRUN+RUNT
2350 IF V3=2 THEN LET I0=5; GOTO 3900
2360 IF V3=4 THEN GOTO 9900
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- Totals"
3010 IF TOTSET=0 AND TOTRUN=0 THEN GOTO 3100
3020 IF L<=LINES THEN GOTO 3080
3030 LET V4$="<Enter>=Continue, <F2>=Op Code"
3040 GOSUB 4000
3045 IF V3=2 THEN LET I0=5; GOTO 3900
3050 IF V3=4 THEN GOTO 9900
3060 LET L=0
3070 PRINT @(0,L0),'CE',
3080 PRINT @(5,L+L0),"***Totals***",@(80-M3*3),TOTSET:M3$,TOTRUN:M3$,
3090 LET L=L+1
3100 REM "--- Another OP?"
3110 LET V4$="<Enter>=Continue"
3120 GOSUB 4000
3140 IF V3=4 THEN GOTO 9900
3150 LET V3=0,I0=3
3160 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 1500
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 0995,1300,1100,1400,1200,1500
4000 REM " --- Continue?"
4020 LET V0$="S",V1$="",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4090 RETURN
5000 REM " --- Display record"
5010 LET D0$(1)=N0$+OP_CODE$
5020 READ (OPCODE_DEV,KEY=D0$(1,5),DOM=1400)IOL=OPCODEA
5030 LET OPERATION$=D0$(3,3)
5080 PRINT @(16,5),D0$(3,3)," ",D0$(6,20),@(AMTCOL,3),D[3]:M3$,@(AMTCOL,4),D[1
5080:]:M2$,
5090 RETURN
5200 REM "--- Detail display ---"
5210 IF L<=LINES THEN GOTO 5260
5220 LET V4$="<Enter>=Continue, <F2>=Op Code"
5230 GOSUB 4000
5235 IF V3=2 OR V3=4 THEN GOTO 5290
5240 LET L=0
5250 PRINT @(0,L0),'CE',
5260 PRINT @(0,L+L0),FNB$(K$(6,3))," ",A1$(97,1)," ",A0$(15,1),"  ",A0$(5,7),"
5260:  ",DESC$,
5270 PRINT @(80-M3*3-8,L+L0),AT$," ",FROM$," ",SET:M3$,RUNT:M3$,MOVE:M3$,
5280 LET L=L+1
5290 RETURN
6000 REM " --- Operation Lookup"
6010 LET CHANNEL=OPCODE_DEV,RECORD$="",NUMBER=1,TITLE$="Operation Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6035 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
6040 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6055 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6060 LET V$="SKIP"
6070 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],
6070:POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN
6100 REM "---- Operation Complete? ------"
6110 DIM W0$(18),W1$(40),W[4]
6120 READ (WOT01_DEV,KEY=A0$(1,11),DOM=6120)
6130 LET WOTKEY$=KEY(WOT01_DEV,END=6290)
6140 IF POS(A0$(1,11)=WOTKEY$)<>1 THEN GOTO 6290
6160 READ (WOT01_DEV)IOL=WOT01A
6200 REM " --- Initialize WO ---"
6210 DIM RUNTIM[OPNMAX],SETUP[OPNMAX],ACTRUN[OPNMAX],ACTSET[OPNMAX],B0$(15),B1
6210:$(152),B[13]
6220 LET OPNSEQ$="",OPNCOD$="",X0=0,NOW=0,AT$="",FROM$="",SET=0,RUNT=0
6230 READ (WOE02_DEV,KEY=A0$(1,11),DOM=6240)
6240 LET WOEKEY$=KEY(WOE02_DEV,END=6400)
6250 IF POS(A0$(1,11)+"A"=WOEKEY$)<>1 THEN GOTO 6400
6260 READ (WOE02_DEV)IOL=WOE02A
6270 IF B1$(91,1)<>"S" THEN GOTO 6240
6280 LET OPNSEQ$=OPNSEQ$+B0$(13,3),OPNCOD$=OPNCOD$+B1$(1,3),RUNTIM[X0]=L[2],SE
6280:TUP[X0]=L[1]
6290 LET X0=X0+1
6300 GOTO 6240
6400 REM " --- Calculate Actual ---"
6405 DIM W0$(18),W1$(40),W[11]
6410 READ (WOT01_DEV,KEY=A0$(1,11),DOM=6420)
6420 LET WOTKEY$=KEY(WOT01_DEV,END=6600)
6430 IF POS(A0$(1,11)=WOTKEY$)<>1 THEN GOTO 6600
6440 READ (WOT01_DEV)IOL=WOT01A
6450 IF W0$(15,1)<>"O" THEN GOTO 6420
6460 LET SEQ$=W1$(1,3),COD$=W1$(4,3),INDX=POS(SEQ$=OPNSEQ$,3)
6470 IF INDX=0 THEN LET OPNSEQ$=OPNSEQ$+SEQ$,OPNCOD$=OPNCOD$+COD$; GOTO 6460
6480 LET INDX=INT(INDX/3),ACTSET[INDX]=ACTSET[INDX]+W[6],ACTRUN[INDX]=ACTRUN[I
6480:NDX]+W[0]
6490 IF NOW<INDX THEN LET NOW=INDX
6500 GOTO 6420
6600 REM "---- This operation? ---"
6610 LET AT$=OPNCOD$(NOW*3+1,3)
6620 LET THISINDX=POS(THISSEQ$=OPNSEQ$,3)
6630 IF THISINDX=0 THEN GOTO 6700
6640 LET THISINDX=INT(THISINDX/3),XFROM=THISINDX-1; IF XFROM<0 THEN LET XFROM=
6640:0
6650 LET FROM$=OPNCOD$(XFROM*3+1,3)
6660 LET RUNT=RUNTIM[THISINDX],SET=SETUP[THISINDX]
6700 RETURN
6800 REM " --- Calculate Remaining Units "
6810 REM " --- UMASK$ = PARAMETER UNIT MASK, UMASK = LEN(UMASK$)"
6815 LET UNITRUN=0,UNITSET=0
6820 LET WOPOS=POS(A0$(5,7)=WOSTR$)
6825 IF WOPOS=0 THEN GOTO 6895
6830 LET UNITRUN=NUM(WOSTR$(WOPOS+7,UMASK))
6835 LET UNITSET=NUM(WOSTR$(WOPOS+7+UMASK,UMASK))
6840 LET RUNT=RUNT-UNITRUN,SET=SET-UNITSET
6845 IF RUNT<0 THEN GOTO 6860
6850 LET WOSTR$(WOPOS+7,UMASK)=STR(0:UMASK$)
6855 GOTO 6870
6860 LET UNITRUN=-RUNT,WOSTR$(WOPOS+7,UMASK)=STR(UNITRUN:UMASK$)
6865 LET RUNT=0
6870 IF SET<0 THEN GOTO 6885
6875 LET WOSTR$(WOPOS+7+UMASK,UMASK)=STR(0:UMASK$)
6880 GOTO 6895
6885 LET UNITSET=-UNITSET,WOSTR$(WOPOS+7+UMASK,UMASK)=STR(UNITSET:UMASK$)
6890 LET SET=0
6895 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNS$(Q$)=Q$(POS(" "<>Q$))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
