0010 REM "WOS - Work Orders By Date Report - Detail Print              "
0020 REM "Program:  WOR.NB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0230 IVM01A: IOLIST D0$,D1$(1)
0500 REM " --- Init Data"
0510 LET HEADINGS=6
0520 DIM HEADINGS$[HEADINGS]
0530 LET H=0,H0=132,WHEN$=F0$(7,3),CLOCK$=""
0540 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H9$+" For "+H4$+" Work
0540: Orders",HEADINGS$[3]=H8$,HEADINGS$[4]=H7$
0550 DIM U[1],T[1]
0560 LET R0=121-C1-I9-67-2
0570 IF R0>30 THEN LET R0=30
0600 REM " --- Additional File Opens "
0610 CALL "SYC.GA",7,1,"","",STATUS
0615 IF STATUS THEN GOTO 9900
0620 LET V=5
0700 REM " --- Background"
0720 CALL "SYC.NB","Printing:",8,COLUMN
0800 REM " --- Options"
1000 REM " --- Init Read"
1020 READ (WOW04_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read"
1120 LET K$=KEY(WOW04_DEV,END=4000)
1140 IF POS(N0$=K$)<>1 THEN GOTO 4000
1200 REM " --- Breaks"
1220 READ (WOW04_DEV,KEY=K$)
1300 PRINT @(COLUMN,11),K$(8)
1320 READ (WOE01_DEV,KEY=K$(1,2)+K$(6),DOM=1120)IOL=WOE01A
1330 IF A0$(14,1)<>"I" THEN GOTO 2000
1340 DIM D1$(60)
1350 LET D1$(1)="Invalid Item"
1360 FIND (IVM01_DEV,KEY=N0$+A0$(56,20),DOM=1361)IOL=IVM01A
1370 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
2000 REM " --- Print"
2020 IF L+1>L9 THEN GOSUB 5000
2040 PRINT (7)A0$(5,7),@(19),A0$(12,2),@(23),A0$(14,1),@(26),A0$(15,1),
2050 IF A0$(15,1)="C" THEN PRINT (7)@(29),"*Closed*", ELSE PRINT (7)@(29),FNB$
2050:(A0$(19)),
2060 PRINT (7)@(9),FNB$(A0$(16)),@(47),FNB$(A0$(25)),@(56),FNB$(A0$(28)),
2080 IF A0$(15,1)="C" THEN PRINT (7)@(38),FNB$(A0$(31,3)), ELSE PRINT (7)@(38)
2080:,FNB$(A0$(22,3)),
2120 IF A0$(14,1)="I" THEN PRINT (7)@(67),D1$(1,DESCLEN),@(121-C1-I9),A0$(56,I
2120:9),J1$(1,10),
2130 IF A0$(14,1)="N" THEN PRINT (7)@(121-C1-I9),"Special",J1$(1,10),
2140 IF POS(A0$(14,1)="NR")>0 THEN PRINT (7)@(67),A1$(1,R0),
2200 IF P3$(2,1)<>"Y" THEN GOTO 2300
2220 IF POS(" "<>A0$(34,6))>0 THEN PRINT (7)@(132-9-C1),FNF$(A0$(34,C0),C1$),
2230 IF POS(" "<>A0$(40,7))>0 THEN PRINT (7)@(132-7),A0$(40,7),
2300 PRINT (7)""
2320 LET L=L+1,L0=L0+1
2900 GOTO 1100
4000 REM " --- End"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)'LF',"Total Records: ",L0
4100 GOTO 9900
5000 REM " --- Heading"
5020 LET L=HEADINGS+2
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5110 PRINT (7)"W.O.",@(11),"Date",@(19),"WO",@(32),"Est",@(42),"Est",@(49),"Ac
5110:tual",@(58),"Last",@(117),"Customer",@(127),"Order"
5120 PRINT (7)"Number",@(9),"Opened",@(18),"Type Ct St",@(31),"Start",@(39),"C
5120:omplete",@(50),"Start",@(57),"Activity",@(67),"Description",@(121-C1-I9),
5120:"Item Code",@(118),"Number",@(126),"Number"
5140 PRINT (7)'LF'
5150 LET L=L+4
5190 RETURN
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),
8090:32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
