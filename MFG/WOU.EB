0010 REM "WOS - WO Period End Update - Transaction Processing          "
0020 REM "Program:  WOU.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0           |
0026 REM "|      Copyright BASIS International Ltd.                   |
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION 4
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$,A1$,A[ALL]
0220 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0230 WOTRAN: IOLIST W0$,W1$,W[ALL]
0240 WOHIST: IOLIST W0$,W1$,W[ALL]
0250 WOT11A: IOLIST H0$,H1$,H2$,H[ALL]
0260 WOT12A: IOLIST H0$,H1$,H2$,H[ALL]
0500 REM " --- Init Data"
0520 DIM A[11],W[11]
0700 REM " --- Background"
0720 CALL "SYC.NB","Updating: ",17,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Process Here
1010 READ (WOE01_DEV,KEY=N0$,DOM=1020)
1020 LET K$=KEY(WOE01_DEV,END=4000)
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1040 READ (WOE01_DEV,KEY=K$)IOL=WOE01A
1050 PRINT @(COLUMN,11),A0$(5,7)
1060 IF POS(A0$(12,2)=T0$,2)>0 THEN GOTO 1100
1070 IF A0$(15,1)<>"C" THEN GOTO 1020
1080 IF A0$(31,3)>P9$ THEN GOTO 1020
1100 REM " --- WOT-01/21/31 To WOT-03/23/33 Here
1110 LET WOTRAN_DEV=WOT01_DEV,WOHIST_DEV=WOT03_DEV
1120 IF POS(A0$(12,2)=T0$,2)>0 THEN LET D9$=A0$(31,3) ELSE LET D9$=P9$
1130 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=1140)
1140 LET K4$=KEY(WOTRAN_DEV,END=1410)
1150 IF K4$(1,11)<>A0$(1,11) THEN GOTO 1410
1160 IF K4$(12,3)>D9$ THEN GOTO 1410
1170 PRINT @(COLUMN+8,11),FNB$(K4$(12,3))
1180 READ (WOTRAN_DEV,KEY=K4$)IOL=WOTRAN
1190 WRITE (WOHIST_DEV,KEY=K4$)IOL=WOHIST
1200 REMOVE (WOTRAN_DEV,KEY=K4$)
1210 IF POS(I3$(17,1)="LS")=0 THEN GOTO 1140
1300 REM " --- WOT-11 To WOT-12 Here
1310 DIM H[2]
1320 READ (WOT11_DEV,KEY=W0$,DOM=1330)
1330 LET K9$=KEY(WOT11_DEV,END=1400)
1340 IF POS(W0$=K9$)<>1 THEN GOTO 1400
1350 READ (WOT11_DEV,KEY=K9$)IOL=WOT11A
1360 PRINT @(COLUMN+8,11),FNB$(K9$(12,3))
1370 WRITE (WOT12_DEV,KEY=K9$)IOL=WOT12A
1380 REMOVE (WOT11_DEV,KEY=K9$)
1390 GOTO 1330
1400 GOTO 1140
1410 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV,WOHIST_DEV=WOT23_DE
1410:V; GOTO 1130
1420 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV,WOHIST_DEV=WOT33_DE
1420:V; GOTO 1130
1430 GOTO 1020
4000 REM " --- Increment Period"
4010 IF P9<P[1] THEN GOTO 4900
4020 LET P[1]=P[1]+1
4030 IF P[1]>G[0] THEN LET P[1]=1,P[2]=P[2]+1
4050 EXTRACT (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
4060 LET P4$(1,2)=STR(P[1]:"00"),P4$(3,2)=FNYEAR_YY21$(P[2])
4070 WRITE (SYS01_DEV,KEY=N0$+N2$+"00")IOL=WOS01A
4900 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8130:DEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
