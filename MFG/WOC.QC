0010 REM "WOC - Work Order SpeedSearch (Work Orders by Vendor)"
0020 REM "Program WOC.QC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="POE-02",FILES$[3]="POE-12"
0130 LET FILES$[4]="POM-02",FILES$[5]="IVM-01",FILES$[6]="IVM-10"
0135 LET FILES$[7]="WOE-01",FILES$[8]="WOE-22",FILES$[9]="WOE-32"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STA
0140:TUS
0150 IF STATUS THEN GOTO 9900
0160 LET APM01_DEV=CHANNELS[1],POE02_DEV=CHANNELS[2],POE12_DEV=CHANNELS[3]
0170 LET POM02_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],IVM10_DEV=CHANNELS[6]
0190 LET WOE01_DEV=CHANNELS[7],WOE22_DEV=CHANNELS[8],WOE32_DEV=CHANNELS[9]
0200 REM " --- IOLIST's"
0210 APM01A: IOLIST A0$(1),A1$(1)
0220 WOE01A: IOLIST B0$,B1$,B[ALL]
0230 IVM01A: IOLIST C0$(1),C1$(1)
0240 POE02A: IOLIST D0$(1),D1$(1),D2$,D4$,D[ALL]
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,X$,X$,X$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0280 WOS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0310 POE12A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0330 WOE22A: IOLIST R0$(1),R1$(1),R[ALL]
0380 IVM10C: IOLIST X0$(1)
0390 POM02A: IOLIST Y0$(1),Y1$(1)
0400 REM " --- Parameters"
0405 DIM P[1],I[5],X$(128)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8)),SNAME$=FNP$(X$(19,8))
0420 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0425 LET SHEIGHT=NUM(X$(45,2)),MAXOPT=NUM(X$(57,2))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0435 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0440 LET PAGES=INT((DSZ-5000)/4000),SYS01_DEV=NUM(STBL("!SYS01"))
0445 IF PAGES<3 THEN GOTO 9900
0450 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0455 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9900)IOL=APS01A
0460 LET P[0]=NUM(P2$(1,2))
0465 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0470 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(3,2))
0475 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0480 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9900)IOL=WOS01A
0485 IF I3$(3,1)<>"Y" THEN GOTO 9900
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXOPT,6],A0$(8),A1$(320),B0$(75),B1$(141),B[11]
0515 DIM C0$(22),C1$(60),X0$(32),HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0520 DIM D0$(15),D1$(160),D[8],W0$(18),W1$(64),W2$(32),W3$(22),W4$(40)
0525 DIM W[12],Y0$(4),Y1$(32),R0$(15),R1$(151),R[11]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,M2=LEN(M2$),DONE=0,OPTION=0
0550 LET A0$(1)=N0$+OPTION$[3],P=WIDTH-12-M2,DW=P-7,PGM$=PGM(-2)
0560 LET NF$="(Not On File)"
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TIT
0610:LE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="PO #    Seq Required WO #    Ty Sts WO Start Required"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1200
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1030 PRINT @(1,0),'SB',"Vendor:",'SF','CL',@(0,1),'CL',@(0,2),'CL',
1035 LET V2$="",V2$=KEY(APM01_DEV,END=1050)
1040 IF POS(A0$(1,2)=V2$)<>1 THEN LET V2$=""
1045 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KE",V3$=M0$,V0=6,V1=9,V2=0,V4$=""
1055 PRINT @(V0+V1+1,V2),'CL',
1060 GOSUB 7000
1063 IF V3=6 THEN GOTO 4000
1065 IF V3=5 THEN GOTO 4000
1070 IF V3=4 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1080 IF V3=3 THEN CALL "SYC.LV",V$
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1200 REM " --- Retrieve records for display"
1210 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2]
1215 DIM DESCRIPTION$(I[2]+I[4]+I[5]),X$(WIDTH-2)
1220 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
1230 READ (APM01_DEV,KEY=A0$,DOM=1000)IOL=APM01A
1250 LET X$(1)='SB'+" Vendor: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+A1$(1,30)
1260 LET X1$=A0$(3,6),X2$=PARAM$
1270 CALL "SYC.QB",X$,X1$,X2$
1280 PRINT @(0,0),X$,
1300 REM " --- Position POE-02"
1390 READ (POE02_DEV,KEY=A0$,DOM=1400)
1400 REM " --- Read next POE-02 record"
1410 LET K$=KEY(POE02_DEV,END=3000)
1420 IF POS(A0$=K$)<>1 THEN GOTO 3000
1430 READ (POE02_DEV)IOL=POE02A
1900 REM " --- Position detail file"
1990 READ (POE12_DEV,KEY=D0$,DOM=2000)
2000 REM " --- Read next POE-12 record"
2010 LET K$=KEY(POE12_DEV,END=2900)
2020 IF POS(D0$=K$)<>1 THEN GOTO 2900
2030 READ (POE12_DEV)IOL=POE12A
2040 IF FNP$(W2$(1,7))="" THEN GOTO 2800
2100 REM " --- Read work order and line code"
2110 LET B0$(1)=N0$+"  "+W2$(1,7),Y0$(1)=N0$+W1$(1,2)
2120 LET X$="B",MS$="Mat",CHANNEL=WOE22_DEV
2130 FIND (WOE01_DEV,KEY=B0$(1,11),DOM=2800)IOL=WOE01A
2140 FIND (POM02_DEV,KEY=Y0$,DOM=2800)IOL=POM02A
2150 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 2800
2160 IF Y1$(21,1)="N" THEN LET X$="C",MS$="Sub",CHANNEL=WOE32_DEV
2170 LET R0$(1)=B0$(1,11)+X$+W2$(8,3)
2180 FIND (CHANNEL,KEY=R0$,DOM=2800)IOL=WOE22A
2200 REM " --- Display page?"
2210 IF L<MAXROW THEN GOTO 2400
2220 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2230 GOSUB 5000
2240 IF DONE THEN GOTO 4000
2270 IF ANSWER$<>"" THEN GOTO 3500
2290 LET L=1,X1=1
2400 REM " --- Middle window record"
2410 DIM G$(WIDTH-2)
2415 LET STATUS$=""
2420 IF B0$(15,1)="O" THEN LET STATUS$="Opn"
2425 IF B0$(15,1)="P" THEN LET STATUS$="Pln"
2430 IF B0$(15,1)="Q" THEN LET STATUS$="Quo"
2435 IF B0$(15,1)="C" THEN LET STATUS$="Cls"
2450 LET G$(2)=W0$(9,7)+" "+W0$(16,3)+" "+FNB6$(W1$(3,6))+" "+B0$(5,7)+" "+B0$
2450:(12,2)+" "+STATUS$
2460 IF FNP$(B0$(28,3))<>"" THEN LET G$(38)=FNB$(B0$(28,3))
2470 IF FNP$(R1$(4,3))<>"" THEN LET G$(47)=FNB$(R1$(4,3))
2490 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=C0$(3)+B0$(5,7)
2500 REM " --- Retrieve inventory item and warehouse"
2510 DIM ITEM$(DW),WHSE$(DW),DESC$(DW)
2515 LET C0$(1)=N0$+W3$(3),C1$(1)=NF$,X0$(1)=N0$+"C"+W3$(1,2)+NF$
2520 IF Y1$(21,1)="N" THEN GOTO 2550
2525 FIND (IVM01_DEV,KEY=C0$,DOM=2530)IOL=IVM01A
2530 LET ITEM$="{Item}"+C0$(3)
2535 LET DESC$="{Desc}"+FNITEM$(C1$,I[3],I[4],I[5])
2540 GOTO 2560
2550 LET ITEM$(1)="{Item}"+W3$(3),DESC$(1)="{Desc}"+W4$
2560 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=2570)IOL=IVM10C
2570 LET WHSE$(1)="{Whse}"+X0$(4,2)+" "+X0$(6,24)
2700 REM " --- Bottom window record"
2710 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),G$(1)=""
2720 LET G$(1)=WHSE$,G$(P-6)="{Cd}"+W1$(1,2),G$(P)="{Qty Req'd}"+STR(W[3]:M2$)
2730 LET MORE$[PAGE,X1,1]=G$,G$(1)=ITEM$,G$(P-6)="{UM}"+W1$(22,2)
2740 LET G$(P)="{Qty Rec'd}"+STR(W[6]:M2$),MORE$[PAGE,X1,2]=G$,G$(1)=DESC$
2750 LET G$(P-6)="{MS}"+MS$,G$(P+2)="{Balance}"+STR(W[3]-W[6]:M2$)
2760 LET MORE$[PAGE,X1,3]=G$,LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
2800 REM " --- Loop back for next detail line"
2890 GOTO 2000
2900 REM " --- Loop back for next purchase order"
2990 GOTO 1400
3000 REM " --- No more work orders"
3010 IF X0>0 THEN GOTO 3200
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No Work Orders Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 LET SELECTION$="PRIOR"
3090 GOTO 3500
3200 REM " --- Display last page"
3210 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
3220 GOSUB 5000
3230 IF DONE THEN GOTO 4000
3500 REM " --- Process option window"
3510 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3520 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3530 LET C0$(1)=N0$+ANSWER$
3540 IF LEN(ANSWER$)>20 THEN LET B0$(1)=N0$+"  "+ANSWER$(21)
3550 FIND (IVM01_DEV,KEY=C0$,DOM=3560)IOL=IVM01A
3560 FIND (WOE01_DEV,KEY=B0$(1,11),DOM=3570)IOL=WOE01A
3570 GOSUB 5000
3580 IF CONTROL=1 THEN GOTO 1000
3590 IF OPTION$[2]="ITEM/ORDER" THEN LET OPTION$[3]=C0$(3)+B0$(5,7)
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],W
5050:ININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR
5080:"
5090 RETURN
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),
8090:32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT
9999 END
