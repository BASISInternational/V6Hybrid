0010 REM "WOM - Operation Code Maintenance"
0020 REM "Program WOM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOM-02",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET WOM02_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 WOM02A: IOLIST A0$(1),A[ALL]
0260 WOS01A: IOLIST X$,P1$,P2$,P3$,X$,M0$,M1$,M2$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0290 IVS01A: IOLIST X$,P5$,P6$
0400 REM " --- Parameters"
0405 DIM P[2]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 LET IVS01_PRECISION=NUM(P6$(5,1))
0490 LET P[0]=NUM(P6$(1,2))
0500 REM " --- Init Data"
0505 PRECISION IVS01_PRECISION
0510 DIM A0$(90),A[13]
0540 LET A0$(1,2)=N0$,K9=5
0550 LET M6$="##.00",M7$="##0.0",M6=LEN(M6$),M7=LEN(M7$)
0560 CALL "SYC.RM",M1$,M1,6,8
0570 CALL "SYC.RM",M2$,M2,6,8
0700 REM " --- Background"
0720 PRINT 'SB',@(8,3),"Operation Code:",@(11,4),"Description:",@(12,5),"Labor
0720: Rate:",@(7,6),"Pieces Per Hour:",@(7,7),"Overhead Factor:",@(12,8),"Queu
0720:e Time:",@(12,9),"Setup Time:",@(13,10),"Move Time:",'SF',
0800 REM " --- Options"
0810 IF P3$(1,1)<>"Y" THEN GOTO 0900
0820 DIM MESS$[1]
0830 LET MESS$[0]="Bill Of Materials Is Installed. Use Operations From"
0840 LET MESS$[1]="That System. Press <Enter> To Return To The Menu: "
0850 CALL "SYC.XA",0,MESS$[ALL],1,22,-1,V$,V3
0870 GOTO 9900
0900 REM " --- Init File"
0920 READ (WOM02_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(WOM02_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="S",V1$="KEC",V3$="",V4$="<F3>=Lookup",V0=3,V1=24,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3,3)=V$
1100 PRINT @(V1,V2),A0$(3,3)
1200 REM " --- Find Key"
1220 FIND (WOM02_DEV,KEY=A0$(1,K9),DOM=1400)IOL=WOM02A
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1420 LET V4$="Is This A New Record: "
1430 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up"
1510 LET A0$(6)=""
1520 DIM A[13]
1530 LET A[1]=1
1540 PRINT @(0,22),'CL'
2000 REM " --- Description"
2010 LET V4$="Enter An Operation Code Description"
2020 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=24,V2=4,I0=0
2040 GOSUB 7000
2050 IF V3=4 THEN GOTO 3900
2060 LET A0$(6,20)=V$
2090 GOTO 3900
2100 REM " --- Operation Rate
2110 LET V4$="Enter An Operation Labor Rate"
2120 LET V0$="N",V1$="",V2$=STR(A[0]),V3$=M2$,V0=0,V1=24,V2=5
2140 GOSUB 7000
2150 LET A[0]=V
2190 GOTO 3900
2300 REM " --- Pieces per Hour
2310 LET V4$="Enter Pieces per Hour"
2320 LET V0$="N",V1$="",V2$=STR(A[1]),V3$=M1$,V0=0,V1=24,V2=6
2340 GOSUB 7000
2360 LET A[1]=V
2370 IF A[1]<>0 THEN GOTO 2390
2380 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Pieces Per Hour May Not Be Zero! <
2380:Enter> To Correct: ",V0=1,V1=FNV(V4$),V2=22
2382 PRINT @(V1,V2),'RB',
2384 GOSUB 7000
2386 GOTO 2300
2390 GOTO 3900
2500 REM " --- Overhead Rate
2510 LET V4$="Enter An Overhead Rate"
2520 LET V0$="N",V1$="",V2$=STR(A[2]),V3$=M2$,V0=0,V1=24,V2=7
2540 GOSUB 7000
2560 LET A[2]=V
2590 GOTO 3900
2700 REM " --- Queue Time
2720 LET V0$="N",V1$="C",V2$=STR(A[3]),V3$=M6$,V4$="Enter Queue Time Using Hun
2720:dredths Of An Hour",V0=0,V1=24,V2=8
2730 GOSUB 7000
2740 IF V3=2 THEN GOTO 3900
2780 LET A[3]=V
2790 GOTO 3900
2800 REM " --- Setup Time
2820 LET V0$="N",V1$="C",V2$=STR(A[4]),V3$=M6$,V4$="Enter Setup Time Using Hun
2820:dredths Of An Hour",V0=0,V1=24,V2=9
2830 GOSUB 7000
2840 IF V3=2 THEN GOTO 3900
2880 LET A[4]=V
2890 GOTO 3900
2900 REM " --- Move Time
2920 LET V0$="N",V1$="C",V2$=STR(A[5]),V3$=M6$,V4$="Enter Move Time Using Hund
2920:redths Of An Hour",V0=0,V1=24,V2=10
2930 GOSUB 7000
2940 IF V3=2 THEN GOTO 3900
2980 LET A[5]=V
2990 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2300,2500,2700,2800,2900,3990
4000 REM " --- Options"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct?
4020: (Y/N/Delete): ",V0=6,V1=64,V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (WOM02_DEV,KEY=A0$(1,K9))IOL=WOM02A
4190 GOTO 1000
4200 REM " --- Delete
4220 REMOVE (WOM02_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print"
4520 LET V4$="Do You Wish To Print A List Of All Records: "
4530 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4560 LET O0=1
4580 RUN "WOM.BB"
5000 REM " --- Display"
5020 PRINT @(24,4),A0$(6,20),
5040 PRINT @(24,5),A[0]:M2$,
5045 PRINT @(24,6),A[1]:M1$,
5050 PRINT @(24,7),A[2]:M2$,
5060 PRINT @(24,8),A[3]:M6$,@(24,9),A[4]:M6$,@(24,10),A[5]:M6$,
5090 RETURN
6000 REM " --- Lookup"
6010 LET CHANNEL=WOM02_DEV,RECORD$="",NUMBER=1,TITLE$="Operation Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6030 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
6040 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6050 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6060 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],
6060:POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3
8040:,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABC
8155:DEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF:
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
